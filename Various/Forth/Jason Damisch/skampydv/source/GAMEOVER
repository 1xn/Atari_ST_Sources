\ *****************
\ **  GAME OVER  **
\ *****************

: gameover ;

decimal

create |game-over|  file, gameover.dat

\ **********
: :big-letter
\ (S  position-of-letter  --  )
\ (I  --  Name
	create
	128 * ,
	does>
	@ |game-over| +
;


0 :big-letter G
1 :big-letter A
2 :big-letter M
3 :big-letter E
4 :big-letter O
5 :big-letter V
6 :big-letter E'
7 :big-letter R


\ *************
: erase-collumn
\ (  collumn#  --  )
	chamber +
	11 0 do
		dup 0 swap c!
		20 +
	loop
	drop
;


\ ************
: clear-screen
	10 0 do
		9 i - erase-collumn
		10 i + erase-collumn
		2000 slower
		.world'  frame-finished
	loop
;


s s s s s s s s
s s s s s s s s
s s s s s s s s
s s s s s s s s
n nw nw h  w sw sw s
40 :path gmv-0path


s s s s s s s s
s s s s s s s s
s s s s s s s s
s s s s s s s s
n nw nw h  sw sw s s
40 :path gmv-1path


s s s s s s s s
s s s s s s s s
s s s s s s s s
s s s s s s s s
n n nw h  sw sw s n
40 :path gmv-2path


s s s s s s s s
s s s s s s s s
s s s s s s s s
s s s s s s s s
n n nw n  sw s s s
40 :path gmv-3path


s s s s s s s s
s s s s s s s s
s s s s s s s s
s s s s s s s s
n ne ne n  s se se e
40 :path gmv-4path


s s s s s s s s
s s s s s s s s
s s s s s s s s
s s s s s s s s
n ne ne e  e se se s
40 :path gmv-5path


s s s s s s s s
s s s s s s s s
s s s s s s s s
s s s s s s s s
n ne ne e  e se se e
40 :path gmv-6path


s s s s s s s s
s s s s s s s s
s s s s s s s s
s s s s s s s s
n ne ne e  e se se se
40 :path gmv-7path


\ **********
: big-letter
\ (  foe-atom  --  )
	drop
;


\ ***********
: .big-letter
\ (  foe-atom  --  )
	>r
	r@ path
	r@ move-player
	r@ >x @   r@ >y @   r@ >frames @   playfield-start   .player
	r> drop
;


\ ***********
: use-letters
\ (  x  y  --  )
	0 0
	['] .big-letter   ['] big-letter   3   0   still   still
;


\ ******
: letter
\ (  ..... L  --  ..L..
	>r
	rot drop r> -rot
;


\ *************
: using-letters
	80   0  use-letters G letter  gmv-0path reset-path  for-foes 0 element!
	96   0  use-letters A letter  gmv-1path reset-path  for-foes 1 element!
	112  0  use-letters M letter  gmv-2path reset-path  for-foes 2 element!	
	128  0  use-letters E letter  gmv-3path reset-path  for-foes 3 element!
	160  0  use-letters O letter  gmv-4path reset-path  for-foes 4 element!
	176  0  use-letters V letter  gmv-5path reset-path  for-foes 5 element!
	192  0  use-letters E' letter gmv-6path reset-path  for-foes 6 element!
	208  0  use-letters R letter  gmv-7path reset-path  for-foes 7 element!
;


\ *****  sounds  *****


\ ******
: crunch
	-((.))0voice
	255 noise-pitch
	0 +noise
	10 30 do
		flash
		i 2/ 0 volume 
	-2 +loop
	-((.))0voice
;


\ ******************
variable cry-counter


\ **********
: <((.))cry>      
\ main body of cry
	cry-counter @ 4 *  0 frequency
	1 cry-counter +!
	cry-counter @ 31 = if  -((.))0voice   then
;


\ ********
: ((.))cry
\  Activates the sound
	13 0 volume
	00 cry-counter !
	['] <((.))cry> is o((.))0voice
	0 +tone
	00 0 frequency
	00 0 step-size
;


\ *********
: game-over
	2 time !
	8 #foes !
	clear-screen
	using-letters
	reset-sounds
	((.))cry
	40 00 do
		.world'
		.foes
		.lives
		sound-task		
		700 slower
		frame-finished
	loop
	crunch
	60000 slower
	\_save-score
	attract-mode
;

decimal
