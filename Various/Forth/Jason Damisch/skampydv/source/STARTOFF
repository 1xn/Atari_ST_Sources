\  ****************
\  **  STARTOFF  **
\  ****************

: startoff ;


\ ***************
: -random-plinging
	-((.))0voice
;


\ ***************
: +random-plinging
	-((.))0voice
	0 +tone
	16 0 volume
	06 sustain-high
	200 sustain-low
	08 waveform	
	3 0 step-size
;


\ ***********
: change-pling
	255 random  0 frequency
;


\ *************
create chamber'  11 20 * allot


\ ***********
: <introduce>
\ (  old-chamber  --  )
	11 10 * 2+  0  do
		.world'
		dup i + c@   i chamber' + c! 
		dup 220 + i - c@   chamber' 220 + i - c!
		frame-finished
		change-pling
	loop
	drop
;


\ *********
: introduce
	reset-sounds
	scampy-pallet
	game-mode
	wipe-canvases
	.scoreboard
	<chamber> @
	chamber' <chamber> !
	chamber'  11 20 * erase  
	+random-plinging
	dup <introduce>
	<chamber> !
	-random-plinging
;


\ *****
: +line
\ (  line  --  +line )
	20 +
;


\ *************
: blank-segment
\  (  line  --  )
	4 erase
;


\ **********
: blank-zone
	chamber   220 2/   2-   20 -   +
	dup blank-segment +line
	dup blank-segment +line
	blank-segment
; 


\ ***** 
: <.go>
	140 80 G playfield-start  .player
	164 80 O playfield-start  .player
;


\ **********
: /((.))woot
	14 14 13 12 11 09 07 06
	08 0 do
		dup 0 volume
		8 * 150 swap -  0 frequency
		4000 slower
	loop 
;


\ **********
: \((.))woot
	06 07 09 11 12 13 14
	07 0 do
		dup  0 volume
		8 * 150 swap - 0 frequency
		4000 slower
	loop 
;


\ **********
: +((.))woot
	-((.))0voice
	0 +tone
	0 -noise
	0 0 step-size
;


\ **************
: present-scampy
	for-scampy
	dup >x @  swap >y @  |scampy|  playfield-start  .player
;


\ ************
: present-foes
	#foes @  0 ?do
		i element-size *  for-foes +
		dup >x @  over >y @  rot >frames @  playfield-start  .player
	loop
;


\ *************
: present-rocks
	#rocks @  0 ?do
		i element-size *  for-rocks +
		dup >x @  swap >y @   |rock|  playfield-start .missle
	loop
;


\ ***************
 : present-players
	present-scampy
	present-foes
	present-rocks
;


\ ********
: blink-go
	blank-zone
	.world'
	present-players  <.go>  frame-finished
	/((.))woot
	blank-zone
	.world' present-players  frame-finished
	\((.))woot
;


\ *************
: copy-chambers	
	chamber chamber' 220 cmove
;


\ ***
: .go
	copy-chambers
	<chamber> @
	chamber'  <chamber> !
	+((.))woot
	blink-go
	blink-go
	blink-go
	5000 slower
	<chamber> !
	-((.))0voice
;
