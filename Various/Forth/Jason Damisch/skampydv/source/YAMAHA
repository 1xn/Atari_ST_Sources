\ ******************************
\ *  YAMAHA SOUND CHIP ACCESS  *
\ ******************************

: yamaha ;

hex


\ ***  YAMAHA-REGISTERS  ***

\ *****  Yamaha 2149 External Registers  *****
ff8800 constant select-register
ff8800 constant read-register
ff8802 constant write-register


\ *****  Yamaha 2149 Internal Regisers  *****
\ Note that in the mode-register ( 7 )  0=on and 1=off

00 constant <0frequency>	01 constant <0step-size>
02 constant <1frequency>	03 constant <1step-size>
04 constant <2frequency>	05 constant <2step-size>
06 constant <noise-pitch>	07 constant <voice-modes>
08 constant <0volume>		09 constant <1volume>
0a constant <2volume>		0b constant <sustain-low>
0c constant <sustain-high>	0d constant <waveform>


\ *****************************
variable <mode-contents>

\ Since the internal register of the sound chip cannot be read, a variable
\ must be used to hold the contents.


\ ***********
: sound-chip!
\ (  byte  register#  --  )
	select-register c!
	write-register c!
;


\ *************
code shift-left
\ (  shift-this-many  shift-me  --  )
	long

	sp )+   d0  move
	sp )+   d1  move
	d1      d0  rol
	d0   sp -)  move
c;


\ **************
code shift-right
\ (  shift-this-many  shift-me  --  )
	long
	
	sp )+    d0  move
	sp )+    d1  move
   	d1       d0  ror
	d0    sp -)  move
c;


\ ******************
: change-voice-modes
	<mode-contents> @
	<voice-modes> sound-chip!
;


\ *******
: -sounds
\ (  --  )
	ff <mode-contents> !
	change-voice-modes
;


\ ******
: +noise
\ (  register#  --  )
	fffffff7 shift-left
	<mode-contents> @ and
	<mode-contents> !
	change-voice-modes	
;


\ ******
: -noise
\ (  register#  --  )
	00000008 shift-left
	<mode-contents> @ or
	<mode-contents> !
	change-voice-modes
;


\ *****
: +tone
\ (  register#  --  )
	fffffffe shift-left
	<mode-contents> @ and
	<mode-contents> !
	change-voice-modes
;


\ *****
: -tone
\ (  register#  --  )
	00000001 shift-left
	<mode-contents> @ or
	<mode-contents> !
	change-voice-modes
;


\ ******
: volume
\ (  quantity  register#  --  )
	<0volume> +   sound-chip!
;


\ *********
: frequency
\ ( quantity register#  --  )
	2* <0frequency> +   sound-chip!
;


\ *********
: step-size
\ ( quantity register#  --  )
	2* <0step-size> +   sound-chip!
;


\ ***********
: noise-pitch
\ (  pitch  --  )
	<noise-pitch> sound-chip!
;


\ ************
: sustain-high
\ (  sustain-high  --  )
	<sustain-high> sound-chip!
;


\ ***********
: sustain-low
\ (  sustain-low  --  )
	<sustain-low> sound-chip!
;


\ ********
: waveform
\ (  wave-form  --  )
	<waveform> sound-chip!
;


\ ****
: tone
\ (  quantity  register#  --  )
	true abort"  TONE  does not work. "

;
