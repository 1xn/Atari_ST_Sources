\ ***************
\ **  DRAWING  **
\ ***************

: drawing ;

\ Words to handle screen buffers and frame management

\ Jason Damisch
\ Forth Games Workshop


hex


\ **************************
\ ***  PALLET RESTORING  ***

\ *************** 
create old-pallet   20 allot 

\ ***************
7 xbios: setcolor {  w.color  w.colornum  --  w.color  }

\ ***************
: save-old-pallet
	0f 0 do
		-1 i setcolor  old-pallet i 2* + w!
	loop
;

save-old-pallet

\ *************
: change-pallet
\ (  pallet-address  --  )
	pallet-change-address !
; 


\ **************
: restore-pallet
	old-pallet change-pallet
;


\ ********************
\ **  CANVAS WORDS  **


\ ********
\ canvases

variable a-canvas
variable b-canvas
variable c-canvas
variable d-canvas

\ ******************
42e constant ceiling

\ **************
variable old-top


\ ****************
: get-first-canvas
	ceiling @ 8000 -  old-top !
;

\ ***********
: restore-top
	old-top @ screen-swap-address !
;

\             ***********
20 k constant screen-size



\ *******************
: initialize-canvases
	old-top @                    d-canvas !
	old-top @ screen-size -      c-canvas !
	old-top @ screen-size 2* -   b-canvas !
	old-top @ screen-size 3 * -  a-canvas !
;


\ **********
: >playfield
\ (  canvas  --  playfield  )
	f00 +
;


\ ***********
create pallet   20 allot


\ *******************************
\ **  VECTORABLE FRAME BUFFERS **


variable  drawee      
variable  prepared    
variable  rastorizee  

alias  refillee d-canvas


\ ************************
: initialize-frame-buffers
	a-canvas @ drawee     !
	b-canvas @ prepared   !
	c-canvas @ rastorizee !
; 


\ ***********
: wipe-drawee
	drawee @    screen-size wipe
;


\ ***************
: playfield-start
 	drawee @ >playfield
;


\ ****************
variable new-frame


\ *************
code vb-routine
long
	-1 l#   new-frame l#)  move
	rts
c;


\ ******************************
0454 constant nvbls
0456 constant vblqueue 

variable vbl-position

variable new-vbl   new-vbl off


\ ********
: +new-vbl
	new-vbl @ false = if
		vblqueue @
		begin
			dup @ 0<> while
			4 +
		repeat
	vbl-position !
	['] vb-routine vbl-position @   ! 
	then
	new-vbl on
;


\ ********
: -new-vbl
	0 vbl-position @   !
	new-vbl off
;


\ ***************
: copy-background
	refillee @ >playfield   playfield-start   b0 blit-screen
;


\ ****
: flip
\ (  address  address  --  )
	2dup >r >r >r >r
	r> @ r> @
	r> ! r> !
;


\ **************
: frame-finished
	
	drawee @ screen-swap-address !
	new-frame @ if
		rastorizee prepared flip
		new-frame off
	then
	drawee prepared flip
;


\ ****  Forward Reference To The Background Space  ****

\ ****************
variable <chamber>


\ *******
: chamber
\ (  --  chamber  )
	<chamber> @
;


\ **************
create |symbols|    file,  world.dat


\ ****************
: build-background
	chamber |symbols| refillee @ >playfield .playfield
;


\ ******
: .world
 	copy-background
;


\ *******
: .world'
	chamber |symbols|   drawee @ >playfield  .playfield
;
