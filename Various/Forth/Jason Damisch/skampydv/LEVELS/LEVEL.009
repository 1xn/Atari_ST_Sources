\ ********************
\ **  END SEQUENCE  **
\ ********************

\ If you have gotten this far, thank you for playing Skampy
\ an original gobble game.

: level ;


medium-res
my-emit-off

create current-chamber

~   OPQ    S    OPPQ  ~
~                     ~
~ O                  Q~
~ O                  S~
~ O                  Q~
~ S     OPPSPSPQ     Q~
~       T      T     S~
~ O     T      T      ~
~       T      T     S~
~ O     T      T      ~
~ OPPSPPP      PSPPPPQ~

current-chamber <chamber> !

2  #fruit !
10  #foes !
0  #rocks !

s h s h s h s h 
08 :path 0path

s h s h s h s h 
08 :path 1path

s h s h s h s h 
08 :path 2path

s h s h s h s h 
08 :path 3path

e h e h e h e h
08 :path 4path

e h e h e h e h
08 :path 5path

w h w h w h w h
08 :path 6path

w h w h w h w h
08 :path 7path

se h se h se h se h
08 :path 8path

sw h sw h sw h sw h
08 :path 9path



n h n h n h n h 
08 :path 0path'

n h n h n h n h 
08 :path 1path'

n h n h n h n h 
08 :path 2path'

n h n h n h n h 
08 :path 3path'

w h w h w h w h
08 :path 4path'

w h w h w h w h
08 :path 5path'

e h e h e h e h
08 :path 6path'

e h e h e h e h
08 :path 7path'

nw h nw h nw h nw h
08 :path 8path'

ne h ne h ne h ne h
08 :path 9path'

\ *********
: use-goon'
	0 0   ['] .skull   ['] skull  3  |goon|  still still 
;


\ **********
: reset-foes
	016 016  use-goon' 8path reset-path   for-foes 0 element!
	288 016  use-goon' 9path reset-path   for-foes 1 element!
	016 096  use-ghost 4path reset-path   for-foes 2 element!
	016 124  use-ghost 5path reset-path   for-foes 3 element!
	288 112  use-ghost 6path reset-path   for-foes 4 element!
	288 144  use-ghost 7path reset-path   for-foes 5 element!
	104 016  use-skull 0path reset-path   for-foes 6 element!
	128 016  use-skull 1path reset-path   for-foes 7 element!
	164 016  use-skull 2path reset-path   for-foes 8 element!
	200 016  use-skull 3path reset-path   for-foes 9 element!

;

\ **********
: reset-foes'
	076 076  use-goon' 8path' reset-path   for-foes 0 element!
	228 076  use-goon' 9path' reset-path   for-foes 1 element!
	076 096  use-ghost 4path' reset-path   for-foes 2 element!
	076 124  use-ghost 5path' reset-path   for-foes 3 element!
	228 112  use-ghost 6path' reset-path   for-foes 4 element!
	228 144  use-ghost 7path' reset-path   for-foes 5 element!
	104 064  use-skull 0path' reset-path   for-foes 6 element!
	128 064  use-skull 1path' reset-path   for-foes 7 element!
	164 064  use-skull 2path' reset-path   for-foes 8 element!
	200 064  use-skull 3path' reset-path   for-foes 9 element!

;


\ ************
: reset-scampy
	152   120   use-scampy   for-scampy 0 element!
;


\ *********
: final-lap
        frame off
 	reset-foes   reset-scampy   reset-sounds
	game-mode   .scoreboard
	build-background   wipe-foreground
	030 0 do
		.world   .foes   .scampy
		sound-task
		frame-finished
		+frame
	loop
	present-players frame-finished
	present-players frame-finished
	80000 slower
;


\ **********
: going-home
 	reset-foes'
	030 0 do
		.world   .foes   .scampy
		sound-task
		frame-finished
		+frame
	loop
	.world present-scampy frame-finished
	.world present-scampy frame-finished
	80000 slower
;


\ ********
: ((.))ert
\ (  slowness --  )
	0 +noise  0 +tone
	15 0 volume   200 0 frequency   31 noise-pitch   10 0 step-size
	55 0 do
		200 i + 0 frequency
		15  i 5 / -   0 volume
		dup slower
	loop
	drop
	0 -noise   0 -tone   0 0 volume
	40000 slower
;


\ ******
: no-way
	06 #row !  03 #collumn !  ." NO WAY!"   2000 ((.))ert
	08 #row !  32 #collumn !  ." RATS!"     2300 ((.))ert
	10 #row !  04 #collumn !  ." HUH?"      2500 ((.))ert
	11 #row !  33 #collumn !  ." !?!?"	2700 ((.))ert
	04 #row !  09 #collumn !
	." I "            1000 ((.))ert
	." AM "           1000 ((.))ert
	." CALLING "      1000 ((.))ert
	." MY "           1000 ((.))ert
	." AGENCY!!! "    2000 ((.))ert
	80000 slower
;


\ *********
: .end-text
	0 #row !   0 #collumn !     
	."   AND AFTER A LONG ORDEAL THE MONSTERS  "	my-cr
	."    DECIDED THEY WERE DONE WITH TRYING   "	my-cr
	." TO CLOBBER / SKAMPY / KNOWING THEY WERE "	my-cr
	." NOT CLEVER ENOUGH TO MAKE A LIVING OF IT"	my-cr
	." AND SO SKAMPY WAS NO LONGER PURSUED AND "	my-cr
	." DID WANDER THE HALLS OF ZOWGO PEACEFULLY"	my-cr
	." / / / / / / / / / / / / / / / / / / / / "    
;


\ *****
: glow
	copy-pallet
	0   <new-pallet>   7 2*   +  !
	0   <new-pallet>   9 2*   +  w!
	0 +tone   1 +tone
	06 0 step-size  06 1 step-size
	14 0 volume   14 1 volume
	03 0 do
		06 i - 0 step-size   06 i - 1 step-size
		4095 0 do
			i <new-pallet> 7 2* +  w!
			i <new-pallet> 8 2* +  w!
			i <new-pallet> 9 2* +  w!
			<new-pallet> change-pallet
			i 256 mod  0 frequency
			256 i 256 mod -  1 frequency		
		loop
	loop
	scampy-pallet
	0 -tone   1 -tone
;


\ **********
: clear-line
\ (  #row  --  )
	#row !   0 #collumn !  ."                                         "
;

80 time !

\ ***************
: congratulations
	introduce
	final-lap
	no-way
	going-home
	0 clear-line
	glow
	.end-text
	2400000 slower
	0 clear-line   1 clear-line   2 clear-line  3 clear-line  
	4 clear-line   5 clear-line
	.scoreboard
	2 #row !   6 #collumn !   ." THANK YOU FOR PLAYING SCAMPY "
	4 #row !   2 #collumn !   ." PRESS FIRE BUTTON TO RESTART PROGRAM"
	begin  interrogate  button1  until
	\_save-score  attract-mode
;

congratulations
