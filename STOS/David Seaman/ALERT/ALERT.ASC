55000 rem ALERT - GEM-like alert box routine
55010 rem By David Preston for ICTARI
55020  : 
55030 rem You'll need to DIM ALERT_MESS$(3),ALERT_BUTT$(2) before use.
55040 rem Controls - ALERT_DBUT=0/1/2/3 : Default button (0 for none)
55050 rem          - ALERT_B0RDER=1-15 : STOS borders
55060 rem          - ALERT_CENTRE=true/false : Centre text if true
55070 rem ALERT_REPLY contains response on return - 1/2/3 is button selected
55080  : 
55090 ALERT_CURRW=windon
55100 if ALERT_B0RDER=0 then ALERT_B0RDER=1
55110 ALERT_MESS_LEN=0
55120 ALERT_LINE_COUNT=0
55130 for ALERT_LOOP=0 to 3
55140  : if len(ALERT_MESS$(ALERT_LOOP))>ALERT_MESS_LEN then ALERT_MESS_LEN=len(ALERT_MESS$(ALERT_LOOP))
55150  : if ALERT_MESS$(ALERT_LOOP)>"" then inc ALERT_LINE_COUNT
55160 next ALERT_LOOP
55170 ALERT_MESS_LEN=ALERT_MESS_LEN+2
55180 ALERT_BUTT0N_LEN=0
55190 ALERT_NUMBUT=0
55200 for ALERT_LOOP=0 to 2
55210  : ALERT_BUTT0N_LEN=ALERT_BUTT0N_LEN+len(ALERT_BUTT$(ALERT_LOOP))
55220  : if ALERT_BUTT$(ALERT_LOOP)>"" then inc ALERT_NUMBUT
55230 next ALERT_LOOP
55240 ALERT_BUTT0N_LEN=ALERT_BUTT0N_LEN+(2*ALERT_NUMBUT)+2
55250 if ALERT_BUTT0N_LEN>ALERT_MESS_LEN then ALERT_MESS_LEN=ALERT_BUTT0N_LEN
55260 windopen 13,40/divx-ALERT_MESS_LEN/2-1,12-(ALERT_LINE_COUNT+7)/2,ALERT_MESS_LEN+2,ALERT_LINE_COUNT+7,ALERT_B0RDER
55270 curs off 
55280 for ALERT_LOOP=0 to ALERT_LINE_COUNT-1
55290  : locate 1,ALERT_LOOP+1
55300  : if ALERT_CENTRE then centre ALERT_MESS$(ALERT_LOOP) else print ALERT_MESS$(ALERT_LOOP)
55310 next ALERT_LOOP
55320 ALERT_BUTT0N_START=ALERT_MESS_LEN/2-ALERT_BUTT0N_LEN/2+1
55330 ALERT_SH0RTCUT$=""
55340 for ALERT_LOOP=0 to ALERT_NUMBUT-1
55350  : locate ALERT_BUTT0N_START,ALERT_LINE_COUNT+2
55360  : ALERT_B0RDER=1 : if ALERT_LOOP=ALERT_DBUT-1 then ALERT_B0RDER=2
55370  : square len(ALERT_BUTT$(ALERT_LOOP))+2,3,ALERT_B0RDER
55380  : set zone ALERT_LOOP+120,xgraphic(xcurs),ygraphic(ycurs) to xgraphic(xcurs+len(ALERT_BUTT$(ALERT_LOOP))),ygraphic(ycurs+1)
55390  : ALERT_SH0RTCUT_FLAG=false
55400  : ALERT_TEMP$=ALERT_BUTT$(ALERT_LOOP)
55410  : for ALERT_LOOP2=1 to len(ALERT_TEMP$)
55420  :  : ALERT_CH$=mid$(ALERT_TEMP$,ALERT_LOOP2,1)
55430  :  : if ALERT_CH$=" " then print ALERT_CH$; : goto 55450
55440  :  : if instr(ALERT_SH0RTCUT$,upper$(ALERT_CH$))=0 and not(ALERT_SH0RTCUT_FLAG) then ALERT_SH0RTCUT$=ALERT_SH0RTCUT$+upper$(ALERT_CH$) : ALERT_SH0RTCUT_FLAG=true : under on : print ALERT_CH$; : under off : else print ALERT_CH$;
55450  : next ALERT_LOOP2
55460  : if not(ALERT_SH0RTCUT_FLAG) then ALERT_SH0RTCUT$=ALERT_SH0RTCUT$+" "
55470  : ALERT_BUTT0N_START=ALERT_BUTT0N_START+len(ALERT_BUTT$(ALERT_LOOP))+2
55480 next ALERT_LOOP
55490 repeat 
55500  : ALERT_ZONE=zone(0) : ALERT_M=mouse key : ALERT_K$=inkey$
55510  : if ALERT_M>0 and ALERT_ZONE<120 then bell : wait 10
55520  : if ALERT_DBUT>0 and (scancode=28 or scancode=114) then ALERT_M=1 : ALERT_ZONE=119+ALERT_DBUT
55530  : if scancode>=59 and scancode<=61 and scancode-58<=ALERT_NUMBUT then ALERT_M=1 : ALERT_ZONE=119+scancode-58
55540  : ALERT_SH0RTCUT=instr(ALERT_SH0RTCUT$,upper$(ALERT_K$))
55550  : if ALERT_SH0RTCUT>0 then ALERT_M=1 : ALERT_ZONE=119+ALERT_SH0RTCUT
55560 until (ALERT_M=1 and ALERT_ZONE>=120)
55570 ALERT_REPLY=ALERT_ZONE-119
55580 for ALERT_LOOP=120 to 122
55590  : reset zone ALERT_LOOP
55600 next ALERT_LOOP
55610 qwindow ALERT_CURRW
55620 windel 13
55630 return 
