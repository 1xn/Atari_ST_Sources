10 key off : curs off : hide 
20 if mode=0 then mode 1
30 dim DCK(51)
40 cls : centre "Shuffling demonstration - STOS Basic"
50 print : centre "By David Preston for ICTARI"
60 print : print : print 
70 rem
80 gosub 310 : rem Reset deck
90 rem
100 T#=timer
110 gosub 370 : rem Repeat...until method
120 print "Loop...until method took";(timer-T#)/50.0;" seconds"
130 print : print 
140 rem
150 gosub 310 : rem Reset deck
160 rem
170 T#=timer
180 gosub 480 : rem Swapping method
190 print "Swapping method took";(timer-T#)/50.0;" seconds"
200 print : print 
210 rem
220 print : print : print 
230 centre "Press <Esc> to quit or any other key to repeat"
240 repeat 
250 K$=inkey$
260 until K$<>""
270 if asc(K$)<>27 then 40 : rem Restart
280 default : end 
290 rem
300 rem Reset dck
310 for CARD=0 to 51
320  : DCK(CARD)=CARD
330 next CARD
340 return 
350 rem
360 rem Repeat...until method
370 for CARD=0 to 51
380  : repeat 
390  :  : THIS_CARD=rnd(51)
400  : until DCK(THIS_CARD)<>-1
410  : DCK(THIS_CARD)=-1
420  : print THIS_CARD;
430 next CARD
440 print : print 
450 return 
460 rem
470 rem Swapping method
480 for CARD=0 to 51
490  : OTHER_CARD=rnd(51)
500  : swap DCK(CARD),DCK(OTHER_CARD)
510 next CARD
520 for CARD=0 to 51
530  : print DCK(CARD);
540 next CARD
550 print : print 
560 return 
