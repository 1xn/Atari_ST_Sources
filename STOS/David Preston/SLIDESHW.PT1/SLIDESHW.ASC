10 rem  ____________________________
20 rem |                            |
30 rem |  STOS Tutorial for ICTARI  |
40 rem |     Slideshow program      |
50 rem |            #1              |
60 rem |                            |
70 rem |  David Preston  - Oct '96  |
80 rem |____________________________|
90 rem
100 key off : curs off : show on : hide 
110 if mode=2 then locate 0,10 : centre "Colour systems only - Press a key..." : wait key : default : end 
120  : 
130 mode 0
140 reserve as screen 5
150 TEST$="PI1NEO"
160  : 
170 show : palette $666,$0 : DUMMY$=file select$("*.*","Select path for picture files.  (Enter 'Q' in filename to Quit)") : hide 
180 if DUMMY$<>"" and left$(DUMMY$,1)="Q" then default : end 
190  : 
200 rem /// Main loop ///
210 EXIT_FLAG=false
220 repeat 
230  : F$=dir first$("*.*",32)
240  : if F$="" then cls : locate 0,10 : centre "No pictures! Press a key..." : wait key : EXIT_FLAG=true : goto 370
250  : repeat 
260  :  : gosub 410
270  :  : F$=left$(F$,12)-" "
280  :  : EXT$=right$(F$,4)-"."
290  :  : gosub 410
300  :  : L0ADED=false
310  :  : gosub 460
320  :  : gosub 410
330  :  : if L0ADED then gosub 560
340  :  : gosub 410
350  :  : F$=dir next$
360  : until F$=""
370 until EXIT_FLAG
380 goto 170
390  : 
400 rem /// Check for ESC key ///
410 K$=inkey$ : SC=scancode : AXI=asc(K$)
420 if AXI=27 then EXIT_FLAG=true
430 return 
440  : 
450 rem /// Test file extender and branch to load routine ///
460 FTYPE=(instr(TEST$,EXT$)+2)/3
470 on FTYPE gosub 510,510
480 return 
490  : 
500 rem /// Load Degas (*.PI1) or Neochrome (*.NEO) ///
510 load F$,5
520 L0ADED=true
530 return 
540  : 
550 rem /// Choose FX ///
560 FX=1
570 on FX gosub 610
580 return 
590  : 
600 rem /// FX ///
610 fade 12 to 5
620 appear 5
630 return 
