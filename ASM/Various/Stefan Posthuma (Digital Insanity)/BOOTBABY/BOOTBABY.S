; BOOTBABY.S
;
; INSTALL COLOR RESTORE, RES FLIPPER AND MOUSE SPEEDER
; AT BOOT TIME.

		OPT	O+
BEGIN		
		
START		PEA	DO_IT(PC)
		MOVE.W	#38,-(SP)
		TRAP	#14
		ADDQ.L	#6,SP

BREAK		CLR.W	-(SP)
		MOVE.L	#$100+(LASTBYTE-BEGIN),-(SP)
		MOVE.W	#$31,-(SP)
		TRAP	#1
		
DO_IT		PEA	T_INSTALL(PC)
		MOVE.W	#9,-(SP)
		TRAP	#1
		ADDQ.L	#6,SP

		MOVE.W	#4,-(SP)
		TRAP	#14
		ADDQ.L	#2,SP
		CMP.W	#2,D0
		BEQ.S	NOCOLOR

		LEA	PALETTE(PC),A0
		MOVE.W	#$8240,A1
		MOVEQ	#15,D0
.1		MOVE.L	(A1)+,(A0)+
		DBRA	D0,.1

		MOVE.L	$502.W,A0
		CMP.L	#$3F3CFFFF,(A0)
		BNE.S	.OK
		PEA	T_ALREADY(PC)
		BRA.S	.PRINT
.OK		MOVE.L	A0,OLDVEC
		MOVE.L	#RESIDENT,$502.W
		PEA	T_COLOR(PC)
.PRINT		MOVE.W	#9,-(SP)
		TRAP	#1
		ADDQ.L	#6,SP

* INSTALL MOUSE SPEEDER
NOCOLOR		MOVE.W	#34,-(SP)
		TRAP	#14
		ADDQ.L	#2,SP
		MOVE.L	D0,A0
		MOVE.L	16(A0),A1
		CMP.L	#$48E7C080,(A1)+
		BNE.S	.OK
		CMP.L	#$52881010,(A1)
		BNE.S	.OK
		PEA	T_MALREADY(PC)
		BRA.S	.PRINT
.OK		MOVE.L	16(A0),OLDMOUSE
		MOVE.L	#MSPEED,16(A0)
		PEA	T_MOUSE(PC)
.PRINT		MOVE.W	#9,-(SP)
		TRAP	#1
		ADDQ.L	#6,SP
		RTS

* MOUSE SPEEDER
* RIPPED FROM THE ACCESSORY BY CH. SIEBERT
* IT FUMBLES WITH THE MOUSE PACKAGE I BELIEVE
MSPEED		MOVEM.L	A0/D0/D1,-(SP)
		ADDQ.L	#1,A0
		MOVE.B	(A0),D0
		BSR.S	MUMBO
		MOVE.B	(A0),D0
		BSR.S	MUMBO
		MOVEM.L	(SP)+,A0/D0/D1
		MOVE.L	OLDMOUSE(PC),-(SP)
		RTS
MUMBO		MOVE.B	D0,D1
		BPL.S	JUMBO
		NEG.B	D1
JUMBO		CMP.B	#2,D1
		BLE.S	FOO
		MULS	D1,D0
		ASR.B	#1,D0
FOO		MOVE.B	D0,(A0)+
		RTS

RESIDENT	MOVE.W	#-1,-(SP)
		MOVE.W	#11,-(SP)
		TRAP	#13
		ADDQ.L	#4,SP
		BTST	#1,D0
		BNE.S	COLORS
		BTST	#0,D0
		BNE.S	RES
		MOVE.L	OLDVEC(PC),A0
		JMP	(A0)
		
COLORS
; RESET THE SCREEN COLORS
		MOVEM.L	A0/A1/D0,-(SP)
		LEA	PALETTE(PC),A0
		MOVE.W	#$8240,A1
		MOVEQ	#15,D0
.1		MOVE.L	(A0)+,(A1)+
		DBRA	D0,.1
		MOVEM.L	(SP)+,A0/A1/D0
		RTS

RES
; FLIP RESOLUTION
		MOVEM.L	D0-D7/A0-A6,-(SP)
		MOVE.B	$FFFF8260.W,D7
		BTST	#0,D7
		BNE.S	.MEDIUM
.LOW		BSET	#0,$FFFF8260.W	; TO MEDIUM
		MOVE.W	#37,-(SP)	; AWAIT VBL
		TRAP	#14
		ADDQ.L	#2,SP
		MOVE.B	D7,$FFFF8260.W	; BACK TO LOW
		BRA.S	.EXIT		; GOODBYE

.MEDIUM		AND.B	#%11111100,D7	; TO LOW
		MOVE.B	D7,$FFFF8260.W
		MOVE.W	#37,-(SP)	; VBL
		TRAP	#14
		ADDQ.L	#2,SP	
		BSET	#0,$FFFF8260.W	; TO MEDIUM

.EXIT		MOVE.W	#37,-(SP)	; VBL
		TRAP	#14
		ADDQ.L	#2,SP	
		MOVEM.L	(SP)+,D0-D7/A0-A6
		RTS

		DATA

T_INSTALL	DC.B	"DIGITAL INSANITY'S BOOT BABY AT WORK",10,13,0
T_COLOR		DC.B	"COLOR RESTORE INSTALLED",10,13
		DC.B	"RESOLUTION FLIPPER INSTALLED",10,13,0
T_MOUSE		DC.B	"MOUSE ACCELERATOR INSTALLED",10,13,0
T_MALREADY	DC.B	"YEAH, MOUSE ACCELERATOR ALREADY INSTALLED",10,13,0
T_ALREADY	DC.B	"YEAH, COLOR RESTORE/RES FLIPPER ALREADY INSTALLED",10,13,0

		BSS

OLDVEC		DS.L	1
OLDMOUSE	DS.L	1
PALETTE		DS.W	32
		
		EVEN
LASTBYTE	