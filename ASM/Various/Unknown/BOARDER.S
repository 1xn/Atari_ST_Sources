	CLR.L	-(SP)			supervisormode
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	MOVE.L	D0,SAVESP

	MOVE.L	$118.W,OLD_KEY		st„ng av musen!
	MOVE.L	#K_BOARD,$118.W

	* Initiera VBL och HBL!
	MOVE.B	$FFFFFA09.W,OLD_IEB
	MOVE.B	$FFFFFA07.W,OLD_IEA
	MOVE.B	$FFFFFA13.W,OLD_IMA
	MOVE.B	$FFFFFA1B.W,OLD_MSK	
	MOVE.L	$70.W,OLD_VC2	VBL VECTOR
	MOVE.L	$120.W,OLD_VC3	HBL VECTOR
	MOVE.W	SR,-(SP)		st„ng av MPF
	MOVE.W	#$2700,SR
	MOVE.B	#0,$FFFFFA1B.W		st„ng av vbl
	AND.B	#$DF,$FFFFFA09.W	st„ng av 200 Hz interrupt
	AND.B	#$FE,$FFFFFA07.W	st„ng av Hbl interrupt
	MOVE.L	#VBL,$70.W
	MOVE.L	#ST_LINE,$120.W
	OR.B	#1,$FFFFFA07.W	†terst„ll Hbl interrupt
	OR.B	#1,$FFFFFA13.W
	MOVE.W	(SP)+,SR

DONT_LEAVE_THIS_NICE_DEMO
	CMP.B	#$39,$FFFFFC02.W
	BNE.B	DONT_LEAVE_THIS_NICE_DEMO

	* OK, LEAVE IT! st„ng av hbl!
	MOVE.W	SR,-(SP)
	MOVE.W	#$2700,SR
	MOVE.B	OLD_MSK,$FFFFFA1B.W
	MOVE.L	OLD_VC2,$70.W
	MOVE.L	OLD_VC3,$120.W
	MOVE.L	OLD_KEY,$118.W  	s„tt p† mus
	MOVE.B	OLD_IEB,$FFFFFA09.W
	MOVE.B	OLD_IEA,$FFFFFA07.W
	MOVE.B	OLD_IMA,$FFFFFA13.W

	MOVE.W	(SP)+,SR

	MOVE.L	SAVESP,-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP

	CLR.W	-(SP)
	TRAP	#1

VBL	CLR.B	$FFFFFA1B.W
	MOVE.B	#199,$FFFFFA21.W	rad 199 byter vi 60/50Hz
	MOVE.B	#8,$FFFFFA1B.W
	RTE

K_BOARD	MOVE.W	#$2500,SR
	BCLR	#6,$FFFFFA11.W
	RTE

	* RUTIN F™R ATT TA BORT NEDRE BOARDER! *
ST_LINE	CLR.B	$FFFFFA1B.W		STOP TIMER
	MOVEM.L	D0/A0,-(A7)	
	LEA	$FFFFFA21.W,A0
	MOVE.B	#200,(A0)
	MOVE.B	#8,$FFFFFA1B.W		START TIMER
	MOVE.B	(A0),D0
WAIT	CMP.B	(A0),D0			YES, WE LOSE A WHOLE LINE HERE...
	BEQ.B	WAIT
	BCLR	#1,$FFFF820A.W		INTO 60 HZ
	MOVE.W	#2,D0			WAIT A WHILE...
LOOP 	NOP
	DBRA	D0,LOOP
	BSET	#1,$FFFF820A.W		INTO 50 HZ
	MOVEM.L	(A7)+,D0/A0
	BCLR	#0,$FFFFFA0F.W		ACKNOWLEDGE
	RTE

	SECTION	BSS
SAVESP	DS.L	1
OLD_KEY	DS.L	1
OLD_VC2	DS.L	1
OLD_VC3	DS.L	1
OLD_IEA	DS.B	1
OLD_IEB	DS.B	1
OLD_MSK	DS.B	1
OLD_IMA	DS.B	1
