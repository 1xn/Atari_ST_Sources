*********************************************************
* GRAPHICS SUBROUTINES							    	*
*														*
* Author: Nick Bates (c) 1994		 	    			*
* Last Modified: 10/07/94								*
********************************************************

*****************************************************
* GO_SUPER											*
*													*
* Goto supervisor mode, 							*
* - Saves old stack to old_stk						*
*****************************************************
go_super
	pea		0			
	move.w	#$20,-(sp)		
	trap	#1
	addq.l	#6,sp
	move.l	d0,old_stk		save stack
	rts

*****************************************************
* SAVE_PALETTE										*
*													*
* Saves pallete to:  old_pal						*
*****************************************************
save_palette
	movem.l	$fff8240,d0-d7
	movem.l	d0-d7,old_pal		
	rts

*****************************************************
* GET_SCREEN_ADDR									*
*													*
* Get logical screen and generates physical			*
* - Saves to log, phys								*
*****************************************************

get_screen_addr

	move.w	#2,-(sp)		
	trap	#14
	addq.l	#2,sp
	move.l	d0,phys

	move.l	d0,old_scrn
	move.l	#screen,d0		PHYSICAL SCREEN
	addi.l	#256,d0			256 BYTE BOUNDARY
	andi.l	#$ffffff00,d0
	move.l	d0,log
	rts

*****************************************************
* SAVE_VBI											*
*													*
* Saves vbi to: old_vbi								*
*****************************************************

save_vbi
	move.l	$70,old_vbi		STORE OLD VBI
	move.l	#vbi,$70		NEW VBI
	rts	

*****************************************************
* GO_LOW											*
* 													*
* Saves current resolution to: old_res				*
* and change to low resolution.						*
*****************************************************

go_low
	move.b	$ff8260,old_res		store resolution
	move.b	#0,$ff8260		low resolution
	rts

*****************************************************
* GO_USER	  										*
*													*
* Restore stack from: old_stk						*
* Goes to User Mode									*
*****************************************************
go_user
	move.l	old_stk,-(sp)		restore stack
	move.w	#$20,-(sp)		GEMDOS - USER MODE
	trap	#1
	addq.l	#6,sp			user mode
	rts

*********************************************************
* CLEAR_SCREEN											*
* 														*
* Clear the logical screen								*
*********************************************************
clear_screen
		move.l	log,a0
		lea	32000(a0),a0
		movem.l	a0-a6/d0-d7,-(sp)
		clr.l	d1
		clr.l	d2
		clr.l	d3
		clr.l	d4
		clr.l	d5
		clr.l	d6
		clr.l	d7
		move.l	d1,a1
		move.l	d1,a2
		move.l	d1,a3
		move.l	d1,a4
		move.l	d1,a5
		move.l	d1,a6

		move.w	#614,d0
.loop	
		movem.l	d1-d7/a1-a6,-(a0)
		dbra	d0,.loop
		movem.l	d1-d5,-(a0)

		movem.l	(sp)+,a0-a6/d0-d7
		rts

*********************************************************
* WAIT_VBI												*
* 														*
* Wait for vbi interupt									*
*********************************************************
wait_vbi
		tst.b	vbi_done
		beq.s	wait_vbi
		rts

vbi		st		vbi_done
		rte


*********************************************************
* FLIP_SCREEN											*
* 														*
* Flips the logical to Physical screens					*
* Requires: log, phys									*
*********************************************************
flip_screen
		movem.l	d0-d1,-(sp)

		move.l	log,d0
		move.l	phys,d1
		move.l	d1,log
		move.l	d0,phys

		lsr.w	#8,d0
		move.l	d0,$ffff8200.w

		sf		vbi_done

		movem.l	(sp)+,d0-d1
		rts

*****************************************************
* RESTORE_SCREEN									*
*													*
* Restores vbi, resolution, logical and physical    *
* screens.											*
* Parameters: old_vbi, old_res, old_scrn			*
*****************************************************
restore_screen 	
	move.l	old_vbi,$70
	move.w  old_res,-(sp)       restore resolution    
    move.l  old_scrn,-(sp)		restore physical
    move.l	old_scrn,-(sp)		restore logical
    move    #5,-(sp)			XBIOS - SET SCREEN
    trap    #14
    lea     12(sp),sp		                   
	rts

*****************************************************
* RESTORE_PALETTE									*
*													*
* Restore old palette from: old_pal					*
*****************************************************
restore_palette
    movem.l old_pal,d0-d7		
	movem.l	d0-d7,$fff8240		restore pallette
	rts
