*************************************
*   CRACKED BY B.I.C & THE BANDIT   *
*              22/4/90              *
*        AT 1:50 AM ,SUNDAY         *
*************************************

	MOVE.L	#COLPAL,-(A7)		;SET COLOUR PALETTE FOR PIC
	MOVE.W	#6,-(A7)
	TRAP	#14
	ADDQ.L	#6,A7

	MOVE.W	#4,-(A7)		;GET SCREEN RES.
	TRAP	#14
	ADDQ.L	#2,A7
	MOVE.W	D0,SAVERES

	MOVE.W	#2,-(A7)		;GET SCREEN PHYS BASE
	TRAP	#14
	ADDQ.L	#2,A7
	MOVE.L	D0,SAVEBASE

	CLR.W	-(A7)			;SET SCREEN PARAMS
	MOVE.L	SAVEBASE,-(A7)
	MOVE.L	SAVEBASE,-(A7)
	MOVE.W	#5,-(A7)
	TRAP	#14
	ADDA.L	#$C,A7

	PEA	MOUSEOFF		;WRITE STRING TO INT KBRD
	MOVE.W	#0,-(A7)
	MOVE.W	#$19,-(A7)
	TRAP	#14
	ADDQ.L	#8,A7

	CLR.L	-(A7)			;SUPER MODE
	MOVE.W	#$20,-(A7)
	TRAP	#1
	ADDQ.L	#6,A7
	MOVE.L	D0,SAVESTK

	MOVEA.L	SAVEBASE,A0		;CLEAR SCREEN
	MOVE.L	#$1FFF,D0
LOOP01	CLR.L	(A0)+
	DBF	D0,LOOP01


	ANDI.B	#$FC,$484		;TURN BELL & KEY REPT OFF
	
	BSR	SET_INT

WAITFORKEY	MOVE.W	#$FF,-(A7)
	MOVE.W	#6,-(A7)
	TRAP	#1
	ADDQ.L	#4,A7
	TST.W	D0
	BNE	QUIT
	BRA.S	WAITFORKEY
	
QUIT	BSR	INT_OFF

	MOVE.L	#MOUSEON,-(A7)		;WRITE STRING TO INT KBRD
	MOVE.W	#0,-(A7)
	MOVE.W	#$19,-(A7)
	TRAP	#14
	ADDQ.L	#8,A7

	MOVE.W	SAVERES,-(A7)		;SET SCREEN PARAMS BACK
	MOVE.L	SAVEBASE,-(A7)
	MOVE.L	SAVEBASE,-(A7)
	MOVE.W	#5,-(A7)
	TRAP	#14
	ADDA.L	#$C,A7

	ORI.B	#3,$484			;TURN BELL & KEY REPT ON

	MOVE.L	SAVESTK,-(A7)		;BACK TO USER MODE
	MOVE.W	#$20,-(A7)
	TRAP	#1
	ADDA.L	#6,A7

	CLR.W	-(A7)			;QUIT
	TRAP	#1

MOUSEOFF	DC.B	$12,0
MOUSEON		DC.B	8,0
SAVEBASE	DS.L	1
SAVESTK	DS.L	1
SET_INT	MOVEA.L	#SAVEINTREG,A1
	MOVE.B	$FFFA07,(A1)+
	MOVE.B	$FFFA09,(A1)+
	MOVE.B	$FFFA0B,(A1)+
	MOVE.B	$FFFA0D,(A1)+
	MOVE.B	$FFFA0F,(A1)+
	MOVE.B	$FFFA11,(A1)+
	MOVE.B	$FFFA13,(A1)+
	MOVE.B	$FFFA15,(A1)+
	MOVE.B	$FFFA1B,(A1)+
	MOVE.B	$FFFA21,(A1)+
	MOVE.L	$70,(A1)+
	MOVE.L	$120,(A1)+
	MOVE.L	$70,SAVEVBL
	MOVE.W	$FF8240,(A1)+
	ANDI.B	#$DF,$FFFA09		;TIMER C OFF
	ANDI.B	#$FE,$FFFA07		;TIMER B OFF
	MOVE.L	#HBL,$120		;SET NEW HBL
	MOVE.L	#VBL,$70		;SET NEW VBL
	ORI.B	#1,$FFFA07		;TIMER B ON
	ORI.B	#1,$FFFA13		;MASK B ON
	RTS
INT_OFF	MOVE	SR,-(A7)
	MOVE.W	#$2700,SR
	MOVEA.L	#SAVEINTREG,A1
	MOVE.B	(A1)+,$FFFA07
	MOVE.B	(A1)+,$FFFA09
	MOVE.B	(A1)+,$FFFA0B
	MOVE.B	(A1)+,$FFFA0D
	MOVE.B	(A1)+,$FFFA0F
	MOVE.B	(A1)+,$FFFA11
	MOVE.B	(A1)+,$FFFA13
	MOVE.B	(A1)+,$FFFA15
	MOVE.B	(A1)+,$FFFA1B
	MOVE.B	(A1)+,$FFFA21
	MOVE.L	(A1)+,$70
	MOVE.L	(A1)+,$120
	MOVE.W	(A1)+,$FF8240
	MOVE.W	(A7)+,SR
	RTS

VBL	CLR.B	$FFFA1B			;STOP TIMER B
	MOVE.B	#1,$FFFA21		;TIMER B DATA
	MOVE.B	#8,$FFFA1B		;EVENT COUNT MODE , TIMER B
	MOVEM.L	D0-D3/A0-A2,-(A7)

	MOVEA.L	#RASTD3,A1

	REPT 	97
	CLR.L	(A1)+			;CLEAR RASTD3
	ENDR
	
;FRONT RASTER 1 (WHITE)

	MOVEA.L	#RASTD6,A0		;POSITION
	MOVEA.L	#RASTDA,A1		;COLOURS
	MOVEA.L	#RASTD3,A2		;SCREEN PIC
	ADDA.W	RASTC7,A0		;ADD COUNTER TO POS.
	MOVE.W	(A0),D0			;MOVE TO D0
	MOVE.W	#$17C,D2
	SUB.W	D0,D2
	ADDA.W	D2,A2

	REPT	7
	MOVE.L	(A1)+,(A2)+
	ENDR
	
	MOVEA.L	#RASTD6,A0		;FRONT RASTER 2 (RED)
	MOVEA.L	#RASTD9,A1
	MOVEA.L	#RASTD3,A2
	ADDA.W	RASTC6,A0
	MOVE.W	(A0),D0
	MOVE.W	#$17C,D2
	SUB.W	D0,D2
	ADDA.W	D2,A2

	REPT	7
	MOVE.L	(A1)+,(A2)+
	ENDR

	MOVEA.L	#RASTD6,A0		;FRONT RASTER 3 (GREEN)
	MOVEA.L	#RASTD8,A1
	MOVEA.L	#RASTD3,A2
	ADDA.W	RASTC5,A0
	MOVE.W	(A0),D0
	MOVE.W	#$17C,D2
	SUB.W	D0,D2
	ADDA.W	D2,A2

	REPT	7
	MOVE.L	(A1)+,(A2)+
	ENDR
	
	MOVEA.L	#RASTD6,A0		;FRONT RASTER 4 (BLUE)
	MOVEA.L	#RASTD7,A1
	MOVEA.L	#RASTD3,A2
	ADDA.W	RASTC4,A0
	MOVE.W	(A0),D0
	MOVE.W	#$17C,D2
	SUB.W	D0,D2
	ADDA.W	D2,A2

	REPT	7
	MOVE.L	(A1)+,(A2)+
	ENDR
	
	ADDQ.W	#2,RASTC4		;BLUE RASTER
	ADDQ.W	#2,RASTC5		;GREEN RASTER
	ADDQ.W	#2,RASTC6		;RED RASTER
	ADDQ.W	#2,RASTC7		;WHITE RASTER

	MOVEA.L	#RASTC4,A0
	MOVEQ	#3,D0

LOOP20	CMPI.W	#$168,(A0)
	BNE	RASTERS2B
	CLR.W	(A0)

RASTERS2B	ADDQ.W	#2,A0
	DBF	D0,LOOP20

SYS_HBL	CLR.W	$FF8240
	MOVEM.L	(A7)+,D0-D3/A0-A2
	DC.W	$4EF9			;JMP 0
SAVEVBL	DS.L	1
	RTE

HBL	CLR.B	$FFFA1B
	MOVEM.L	D0-D3/A3-A4,-(A7)
	MOVEA.L	#$FFFA21,A4
	MOVE.B	#$F0,(A4)
	MOVE.B	#8,$FFFA1B
	MOVE.W	#$C6,D0
	MOVEA.L	#RASTD3,A3
LOOP23	MOVE.B	(A4),D1
HBLOOP	CMP.B	(A4),D1
	BEQ.S	HBLOOP
	MULU	D3,D3
	MOVE.W	(A3)+,$FF8240
	DBF	D0,LOOP23
	CLR.W	$FF8240
	MOVEM.L	(A7)+,D0-D3/A3-A4
	BCLR	#0,$FFFA0F
	RTE

DATA

SAVEINTREG	DS.L	6

RASTC4	DC.W	0

RASTC5	DC.W	$A

RASTC6	DC.W	$14

RASTC7	DC.W	$1E

RASTDA	DC.W	$111,$222,$333,$444		;WHITE
	DC.W	$555,$666,$777,$777
	DC.W	$666,$555,$444,$333
	DC.W	$222,$111
	
RASTD9	DC.W	$100,$200,$300,$400		;RED
	DC.W	$500,$600,$700,$700
	DC.W	$600,$500,$400,$300
	DC.W	$200,$100
	
RASTD8	DC.W	$010,$020,$030,$040		;GREEN	
	DC.W	$050,$060,$070,$070
	DC.W	$060,$050,$040,$030
	DC.W	$020,$010
	
RASTD7	DC.W	$001,$002,$003,$004		;BLUE
	DC.W	$005,$006,$007,$007
	DC.W	$006,$005,$004,$003
	DC.W	$002,$001
	
RASTD3	DS.L	100

RASTD6	DC.W	$0BE,$0C4,$0CA
	DC.W	$0D0,$0D6,$0DC,$0E2
	DC.W	$0E8,$0EC,$0F2,$0F8
	DC.W	$0FE,$104,$108,$10E
	DC.W	$114,$118,$11E,$122
	DC.W	$126,$12C,$130,$134
	DC.W	$138,$13C,$140,$144
	DC.W	$148,$14A,$14E,$152
	DC.W	$154,$156,$15A,$15C
	DC.W	$15E,$160,$162,$162
	DC.W	$164,$166,$166,$168
	DC.W	$168,$168,$168,$168
	DC.W	$168,$168,$166,$166
	DC.W	$164,$162,$162,$160
	DC.W	$15E,$15C,$15A,$156
	DC.W	$154,$152,$14E,$14A
	DC.W	$148,$144,$140,$13C
	DC.W	$138,$134,$130,$12C
	DC.W	$126,$122,$11E,$118
	DC.W	$114,$10E,$108,$104
	DC.W	$0FE,$0F8,$0F2,$0EC
	DC.W	$0E8,$0E2,$0DC,$0D6
	DC.W	$0D0,$0CA,$0C4,$0BE
	DC.W	$0B8,$0B2,$0AC,$0A6
	DC.W	$0A0,$09A,$094,$090
	DC.W	$08A,$084,$07E,$078
	DC.W	$074,$06E,$068,$064
	DC.W	$05E,$05A,$056,$050
	DC.W	$04C,$048,$044,$040
	DC.W	$03C,$038,$034,$032
	DC.W	$02E,$02A,$028,$026
	DC.W	$022,$020,$01E,$01C
	DC.W	$01A,$01A,$018,$016
	DC.W	$016,$014,$014,$014
	DC.W	$014,$014,$014,$014
	DC.W	$016,$016,$018,$01A
	DC.W	$01A,$01C,$01E,$020
	DC.W	$022,$026,$028,$02A
	DC.W	$02E,$032,$034,$038
	DC.W	$03C,$040,$044,$048
	DC.W	$04C,$050,$056,$05A
	DC.W	$05E,$064,$068,$06E
	DC.W	$074,$078,$07E,$084
	DC.W	$08A,$090,$094,$09A
	DC.W	$0A0,$0A6,$0AC,$0B2
	DC.W	$0B8
		
SAVERES	DC.B	0,1

COLPAL	DC.W	$000,$464,$333,$363
	DC.W	$302,$503,$252,$402
	DC.W	$142,$020,$131,$725
	DC.W	$614,$770,$555,$777
