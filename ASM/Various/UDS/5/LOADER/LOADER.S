	JSR	SETSUPER
	JSR	SETSCREEN
	JSR	CLEARSCREENS
	JSR	SETPREDISTTEXT
	JSR	FIXCURVE
	JSR	FIXSINCURVE
	JSR	INIT
SETVAR:
	MOVE.L	#SINCURVE,SINCURVEPTR
	MOVE.L	#SINCURVE,OLDSCURVEPTR
	MOVE.L	#SINCURVE_2,SINCURVEPTR_2
	MOVE.L	#SINCURVE_2,OLDSCURVEPTR_2
	MOVE.L	#$00000777,$FF8240
	MOVE.L	#$00070777,$FF8244
	MOVE.L	#$00060777,$FF8248
	MOVE.L	#$02270777,$FF824C
*	RTS
 	MOVE.W	#5120-32*16,DISTTEXTPTRP
DEMOLOOP:
	JSR	WAITVBL

	CMP.B	#$4D,$FFFFFC02.W
	BNE	NOA
 	MOVE.W	#0,DISTTEXTPTRP
NOA:
	CMP.B	#$4B,$FFFFFC02.W
	BNE	NOB
 	MOVE.W	#5120-32*16,DISTTEXTPTRP
NOB:

	JMP	DEMOLOOP
*******************************
*         SUB - ROUTS         *
*******************************
SETSUPER:
	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	MOVE.L	D0,SAVESUPER
	RTS
ENDSUPER:
	MOVE.L	SAVESUPER,-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	RTS
*******************************
SETSCREEN:
	MOVE.W	#0,-(SP)
	MOVE.L	#$78000,-(SP)
	MOVE.L	#$78000,-(SP)
	MOVE.W	#$5,-(SP)
	TRAP	#14
	ADD.L	#12,SP
	RTS
*******************************
SCREENSWAP:
	MOVE.W	#-1,-(SP)
	MOVE.L	$44E,-(SP)
	MOVE.L	$44E,-(SP)
	MOVE.W	#$5,-(SP)
	TRAP	#14
	ADD.L	#12,SP

	CMP.L	#$70000,$44E
	BEQ	SCREENSWAP2
	MOVE.L	#$70000,$44E
	RTS
SCREENSWAP2:
	MOVE.L	#$78000,$44E
	RTS
*******************************
INIT:
	MOVE.W	 #$2700,SR
	MOVE.L	 #NEWVBL,$70.W
	MOVE.L	 #NEWHBL,$120.W
	CLR.B	 $FFFFFA07.W	
	CLR.B 	 $FFFFFA09.W
	ORI.B 	 #1,$FFFFFA07.W	
	ORI.B 	 #1,$FFFFFA13.W
	CLR.B 	 $FFFFFA1B.W	
	MOVE.W 	 #$2300,SR	
	RTS
*******************************
CLEARSCREENS:
	MOVE.L	#$70000,A0
	MOVE.L	#$78000,A1
	MOVE.W	#7999,D0
CLEARSCREENSLOOP:
	CLR.L	(A0)+
	CLR.L	(A1)+
	DBF.W	D0,CLEARSCREENSLOOP
	RTS
*******************************
WAITVBL:
	CMP.W	#1,WFNC
	BNE.S	WAITVBL
	MOVE.W	#0,WFNC
	RTS
*******************************
* FIX SINUS CURVE TO SCR.FORM *
*******************************
FIXSINCURVE:
	MOVE.L	#SINCURVE,A0
	MOVE.L	#SINCURVE,A1
	MOVE.L	#NIBBLES,A2
	MOVE.L	#(1080*2*2)-1,D3
FIXSINCURVELOOP:
	CLR.L	D0
	CLR.L	D1
	CLR.L	D2
	MOVE.W	(A0)+,D0
	MOVE.W	(A0)+,D1

	MULS	#160,D1
	MOVE.W	D0,D2
	AND.W	#$FFF0,D2
	ASR.W	D2
	ADD.W	D2,D1
	AND.W	#$000F,D0
	ASL.W	D0
	MOVE.W	(A2,D0.W),D0
	MOVE.W	D1,(A1)+
	MOVE.W	D0,(A1)+
	DBF.W	D3,FIXSINCURVELOOP
	RTS
*******************************
*   PUT SINUSCURVE ON SCREEN  * 
*******************************
CLRSINCURVE:
	CLR.L	D1
	CLR.L	D2
	MOVE.L	OLDSCURVEPTR2,A0
	MOVE.L	$44E,A1
	MOVE.L	#359,D0
CLRSINCURVELOOP:
	MOVE.W	(A0),D1
	CLR.W	2(A1,D1.W)
	ADD.W	#4,A0
	DBF.W	D0,CLRSINCURVELOOP
	RTS		
*******************************
CLRSINCURVE_2:
	CLR.L	D1
	CLR.L	D2
	MOVE.L	OLDSCURVEPTR2_2,A0
	MOVE.L	$44E,A1
	MOVE.L	#359,D0
CLRSINCURVELOOP_2:
	MOVE.W	(A0),D1
	CLR.W	4(A1,D1.W)
	ADD.W	#4,A0
	DBF.W	D0,CLRSINCURVELOOP_2
	RTS		
*******************************
SHOWSINCURVE:
	CLR.L	D1
	CLR.L	D2
	MOVE.L	SINCURVEPTR,A0
	MOVE.L	$44E,A1
	MOVE.L	#359,D0
SHOWSINCURVELOOP:
	MOVE.W	(A0),D1
	MOVE.W	2(A0),D2
	OR.W	D2,2(A1,D1.W)
	ADD.W	#4,A0
	DBF.W	D0,SHOWSINCURVELOOP
	RTS
*******************************
SHOWSINCURVE_2:
	CLR.L	D1
	CLR.L	D2
	MOVE.L	SINCURVEPTR_2,A0
	MOVE.L	$44E,A1
	MOVE.L	#359,D0
SHOWSINCURVELOOP_2:
	MOVE.W	(A0),D1
	MOVE.W	2(A0),D2
	OR.W	D2,4(A1,D1.W)
	ADD.W	#4,A0
	DBF.W	D0,SHOWSINCURVELOOP_2
	RTS
*******************************
*   1 - PIXEL DISTING LOGO    * 
*******************************
FIXCURVE:
	LEA	SLOGOCURVE,A0
	MOVE.W	#720*2-1,D0
FIXCURVELOOP:
	CLR.L	D1
	MOVE.W	(A0),D1	
	MULS	#160,D1
	MOVE.W	D1,(A0)+
	DBF.W	D0,FIXCURVELOOP
*******************************
GETNEWCURVEP:
	ADDQ.L	#2,SLOGOCURVEPTR
	CMP.L	#SLOGOCURVEEND,SLOGOCURVEPTR
	BNE	GETNEWCURVEPEND
	MOVE.L	#SLOGOCURVE,SLOGOCURVEPTR
GETNEWCURVEPEND:
	RTS
*******************************
SETPREDISTTEXT:
	MOVE.L	#DISTTEXTBUFF,A0
	MOVE.L	#DISTTEXT,DISTTEXTPTR
	MOVE.L	#1,D7
SETPREDISTTEXTLOOP:
	CLR.L	D0
	MOVE.L	DISTTEXTPTR,A1
	MOVE.B	(A1)+,D0
	CMP.B	#$FF,D0
	BEQ	SETPREDISTWRAP
	MOVE.L	A1,DISTTEXTPTR
	SUB.W	#32,D0
	MULS.W	#16*2,D0

	MOVE.L	#FONT+34,A1
	ADD.W	D0,A1
	REPT	16
	CLR.L	D1
	MOVE.W	(A1)+,D1
	AND.W	#%1000000000000000,D1
	MOVE.W	D1,(A0)+
	ENDR
	MOVE.L	#FONT+34,A1
	ADD.W	D0,A1
	REPT	16
	CLR.L	D1
	MOVE.W	(A1)+,D1
	AND.W	#%0100000000000000,D1
	MOVE.W	D1,(A0)+
	ENDR
	MOVE.L	#FONT+34,A1
	ADD.W	D0,A1
	REPT	16
	CLR.L	D1
	MOVE.W	(A1)+,D1
	AND.W	#%0010000000000000,D1
	MOVE.W	D1,(A0)+
	ENDR
	MOVE.L	#FONT+34,A1
	ADD.W	D0,A1
	REPT	16
	CLR.L	D1
	MOVE.W	(A1)+,D1
	AND.W	#%0001000000000000,D1
	MOVE.W	D1,(A0)+
	ENDR
	MOVE.L	#FONT+34,A1
	ADD.W	D0,A1
	REPT	16
	CLR.L	D1
	MOVE.W	(A1)+,D1
	AND.W	#%0000100000000000,D1
	MOVE.W	D1,(A0)+
	ENDR
	MOVE.L	#FONT+34,A1
	ADD.W	D0,A1
	REPT	16
	CLR.L	D1
	MOVE.W	(A1)+,D1
	AND.W	#%0000010000000000,D1
	MOVE.W	D1,(A0)+
	ENDR
	MOVE.L	#FONT+34,A1
	ADD.W	D0,A1
	REPT	16
	CLR.L	D1
	MOVE.W	(A1)+,D1
	AND.W	#%0000001000000000,D1
	MOVE.W	D1,(A0)+
	ENDR
	MOVE.L	#FONT+34,A1
	ADD.W	D0,A1
	REPT	16
	CLR.L	D1
	MOVE.W	(A1)+,D1
	AND.W	#%0000000100000000,D1
	MOVE.W	D1,(A0)+
	ENDR
	MOVE.L	#FONT+34,A1
	ADD.W	D0,A1
	REPT	16
	CLR.L	D1
	MOVE.W	(A1)+,D1
	AND.W	#%0000000010000000,D1
	MOVE.W	D1,(A0)+
	ENDR
	MOVE.L	#FONT+34,A1
	ADD.W	D0,A1
	REPT	16
	CLR.L	D1
	MOVE.W	(A1)+,D1
	AND.W	#%0000000001000000,D1
	MOVE.W	D1,(A0)+
	ENDR
	MOVE.L	#FONT+34,A1
	ADD.W	D0,A1
	REPT	16
	CLR.L	D1
	MOVE.W	(A1)+,D1
	AND.W	#%0000000000100000,D1
	MOVE.W	D1,(A0)+
	ENDR
	MOVE.L	#FONT+34,A1
	ADD.W	D0,A1
	REPT	16
	CLR.L	D1
	MOVE.W	(A1)+,D1
	AND.W	#%0000000000010000,D1
	MOVE.W	D1,(A0)+
	ENDR
	MOVE.L	#FONT+34,A1
	ADD.W	D0,A1
	REPT	16
	CLR.L	D1
	MOVE.W	(A1)+,D1
	AND.W	#%0000000000001000,D1
	MOVE.W	D1,(A0)+
	ENDR
	MOVE.L	#FONT+34,A1
	ADD.W	D0,A1
	REPT	16
	CLR.L	D1
	MOVE.W	(A1)+,D1
	AND.W	#%0000000000000100,D1
	MOVE.W	D1,(A0)+
	ENDR
	MOVE.L	#FONT+34,A1
	ADD.W	D0,A1
	REPT	16
	CLR.L	D1
	MOVE.W	(A1)+,D1
	AND.W	#%0000000000000010,D1
	MOVE.W	D1,(A0)+
	ENDR
	MOVE.L	#FONT+34,A1
	ADD.W	D0,A1
	REPT	16
	CLR.L	D1
	MOVE.W	(A1)+,D1
	AND.W	#%0000000000000001,D1
	MOVE.W	D1,(A0)+
	ENDR
	BRA	SETPREDISTTEXTLOOP
SETPREDISTWRAP:
	MOVE.L	#DISTTEXT2,DISTTEXTPTR
	DBF.W	D7,SETPREDISTTEXTLOOP
	RTS
*******************************
SETDISTTEXT:
	MOVE.L	#DISTTEXTBUFF,A0
	ADD.W	DISTTEXTPTRP,A0
	MOVE.L	$44E,A1
	SUB.W	#160*5+120,A1
	MOVE.L	SLOGOCURVEPTR,A2
	MOVE.W	#8,D7
	CLR.L	D0
SETDISTTEXTLOOP:
	REPT	16
	MOVE.W	(A2)+,D0
	MOVE.L	A1,A3
	ADD.W	D0,A3

	MOVE.W	(A0)+,D1
	OR.W	D1,(A3)
	MOVE.W	(A0)+,D1
	OR.W	D1,160(A3)
	MOVE.W	(A0)+,D1
	OR.W	D1,2*160(A3)
	MOVE.W	(A0)+,D1
	OR.W	D1,3*160(A3)
	MOVE.W	(A0)+,D1
	OR.W	D1,4*160(A3)
	MOVE.W	(A0)+,D1
	OR.W	D1,5*160(A3)
	MOVE.W	(A0)+,D1
	OR.W	D1,6*160(A3)
	MOVE.W	(A0)+,D1
	OR.W	D1,7*160(A3)
	MOVE.W	(A0)+,D1
	OR.W	D1,8*160(A3)
	MOVE.W	(A0)+,D1
	OR.W	D1,9*160(A3)
	MOVE.W	(A0)+,D1
	OR.W	D1,10*160(A3)
	MOVE.W	(A0)+,D1
	OR.W	D1,11*160(A3)
	MOVE.W	(A0)+,D1
	OR.W	D1,12*160(A3)
	MOVE.W	(A0)+,D1
	OR.W	D1,13*160(A3)
	MOVE.W	(A0)+,D1
	OR.W	D1,14*160(A3)
	MOVE.W	(A0)+,D1
	OR.W	D1,15*160(A3)
	ENDR			
	ADDQ.W	#8,A1
	DBF.W	D7,SETDISTTEXTLOOP
	RTS	
*******************************
CLRDISTTEXT:
	MOVE.L	#DISTTEXTBUFF,A0
	ADD.W	DISTTEXTPTRP,A0
	MOVE.L	$44E,A1
	SUB.W	#160*5+120,A1
	MOVE.L	SLOGOCURVEPTR,A2
	MOVE.W	#8,D7
CLRDISTTEXTLOOP:
	MOVE.W	(A2),D0
	MOVE.W	32(A2),D1
	MOVE.L	A1,A3
	MOVE.L	A1,A4
	ADD.W	#32,A2
	ADD.W	D0,A3
	ADD.W	D1,A4
	CLR.W	-160*5(A3)
	CLR.W	-160*4(A3)
	CLR.W	-160*3(A3)
	CLR.W	-160*2(A3)
	CLR.W	-160(A3)
	CLR.W	(A3)
	CLR.W	160(A3)
	CLR.W	160*2(A3)
	CLR.W	160*3(A3)
	CLR.W	160*4(A3)
	CLR.W	160*5(A3)
	CLR.W	160*6(A3)
	CLR.W	160*7(A3)
	CLR.W	160*8(A3)
	CLR.W	160*9(A3)
	CLR.W	160*10(A3)
	CLR.W	160*11(A3)
	CLR.W	160*12(A3)
	CLR.W	160*13(A3)
	CLR.W	160*14(A3)
	CLR.W	160*15(A3)
	CLR.W	160*16(A3)
	CLR.W	160*17(A3)
	CLR.W	160*18(A3)
	CLR.W	160*19(A3)
	CLR.W	160*20(A3)
	CLR.W	-160*5(A4)
	CLR.W	-160*4(A4)
	CLR.W	-160*3(A4)
	CLR.W	-160*2(A4)
	CLR.W	-160(A4)
	CLR.W	(A4)
	CLR.W	160(A4)
	CLR.W	160*2(A4)
	CLR.W	160*3(A4)
	CLR.W	160*4(A4)
	CLR.W	160*5(A4)
	CLR.W	160*6(A4)
	CLR.W	160*7(A4)
	CLR.W	160*8(A4)
	CLR.W	160*9(A4)
	CLR.W	160*10(A4)
	CLR.W	160*11(A4)
	CLR.W	160*12(A4)
	CLR.W	160*13(A4)
	CLR.W	160*14(A4)
	CLR.W	160*15(A4)
	CLR.W	160*16(A4)
	CLR.W	160*17(A4)
	CLR.W	160*18(A4)
	CLR.W	160*19(A4)
	CLR.W	160*20(A4)
	ADDQ.W	#8,A1
	DBF.W	D7,CLRDISTTEXTLOOP
	RTS
*******************************
*            VBL              * 
*******************************
NEWVBL:
	MOVE.W	#$000,$FF8240
	MOVE.L	#NEWHBL,$120.W
	MOVE.B 	#1,$FFFFFA21.W	
	MOVE.B 	#8,$FFFFFA1B.W	

	JSR	CLRDISTTEXT
	JSR	SETDISTTEXT
	JSR	GETNEWCURVEP
	JSR	GETNEWCURVEP
	JSR	GETNEWCURVEP
	MOVE.L	OLDSCURVEPTR,OLDSCURVEPTR2
	MOVE.L	SINCURVEPTR,OLDSCURVEPTR
	MOVE.L	OLDSCURVEPTR_2,OLDSCURVEPTR2_2
	MOVE.L	SINCURVEPTR_2,OLDSCURVEPTR_2

	ADDQ.L	#4,SINCURVEPTR
	CMP.L	#SINCURVEEND,SINCURVEPTR
	BNE	NORESTORESINCURVE
	MOVE.L	#SINCURVE,SINCURVEPTR
NORESTORESINCURVE:
	ADDQ.L	#4,SINCURVEPTR_2
	CMP.L	#SINCURVEEND_2,SINCURVEPTR_2
	BNE	NORESTORESINCURVE_2
	MOVE.L	#SINCURVE_2,SINCURVEPTR_2
NORESTORESINCURVE_2:

	JSR	CLRSINCURVE
	JSR	CLRSINCURVE_2
	JSR	SHOWSINCURVE
	JSR	SHOWSINCURVE_2

	JSR	SCREENSWAP

	MOVE.W	#1,WFNC
	RTE
*******************************
NEWHBL:
	CLR.B	$FFFFFA1B.W
	MOVE.W	#$227,$FF8240
	MOVE.L	#NEWHBL2,$120.W
	MOVE.B 	#1,$FFFFFA21.W	
	MOVE.B 	#8,$FFFFFA1B.W	
	BCLR 	#0,$FFFFFA0F.W	
	RTE			
NEWHBL2:
	MOVE.W	#$004,$FF8240
	CLR.B	$FFFFFA1B.W
	MOVE.L	#NEWHBL3,$120.W
	MOVE.B 	#197,$FFFFFA21.W	
	MOVE.B 	#8,$FFFFFA1B.W	
	BCLR 	#0,$FFFFFA0F.W	
	RTE			
NEWHBL3:
	MOVE.W	#$227,$FF8240
	CLR.B	$FFFFFA1B.W
	MOVE.L	#NEWHBL4,$120.W
	MOVE.B 	#1,$FFFFFA21.W	
	MOVE.B 	#8,$FFFFFA1B.W	
	BCLR 	#0,$FFFFFA0F.W	
	RTE			
NEWHBL4:
	MOVE.W	#$000,$FF8240
	CLR.B	$FFFFFA1B.W
	BCLR 	#0,$FFFFFA0F.W	
	RTE			
*******************************
*          VARIABLES          *
*******************************
SAVESUPER:	DC.L	0
OLDVBL:		DC.L	0
WFNC:		DC.W	0
DISTTEXTPTR:	DC.L	0
DISTTEXTPTRP:	DC.W	0
DISTTEXT:	DC.B	' LOADING ',$FF
DISTTEXT2:	DC.B	'DEPACKING',$FF
FONT:		INCBIN	B:\16FONT1.DAT
NIBBLES:	DC.W	$8000,$4000,$2000,$1000,$800,$400,$200,$100,$80,$40,$20,$10,$8,$4,$2,$1
SLOGOCURVEPTR:	DC.L	SLOGOCURVE
SLOGOCURVE:	INCLUDE	A:\BDISTCUR.DAT
SLOGOCURVEEND:	INCLUDE	A:\BDISTCUR.DAT
SINCURVEPTR:	DC.L	0
OLDSCURVEPTR:	DC.L	0
OLDSCURVEPTR2:	DC.L	0
SINCURVEPTR_2:	DC.L	0
OLDSCURVEPTR_2:	DC.L	0
OLDSCURVEPTR2_2:DC.L	0
SINCURVE:	INCLUDE	A:\BRAIN.DAT
SINCURVEEND:	INCLUDE	A:\BRAIN.DAT
SINCURVE_2:	INCLUDE	A:\BLOMMA.DAT
SINCURVEEND_2:	INCLUDE	A:\BLOMMA.DAT
		EVEN
DISTTEXTBUFF:	DS.B	5120
DISTTEXTBUFF2:	DS.B	5120
