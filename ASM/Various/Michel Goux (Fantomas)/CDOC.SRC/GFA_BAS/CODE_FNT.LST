' -------------------------------
' Codage d'une fonte 16*16 1 plan
'      (C) FANTOMAS 1991
' -------------------------------
'
RESERVE 10000
font%=MALLOC(45*32) ! font%=malloc(nb_caracteres*32)
'
' ------------------------------------------------------------
' Charge la fonte
' Les caracteres devront etre dessines avec la couleur 1 (2eme
' registre a gauche dans DEGAS ELITE)
' ------------------------------------------------------------
FILESELECT "A:\*.PI1","FONT.PI1",nm$
BLOAD nm$,XBIOS(2)-34
~XBIOS(6,L:XBIOS(2)-32)
' ----------------------------------------------------------------
' Ligne 1 (20 caracteres: A,B,C,D,E,F,G,H,I,J,K,L,M,N,O,P,Q,R,S,T)
' ----------------------------------------------------------------
FOR n%=0 TO 19
  @cree_car(MUL(n%,16),0,ADD(font%,MUL(n%,32)))
NEXT n%
' ---------------------------------------------------------------
' Ligne 2 (20 caracteres: U,V,W,X,Y,Z,?,.,!,:,(,),,,',-,1,2,3,4,5
' ---------------------------------------------------------------
FOR n%=0 TO 19
  @cree_car(MUL(n%,16),16,ADD(font%,ADD(640,MUL(n%,32))))
NEXT n%
' --------------------------------------
' Ligne 3 (5 caracteres: 6,7,8,9,ESPACE)
' --------------------------------------
FOR n%=0 TO 4
  @cree_car(MUL(n%,16),32,ADD(font%,ADD(1280,MUL(n%,32))))
NEXT n%
' ----------
' Sauvegarde
' ----------
BSAVE "A:\CDOC.SRC\DATA\FONTCAR1.DAT",font%,45*32
' ---
' Fin
' ---
~MFREE(font%)
END
' -----------------------------------------
' Codages des caracteres sur 1 seul plan
'   x%= coordonnee en x du caractere
'   y%= coordonnee en y du caractere
'   dest%= zone ou l'on stocke le caractere
' -----------------------------------------
PROCEDURE cree_car(x%,y%,dest%)
  hauteur%=15
  x%=x%/2
  x%=x% AND &HF8
  source%=ADD(XBIOS(2),ADD(MUL(y%,160),x%))
  FOR i%=0 TO hauteur%
    DPOKE dest%,DPEEK(ADD(source%,MUL(i%,160)))
    ADD dest%,2
  NEXT i%
RETURN
