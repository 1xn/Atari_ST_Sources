
**************************************
*  Regent Base 2: 4GL SQL Database   *
*  Utilities Program by Frank Cohen  *
*  (c) 1988 Regent Software          *
**************************************

* 063088 Release

 TEXT

START MOVE.L A7,A5
 MOVE.L #USTK,A7

 MOVE.L 4(A5),A5
 MOVE.L $C(A5),D0
 ADD.L $14(A5),D0
 ADD.L $1C(A5),D0
 ADD.L #$100,D0
 MOVE.L D0,-(A7)
 MOVE.L A5,-(A7)
 MOVE D0,-(A7)
 MOVE #$4A,-(A7)
 TRAP #1
 ADD.L #12,A7

 MOVE #GEMEND-GEMSTART,D0
 LEA GEMSTART,A0
CLRGEM CLR.B (A0)+
 DBF D0,CLRGEM

*--------------INIT GEM VARIABLES-------------

 CLR.L AP1RESV   ;APPL_INIT
 CLR.L AP2RESV
 CLR.L AP3RESV
 CLR.L AP4RESV
 CLR.L APPTREE
 MOVE #10,CONTRL
 CLR CONTRL+2
 MOVE #1,CONTRL+4
 CLR CONTRL+6
 CLR CONTRL+8
 JSR AES

 MOVE #77,CONTRL  ;GRAF_HANDLE
 CLR CONTRL+2
 MOVE #5,CONTRL+4
 CLR CONTRL+6
 CLR CONTRL+8
 JSR AES
 MOVE INTOUT,DRHANDLE

 MOVE #100,CONTRL ;OPEN_VWORK
 CLR CONTRL+2
 MOVE #11,CONTRL+6
 MOVE DRHANDLE,CONTRL+12
 MOVE #1,INTIN
 MOVE #1,INTIN+2
 MOVE #1,INTIN+4
 MOVE #1,INTIN+6
 MOVE #1,INTIN+8
 MOVE #1,INTIN+10
 MOVE #1,INTIN+12
 MOVE #1,INTIN+14
 MOVE #1,INTIN+16
 MOVE #1,INTIN+18
 MOVE #2,INTIN+20
 JSR VDI

 MOVE CONTRL+12,GRHANDLE

 MOVE #104,OPCODE     ;DETERMINE WORK STORAGE
 MOVE #2,SINTIN       ;OF THE DESKTOP WINDOW
 MOVE #5,SINTOUT
 CLR SADDRIN
 CLR SADDROUT
 CLR INTIN
 MOVE #4,INTIN+2
 JSR AES
 MOVE INTOUT+2,GEMXPOS
 MOVE INTOUT+4,GEMYPOS
 MOVE INTOUT+6,GEMWIDTH
 MOVE INTOUT+8,GEMHOEHE

 MOVE #1,XPOS
 MOVE #1,YPOS
 CLR D0
 JSR SHWMOUSE

 MOVE #3,-(A7)  ;SCREEN LOCATION
 TRAP #14
 ADD.L #2,A7
 MOVE.L D0,SCREEN

 MOVE.L #DISKBUFF,-(A7)  ;SET UP DISK I/O BLOCK
 MOVE #26,-(A7)
 TRAP #1
 ADD.L #6,A7

 JSR GETPATH

*----------------LOAD RESOURCE FILE------------------

 MOVE #110,CONTRL
 CLR CONTRL+2
 MOVE #1,CONTRL+4
 MOVE #1,CONTRL+6
 CLR CONTRL+8
 LEA RSCNAME,A0
 JSR ADDPATH
 MOVE.L A0,ADDRIN
 JSR AES
 TST INTOUT
 BEQ NORSRC

 MOVE #UTILMENU,D0
 JSR GADDR
 MOVE.L A0,MENUADR

 MOVE #WORKING,D0
 JSR GADDR
 MOVE.L A0,WORKADR

 MOVE #CHOSETBL,D0
 JSR GADDR
 MOVE.L A0,TABLADR

 MOVE #ERASE,D0
 JSR GADDR
 MOVE.L A0,ERASEADR

 MOVE #TABLEBOX,D0
 JSR GADDR
 MOVE.L A0,TBLADR

 MOVE #CHOICES,D0
 JSR GADDR
 MOVE.L A0,CHOICDR

 MOVE #TRASH,D0
 JSR GADDR
 MOVE.L A0,TRASHADR

 MOVE #NAMETBL,D0
 JSR GADDR
 MOVE.L A0,NAMEDIAL

 MOVE #NEWDATE,D0
 JSR GADDR
 MOVE.L A0,DTEDIAL

 MOVE #NEWTEXT,D0
 JSR GADDR
 MOVE.L A0,TXTDIAL

 MOVE #NEWNUM,D0
 JSR GADDR
 MOVE.L A0,NUMDIAL

 MOVE #ARESURE,D0
 JSR GADDR
 MOVE.L A0,SUREADR

 MOVE #NEWDRIVE,D0
 JSR GADDR
 MOVE.L A0,DRVADR

 MOVE #BADSPEC,D0
 JSR GADDR
 MOVE.L A0,BADDADR

 MOVE #IOERRS,D0
 JSR GADDR
 MOVE.L A0,IOERRADR

 MOVE #STRINGS,D0
 JSR GADDR
 MOVE.L A0,STRINGADR

 MOVE #DISKERRD,D0
 JSR GADDR
 MOVE.L A0,ERRDLG

*--------------Check graphic mode-------------

 MOVE #4,-(A7)
 TRAP #14
 ADD #2,A7
 MOVE D0,GRMODE
 TST D0
 BNE GOODMD
 JMP BADREZ

GOODMD CMP #2,GRMODE
 BNE NOBQM

 MOVE.L TABLADR,A0
 ADD.L #TSLIDER*OBSIZE,A0
 ADD #4,OB_Y(A0)

NOBQM JSR TEXTINIT
 JSR CLS

*------------ALLOCATE BIGBUFF------------

 MOVE.L #-1,-(A7)  ;MALLOC
 MOVE #$48,-(A7)
 TRAP #1
 ADD.L #6,A7

MOMONEY SUB.L #32000,D0
 CMP.L #200000,D0
 BCS NOMEM

 MOVE.L D0,BUFFSIZE

 MOVE.L D0,-(A7)  ;MALLOC
 MOVE #$48,-(A7)
 TRAP #1
 ADD #6,A7
 TST.L D0
 BEQ NOMEM
 MOVE.L D0,BIGBUFF

*---------------TOP MENU----------------

GETEVENT CLR ACTNUMB

 MOVE.L MENUADR,A0
 MOVE #-1,D0
 JSR SHOWPANL

GET2 MOVE.L MENUADR,TREEADR
 JSR WAITMOUSE

 MOVE #UMODTBL,D0  ;MODFY TABLE
 JSR OBJCFIND
 TST D0
 BEQ MODIFY

 MOVE #UMOVE,D0  ;MOVE TABLE
 JSR OBJCFIND
 TST D0
 BEQ MOVER

 MOVE #UCREATE,D0  ;CREATE A TABLE
 JSR OBJCFIND
 TST D0
 BEQ CREATE

 MOVE #UDROP,D0  ;DROP A TABLE
 JSR OBJCFIND
 TST D0
 BEQ DROP

 MOVE #UEXPORT,D0  ;EXPORT A TABLE
 JSR OBJCFIND
 TST D0
 BEQ EXPORTIT

 MOVE #UIMPORT,D0  ;IMPORT A TABLE
 JSR OBJCFIND
 TST D0
 BEQ IMPORTIT

 MOVE #UCANCEL,D0  ;QUIT
 JSR OBJCFIND
 TST D0
 BEQ QUIT

 JMP GET2

*-----------MOVE TABLE-----------

MOVER JSR CLS

 MOVE.L #UINFO*OBSIZE,A0
 ADD.L TABLADR,A0
 MOVE.L STRINGADR,A1
 ADD #MOVEMES1*OBSIZE,A1
 MOVE.L OB_SPEC(A1),OB_SPEC(A0)
 JSR GETTABLE
 TST D0
 BMI GETEVENT

 JSR INITMEM

 JSR CLS
 JSR FINDTBLS
 TST D0
 BMI NOTABLE

 CLR LDINDEX
 JSR LDFIELDS
 TST D0
 BMI NOFLDS
 JSR CALROW

 MOVE.L #39,D0
 LEA TNAME,A0
 LEA T1FNAME5,A1
 LEA T1FNAME,A2
 LEA T1FNAME2,A3
MOV100 MOVE.B (A0)+,(A1)+
 MOVE.B (A2)+,(A3)+
 DBF D0,MOV100

 MOVE.L DRVADR,A0
 ADD.L #DRVNAME*OBSIZE,A0
 MOVE.L OB_SPEC(A0),A0
 MOVE.L TE_PTEXT(A0),A0
 MOVE.B #'@',(A0)

 JSR CLRDIALS
 MOVE.L DRVADR,A0
 MOVE.L #DRVNAME,D0
 JSR FORMDO
 JSR CLS
 CMP #DRVCNCL,SAVERTRN
 BEQ GETEVENT

 JSR LOVELY

 MOVE.L DRVADR,A0
 ADD.L #DRVNAME*OBSIZE,A0
 MOVE.L OB_SPEC(A0),A0
 MOVE.L TE_PTEXT(A0),A0

 LEA T1FNAME7,A1
 MOVE #18,D0
MOV200 TST.B (A0)
 BEQ MOV201
 MOVE.B (A0)+,(A1)+
 DBF D0,MOV200
MOV201 MOVE.B #'*',(A1)+
 MOVE.B #'.',(A1)+
 MOVE.B #'*',(A1)+
 CLR.B (A1)

 CLR -(A7)  ;F_SFIRST
 MOVE.L #T1FNAME7,-(A7)
 MOVE #78,-(A7)
 TRAP #1
 ADD.L #8,A7
 TST D0
 BMI MOV999

 LEA T1FNAME7,A0
 LEA T1FNAME3,A1
 LEA T1FNAME6,A2
 MOVE #39,D0
MOV202 TST.B (A0)
 BEQ MOV203
 CMP.B #'*',(A0)
 BEQ MOV203
 MOVE.B (A0)+,D1
 MOVE.B D1,(A2)+
 MOVE.B D1,(A1)+
 DBF D0,MOV202
 JMP INTERNAL

MOV203 LEA TNAME2,A0
MOV204 TST.B (A0)
 BEQ MOV205
 CMP.B #' ',(A0)
 BEQ MOV205
 MOVE.B (A0)+,D1
 MOVE.B D1,(A1)+
 MOVE.B D1,(A2)+
 DBF D0,MOV204
 JMP INTERNAL

MOV205 MOVE.B #'.',(A1)+
 MOVE.B #'T',(A1)+
 MOVE.B #'B',(A1)+
 MOVE.B #'L',(A1)+
 CLR.B (A1)

 MOVE.B #'.',(A2)+
 MOVE.B #'I',(A2)+
 MOVE.B #'N',(A2)+
 MOVE.B #'D',(A2)+
 CLR.B (A2)

 JSR CLRDIALS
 MOVE.L SUREADR,A0
 MOVE.L #-1,D0
 JSR FORMDO
 JSR CLS
 CMP #SUREYES,SAVERTRN
 BNE GETEVENT

 JSR CLS
 JSR CLRACT
 MOVE.L WORKADR,A0
 MOVE #-1,D0
 JSR SHOWPANL

 JSR ACTIVITY
 CLR -(A7)  ;F_CREATE
 MOVE.L #T1FNAME3,-(A7)
 MOVE #$3C,-(A7)
 TRAP #1
 ADD.L #8,A7
 TST D0
 BMI DISKERR
 MOVE D0,MHANDLE2

 JSR ACTIVITY
 LEA T1FNAME2,A0
 JSR F_OPEN
 TST D0
 BMI DISKERR
 MOVE D0,MHANDLE

MOV400 JSR ACTIVITY

 MOVE MHANDLE,A0
 MOVE.L ROWSIZE,D0
 LEA DBUFFER,A1
 JSR F_READ
 TST D0
 BMI DISKERR
 CMP.L ROWSIZE,D0
 BNE MOV300

 MOVE MHANDLE2,A0
 LEA DBUFFER,A1
 MOVE.L ROWSIZE,D0
 JSR F_WRITE
 CMP.L ROWSIZE,D0
 BNE DISKERR
 JMP MOV400

MOV300 MOVE MHANDLE,A0
 JSR F_CLOSE
 MOVE MHANDLE2,A0
 JSR F_CLOSE

* CHANGE SPEC IN TABLES.TBL

 LEA TBLSPEC,A0
 JSR YSOLOAD

 JSR FINDTBLS
 TST D0
 BMI NOTABLE

 CLR LDINDEX
 JSR LDFIELDS
 TST D0
 BMI NOFLDS
 JSR CALROW

 JSR ACTIVITY
 LEA TBLSPEC,A0
 JSR ADDPATH
 JSR F_OPEN
 TST D0
 BMI DISKERR
 MOVE D0,MHANDLE

 CLR -(A7)  ;F_CREATE
 MOVE.L #TEMPNAME,-(A7)
 MOVE #$3C,-(A7)
 TRAP #1
 ADD #8,A7
 TST D0
 BMI DISKERR
 MOVE D0,MHANDLE2

MOV700 MOVE MHANDLE,A0
 MOVE.L ROWSIZE,D0
 LEA DBUFFER,A1
 JSR F_READ
 TST D0
 BMI DISKERR
 CMP.L ROWSIZE,D0
 BNE MOV799

 JSR ACTIVITY

 CLR.B DBUFFER+19
 LEA DBUFFER,A0
 LEA TNAME2,A1
 JSR MATCHER
 TST D0
 BMI MOV704

 MOVE.L DRVADR,A0
 ADD.L #DRVNAME*OBSIZE,A0
 MOVE.L OB_SPEC(A0),A0
 MOVE.L TE_PTEXT(A0),A0
 MOVE.L #18,D0
 LEA DBUFFER+20,A1
MOV702 TST.B (A0)
 BEQ MOV703
 CMP.B #' ',(A0)
 BEQ MOV703
 MOVE.B (A0)+,(A1)+
 DBF D0,MOV702
 CLR.B (A1)
 JMP MOV704

MOV703 CLR.B (A1)+
 DBF D0,MOV703

MOV704 MOVE MHANDLE2,A0
 LEA DBUFFER,A1
 MOVE.L ROWSIZE,D0
 JSR F_WRITE
 CMP.L ROWSIZE,D0
 BNE DISKERR
 JSR ACTIVITY
 JMP MOV700

MOV799 MOVE MHANDLE,A0
 JSR F_CLOSE
 MOVE MHANDLE2,A0
 JSR F_CLOSE

 LEA TBLSPEC,A0
 JSR ADDPATH
 MOVE.L A0,-(A7) ;F_DELETE
 MOVE #65,-(A7)
 TRAP #1
 ADD #6,A7

 LEA TBLSPEC,A0
 JSR ADDPATH
 MOVE.L A0,-(A7)  ;F_RENAME
 MOVE.L #TEMPNAME,-(A7)
 CLR -(A7)
 MOVE #86,-(A7)
 TRAP #1
 ADD #12,A7
 TST D0
 BMI DISKERR

 MOVE.L #T1FNAME2,-(A7) ;F_DELETE
 MOVE #65,-(A7)
 TRAP #1
 ADD.L #6,A7

 JSR DROPINDX
 JSR CLS
 JMP GETEVENT

MOV999 MOVE.L BADDADR,A0
 ADD.L #BADSMES*OBSIZE,A0
 MOVE.L #T1FNAME7,OB_SPEC(A0)

 MOVE.L BADDADR,A0
 MOVE.L #-1,D0
 JSR FORMDO
 JSR CLS
 JMP GETEVENT

*-----------DROP TABLE-----------

DROP JSR CLS

 MOVE.L #UINFO*OBSIZE,A0
 ADD.L TABLADR,A0
 MOVE.L STRINGADR,A1
 ADD #DROPMES1*OBSIZE,A1
 MOVE.L OB_SPEC(A1),OB_SPEC(A0)
 JSR GETTABLE
 TST D0
 BMI GETEVENT

 JSR INITMEM

 JSR CLS
 JSR FINDTBLS
 TST D0
 BMI NOTABLE

 MOVE.L #39,D0
 LEA TNAME,A0
 LEA T1FNAME5,A1
 LEA T1FNAME,A2
 LEA T1FNAME2,A3
DROP100 MOVE.B (A0)+,(A1)+
 MOVE.B (A2)+,(A3)+
 DBF D0,DROP100

 JSR CLRDIALS
 MOVE.L SUREADR,A0
 MOVE.L #-1,D0
 JSR FORMDO
 JSR CLS
 CMP #SUREYES,SAVERTRN
 BNE GETEVENT

 JSR CLS
 JSR CLRACT
 MOVE.L WORKADR,A0
 MOVE #-1,D0
 JSR SHOWPANL
 JSR ACTIVITY

 JSR LOVELY

 JSR DROPINDX
 JSR REMOVE1
 JSR REMOVE2

 MOVE.L #T1FNAME2,-(A7) ;F_DELETE
 MOVE #65,-(A7)
 TRAP #1
 ADD #6,A7

 JMP GETEVENT

*--------------CREATE A TABLE--------------

CREATE JSR INITMEM
 JSR CLS
 JSR GETTNAME
 TST D0
 BNE GETEVENT

 JSR CLS
 JSR FINDTBLS
 TST D0
 BPL EXISTS

 MOVE.L #TNAMED*OBSIZE,A0
 ADD.L TBLADR,A0
 MOVE.L #TNAME,OB_SPEC(A0)

 JSR INITMEM
 MOVE.L #FNAMES2,FPTR

 JSR DOACTION
 TST D0
 BMI GETEVENT
 TST OBJCNT
 BEQ GETEVENT

 JSR CLS
 JSR CLRACT
 MOVE.L WORKADR,A0
 MOVE #-1,D0
 JSR SHOWPANL

 JSR LOVELY

 LEA TBLSPEC,A0
 JSR YSOLOAD

 JSR ACTIVITY

 JSR FINDTBLS
 TST D0
 BMI INTERNAL

 JSR ACTIVITY

 CLR LDINDEX
 JSR LDFIELDS
 TST D0
 BMI NOFLDS
 JSR CALROW

 LEA FIELDS,A0
 MOVE.L #TNAME2,FVALU(A0)
 ADD.L #RECSIZE,A0
 MOVE.L #PATHNAME,FVALU(A0)
 ADD.L #RECSIZE,A0
 MOVE.L #ZEROS,FVALU(A0)

 JSR BUILDROW

 LEA TBLSPEC,A0
 JSR ADDPATH
 JSR F_OPENW
 TST D0
 BMI DISKERR
 MOVE D0,DHANDLE
 JSR ACTIVITY

 MOVE DHANDLE,A0
 JSR F_END
 JSR ACTIVITY

 MOVE DHANDLE,A0
 LEA DBUFFER,A1
 MOVE.L ROWSIZE,D0
 JSR F_WRITE
 CMP.L ROWSIZE,D0
 BNE DISKERR
 JSR ACTIVITY

 MOVE DHANDLE,A0
 JSR F_CLOSE

 JSR ENTERFLD

 LEA TNAME2,A0
WONGS3 TST.B (A0)
 BEQ WONGS4
 CMP.B #' ',(A0)
 BEQ WONGS4
 ADD.L #1,A0
 JMP WONGS3
WONGS4 MOVE.B #'.',(A0)+
 MOVE.B #'T',(A0)+
 MOVE.B #'B',(A0)+
 MOVE.B #'L',(A0)+
 CLR.B (A0)+

 LEA TNAME2,A0
 JSR ADDPATH
 CLR -(A7)  ;F_CREATE
 MOVE.L A0,-(A7)
 MOVE #$3C,-(A7)
 TRAP #1
 ADD #8,A7
 TST D0
 BMI DISKERR
 MOVE D0,-(A7) ;F_CLOSE
 MOVE #$3E,-(A7)
 TRAP #1
 ADD #4,A7

 JMP GETEVENT

*---------ENTER FIELD DEFINITIONS-------

ENTERFLD LEA FLDSPEC,A0   ;MAKE FIELDS.TBL ENTRIES
 JSR YSOLOAD

 JSR ACTIVITY
 JSR FINDTBLS
 TST D0
 BMI INTERNAL

 JSR ACTIVITY
 CLR LDINDEX
 JSR LDFIELDS
 TST D0
 BMI NOFLDS
 JSR CALROW

 JSR ACTIVITY
 LEA FLDSPEC,A0
 JSR ADDPATH
 JSR F_OPENW
 TST D0
 BMI DISKERR
 MOVE D0,DHANDLE

 JSR ACTIVITY
 MOVE DHANDLE,A0
 JSR F_END

 CLR XCOUNT
 CLR XDISP

WRONG1 CLR.L D0
 MOVE XCOUNT,D0
 MULU #RECSIZE,D0
 ADD.L #FIELDS2,D0
 MOVE.L D0,A4

 TST.L FNAME(A4)
 BEQ WRONG2

 LEA FIELDS,A2
 MOVE.L FNAME(A4),FVALU(A2)   ;FNAME
 ADD.L #RECSIZE,A2

 MOVE.L #TNAME2,FVALU(A2)     ;TNAME
 ADD.L #RECSIZE,A2

 MOVE FTYPE(A4),D0
 MOVE.B D0,XHOLD1
 MOVE.L #XHOLD1,FVALU(A2)     ;TYPE
 ADD.L #RECSIZE,A2

 CLR.L D0
 MOVE XDISP,D0
 LEA XHOLD3,A3
 JSR CONV2DEC
 MOVE.L #XHOLD3,FVALU(A2)     ;DISP
 ADD.L #RECSIZE,A2

 CLR.L D0
 MOVE XDISP,D0
 ADD FSIZE(A4),D0
 MOVE D0,XDISP

 CLR.L D0
 MOVE FSIZE(A4),D0
 LEA XHOLD2,A3
 JSR CONV2DEC
 MOVE.L #XHOLD2,FVALU(A2)     ;LENGTH
 ADD.L #RECSIZE,A2

 MOVE FEXTR(A4),D0
 LEA XHOLD4,A3
 JSR CONV2DEC
 MOVE.L #XHOLD4,FVALU(A2)     ;DEC
 ADD.L #RECSIZE,A2

 MOVE.L #ZNULL,FVALU(A2)      ;UNUSED

 JSR ACTIVITY
 JSR BUILDROW

 JSR ACTIVITY
 MOVE DHANDLE,A0
 LEA DBUFFER,A1
 MOVE.L ROWSIZE,D0
 JSR F_WRITE
 CMP.L ROWSIZE,D0
 BNE DISKERR
 JSR ACTIVITY

 ADD #1,XCOUNT
 CMP #MAXFIELD,XCOUNT
 BCS WRONG1

WRONG2 JSR ACTIVITY

 MOVE DHANDLE,A0
 JMP F_CLOSE

XCOUNT DC 0
XHOLD1 DC 0
XDISP DC 0
XHOLD2 DC.L 0,0
XHOLD3 DC.L 0,0
XHOLD4 DC.L 0

*----------PARSE TABLE FILE NAME-----------

PARSEIT LEA T1FNAME,A0
 LEA T1FNAME2,A1
 LEA T1FNAME6,A2
 MOVE #39,D0
MOD499 MOVE.B (A0)+,D0
 MOVE.B D0,(A1)+
 MOVE.B D0,(A2)+
 DBF D0,MOD499

 LEA T1FNAME2,A0
 LEA T1FNAME6,A2
MOD500 TST.B (A0)
 BEQ MOD501
 ADD.L #1,A0
 ADD.L #1,A2
 JMP MOD500

MOD501 CMP.L #T1FNAME2,A0
 BEQ MOD502
 SUB.L #1,A2
 CMP.B #'\',-(A0)
 BNE MOD501

MOD502 TST.B (A0)
 BEQ MOD503
 CMP.B #'.',(A0)
 BEQ MOD503
 ADD.L #1,A0
 ADD.L #1,A2
 JMP MOD502

MOD503 MOVE.B #'.',(A0)+
 MOVE.B #'$',(A0)+
 MOVE.B #'$',(A0)+
 MOVE.B #'$',(A0)+
 CLR.B (A0)+

 MOVE.B #'.',(A2)+
 MOVE.B #'I',(A2)+
 MOVE.B #'N',(A2)+
 MOVE.B #'D',(A2)+
 CLR.B (A2)+
 RTS

*--------------MODIFY A TABLE--------------

MODIFY JSR CLS

 MOVE.L #UINFO*OBSIZE,A0
 ADD.L TABLADR,A0
 MOVE.L STRINGADR,A1
 ADD #MODMES1*OBSIZE,A1
 MOVE.L OB_SPEC(A1),OB_SPEC(A0)
 JSR GETTABLE
 TST D0
 BMI GETEVENT

 JSR INITMEM

 JSR CLS
 JSR FINDTBLS
 TST D0
 BMI EXISTS

 CLR LDINDEX
 JSR LDFIELDS
 TST D0
 BMI NOFLDS

 MOVE.L #TNAMED*OBSIZE,A0
 ADD.L TBLADR,A0
 MOVE.L #TNAME,OB_SPEC(A0)

 JSR DOACTION
 TST D0
 BMI GETEVENT
 TST OBJCNT
 BEQ CRAZY

 JSR CLS
 JSR CLRDIALS
 MOVE.L SUREADR,A0
 MOVE.L #-1,D0
 JSR FORMDO
 JSR CLS
 CMP #SUREYES,SAVERTRN
 BNE GETEVENT

 MOVE.L #39,D0
 LEA TNAME,A0
 LEA T1FNAME5,A1
SHER100 MOVE.B (A0)+,(A1)+
 DBF D0,SHER100

 JSR CLS
 JSR CLRACT
 MOVE.L WORKADR,A0
 MOVE #-1,D0
 JSR SHOWPANL
 JSR ACTIVITY

* COPY FNAME TO FNAME2 ADJUSTING FIELDS FNAME()

 JSR LOVELY
 MOVE.L #FNAMES2,FPTR

 CLR XCOUNT
 CLR XDISP

MOD100 CLR.L D0
 MOVE XCOUNT,D0
 MULU #RECSIZE,D0
 ADD.L #FIELDS2,D0
 MOVE.L D0,A4

 TST.L FNAME(A4)
 BEQ MOD101

 MOVE XDISP,FDISP(A4)
 CLR.L D0
 MOVE XDISP,D0
 ADD FSIZE(A4),D0
 MOVE D0,XDISP

 MOVE.L FNAME(A4),A0
 MOVE.L FPTR,FNAME(A4)
 JSR MODIT
 TST.L FOLDY(A4)
 BNE MOD102
 MOVE.L FNAME(A4),FOLDY(A4)
 JMP MOD103
MOD102 MOVE.L FOLDY(A4),A0
 MOVE.L FPTR,FOLDY(A4)
 JSR MODIT

MOD103 ADD #1,XCOUNT
 CMP #MAXFIELD,XCOUNT
 BNE MOD100

MOD101 ADD #2,XDISP
 CLR.L D0
 MOVE XDISP,D0
 MOVE.L D0,NEWSIZE

* LOAD FIELDS FOR TABLE

 JSR ACTIVITY
 CLR LDINDEX
 JSR FINDTBLS
 TST D0
 BMI INTERNAL

 JSR ACTIVITY
 JSR LDFIELDS
 TST D0
 BMI NOFLDS
 JSR CALROW
 MOVE.L ROWSIZE,OLDSIZE

 LEA T1FNAME,A0
 JSR F_OPEN
 TST D0
 BMI DISKERR
 MOVE D0,MHANDLE

* CREATE TEMP FILE

 JSR ACTIVITY
 JSR PARSEIT

 LEA T1FNAME2,A0
 LEA T1FNAME3,A1
 MOVE #39,D0
MOD498 MOVE.B (A0)+,(A1)+
 DBF D0,MOD498

 LEA T1FNAME,A0
 LEA T1FNAME4,A1
 MOVE #39,D0
MOD497 MOVE.B (A0)+,(A1)+
 DBF D0,MOD497

 CLR -(A7)  ;F_CREATE
 MOVE.L #T1FNAME2,-(A7)
 MOVE #$3C,-(A7)
 TRAP #1
 ADD #8,A7
 TST D0
 BMI DISKERR
 MOVE D0,MHANDLE2

* READ RECORD

MOD700 JSR ACTIVITY
 MOVE MHANDLE,A0
 LEA DBUFFER,A1
 MOVE.L OLDSIZE,D0
 JSR F_READ
 CMP.L OLDSIZE,D0
 BNE MOD400

* LOAD FNAME2 VALUES FROM DBUFFER

 JSR ACTIVITY
 CLR XCOUNT
 MOVE.L BIGBUFF,A2

MOD200 CLR.L D0
 MOVE XCOUNT,D0
 MULU #RECSIZE,D0
 ADD.L #FIELDS,D0
 MOVE.L D0,A4

 TST.L FNAME(A4)
 BEQ DOS199

 MOVE.L A2,FVALU(A4)

 LEA DBUFFER,A0
 CLR.L D0
 MOVE FDISP(A4),D0
 ADD.L D0,A0

 MOVE FTYPE(A4),D0
 CMP.B #'D',D0
 BEQ DOS106
 CMP.B #'I',D0
 BEQ DOS106

 CLR.L D3
 MOVE FSIZE(A4),D3
 SUB #1,D3
DOS107 MOVE.B (A0)+,(A2)+
 DBF D3,DOS107
 CLR.B (A2)+
 JMP DOS102

DOS106 CLR.L D2  ;LOAD NUMERIC VALUE
 CLR.L D3
 MOVE FEXTR(A4),D2
 MOVE FSIZE(A4),D3
 SUB D2,D3
 SUB #1,D3
 CLR FNEGT(A4)
 MOVE.B (A0)+,D0
 CMP.B #'-',D0
 BNE DOS109Z
 MOVE #-1,FNEGT(A4)

DOS109Z MOVE.B (A0),D0
 BEQ DOSNULL
 CMP.B #' ',D0
 BEQ DOSNULL

DOS109 CLR.L D2
 CLR.L D4
DOS110 CLR.L D0
 MOVE.B (A0),D0
 BEQ DOS115
 CMP.B #' ',D0
 BEQ DOS115
 MOVE #1,D4
 TST D2
 BNE DOS112
 CMP.B #'0',D0
 BEQ DOS113
 MOVE #1,D2
DOS112 MOVE.B (A0),(A2)+
DOS113 ADD.L #1,A0
 SUB #1,D3
 BNE DOS110

 TST D2
 BNE DOS111
 MOVE.B #'0',(A2)+
DOS111 MOVE.B #'.',(A2)+
 MOVE #1,D4

 MOVE FEXTR(A4),D3
 BEQ DOS115
DOS116 MOVE.B (A0),D0
 BEQ DOS115
 CMP.B #' ',D0
 BEQ DOS115
 MOVE.B (A0)+,(A2)+
 SUB #1,D3
 BNE DOS116
 JMP DOS117

DOS115 TST D3
 BEQ DOS117
 MOVE.B #'0',(A2)+
 SUB #1,D3
 JMP DOS115

DOS117 TST D4
 BNE DOS118
 MOVE.B #'0',(A2)+
 MOVE.B #'.',(A2)+
 JMP DOS118

DOSNULL CLR FNEGT(A4)
 MOVE.B #' ',(A2)+

DOS118 CLR.B (A2)

DOS102 ADD #1,XCOUNT
 CMP #MAXFIELD,XCOUNT
 BNE MOD200

* MATCH FNAME2 TO FNAME  COPY INFO TO FNAME ON MATCH

DOS199 JSR ACTIVITY
 CLR RCOUNT

MOD300 CLR.L D0
 MOVE RCOUNT,D0
 MULU #RECSIZE,D0
 ADD.L #FIELDS2,D0
 MOVE.L D0,A3

 CLR RCOUNT2

 TST.L FNAME(A3)
 BEQ MOD305

MOD306 CLR.L D0
 MOVE RCOUNT2,D0
 MULU #RECSIZE,D0
 ADD.L #FIELDS,D0
 MOVE.L D0,A4

 TST.L FNAME(A4)
 BEQ MOD302

 MOVE.L FOLDY(A3),A0
 MOVE.L FNAME(A4),A1
 JSR MATCHER
 TST D0
 BMI MOD303

 MOVE.L FVALU(A4),FVALU(A3)
 MOVE FNEGT(A4),FNEGT(A3)
 JMP MOD302

MOD303 ADD #1,RCOUNT2
 CMP #MAXFIELD,RCOUNT2
 BNE MOD306

MOD302 ADD #1,RCOUNT
 CMP #MAXFIELD,RCOUNT
 BNE MOD300

* BUILDROW

MOD305 MOVE #1,SPCLFLAG
 JSR BUILDROW
 CLR SPCLFLAG

* SAVE ROW TO TEMP FILE

 MOVE MHANDLE2,A0
 LEA DBUFFER,A1
 MOVE.L NEWSIZE,D0
 JSR F_WRITE
 CMP.L NEWSIZE,D0
 BNE DISKERR
 JSR ACTIVITY
 JMP MOD700

* READ ANOTHER RECORD UNTIL DONE

MOD400 MOVE MHANDLE,A0
 JSR F_CLOSE
 MOVE MHANDLE2,A0
 JSR F_CLOSE

 JSR REMOVE1  ;MOVE FIELD DEFS FROM TNAME2

* RENAME TEMP FILE TO .TBL EXTENDOR, DELETE OLD TABLE FILE

 MOVE.L #T1FNAME4,-(A7) ;F_DELETE
 MOVE #65,-(A7)
 TRAP #1
 ADD #6,A7

 MOVE.L #T1FNAME4,-(A7)  ;F_RENAME
 MOVE.L #T1FNAME3,-(A7)
 CLR -(A7)
 MOVE #86,-(A7)
 TRAP #1
 ADD #12,A7
 TST D0
 BMI DISKERR

 JSR ENTERFLD

 JSR DROPINDX

 MOVE #39,D0
 LEA T1FNAME5,A0
 LEA TNAME,A1
HOLMES1 MOVE.B (A0)+,(A1)+
 DBF D0,HOLMES1

 MOVE #39,D0
 LEA T1FNAME4,A0
 LEA T1FNAME,A1
HOLMES2 MOVE.B (A0)+,(A1)+
 DBF D0,HOLMES2

 JSR REORGANIZE

 JMP GETEVENT

RCOUNT DC 0
RCOUNT2 DC 0
SPCLFLAG DC 0
MHANDLE DC 0
MHANDLE2 DC 0

*-----------REMOVE TNAME2 FROM FIELDS.TBL-----------

REMOVE1 LEA FLDSPEC,A0
 JSR YSOLOAD

 JSR ACTIVITY
 JSR FINDTBLS
 TST D0
 BMI INTERNAL

 JSR ACTIVITY
 CLR LDINDEX
 JSR LDFIELDS
 TST D0
 BMI NOFLDS
 JSR CALROW

 JSR ACTIVITY
 LEA FLDSPEC,A0
 JSR ADDPATH
 JSR F_OPEN
 TST D0
 BMI DISKERR
 MOVE D0,MHANDLE

 CLR -(A7)  ;F_CREATE
 MOVE.L #TEMPNAME,-(A7)
 MOVE #$3C,-(A7)
 TRAP #1
 ADD #8,A7
 TST D0
 BMI DISKERR
 MOVE D0,MHANDLE2

* READ RECORD

MOD800 MOVE MHANDLE,A0
 MOVE.L #65,D0
 LEA DBUFFER,A1
 JSR F_READ
 TST D0
 BMI DISKERR
 CMP.L #65,D0
 BNE MOD801

* MATCH TO FNAME2

 LEA DBUFFER+20,A0
 MOVE.L #18,D0
MOD803 CMP.B #' ',(A0)
 BEQ MOD802
 TST.B (A0)
 BEQ MOD802
 ADD.L #1,A0
 DBF D0,MOD803
MOD802 CLR.B (A0)

 LEA TNAME2,A0
 MOVE.L #18,D0
MOD806 CMP.B #' ',(A0)
 BEQ MOD807
 TST.B (A0)
 BEQ MOD807
 ADD.L #1,A0
 DBF D0,MOD806
MOD807 CLR.B (A0)

 LEA DBUFFER+20,A0
 LEA TNAME2,A1
 JSR MATCHER
 TST D0
 BPL MOD800

* SAVE RECORD UNLESS NO MATCH

 MOVE MHANDLE2,A0
 LEA DBUFFER,A1
 MOVE.L ROWSIZE,D0
 JSR F_WRITE
 CMP.L ROWSIZE,D0
 BNE DISKERR
 JSR ACTIVITY
 JMP MOD800

MOD801 MOVE MHANDLE,A0
 JSR F_CLOSE
 MOVE MHANDLE2,A0
 JSR F_CLOSE

* RENAME TEMP FILE TO FIELDS.TBL

 LEA FLDSPEC,A0
 JSR ADDPATH
 MOVE.L A0,-(A7) ;F_DELETE
 MOVE #65,-(A7)
 TRAP #1
 ADD #6,A7

 LEA FLDSPEC,A0
 JSR ADDPATH
 MOVE.L A0,-(A7)  ;F_RENAME
 MOVE.L #TEMPNAME,-(A7)
 CLR -(A7)
 MOVE #86,-(A7)
 TRAP #1
 ADD #12,A7
 TST D0
 BMI DISKERR
 RTS

*-----------REMOVE TNAME2 FROM TABLES.TBL-----------

REMOVE2 LEA TBLSPEC,A0
 JSR YSOLOAD

 JSR ACTIVITY
 JSR FINDTBLS
 TST D0
 BMI INTERNAL

 JSR ACTIVITY
 CLR LDINDEX
 JSR LDFIELDS
 TST D0
 BMI NOFLDS
 JSR CALROW

 JSR ACTIVITY
 LEA TBLSPEC,A0
 JSR ADDPATH
 JSR F_OPEN
 TST D0
 BMI DISKERR
 MOVE D0,MHANDLE

 CLR -(A7)  ;F_CREATE
 MOVE.L #TEMPNAME,-(A7)
 MOVE #$3C,-(A7)
 TRAP #1
 ADD #8,A7
 TST D0
 BMI DISKERR
 MOVE D0,MHANDLE2

* READ RECORD

TMOD800 MOVE MHANDLE,A0
 MOVE.L ROWSIZE,D0
 LEA DBUFFER,A1
 JSR F_READ
 TST D0
 BMI DISKERR
 CMP.L ROWSIZE,D0
 BNE TMOD801

* MATCH TO FNAME2

 LEA DBUFFER,A0
 MOVE.L #18,D0
TMOD803 CMP.B #' ',(A0)
 BEQ TMOD802
 TST.B (A0)
 BEQ TMOD802
 ADD.L #1,A0
 DBF D0,TMOD803
TMOD802 CLR.B (A0)

 LEA TNAME2,A0
 MOVE.L #18,D0
TMOD806 CMP.B #' ',(A0)
 BEQ TMOD807
 TST.B (A0)
 BEQ TMOD807
 ADD.L #1,A0
 DBF D0,TMOD806
TMOD807 CLR.B (A0)

 LEA DBUFFER,A0
 LEA TNAME2,A1
 JSR MATCHER
 TST D0
 BPL TMOD800

* SAVE RECORD UNLESS NO MATCH

 MOVE MHANDLE2,A0
 LEA DBUFFER,A1
 MOVE.L ROWSIZE,D0
 JSR F_WRITE
 CMP.L ROWSIZE,D0
 BNE DISKERR
 JSR ACTIVITY
 JMP TMOD800

TMOD801 MOVE MHANDLE,A0
 JSR F_CLOSE
 MOVE MHANDLE2,A0
 JSR F_CLOSE

* RENAME TEMP FILE TO FIELDS.TBL

 LEA TBLSPEC,A0
 JSR ADDPATH
 MOVE.L A0,-(A7) ;F_DELETE
 MOVE #65,-(A7)
 TRAP #1
 ADD #6,A7

 LEA TBLSPEC,A0
 JSR ADDPATH
 MOVE.L A0,-(A7)  ;F_RENAME
 MOVE.L #TEMPNAME,-(A7)
 CLR -(A7)
 MOVE #86,-(A7)
 TRAP #1
 ADD #12,A7
 TST D0
 BMI DISKERR
 RTS

*-------DROP INDEXES FOR FILE IN T1FNAME5---------

DROPINDX LEA INDXSPEC,A0
 JSR YSOLOAD

 JSR ACTIVITY
 JSR FINDTBLS
 TST D0
 BMI INTERNAL

 JSR ACTIVITY
 CLR LDINDEX
 JSR LDFIELDS
 TST D0
 BMI NOFLDS
 JSR CALROW

 JSR ACTIVITY
 LEA INDXSPEC,A0
 JSR ADDPATH
 JSR F_OPEN
 TST D0
 BMI DISKERR
 MOVE D0,MHANDLE

 CLR -(A7)  ;F_CREATE
 MOVE.L #TEMPNAME,-(A7)
 MOVE #$3C,-(A7)
 TRAP #1
 ADD #8,A7
 TST D0
 BMI DISKERR
 MOVE D0,MHANDLE2

* READ RECORD

DI100 MOVE MHANDLE,A0
 MOVE.L #65,D0
 LEA DBUFFER,A1
 JSR F_READ
 TST D0
 BMI DISKERR
 CMP.L #65,D0
 BNE DI900

* MATCH TO T1FNAME5

 LEA DBUFFER+20,A0
 MOVE.L #18,D0
DI803 CMP.B #' ',(A0)
 BEQ DI802
 TST.B (A0)
 BEQ DI802
 ADD.L #1,A0
 DBF D0,DI803
DI802 CLR.B (A0)

 LEA T1FNAME5,A0
 MOVE.L #18,D0
DI806 CMP.B #' ',(A0)
 BEQ DI807
 TST.B (A0)
 BEQ DI807
 ADD.L #1,A0
 DBF D0,DI806
DI807 CLR.B (A0)

 LEA DBUFFER+20,A0
 LEA T1FNAME5,A1
 JSR MATCHER
 TST D0
 BPL DI100

* SAVE RECORD UNLESS NO MATCH

 MOVE MHANDLE2,A0
 LEA DBUFFER,A1
 MOVE.L ROWSIZE,D0
 JSR F_WRITE
 CMP.L ROWSIZE,D0
 BNE DISKERR
 JSR ACTIVITY
 JMP DI100

DI900 MOVE MHANDLE,A0
 JSR F_CLOSE
 MOVE MHANDLE2,A0
 JSR F_CLOSE

 MOVE.L #T1FNAME6,-(A7) ;F_DELETE
 MOVE #65,-(A7)
 TRAP #1
 ADD #6,A7

 LEA INDXSPEC,A0
 JSR ADDPATH
 MOVE.L A0,-(A7) ;F_DELETE
 MOVE #65,-(A7)
 TRAP #1
 ADD #6,A7

 LEA INDXSPEC,A0
 JSR ADDPATH
 MOVE.L A0,-(A7)  ;F_RENAME
 MOVE.L #TEMPNAME,-(A7)
 CLR -(A7)
 MOVE #86,-(A7)
 TRAP #1
 ADD #12,A7
 TST D0
 BMI DISKERR
 RTS

*------MOVE FIELD NAMES------

MODIT MOVE.L FPTR,A1
MODI1 TST.B (A0)
 BEQ MODI2
 MOVE.B (A0)+,(A1)+
 ADD.L #1,FPTR
 JMP MODI1
MODI2 CLR.B (A1)
 ADD.L #1,FPTR
 RTS

*------LOAD TABLE NAME------

YSOLOAD LEA TNAME,A1
YSO100 CMP.B #' ',(A0)
 BEQ YSO101
 CMP.B #'.',(A0)
 BEQ YSO101
 TST.B (A0)
 BEQ YSO101
 MOVE.B (A0)+,(A1)+
 JMP YSO100
YSO101 CLR.B (A1)
 RTS

*-----SAVE TABLE CONTENTS------

LOVELY MOVE.L #RECSIZE*MAXFIELD,D0
 SUB.L #1,D0
 LEA FIELDS,A0
 LEA FIELDS2,A1
PEEWEE2 MOVE.B (A0)+,(A1)+
 DBF D0,PEEWEE2

 LEA TNAME,A0
 LEA TNAME2,A1
 MOVE.L #21,D0
PEEWEE3 MOVE.B (A0)+,(A1)+
 DBF D0,PEEWEE3
 RTS

*----------PROCESS TABLE SCREEN-----------

DOACTION JSR CLS

 MOVE.L #CRTHUMB*OBSIZE,A1
 ADD.L TBLADR,A1
 CLR OB_Y(A1)

 CLR FILEY
 JSR CALCOBJ

 JSR DRAWIT

TABLEV JSR WAITMOUSE
 CMP #2,D0
 BEQ CHANGE

NOCHANGE MOVE.L TRASHADR,TREEADR

 MOVE #GOK,D0
 JSR OBJCFIND
 TST D0
 BEQ ACTDONE

 MOVE #GCANCEL,D0
 JSR OBJCFIND
 TST D0
 BEQ ACTBAD

 MOVE.L TBLADR,TREEADR

 MOVE #CRTHUMB,D0
 JSR OBJCFIND
 TST D0
 BEQ ATHUMB

 MOVE #CRSLIDE,D0
 JSR OBJCFIND
 TST D0
 BEQ APAGED

 MOVE #CRUPARW,D0
 JSR OBJCFIND
 TST D0
 BEQ AUP

 MOVE #CRDNARW,D0
 JSR OBJCFIND
 TST D0
 BEQ ADN

 MOVE #43,CONTRL
 MOVE #4,CONTRL+2
 MOVE #1,CONTRL+4
 MOVE #1,CONTRL+6
 CLR CONTRL+8
 CLR INTIN
 MOVE #5,INTIN+2
 MOVE MOUSEX,INTIN+4
 MOVE MOUSEY,INTIN+6
 MOVE.L CHOICDR,ADDRIN
 JSR AES
 TST INTOUT
 BMI USERBUTN

 CLR OBJTYPE
 CMP #TEXTS,INTOUT
 BEQ DRAGIT
 MOVE #1,OBJTYPE
 CMP #NUMBERS,INTOUT
 BEQ DRAGIT
 MOVE #2,OBJTYPE
 CMP #DATE,INTOUT
 BNE TABLEV

DRAGIT MOVE.L CHOICDR,A0
 CLR.L D2
 CLR.L D3
 MOVE OB_X(A0),D2
 MOVE OB_Y(A0),D3

 CLR.L D0
 MOVE INTOUT,D0
 MULU #OBSIZE,D0
 ADD.L D0,A0

 MOVE OB_WIDTH(A0),INTIN
 MOVE OB_HEIGHT(A0),INTIN+2

 CMP #2,GRMODE
 BNE BW1
 CLR.L D0
 MOVE INTIN+2,D0
 LSR.L #1,D0
 MOVE D0,INTIN+2

BW1 CLR.L D0
 MOVE OB_X(A0),D0
 ADD D2,D0
 ADD #86,D0
 MOVE D0,INTIN+4

 CLR.L D0
 MOVE OB_Y(A0),D0
 ADD D3,D0
 ADD #9,D0
 MOVE D0,INTIN+6

 MOVE #71,CONTRL    ;GRAF_DRAGBOX
 MOVE #8,CONTRL+2
 MOVE #3,CONTRL+4
 CLR CONTRL+6
 CLR CONTRL+8
 MOVE GEMXPOS,INTIN+8
 MOVE GEMYPOS,INTIN+10
 MOVE GEMWIDTH,INTIN+12
 MOVE GEMHOEHE,INTIN+14
 JSR AES
 TST INTOUT
 BEQ TABLEV

 MOVE #43,CONTRL  ;FIND
 MOVE #4,CONTRL+2
 MOVE #1,CONTRL+4
 MOVE #1,CONTRL+6
 CLR CONTRL+8
 CLR INTIN
 CLR INTIN+2
 MOVE INTOUT+2,INTIN+4
 MOVE INTOUT+4,INTIN+6
 MOVE.L TBLADR,ADDRIN
 JSR AES
 TST INTOUT
 BMI TABLEV

 JSR PRESET  ;RESET FIELD NAME

 CMP #1,OBJTYPE
 BEQ DONUMS
 CMP #2,OBJTYPE
 BEQ DODATE

*------TEXT------

 MOVE OBJCNT,FCOUNT
 JSR DOTEXTY
 JMP FLYAWAY

DOTEXTY JSR CLRDIALS
 MOVE.L TXTDIAL,A0
 MOVE #FTXNAME,D0
 JSR FORMDO
 CMP #CANCEL2,SAVERTRN
 BEQ REDOIT

 CLR.L D0
 MOVE FCOUNT,D0
 MULU #RECSIZE,D0
 ADD.L #FIELDS,D0
 MOVE.L D0,A4

 CLR.L D0
 MOVE.B #'C',D0
 MOVE D0,FTYPE(A4)

 MOVE.L FPTR,FNAME(A4)
 MOVE.L FPTR,A1
 MOVE.L TXTDIAL,A0
 ADD.L #FTXNAME*OBSIZE,A0
 JSR GETNAME

 CLR.L D1

 MOVE.L TXTDIAL,A0
 ADD.L #TEXTNUM*OBSIZE,A0
 MOVE.L OB_SPEC(A0),A0
 MOVE.L TE_PTEXT(A0),A2
 MOVE.L TE_PTEXT(A0),A0
 CLR.L D2
NUMAGN TST.B (A2)
 BEQ NUMFND
 CMP.B #'_',(A2)+
 BEQ NUMFND
 ADD #1,D2
 JMP NUMAGN

NUMFND TST D2
 BEQ NOLIKEY
 CMP #1,D2
 BEQ DOTEXT3
 CMP #2,D2
 BEQ DOTEXT2
 CMP #3,D2
 BEQ DOTEXT1
 CMP #5,D2
 BCC NOLIKEY

 CLR.L D0
 MOVE.B (A0)+,D0
 SUB.B #'0',D0
 MULU #1000,D0
 ADD D0,D1
DOTEXT1 CLR.L D0
 MOVE.B (A0)+,D0
 SUB.B #'0',D0
 MULU #100,D0
 ADD D0,D1
DOTEXT2 CLR.L D0
 MOVE.B (A0)+,D0
 SUB.B #'0',D0
 MULU #10,D0
 ADD D0,D1
DOTEXT3 CLR.L D0
 MOVE.B (A0)+,D0
 SUB.B #'0',D0
 ADD D0,D1
 TST D1
 BEQ NOLIKEY
 MOVE D1,FSIZE(A4)
 RTS

*------NUMERIC------

DONUMS MOVE OBJCNT,FCOUNT
 JSR DONUMSY
 JMP FLYAWAY

DONUMSY JSR CLRDIALS
 MOVE.L NUMDIAL,A0
 MOVE #FNMNAME,D0
 JSR FORMDO
 CMP #CANCEL3,SAVERTRN
 BEQ REDOIT

 CLR.L D0
 MOVE FCOUNT,D0
 MULU #RECSIZE,D0
 ADD.L #FIELDS,D0
 MOVE.L D0,A4

 CLR.L D0
 MOVE.B #'D',D0
 CMP #INTVALUE,SAVERTRN
 BNE WORM
 MOVE.L NUMDIAL,A0
 ADD.L #DECPOINT*OBSIZE,A0
 MOVE.L OB_SPEC(A0),A0
 MOVE.L TE_PTEXT(A0),A0
 MOVE.B #'0',(A0)+
 CLR.B (A0)
 MOVE.B #'I',D0
WORM MOVE D0,FTYPE(A4)

 MOVE.L FPTR,FNAME(A4)
 MOVE.L FPTR,A1
 MOVE.L NUMDIAL,A0
 ADD.L #FNMNAME*OBSIZE,A0
 JSR GETNAME

 MOVE.L NUMDIAL,A0
 ADD.L #DIGITS*OBSIZE,A0
 MOVE.L OB_SPEC(A0),A0
 MOVE.L TE_PTEXT(A0),A0

 CMP.B #'_',1(A0)
 BEQ DONUMS2
 TST.B 1(A0)
 BEQ DONUMS2

 CLR.L D0
 MOVE.B (A0),D0
 SUB.B #'0',D0
 MULU #10,D0
 CLR.L D1
 MOVE.B 1(A0),D1
 SUB.B #'0',D1
 ADD D1,D0
 TST D0
 BEQ NOLIKEY
 CMP #12,D0
 BCC NOLIKEY
 MOVE D0,FSIZE(A4)
 JMP DONUMS3

DONUMS2 CLR.L D0
 MOVE.B (A0),D0
 SUB.B #'0',D0
 TST D0
 BEQ NOLIKEY
 CMP #12,D0
 BCC NOLIKEY
 MOVE D0,FSIZE(A4)

DONUMS3 MOVE.L NUMDIAL,A0
 ADD.L #DECPOINT*OBSIZE,A0
 MOVE.L OB_SPEC(A0),A0
 MOVE.L TE_PTEXT(A0),A0
 CLR.L D0
 MOVE.B (A0),D0
 CMP.B #' ',D0
 BEQ DONUMS4
 CMP.B #'_',D0
 BEQ DONUMS4
 TST.B D0
 BEQ DONUMS4
 SUB.B #'0',D0
 CMP.B #10,D0
 BCC NOLIKEY
 MOVE D0,FEXTR(A4)
 RTS

DONUMS4 CLR FEXTR(A4)
 RTS

*------DATES------

DODATE MOVE OBJCNT,FCOUNT
 JSR DODATEY
 JMP FLYAWAY

DODATEY JSR CLRDIALS
 MOVE.L DTEDIAL,A0
 MOVE #FDTNAME,D0
 JSR FORMDO
 CMP #CANCEL1,SAVERTRN
 BEQ REDOIT

 CLR.L D0
 MOVE FCOUNT,D0
 MULU #RECSIZE,D0
 ADD.L #FIELDS,D0
 MOVE.L D0,A4

 CLR.L D0
 MOVE.B #'A',D0
 CMP #DATEA,SAVERTRN
 BEQ DODATE2
 MOVE.B #'L',D0
 CMP #DATEL,SAVERTRN
 BEQ DODATE2
 MOVE.B #'S',D0
 CMP #DATES,SAVERTRN
 BEQ DODATE2
 MOVE.B #'E',D0
DODATE2 MOVE D0,FTYPE(A4)
 MOVE #6,FSIZE(A4)

 MOVE.L FPTR,FNAME(A4)
 MOVE.L FPTR,A1
 MOVE.L DTEDIAL,A0
 ADD.L #FDTNAME*OBSIZE,A0
 JMP GETNAME

FLYAWAY TST OBJCNT
 BEQ FLY102
 CLR COUNT

FLY100 CLR.L D0
 MOVE COUNT,D0
 MULU #RECSIZE,D0
 ADD.L #FIELDS,D0
 MOVE.L D0,A0
 TST.L FNAME(A0)
 BEQ FLY102
 MOVE.L FNAME(A0),A0
 MOVE.L FNAME(A4),A1
 JSR MATCHER
 TST D0
 BPL FLYOOPS
 ADD #1,COUNT
 MOVE COUNT,D0
 CMP OBJCNT,D0
 BNE FLY100

FLY102 ADD #1,OBJCNT
 JSR DRAWIT
 JMP APAGEBOT

FLYOOPS CLR.L FNAME(A4)
 MOVE #FLDEXSTS,D0
 JSR DOALERT
 JSR DRAWIT
 JMP TABLEV

NOLIKEY MOVE #BADNUM,D0
 JSR DOALERT
REDOIT JSR DRAWIT
 JMP TABLEV

*---------CHANGE DEFINITION--------

CHANGE MOVE.L TBLADR,TREEADR
 CLR COUNT
CNG99 CLR.L D0
 MOVE COUNT,D0
 MULU #2,D0
 ADD.L #FLDS,D0
 MOVE.L D0,A0
 MOVE (A0),D0
 MOVE D0,USERNUM
 JSR OBJCFIND
 TST D0
 BEQ CNG100
 ADD #1,COUNT
 CMP #8,COUNT
 BNE CNG99
 JMP NOCHANGE

CNG100 CLR.L D0
 MOVE COUNT,D0
 ADD FILEY,D0
 CMP OBJCNT,D0
 BCC NOCHANGE

 MOVE D0,FCOUNT
 CLR.L D0
 MOVE FCOUNT,D0
 MULU #RECSIZE,D0
 ADD.L #FIELDS,D0
 MOVE.L D0,A4

 TST.L FNAME(A4)
 BEQ TABLEV

 MOVE.L FNAME(A4),A0

 MOVE FTYPE(A4),D0
 CMP.B #'C',D0
 BEQ ITSTEXT
 CMP.B #'D',D0
 BEQ ITSNUM
 CMP.B #'I',D0
 BEQ ITSNUM

 MOVE.L DTEDIAL,A1
 ADD.L #FDTNAME*OBSIZE,A1
 JSR ITSLOAD
 JSR DODATEY
 JMP CHNGDONE

ITSTEXT MOVE.L TXTDIAL,A1
 ADD.L #FTXNAME*OBSIZE,A1
 JSR ITSLOAD
 MOVE.L TXTDIAL,A0
 ADD.L #TEXTNUM*OBSIZE,A0
 MOVE.L OB_SPEC(A0),A0
 MOVE.L TE_PTEXT(A0),A3
 CLR.L D0
 MOVE FSIZE(A4),D0
 JSR CONV2DEC
 JSR DOTEXTY
 JMP CHNGDONE

ITSNUM MOVE.L NUMDIAL,A1
 ADD.L #FNMNAME*OBSIZE,A1
 JSR ITSLOAD

 MOVE.L NUMDIAL,A0
 ADD.L #DIGITS*OBSIZE,A0
 MOVE.L OB_SPEC(A0),A0
 MOVE.L TE_PTEXT(A0),A0
 CLR.L D0
 MOVE FSIZE(A4),D0
 CMP #10,D0
 BCS NOTENS
 MOVE.B #'1',(A0)+
 SUB #10,D0
NOTENS ADD.B #'0',D0
 MOVE.B D0,(A0)+
 CLR.B (A0)

 MOVE.L NUMDIAL,A0
 ADD.L #DECPOINT*OBSIZE,A0
 MOVE.L OB_SPEC(A0),A0
 MOVE.L TE_PTEXT(A0),A0
 MOVE FEXTR(A4),D0
 TST D0
 BEQ NOEXTR
 ADD.B #'0',D0
 MOVE.B D0,(A0)+
 JMP CHNG200
NOEXTR MOVE.B #' ',(A0)+
CHNG200 CLR.B (A0)
 JSR DONUMSY

CHNGDONE CLR COUNT
 TST OBJCNT
 BEQ SIMONE2
 TST FCOUNT
 BEQ SIMONE2

SIMONE CLR.L D0
 MOVE COUNT,D0
 MULU #RECSIZE,D0
 ADD.L #FIELDS,D0
 MOVE.L D0,A0
 TST.L FNAME(A0)
 BEQ SIMONE2
 MOVE.L FNAME(A0),A0
 MOVE.L FNAME(A4),A1
 JSR MATCHER
 TST D0
 BPL SIMONE3

SIMONE4 ADD #1,COUNT
 MOVE COUNT,D0
 CMP FCOUNT,D0
 BEQ SIMONE4

 MOVE COUNT,D0
 CMP OBJCNT,D0
 BNE SIMONE

SIMONE2 JSR DRAWIT
 JMP TABLEV

SIMONE3 MOVE.L FOLDY(A4),FNAME(A4)
 JMP FLYOOPS

*-----LOAD FIELD NAME------

ITSLOAD MOVE.L OB_SPEC(A1),A1
 MOVE.L TE_PTEXT(A1),A1
 MOVE #19,D0
ITS101 TST.B (A0)
 BEQ ITS102
 MOVE.B (A0)+,(A1)+
 DBF D0,ITS101
ITS102 CLR.B (A1)
 RTS

PRESET MOVE.L DTEDIAL,A1
 ADD.L #FDTNAME*OBSIZE,A1
 JSR PRESET1
 MOVE.L TXTDIAL,A1
 ADD.L #FTXNAME*OBSIZE,A1
 JSR PRESET1
 MOVE.L NUMDIAL,A1
 ADD.L #FNMNAME*OBSIZE,A1
PRESET1 MOVE.L OB_SPEC(A1),A1
 MOVE.L TE_PTEXT(A1),A1
 MOVE.B #'@',(A1)
 RTS

*------------GET FIELD NAME-------------

GETNAME MOVE.L OB_SPEC(A0),A0
 MOVE.L TE_PTEXT(A0),A2
 MOVE.L TE_PTEXT(A0),A0
 CLR.L D2
 MOVE.L #19,D1
GET100 MOVE.B (A0)+,D0
 BEQ GETNDN
 CMP.B #'@',D0
 BEQ GETNDN
 CMP.B #' ',D0
 BEQ GETNDN

 CMP.B #'a',D0
 BCS GET101
 CMP.B #'z'+1,D0
 BCC GET101
 SUB.B #32,D0

GET101 MOVE.B D0,(A1)+
 ADD.L #1,FPTR
 MOVE #1,D2
 DBF D1,GET100
GETNDN CLR.B (A1)+
 ADD.L #1,FPTR
 TST D2
 BEQ NOTNMD
 MOVE.B #'@',(A2)
 RTS

NOTNMD MOVE.B #'@',(A2)
 MOVE #NONAME,D0
 JSR DOALERT
 JSR DRAWIT
 JMP TABLEV

ACTDONE JSR CLS
 CLR.L D0
 RTS

ACTBAD JSR CLS
 MOVE #-1,D0
 RTS

*-----------------DRAG EXISTING FIELD-------------

USERBUTN MOVE.L TBLADR,TREEADR
 CLR COUNT
USER99 CLR.L D0
 MOVE COUNT,D0
 MULU #2,D0
 ADD.L #FLDS,D0
 MOVE.L D0,A0
 MOVE (A0),D0
 MOVE D0,USERNUM
 JSR OBJCFIND
 TST D0
 BEQ USER100
 ADD #1,COUNT
 CMP #8,COUNT
 BNE USER99
 JMP TABLEV

USER100 MOVE COUNT,D0
 ADD FILEY,D0
 CMP OBJCNT,D0
 BCC TABLEV

 MOVE.L TBLADR,A0
 CLR.L D2
 CLR.L D3
 MOVE OB_X(A0),D2
 MOVE OB_Y(A0),D3

 CLR.L D0
 MOVE USERNUM,D0
 MULU #OBSIZE,D0
 ADD.L D0,A0

 MOVE OB_WIDTH(A0),INTIN
 MOVE OB_HEIGHT(A0),INTIN+2

 CMP #2,GRMODE
 BNE UBW1
 CLR.L D0
 MOVE INTIN+2,D0
 LSR.L #1,D0
 MOVE D0,INTIN+2

UBW1 CLR.L D0
 MOVE OB_X(A0),D0
 ADD D2,D0
 ADD #86,D0
 MOVE D0,INTIN+4

 CLR.L D0
 MOVE OB_Y(A0),D0
 ADD D3,D0
 ADD #9,D0
 MOVE D0,INTIN+6

 MOVE #71,CONTRL    ;GRAF_DRAGBOX
 MOVE #8,CONTRL+2
 MOVE #3,CONTRL+4
 CLR CONTRL+6
 CLR CONTRL+8
 MOVE GEMXPOS,INTIN+8
 MOVE GEMYPOS,INTIN+10
 MOVE GEMWIDTH,INTIN+12
 MOVE GEMHOEHE,INTIN+14
 JSR AES
 TST INTOUT
 BEQ TABLEV

 MOVE #79,CONTRL  ;MOUSE STATE
 CLR CONTRL+2
 MOVE #5,CONTRL+4
 CLR CONTRL+6
 CLR CONTRL+8
 JSR AES

 MOVE #43,CONTRL
 MOVE #4,CONTRL+2
 MOVE #1,CONTRL+4
 MOVE #1,CONTRL+6
 CLR CONTRL+8
 CLR INTIN
 CLR INTIN+2
 MOVE INTOUT+2,INTIN+4
 MOVE INTOUT+4,INTIN+6
 MOVE.L TRASHADR,ADDRIN
 JSR AES
 TST INTOUT
 BMI TABLEV

 MOVE #TRASHICN*OBSIZE,A0
 ADD.L TRASHADR,A0
 MOVE #1,OB_STATE(A0)
 MOVE #TRASHICN,D0
 MOVE.L TRASHADR,TREEADR
 JSR OBJCDRAW

USER103 MOVE COUNT,D0
 ADD FILEY,D0
 MULU #RECSIZE,D0
 ADD.L #FIELDS,D0
 MOVE.L D0,A0

 MOVE.L #RECSIZE-1,D1
USER102 MOVE.B RECSIZE(A0),(A0)+
 DBF D1,USER102

 ADD #1,COUNT
 CMP #MAXFIELD,COUNT
 BNE USER103

 SUB #1,OBJCNT

 MOVE #TRASHICN*OBSIZE,A0
 ADD.L TRASHADR,A0
 CLR OB_STATE(A0)
 MOVE #TRASHICN,D0
 MOVE.L TRASHADR,TREEADR
 JSR OBJCDRAW
 JSR SHOWTBL
 JMP TABLEV

USERNUM DC 0

*--------------SLIDE BAR ACTION-------------

APAGED MOVE.L #CRTHUMB,D0
 JSR ACALC

 MOVE #85,D1
 CMP #2,GRMODE
 BNE APAGED2
 MOVE #170,D1

APAGED2 CLR.L D0
 MOVE OB_Y(A0),D0
 ADD D1,D0
 CMP MOUSEY,D0
 BCC APAGEUP

APAGEDN CMP #8,OBJCNT
 BCS TABLEV

 CLR.L D0
 MOVE FILEY,D0
 ADD #16,D0
 CMP OBJCNT,D0
 BCC APAGEBOT
 ADD #8,FILEY
 JMP AKINKY

APAGEBOT CMP #8,OBJCNT
 BCS AKINKY
 CLR.L D0
 MOVE OBJCNT,D0
 SUB #8,D0
 MOVE D0,FILEY
 JMP AKINKY

APAGEUP CMP #8,FILEY
 BCS APAGETOP
 SUB #8,FILEY
 JMP AKINKY
APAGETOP CLR FILEY
 JMP AKINKY

AUP CMP #4,FILEY
 BCS APAGETOP
 SUB #4,FILEY
 JMP AKINKY

ADN CMP #8,OBJCNT
 BCS TABLEV

 CLR.L D0
 MOVE FILEY,D0
 ADD #12,D0
 CMP OBJCNT,D0
 BCC APAGEBOT
 ADD #4,FILEY
 JMP AKINKY

ATHUMB MOVE #76,CONTRL  ;GRAF_SLIDBOX  DRAG IT
 MOVE #3,CONTRL+2
 MOVE #1,CONTRL+4
 MOVE #1,CONTRL+6
 CLR CONTRL+8
 MOVE #CRSLIDE,INTIN
 MOVE #CRTHUMB,INTIN+2
 MOVE #1,INTIN+4
 MOVE.L TBLADR,ADDRIN
 JSR AES
 CMP #8,OBJCNT
 BCS TABLEV
 MOVE INTOUT,HOLDER

 MOVE.L #CRSLIDE,D0
 JSR ACALC

 CLR.L D0
 MOVE OBJCNT,D0
 MOVE.L #1000,D1
 DIVU D0,D1
 AND.L #$FFFF,D1
 CLR.L D2
 MOVE HOLDER,D2
 DIVU D1,D2
 AND.L #$FFFF,D2

 MOVE.L D2,D0
 ADD #16,D0
 CMP OBJCNT,D0
 BCC APAGEBOT
 MOVE D2,FILEY

AKINKY JSR SHOWTBL

 CLR SLIDEPOS
 CLR.L D1
 MOVE OBJCNT,D1
 CMP #8,D1
 BCS AKINKTOP
 SUB #8,D1

 CLR.L D0
 MOVE FILEY,D0
 MULU #1000,D0
 DIVU D1,D0
 AND.L #$FFFF,D0
 MOVE D0,SLIDEPOS

AKINKTOP MOVE.L #CRTHUMB,D0
 JSR ACALC
 CLR.L D3
 MOVE OB_HEIGHT(A0),D3

 MOVE.L #CRSLIDE,D0
 JSR ACALC

 CLR.L D0
 MOVE OB_HEIGHT(A0),D0
 SUB #1,D0
 SUB D3,D0

 MULU SLIDEPOS,D0
 DIVU #1000,D0
 MOVE D0,D2

 MOVE.L #CRTHUMB,D0
 JSR ACALC
 MOVE D2,OB_Y(A0)

 MOVE.L TBLADR,TREEADR
 MOVE #CRSLIDE,D0
 JSR OBJCDRAW
 MOVE #CRTHUMB,D0
 JSR OBJCDRAW
 JMP TABLEV

ACALC MULU #OBSIZE,D0
 ADD.L TBLADR,D0
 MOVE.L D0,A0
 RTS

*----------COUNT OBJECTS---------

CALCOBJ CLR OBJCNT

CALO10 CLR.L D0
 MOVE OBJCNT,D0
 MULU #RECSIZE,D0
 ADD.L #FIELDS,D0
 MOVE.L D0,A0
 TST.L FNAME(A0)
 BEQ CALO20
 TST FSIZE(A0)
 BEQ CALO20
 ADD #1,OBJCNT
 JMP CALO10

CALO20 RTS

*--------CLEAR DIALOG FIELDS-----------

CLRDIALS MOVE.L TRASHADR,A0
 ADD.L #GOK*OBSIZE,A0
 CLR OB_STATE(A0)

 MOVE.L TRASHADR,A0
 ADD.L #GCANCEL*OBSIZE,A0
 CLR OB_STATE(A0)

 MOVE.L TXTDIAL,A0
 ADD.L #TEXTNUM*OBSIZE,A0
 CLR OB_STATE(A0)
 MOVE.L TXTDIAL,A0
 ADD.L #FTXNAME*OBSIZE,A0
 CLR OB_STATE(A0)
 MOVE.L TXTDIAL,A0
 ADD.L #CANCEL2*OBSIZE,A0
 CLR OB_STATE(A0)

 MOVE.L NUMDIAL,A0
 ADD.L #INTVALUE*OBSIZE,A0
 CLR OB_STATE(A0)
 MOVE.L NUMDIAL,A0
 ADD.L #DECVALUE*OBSIZE,A0
 CLR OB_STATE(A0)
 MOVE.L NUMDIAL,A0
 ADD.L #CANCEL3*OBSIZE,A0
 CLR OB_STATE(A0)

 MOVE.L DTEDIAL,A0
 ADD.L #DATEA*OBSIZE,A0
 CLR OB_STATE(A0)
 MOVE.L DTEDIAL,A0
 ADD.L #DATEL*OBSIZE,A0
 CLR OB_STATE(A0)
 MOVE.L DTEDIAL,A0
 ADD.L #DATES*OBSIZE,A0
 CLR OB_STATE(A0)
 MOVE.L DTEDIAL,A0
 ADD.L #DATEE*OBSIZE,A0
 CLR OB_STATE(A0)
 MOVE.L DTEDIAL,A0
 ADD.L #CANCEL1*OBSIZE,A0
 CLR OB_STATE(A0)

 MOVE.L SUREADR,A0
 ADD.L #SUREYES*OBSIZE,A0
 CLR OB_STATE(A0)
 MOVE.L SUREADR,A0
 ADD.L #SURENO*OBSIZE,A0
 CLR OB_STATE(A0)

 MOVE.L DRVADR,A0
 ADD.L #DRVNAME*OBSIZE,A0
 CLR OB_STATE(A0)
 MOVE.L DRVADR,A0
 ADD.L #DRVCNCL*OBSIZE,A0
 CLR OB_STATE(A0)

 MOVE.L BADDADR,A0
 ADD.L #BADCONT*OBSIZE,A0
 CLR OB_STATE(A0)

 RTS

*---------------GET TABLE NAME---------------

GETTNAME MOVE.L NAMEDIAL,A0
 ADD.L #TBLNAME*OBSIZE,A0
 CLR OB_STATE(A0)
 MOVE.L OB_SPEC(A0),A0
 MOVE.L TE_PTEXT(A0),A0
 MOVE.B #'@',(A0)

 MOVE.L NAMEDIAL,A0
 MOVE #TBLNAME,D0
 JSR FORMDO

 MOVE.L NAMEDIAL,A0
 ADD.L #TBLNAME*OBSIZE,A0
 MOVE.L OB_SPEC(A0),A0
 MOVE.L TE_PTEXT(A0),A0

 CLR.L D2
 LEA TBLED,A2
 LEA TNAME,A3
 MOVE.L #19,D0
TUT100 MOVE.B (A0)+,D1
 BEQ TUT101
 CMP.B #'_',D1
 BEQ TUT101
 CMP.B #'@',D1
 BEQ TUT101

 CMP.B #'a',D1
 BCS TUT1X1
 CMP.B #'z'+1,D1
 BCC TUT1X1
 SUB.B #32,D1

TUT1X1 MOVE.B D1,(A2)+
 MOVE.B D1,(A3)+
 MOVE #1,D2
 DBF D0,TUT100

TUT101 CLR.B (A3)
 CLR.B (A2)
 TST D2
 BNE TUT102
 MOVE #1,D0
 RTS

TUT102 MOVE.L TBLADR,A1
 ADD.L #TNAMED*OBSIZE,A1
 MOVE.L #TBLED,OB_SPEC(A1)
 CLR D0
 RTS

*----------UPDATE SCREEN----------

DRAWIT MOVE.L CHOICDR,TREEADR
 MOVE.L CHOICDR,A0
 MOVE #13,OB_Y(A0)  ;B&W CHANGE
 MOVE #10,OB_X(A0)

 CMP #2,GRMODE
 BNE BWT1
 MOVE #26,OB_Y(A0)
BWT1 JSR DRAWTREE

 MOVE.L TRASHADR,TREEADR
 MOVE.L TRASHADR,A0
 MOVE #10,OB_Y(A0)  ;B&W CHANGE
 MOVE #460,OB_X(A0)

 CMP #2,GRMODE
 BNE BWT2
 MOVE #20,OB_Y(A0)
BWT2 JSR DRAWTREE

 MOVE.L TBLADR,TREEADR
 MOVE.L TBLADR,A0
 MOVE #68,OB_Y(A0)  ;B&W CHANGE
 MOVE #20,OB_X(A0)

 CMP #2,GRMODE
 BNE BWT3
 MOVE #136,OB_Y(A0)
BWT3 JSR DRAWTREE

*------------SHOW TABLE CONTENTS------------

SHOWTBL MOVE FILEY,COUNT
 CLR COUNT2

SHOW1 CLR.L D0
 MOVE COUNT,D0
 MULU #RECSIZE,D0
 ADD.L #FIELDS,D0
 MOVE.L D0,A4

 TST.L FNAME(A4)
 BEQ SHOWIT

 CLR.L D0           ;SET ICON PATTERN
 MOVE #TEXTS,D0
 MOVE FTYPE(A4),D1
 CMP.B #'C',D1
 BEQ SHOW2
 MOVE #NUMBERS,D0
 CMP.B #'D',D1
 BEQ SHOW2
 CMP.B #'I',D1
 BEQ SHOW2
 MOVE #DATE,D0
SHOW2 MULU #OBSIZE,D0
 ADD.L CHOICDR,D0
 MOVE.L D0,A1
 MOVE.L OB_SPEC(A1),A1

 CLR.L D0
 MOVE COUNT2,D0
 MULU #2,D0
 MOVE.L D0,A0
 ADD.L #ICONS,A0
 CLR.L D1
 MOVE (A0),D1
 MULU #OBSIZE,D1
 ADD.L TBLADR,D1
 MOVE.L D1,A0
 MOVE.L OB_SPEC(A0),A0
 MOVE.L IB_PDATA(A1),IB_PDATA(A0)

 CLR.L D0        ;SET FIELD NAME
 MOVE COUNT2,D0
 MULU #2,D0
 MOVE.L D0,A0
 ADD.L #NAMEL,A0
 CLR.L D1
 MOVE (A0),D1
 MULU #OBSIZE,D1
 ADD.L TBLADR,D1
 MOVE.L D1,A0
 MOVE.L FNAME(A4),OB_SPEC(A0)

 CLR.L D0        ;SET TYPE
 MOVE FTYPE(A4),D0
 LEA DTYPE,A0
 CMP.B #'D',D0
 BEQ TYPED
 LEA ITYPE,A0
 CMP.B #'I',D0
 BEQ TYPED
 LEA ATYPE,A0
 CMP.B #'A',D0
 BEQ TYPED
 LEA ETYPE,A0
 CMP.B #'E',D0
 BEQ TYPED
 LEA STYPE,A0
 CMP.B #'S',D0
 BEQ TYPED
 LEA LTYPE,A0
 CMP.B #'L',D0
 BEQ TYPED
 LEA CTYPE,A0
TYPED CLR.L D0
 MOVE COUNT2,D0
 MULU #2,D0
 MOVE.L D0,A1
 ADD.L #TYPEL,A1
 CLR.L D1
 MOVE (A1),D1
 MULU #OBSIZE,D1
 ADD.L TBLADR,D1
 MOVE.L D1,A1
 MOVE.L A0,OB_SPEC(A1)

 CLR.L D0        ;SET FIELD SIZE
 MOVE COUNT2,D0
 MULU #2,D0
 MOVE.L D0,A0
 ADD.L #SIZEL,A0
 CLR.L D1
 MOVE (A0),D1
 MULU #OBSIZE,D1
 ADD.L TBLADR,D1
 MOVE.L D1,A0
 MOVE.L OB_SPEC(A0),A3
 CLR.L D0
 MOVE FSIZE(A4),D0
 JSR CONV2DEC

 CLR.L D0        ;SET FIELD EXT
 MOVE COUNT2,D0
 MULU #2,D0
 MOVE.L D0,A0
 ADD.L #DECL,A0
 CLR.L D1
 MOVE (A0),D1
 MULU #OBSIZE,D1
 ADD.L TBLADR,D1
 MOVE.L D1,A0
 MOVE.L OB_SPEC(A0),A3
 CLR.L D0
 MOVE FEXTR(A4),D0
 TST.B D0
 BNE BILLYH
 MOVE.B #' ',D0
 JMP JAZZ
BILLYH ADD.B #'0',D0
JAZZ MOVE.B D0,(A3)
 CLR.B 1(A3)

 ADD #1,COUNT
 ADD #1,COUNT2
 CMP #8,COUNT2
 BCS SHOW1
 JMP SHOWIT

SHOWIT CLR COUNT
 MOVE #6,SHOWTMP

 TST OBJCNT
 BNE SHOWLP1
 CLR SHOWTMP

SHOWLP1 CLR.L D0
 MOVE COUNT,D0
 MULU #2,D0
 ADD.L #FLDS,D0
 MOVE.L D0,A0
 CLR.L D0
 MOVE (A0),D0
 MOVE SHOWTMP,D2

 MOVE #42,CONTRL ;OBJC_DRAW
 MOVE #6,CONTRL+2
 MOVE #1,CONTRL+4
 MOVE #1,CONTRL+6
 CLR CONTRL+8
 MOVE D0,INTIN
 MOVE D2,INTIN+2
 MOVE GEMXPOS,INTIN+4
 MOVE GEMYPOS,INTIN+6
 MOVE GEMWIDTH,INTIN+8
 MOVE GEMHOEHE,INTIN+10
 MOVE.L TBLADR,ADDRIN
 DC $A00A
 JSR AES
 DC $A009

 ADD #1,COUNT
 CMP #8,COUNT
 BEQ SHOWLP2

 MOVE OBJCNT,D0
 CMP COUNT,D0
 BNE SHOWLP1
 CLR SHOWTMP
 JMP SHOWLP1

SHOWLP2 RTS

CTYPE DC.B 'Char ',0
DTYPE DC.B 'Dec  ',0
ITYPE DC.B 'Int  ',0
ATYPE DC.B 'Date ',0
ETYPE DC.B 'EDate',0
STYPE DC.B 'SDate',0
LTYPE DC.B 'LDate',0
ZNULL DC.B 0,0
 EVEN

SHOWTMP DC 0

*----------INIT MEM STORAGE---------

INITMEM MOVE.L #FIELDSIZ-1,D0
 LEA FNAMES,A0
 LEA FNAMES,A2
 LEA INAMES,A1
IM100 CLR.B (A0)+
 CLR.B (A1)+
 CLR.B (A2)+
 DBF D0,IM100

 MOVE.L #RECSIZE*MAXFIELD,D0
 SUB.L #1,D0
 LEA FIELDS,A0
 LEA INDXBUFF,A1
IM101 CLR.B (A0)+
 CLR.B (A1)+
 DBF D0,IM101
 RTS

*-------------EXPORT A TABLE-------------

EXPORTIT JSR CLS  ;WHAT TYPE OF DATA?

 MOVE.L #UINFO*OBSIZE,A0
 ADD.L TABLADR,A0
 MOVE.L STRINGADR,A1
 ADD #EXPMES1*OBSIZE,A1
 MOVE.L OB_SPEC(A1),OB_SPEC(A0)
 JSR GETTABLE
 TST D0
 BMI GETEVENT
 JSR CLS

 JSR FINDTBLS
 TST D0
 BMI NOTABLE

 CLR LDINDEX
 JSR LDFIELDS
 TST D0
 BMI NOFLDS

 MOVE #28,XPOS
 MOVE #3,YPOS
 JSR TEXTINIT
 MOVE.L STRINGADR,A0
 ADD #ASK2MES*OBSIZE,A0
 MOVE.L OB_SPEC(A0),A0
 JSR GEMTEXT

 JSR GETFILE
 JSR CLS
 TST INTOUT
 BEQ GETEVENT
 TST INTOUT+2
 BEQ GETEVENT

 JSR CLRACT
 MOVE.L WORKADR,A0
 MOVE #-1,D0
 JSR SHOWPANL

 JSR CALROW
 JSR DERIVE
 JSR ACTIVITY

 CLR -(A7)      ;F_CREATE
 MOVE.L #FILENAME,-(A7)
 MOVE #60,-(A7)
 TRAP #1
 ADD #8,A7
 TST D0
 BMI DISKERR
 MOVE D0,FHANDLE

 JSR ACTIVITY

 LEA T1FNAME,A0
 JSR F_OPEN
 TST D0
 BMI DISKERR
 MOVE D0,DHANDLE

EXPD99 MOVE DHANDLE,A0
 MOVE.L #DBUFFER,A1
 MOVE.L ROWSIZE,D0
 JSR F_READ
 CMP.L ROWSIZE,D0
 BNE EXOVER

 MOVE.L BIGBUFF,A3

 JSR ACTIVITY

 CLR FCOUNT
EXPD100 CLR.L D0
 MOVE FCOUNT,D0
 MULU #RECSIZE,D0
 ADD.L #FIELDS,D0
 MOVE.L D0,A4

 TST.L FNAME(A4)
 BEQ EXPDWRIT

 CLR.L D0
 MOVE FDISP(A4),D0
 ADD.L #DBUFFER,D0
 MOVE.L D0,A0

 TST.B (A0)  ;Null field?
 BEQ EXPDRCRS

 MOVE FTYPE(A4),D0
 CMP.B #'C',D0
 BEQ EXCHAR
 CMP.B #'I',D0
 BEQ EXINT
 CMP.B #'D',D0
 BEQ EXDEC

 MOVE.B #'"',(A3)+
 MOVE.B #'1',(A3)+
 MOVE.B #'9',(A3)+
 MOVE.B 4(A0),(A3)+
 MOVE.B 5(A0),(A3)+
 MOVE.B (A0),(A3)+
 MOVE.B 1(A0),(A3)+
 MOVE.B 2(A0),(A3)+
 MOVE.B 3(A0),(A3)+
 MOVE.B #'"',(A3)+
 JMP EXPDRCRS

EXCHAR MOVE.B #34,(A3)+

 CLR.L D0
 MOVE FSIZE(A4),D0
 SUB #1,D0
EXCH2 MOVE.B (A0)+,D2
 TST.B D2
 BEQ EXCH3
 MOVE.B D2,(A3)+
 DBF D0,EXCH2

EXCH3 MOVE.B #34,(A3)+
 JMP EXPDRCRS

EXINT CMP.B #'-',(A0)+
 BNE EXIN2
 MOVE.B #'-',(A3)+

EXIN2 TST.B (A0)
 BEQ EXPDRCRS

 CLR.L D0
 CLR.L D1
 CLR.L D2
 MOVE FSIZE(A4),D0
 SUB #2,D0

EXIN3 MOVE.B (A0)+,D2
 TST D1
 BNE PEOPLE
 CMP.B #'0',D2
 BEQ SURFACE
 MOVE #1,D1
PEOPLE MOVE.B D2,(A3)+
SURFACE DBF D0,EXIN3
 JMP EXPDRCRS

EXDEC CMP.B #'-',(A0)+
 BNE EXDE2
 MOVE.B #'-',(A3)+

EXDE2 TST.B (A0)
 BEQ EXPDRCRS

 CLR.L D0
 CLR.L D1
 CLR.L D2
 MOVE FSIZE(A4),D0
 SUB FEXTR(A4),D0
 SUB #2,D0

EXDE3 MOVE.B (A0)+,D2
 TST D1
 BNE DPEOPLE
 CMP.B #'0',D2
 BEQ DSURFACE
 MOVE #1,D1
DPEOPLE MOVE.B D2,(A3)+
DSURFACE DBF D0,EXDE3

 MOVE.B #'.',(A3)+

 CLR.L D2
 CLR.L D0
 MOVE FEXTR(A4),D0
 SUB #1,D0

EXDE4 MOVE.B (A0)+,D2
 MOVE.B D2,(A3)+
 DBF D0,EXDE4

EXPDRCRS MOVE.B #',',(A3)+
 ADD #1,FCOUNT
 JMP EXPD100

EXPDWRIT SUB.L #1,A3
 MOVE.B #13,(A3)+
 MOVE.B #10,(A3)+

 MOVE.L A3,D0
 SUB.L BIGBUFF,D0
 MOVE.L D0,BUFFCNT
 MOVE FHANDLE,A0
 MOVE.L BIGBUFF,A1
 JSR F_WRITE
 CMP.L BUFFCNT,D0
 BNE DISKFUL

 MOVE #11,-(A7)  ;KEYSTOP
 TRAP #1
 ADD #2,A7
 TST D0
 BEQ EXPD99

 MOVE #11,-(A7)  ;KEYPRESSED?
 TRAP #1
 ADD #2,A7
 TST D0
 BEQ EXPD99
 JSR WAIT_KEY
 CMP #27,D0
 BNE EXPD99
 MOVE FHANDLE,A0
 JSR F_CLOSE
 MOVE DHANDLE,A0
 JSR F_CLOSE
 JSR CLS
 JMP GETEVENT

EXOVER MOVE FHANDLE,A0
 JSR F_CLOSE
 MOVE DHANDLE,A0
 JSR F_CLOSE

 JSR REORGANIZE

 JMP GETEVENT

*-------------IMPORT A TABLE-------------

IMPORTIT JSR CLS

 MOVE.L #UINFO*OBSIZE,A0
 ADD.L TABLADR,A0
 MOVE.L STRINGADR,A1
 ADD #IMPMES1*OBSIZE,A1
 MOVE.L OB_SPEC(A1),OB_SPEC(A0)
 JSR GETTABLE
 TST D0
 BMI GETEVENT
 JSR CLS

 JSR FINDTBLS
 TST D0
 BMI NOTABLE

 CLR LDINDEX
 JSR LDFIELDS
 TST D0
 BMI NOFLDS

 MOVE #22,XPOS
 MOVE #3,YPOS
 JSR TEXTINIT
 MOVE.L STRINGADR,A0
 ADD #ASK1MES*OBSIZE,A0
 MOVE.L OB_SPEC(A0),A0
 JSR GEMTEXT

 JSR GETFILE
 JSR CLS
 TST INTOUT
 BEQ GETEVENT
 TST INTOUT+2
 BEQ GETEVENT

 MOVE.L ERASEADR,A0
 MOVE #-1,D0
 JSR FORMDO

 MOVE D0,HOLDIT
 MULU #24,D0
 ADD.L ERASEADR,D0
 MOVE.L D0,A0
 CLR OB_STATE(A0)

 MOVE HOLDIT,D0
 CMP #ERCANCL,D0
 BEQ GETEVENT
 CLR ERASFLAG
 CMP #ERYES,D0
 BEQ KARET
 MOVE #1,ERASFLAG

KARET JSR CLS
 JSR CLRACT
 MOVE.L WORKADR,A0
 MOVE #-1,D0
 JSR SHOWPANL

 JSR CALROW
 JSR DERIVE
 JSR ACTIVITY

 LEA FILENAME,A0
 JSR F_OPEN
 TST D0
 BMI DISKERR
 MOVE D0,FHANDLE

 MOVE FHANDLE,A0
 MOVE.L BIGBUFF,A1
 MOVE.L BUFFSIZE,D0
 JSR F_READ
 CMP.L BUFFSIZE,D0
 BEQ TOOBIG

 MOVE.L D0,D1
 ADD.L BIGBUFF,D1
 MOVE.L D1,TBLOAD

 MOVE FHANDLE,A0
 JSR F_CLOSE

 JSR ACTIVITY

 TST ERASFLAG
 BNE IMPRTOV

 CLR -(A7)      ;F_CREATE
 MOVE.L #T1FNAME,-(A7)
 MOVE #60,-(A7)
 TRAP #1
 ADD #8,A7
 TST D0
 BMI DISKERR
 MOVE D0,FHANDLE
 JMP SMITHS

IMPRTOV LEA T1FNAME,A0
 JSR F_OPENW
 TST D0
 BMI DISKERR
 MOVE D0,FHANDLE

 MOVE FHANDLE,A0
 JSR F_END
 TST.L D0
 BMI DISKERR

 JSR ACTIVITY

SMITHS MOVE.L BIGBUFF,A3

IMPNEW MOVE #MAXFIELD-1,D0
 LEA FIELDS,A4
IMPCLR MOVE.L #NULLVAL,FVALU(A4)
 ADD.L #RECSIZE,A4
 DBF D0,IMPCLR

*-----------PARSE RAW INPUT DATA-------------

 CLR FCOUNT
 LEA FIELDS,A4

IMP100 TST.L FNAME(A4)
 BEQ IMPCHOP

 MOVE.L A3,D0
 CMP.L TBLOAD,D0
 BCC IMPDONE

 MOVE FTYPE(A4),D1
 CMP.B #'C',D1
 BEQ IMPSTRING
 CMP.B #'D',D1
 BEQ IMPNUMBR
 CMP.B #'I',D1
 BEQ IMPNUMBR

*----DATES----

 MOVE #'A',FTYPE(A4)

 CMP.B #34,D0
 BNE DBASE

 CLR.L D0
 MOVE.L A3,FVALU(A4)

 MOVE.B (A3),DATEHLD
 MOVE.B 1(A3),DATEHLD+1
 MOVE.B 3(A3),DATEHLD+2
 MOVE.B 4(A3),DATEHLD+3
 MOVE.B 6(A3),DATEHLD+4
 MOVE.B 7(A3),DATEHLD+5

 JMP HUMAN

DBASE SUB.L #1,A3
 MOVE.L A3,FVALU(A4)

 MOVE.B 4(A3),DATEHLD
 MOVE.B 5(A3),DATEHLD+1
 MOVE.B 6(A3),DATEHLD+2
 MOVE.B 7(A3),DATEHLD+3
 MOVE.B 2(A3),DATEHLD+4
 MOVE.B 3(A3),DATEHLD+5

HUMAN MOVE #5,D0
 LEA DATEHLD,A0
KAREN MOVE.B (A0)+,(A3)+
 DBF D0,KAREN

SEXY MOVE.B (A3)+,D0
 CMP.B #',',D0
 BEQ SEXYLADY
 CMP.B #13,D0
 BEQ IMPCRTN
 ADD.L #1,A3
 JMP SEXY

SEXYLADY ADD.L #1,A3
 JMP IMPRCRS

DATEHLD DC.B '000000'
 EVEN

*---NUMBER----

IMPNUMBR CLR FNEGT(A4)
 CMP.B #'-',D0
 BNE IMPN100
 MOVE #-1,FNEGT(A4)

IMPN100 MOVE.L A3,FVALU(A4)

IMPN101 MOVE.B (A3)+,D0
 CMP.B #13,(A3)
 BEQ IMPS101
 CMP.B #',',(A3)
 BNE IMPN101
 JMP IMPS101

*---STRING----

IMPSTRING MOVE.L A3,D0
 CMP.L TBLOAD,D0
 BCC IMPDONE

 MOVE.B (A3)+,D0
 CMP.B #34,D0
 BNE IMPSTRING

 MOVE.L A3,FVALU(A4)

IMPS100 CMP.B #34,(A3)+
 BNE IMPS100

 CLR.B -1(A3)

IMPS102 CMP.B #13,(A3)
 BEQ IMPCRTN
 CMP.B #',',(A3)
 BEQ IMPS101
 ADD #1,A3

 MOVE.L A3,D0
 CMP.L TBLOAD,D0
 BCC IMPDONE
 JMP IMPS102

IMPS101 ADD #1,A3
 JMP IMPRCRS

IMPCRTN CMP.B #13,(A3)
 BNE IMPCRTN2
 ADD #1,A3
IMPCRTN2 CMP.B #10,(A3)
 BNE IMPRCRS
 ADD #1,A3
 JMP IMPSAVE

DONULL MOVE.L #NULLVAL,FVALU(A4)
 ADD.L #1,A3

IMPRCRS ADD.L #RECSIZE,A4
 ADD #1,FCOUNT
 CMP #MAXFIELD,FCOUNT
 BNE IMP100

IMPRCRS2 CMP.B #13,(A3)
 BEQ IMPCRTN
 CMP.B #10,(A3)
 BEQ IMPCRTN
 ADD #1,A3
 JMP IMPRCRS2

IMPCHOP MOVE.L A3,D0
 CMP.L TBLOAD,D0
 BCC IMPDONE
 CMP.B #13,(A3)+
 BNE IMPCHOP
 CMP.B #10,(A3)
 BNE IMPSAVE

IMPSAVE JSR ACTIVITY
 JSR BUILDROW

 MOVE FHANDLE,A0
 MOVE.L #DBUFFER,A1
 MOVE.L ROWSIZE,D0
 JSR F_WRITE
 CMP.L ROWSIZE,D0
 BNE DISKFUL

 MOVE #11,-(A7)  ;KEYSTOP
 TRAP #1
 ADD #2,A7
 TST D0
 BEQ IMPNEW
 JSR WAIT_KEY

IMPDONE MOVE FHANDLE,A0
 JSR F_CLOSE

 JSR REORGANIZE

 JMP GETEVENT

STRING MOVE.L A0,-(A7)
 MOVE #9,-(A7)
 TRAP #1
 ADD #6,A7
 RTS

NULLVAL DC.B 0,0
 EVEN

*-----------DERIVE FILE NAME------------

DERIVE LEA INPATH,A0
 LEA FILENAME,A1
AST1 MOVE.B (A0)+,D0
 CMP.B #' ',D0
 BEQ LOAD99
 CMP.B #'*',D0
 BEQ LOAD99
 MOVE.B D0,(A1)+
 JMP AST1

LOAD99 LEA INSEL,A0
LOAD100 MOVE.B (A0)+,D0
 TST.B D0
 BEQ LOAD101
 CMP.B #' ',D0
 BEQ LOAD101
 MOVE.B D0,(A1)+
 JMP LOAD100

LOAD101 CLR.B (A1)+
 RTS

*---------------------REORGANIZE TABLE---------------------

* ENTER WITH:
* TNAME - TABLE
* T1FNAME - TABLE FILE NAME
* FIELDS LOADED

REORGANIZE CLR LDINDEX
 JSR LDFIELDS
 JSR CALROW
 MOVE.L ROWSIZE,RSIZE1

 LEA TNAME,A0
 LEA INNAME,A1
IND90 MOVE.B (A0)+,D0
 MOVE.B D0,(A1)+
 TST.B D0
 BNE IND90

 LEA T1FNAME,A0
 LEA INFNAME,A1
IND100 MOVE.B (A0)+,D0
 MOVE.B D0,(A1)+
 CMP.B #'.',D0
 BNE IND100
 MOVE.B #'I',(A1)+
 MOVE.B #'N',(A1)+
 MOVE.B #'D',(A1)+
 CLR.B (A1)+

 MOVE #1,LDINDEX
 JSR LDFIELDS
 CLR LDINDEX
 TST D0
 BMI REORG999
 MOVE #1,LDINDEX
 JSR CALROW
 CLR LDINDEX
 MOVE.L ROWSIZE,RSIZE2

 JSR DELTEMPS

 LEA TEMPNAME,A0  ;F_CREATE
 JSR ADDPATH
 CLR -(A7)
 MOVE.L A0,-(A7)
 MOVE #60,-(A7)
 TRAP #1
 ADD #8,A7
 TST D0
 BMI DISKERR
 MOVE D0,HANDLE2

 LEA T1FNAME,A0
 JSR F_OPEN
 TST D0
 BMI DISKERR
 MOVE D0,HANDLE1

REOLOOP MOVE.L #BUFSPACE-1,D0
 LEA DBUFFER2,A0
REO100 CLR.B (A0)+
 DBF D0,REO100

 MOVE.L RSIZE2,D0
 ADD.L #DBUFFER2,D0
 SUB.L #2,D0
 MOVE.L D0,A0
 MOVE.B #13,(A0)+
 MOVE.B #10,(A0)+

 MOVE HANDLE1,A0
 MOVE.L RSIZE1,D0
 LEA DBUFFER,A1
 JSR F_READ
 TST D0
 BMI DISKERR
 CMP.L RSIZE1,D0
 BNE REODONE

 LEA DBUFFER,A1
 MOVE.L RSIZE1,D0
 SUB.L #3,D0
 CLR.L D1

QUAL101 MOVE.B (A1)+,D1
 BEQ QUAL100
 CMP.B #' ',D1
 BNE QUALITY
QUAL100 DBF D0,QUAL101
 JMP REOLOOP

QUALITY JSR ACTIVITY

 CLR COUNT
REOZO CLR.L D0
 MOVE COUNT,D0
 MULU #RECSIZE,D0
 ADD.L #INDXBUFF,D0
 MOVE.L D0,A3

 TST.L FNAME(A3)
 BEQ REOZSV

 CLR COUNT2

REOZLP CLR.L D0
 MOVE COUNT2,D0
 MULU #RECSIZE,D0
 ADD.L #FIELDS,D0
 MOVE.L D0,A4

 TST.L FNAME(A4)
 BEQ REOZRCRS

 MOVE.L FNAME(A4),A0
 MOVE.L FNAME(A3),A1
 JSR MATCHER
 TST D0
 BPL REOZIT

 ADD #1,COUNT2
 CMP #MAXFIELD,COUNT2
 BNE REOZLP
 JMP REOZRCRS

REOZIT CLR.L D1
 CLR.L D2
 CLR.L D3
 MOVE FSIZE(A4),D1
 MOVE FDISP(A4),D2
 MOVE FDISP(A3),D3

 ADD.L #DBUFFER,D2
 MOVE.L D2,A0
 ADD.L #DBUFFER2,D3
 MOVE.L D3,A1

RLZDP MOVE.B (A0)+,(A1)+
 SUB #1,D1
 BNE RLZDP

REOZRCRS ADD #1,COUNT
 CMP #MAXFIELD,COUNT
 BNE REOZO

REOZSV MOVE HANDLE2,A0
 LEA DBUFFER2,A1
 MOVE.L RSIZE2,D0
 JSR F_WRITE
 TST D0
 BMI ERRORED
 JMP REOLOOP

REODONE MOVE.L #INFNAME,-(A7) ;F_DELETE
 MOVE #65,-(A7)
 TRAP #1
 ADD #6,A7

 MOVE.L #INFNAME,-(A7)  ;F_RENAME
 MOVE.L #TEMPNAME,-(A7)
 CLR -(A7)
 MOVE #86,-(A7)
 TRAP #1
 ADD #12,A7
 TST D0
 BMI DISKERR

REORG999 RTS

ERRORED MOVE D0,ERSV

 MOVE HANDLE1,A0
 JSR F_CLOSE
 MOVE HANDLE2,A0
 JSR F_CLOSE

 JSR DELTEMPS

 MOVE ERSV,D0
 JMP DISKERR

ERSV DC 0

RSIZE1 DC.L 0
RSIZE2 DC.L 0

*----------DELETE TEMPORARY FILES-----------

DELTEMPS LEA TEMPNAME,A0   ;F_DELETE
 JSR ADDPATH
 MOVE.L A0,-(A7)
 MOVE #65,-(A7)
 TRAP #1
 ADD #6,A7

 LEA TMP2NAME,A0   ;F_DELETE
 JSR ADDPATH
 MOVE.L A0,-(A7)
 MOVE #65,-(A7)
 TRAP #1
 ADD #6,A7
 RTS

*---------CLEAR BIGBUFF---------

CLRBIG MOVE.L BIGBUFF,A0
 MOVE.L BUFFSIZE,D0
CLRBIG2 CLR.B (A0)+
 SUB.L #1,D0
 TST.L D0
 BNE CLRBIG2
 RTS

*---------BUILD ROW ACCORDING TO FIELDS-------

BUILDROW LEA DBUFFER,A0
 MOVE.L #BUFSPACE-1,D0
BUI100 CLR.B (A0)+
 DBF D0,BUI100

BUILDR2 CLR SCOUNT

BUI101 LEA FIELDS,A4
 TST SPCLFLAG
 BEQ BUILD101
 LEA FIELDS2,A4
BUILD101 CLR.L D0
 MOVE SCOUNT,D0
 MULU #RECSIZE,D0
 ADD.L D0,A4

 MOVE FTYPE(A4),D0
 BEQ BUI103
 TST.L FNAME(A4)
 BEQ BUI103

 CLR.L D0
 MOVE FDISP(A4),D0
 ADD.L #DBUFFER,D0
 MOVE.L D0,A1

 MOVE FSIZE(A4),D4
 SUB #1,D4

 TST.L FVALU(A4)
 BEQ BUI104
 MOVE.L FVALU(A4),A0

 MOVE FTYPE(A4),D0
 CMP.B #'C',D0
 BEQ BUILDC
 CMP.B #'D',D0
 BEQ BUILDD
 CMP.B #'I',D0
 BEQ BUILDI
 CMP.B #'E',D0
 BEQ BUILDE
 CMP.B #'A',D0
 BEQ BUILDT
 CMP.B #'S',D0
 BEQ BUILDT
 CMP.B #'L',D0
 BNE BUI103

BUILDT MOVE.B (A0)+,D0  ;NORMAL DATE
 TST.B D0
 BEQ BUI104

 MOVE.B (A0)+,D1
 CMP.B #'/',D1
 BNE BUI105
 MOVE.B #'0',(A1)+
 MOVE.B D0,(A1)+
 JMP BUI106

BUI105 MOVE.B D0,(A1)+
 MOVE.B D1,(A1)+

 CMP.B #'/',(A0)
 BNE BUI106
 ADD.L #1,A0

BUI106 MOVE.B (A0)+,D0
 MOVE.B (A0)+,D1
 CMP.B #'/',D1
 BNE BUI107
 MOVE.B #'0',(A1)+
 MOVE.B D0,(A1)+
 JMP BUI108

BUI107 MOVE.B D0,(A1)+
 MOVE.B D1,(A1)+

 CMP.B #'/',(A0)
 BNE BUI108
 ADD.L #1,A0

BUI108 MOVE.B (A0)+,(A1)+
 MOVE.B (A0)+,(A1)+
 JMP BUI104


BUILDE MOVE.B (A0)+,D0  ;EUROPEAN DATE
 TST.B D0
 BEQ BUI104

 MOVE.B (A0)+,D1
 CMP.B #'/',D1
 BNE BUI115
 MOVE.B #'0',D2
 MOVE.B D0,D3
 JMP BUI116

BUI115 MOVE.B D0,D2
 MOVE.B D1,D3

 CMP.B #'/',(A0)
 BNE BUI116
 ADD.L #1,A0

BUI116 MOVE.B (A0)+,D0
 MOVE.B (A0)+,D1
 CMP.B #'/',D1
 BNE BUI117
 MOVE.B #'0',(A1)+
 MOVE.B D0,(A1)+
 JMP BUI118

BUI117 MOVE.B D0,(A1)+
 MOVE.B D1,(A1)+

 CMP.B #'/',(A0)
 BNE BUI118
 ADD.L #1,A0

BUI118 MOVE.B D2,(A1)+
 MOVE.B D3,(A1)+
 MOVE.B (A0)+,(A1)+
 MOVE.B (A0)+,(A1)+
 JMP BUI104


BUILDI MOVE.B #' ',D1  ;INTEGER
 TST FNEGT(A4)
 BEQ BUI120
 MOVE.B #'-',D1
BUI120 MOVE.B D1,(A1)+

BUI121 MOVE FSIZE(A4),D1
 SUB #2,D1
 MOVE.L A1,A2
BUI300 MOVE.B #'0',(A2)+
 DBF D1,BUI300

BUI301 MOVE.B (A0),D0
 BEQ BUI104
 CMP.B #'.',D0
 BEQ BUI104

 CMP.B #'9'+1,D0
 BCC BUI99S
 CMP.B #'0',D0
 BCC BUI303
BUI99S ADD.L #1,A0
 JMP BUI301

BUI303 MOVE FSIZE(A4),D0
 SUB #1,D0
 MOVE.L A1,A2
BUI304 MOVE.B 1(A2),(A2)+
 DBF D0,BUI304

 MOVE.L A1,A2
 CLR.L D0
 MOVE FSIZE(A4),D0
 SUB #2,D0
 ADD.L D0,A2
 MOVE.B (A0)+,(A2)
 JMP BUI301

BUILDD MOVE.B #' ',D1  ;DECIMAL
 TST FNEGT(A4)
 BEQ BUIR120
 MOVE.B #'-',D1
BUIR120 MOVE.B D1,(A1)+

BUIR121 CLR.L D1
 MOVE FSIZE(A4),D1
 SUB #2,D1
 MOVE.L A1,A2
BUIR300 MOVE.B #'0',(A2)+
 DBF D1,BUIR300

BUIR301 MOVE.B (A0),D0
 BEQ BUI104
 CMP.B #'.',D0
 BEQ BUIR400

 CMP.B #'9'+1,D0
 BCC BUI88S
 CMP.B #'0',D0
 BCC BUIR303
BUI88S ADD.L #1,A0
 JMP BUIR301

BUIR303 MOVE FSIZE(A4),D0
 SUB FEXTR(A4),D0
 SUB #1,D0
 MOVE.L A1,A2
BUIR304 MOVE.B 1(A2),(A2)+
 DBF D0,BUIR304

 MOVE.L A1,A2
 CLR.L D0
 MOVE FSIZE(A4),D0
 SUB FEXTR(A4),D0
 SUB #2,D0
 ADD.L D0,A2
 MOVE.B (A0)+,(A2)
 JMP BUIR301

BUIR400 ADD.L #1,A0
 MOVE.L A1,A2
 CLR.L D0
 MOVE FSIZE(A4),D0
 SUB FEXTR(A4),D0
 SUB #1,D0
 ADD.L D0,A2
 CLR.L D0
 MOVE FEXTR(A4),D0
 SUB #1,D0

BUIR401 TST.B (A0)
 BEQ BUI104

 CMP.B #'9'+1,(A0)
 BCC BUI77S
 CMP.B #'0',(A0)
 BCC BUI88
BUI77S ADD.L #1,A0
 JMP BUIR401

BUI88 MOVE.B (A0)+,(A2)+
 DBF D0,BUIR401
 JMP BUI104

BUILDC TST.B (A0)  ;CHARACTER
 BEQ BUILDC2
 MOVE.B (A0)+,(A1)+
 DBF D4,BUILDC
 JMP BUI104

BUILDC2 CLR.B (A1)+
 DBF D4,BUILDC2

BUI104 ADD #1,SCOUNT
 CMP #MAXFIELD,SCOUNT
 BNE BUI101

BUI103 MOVE.L ROWSIZE,D0
 TST SPCLFLAG
 BEQ BUI200
 MOVE.L NEWSIZE,D0

BUI200 SUB.L #2,D0
 ADD.L #DBUFFER,D0
 MOVE.L D0,A0
 MOVE.B #13,(A0)+
 MOVE.B #10,(A0)+
 RTS

*--------------ASK WHICH FILE-----------

GETFILE LEA FSPEC1,A0
 JSR ADDPATH
 LEA INPATH,A1
INFL101 TST.B (A0)
 BEQ INFL100
 MOVE.B (A0)+,(A1)+
 JMP INFL101
INFL100 CLR.B (A1)
 CLR.B INSEL

 MOVE #90,CONTRL   ;LOAD FILE DIALOG
 CLR CONTRL+2
 MOVE #2,CONTRL+4
 MOVE #2,CONTRL+6
 CLR CONTRL+8
 MOVE.L #INPATH,ADDRIN
 MOVE.L #INSEL,ADDRIN+4
 JMP AES

FSPEC1 DC.B '*.*',0
 EVEN

*-------------SELECT TABLE DIALOG-------------

GETTABLE JSR CLS

 MOVE.L #TTHUMB*OBSIZE,D0
 ADD.L TABLADR,D0
 MOVE.L D0,A1
 CLR OB_Y(A1)

 MOVE.L #19,D0
 LEA TNAME,A0
GETT2 CLR.B (A0)+
 DBF D0,GETT2

 MOVE.L TABLADR,TREEADR

 JSR LDTABLES

 MOVE.L TABLADR,A0
 MOVE #-1,D0
 JSR SHOWPANL
 JSR SHOWFILE

TBLEVENT JSR WAITMOUSE

 MOVE #TTHUMB,D0
 JSR OBJCFIND
 TST D0
 BEQ MVTHUMB

 MOVE #TSLIDER,D0
 JSR OBJCFIND
 TST D0
 BEQ PAGED

 MOVE #TUPARW,D0
 JSR OBJCFIND
 TST D0
 BEQ ARROWUP

 MOVE #TDNARW,D0
 JSR OBJCFIND
 TST D0
 BEQ ARROWDN

 MOVE #TCANCEL,D0
 JSR OBJCFIND
 TST D0
 BEQ TBLCNCL

 MOVE #43,CONTRL  ;OBJC_FIND
 MOVE #4,CONTRL+2
 MOVE #1,CONTRL+4
 MOVE #1,CONTRL+6
 CLR CONTRL+8
 CLR INTIN
 MOVE #30,INTIN+2
 MOVE MOUSEX,INTIN+4
 MOVE MOUSEY,INTIN+6
 MOVE.L TABLADR,ADDRIN
 JSR AES
 TST INTOUT
 BMI TBLEVENT

 CMP #TBL1,INTOUT
 BCS TBLEVENT
 CMP #TBL8+1,INTOUT
 BCC TBLEVENT

 MOVE INTOUT,HOLDIT
 CLR.L D0
 MOVE INTOUT,D0
 MULU #OBSIZE,D0
 ADD.L TABLADR,D0
 MOVE.L D0,A0
 MOVE #1,OB_STATE(A0)

 MOVE.L #19,D1
 MOVE.L OB_SPEC(A0),A0
 LEA TNAME,A1
HUGO MOVE.B (A0)+,D0
 MOVE.B D0,(A1)+
 TST.B D0
 BEQ WIZKIDY
 DBF D1,HUGO
 CLR.B (A1)

 MOVE INTOUT,D0
 JSR OBJCDRAW

WIZKIDY JSR WAITUP

 CLR.L D0
 MOVE HOLDIT,D0
 MULU #OBSIZE,D0
 ADD.L TABLADR,D0
 MOVE.L D0,A0
 CLR OB_STATE(A0)
 MOVE INTOUT,D0
 JSR OBJCDRAW
 CLR.L D0
 RTS

TBLCNCL MOVE #-1,D0
 RTS

*-----------LOAD TABLE INFO AND SORT----------

LDTABLES CLR FILEY
 CLR FILECNT
 MOVE.L #TBLINFO,POINTER

 MOVE.L #TBLLEN*TBLMAX,D0
 SUB #1,D0
 LEA TBLINFO,A0
LORETTE CLR.B (A0)+
 DBF D0,LORETTE

 LEA TBLSPEC,A0
 JSR ADDPATH
 JSR F_OPEN
 TST D0
 BMI DISKERR
 MOVE D0,FHANDLE

LORI2 MOVE FHANDLE,A0
 MOVE.L POINTER,A1
 MOVE.L #TBLLEN,D0
 JSR F_READ
 TST.L D0
 BMI DISKERR
 CMP.L #TBLLEN,D0
 BNE LORI3

 MOVE.L POINTER,A1
 TST.B (A1)
 BEQ LORI2
 CMP.B #' ',(A1)
 BEQ LORI2
 MOVE.L POINTER,D0
 ADD.L #TBLLEN,D0
 MOVE.L D0,POINTER
 ADD #1,FILECNT
 JMP LORI2

LORI3 MOVE FHANDLE,A0
 JSR F_CLOSE

*-----------SORT TABLE NAMES----------

SORT1 LEA TBLINFO,A0
 MOVE.L A0,A2
 LEA TBLINFO,A1
 ADD.L #48,A1
 MOVE.L A1,A3
 CLR FLAG

SORT2 CMP.B #' ',(A1)
 BEQ SORT6
 TST.B (A1)
 BEQ SORT6

SORT3 TST.B (A0)
 BEQ SORT4
 MOVE.B (A0)+,D0
 MOVE.B (A1)+,D1
 CMP.B D0,D1
 BEQ SORT3
 BCC SORT4

 MOVE.L A2,A0
 MOVE.L A3,A1
 MOVE.L #47,D3
SORT5 MOVE.B (A0),D0
 MOVE.B (A1),D1
 MOVE.B D1,(A0)+
 MOVE.B D0,(A1)+
 DBF D3,SORT5
 MOVE #1,FLAG

SORT4 ADD.L #48,A2
 ADD.L #48,A3
 MOVE.L A2,A0
 MOVE.L A3,A1
 JMP SORT2

SORT6 TST FLAG
 BNE SORT1
 RTS

*--------------SHOW TABLES-------------

SHOWFILE CLR.L D0
 MOVE FILEY,D0
 MULU #48,D0
 ADD.L #TBLINFO,D0
 MOVE.L D0,A2

 CLR COUNT

TALKING CLR.L D0
 MOVE COUNT,D0
 ADD #TBL1,D0
 MULU #OBSIZE,D0
 ADD.L TABLADR,D0
 MOVE.L D0,A1

 MOVE.L OB_SPEC(A1),A1

 MOVE.L A2,A0

 MOVE #19,D0
HEADS TST.B (A0)
 BEQ HEADSR
 CMP.B #' ',(A0)
 BEQ HEADSR
 MOVE.B (A0)+,(A1)+
 DBF D0,HEADS
 JMP RICK

HEADSR MOVE.B #' ',(A1)+
 DBF D0,HEADSR

RICK ADD #48,A2

 ADD #1,COUNT
 CMP #8,COUNT
 BNE TALKING

 MOVE #1,D0
 JSR SETMODE

 MOVE #TTABLES,D0
 JSR OBJCDRAW
 MOVE #TBL1,D0
 JSR OBJCDRAW
 MOVE #TBL2,D0
 JSR OBJCDRAW
 MOVE #TBL3,D0
 JSR OBJCDRAW
 MOVE #TBL4,D0
 JSR OBJCDRAW
 MOVE #TBL5,D0
 JSR OBJCDRAW
 MOVE #TBL6,D0
 JSR OBJCDRAW
 MOVE #TBL7,D0
 JSR OBJCDRAW
 MOVE #TBL8,D0
 JMP OBJCDRAW

*--------------SLIDE BAR ACTION-------------

PAGED MOVE.L #TTHUMB,D0
 JSR CALC

 MOVE #85,D1
 CMP #2,GRMODE
 BNE PAGED2
 MOVE #170,D1

PAGED2 CLR.L D0
 MOVE OB_Y(A0),D0
 ADD D1,D0
 CMP MOUSEY,D0
 BCC PAGEUP

PAGEDN CMP #8,FILECNT
 BCS TBLEVENT

 CLR.L D0
 MOVE FILEY,D0
 ADD #16,D0
 CMP FILECNT,D0
 BCC PAGEBOT
 ADD #8,FILEY
 JMP KINKY

PAGEBOT CLR.L D0
 MOVE FILECNT,D0
 SUB #8,D0
 MOVE D0,FILEY
 JMP KINKY

PAGEUP CMP #8,FILEY
 BCS PAGETOP
 SUB #8,FILEY
 JMP KINKY
PAGETOP CLR FILEY
 JMP KINKY

ARROWUP TST FILEY
 BEQ TBLEVENT
 SUB #1,FILEY
 JMP KINKY

ARROWDN CMP #8,FILECNT
 BCS TBLEVENT
 CLR.L D0
 MOVE FILECNT,D0
 SUB #9,D0
 CMP FILEY,D0
 BCS TBLEVENT
 ADD #1,FILEY
 JMP KINKY

MVTHUMB MOVE #76,CONTRL  ;GRAF_SLIDBOX  DRAG IT
 MOVE #3,CONTRL+2
 MOVE #1,CONTRL+4
 MOVE #1,CONTRL+6
 CLR CONTRL+8
 MOVE #TSLIDER,INTIN
 MOVE #TTHUMB,INTIN+2
 MOVE #1,INTIN+4
 MOVE.L TABLADR,ADDRIN
 JSR AES

 CMP #8,FILECNT
 BCS TBLEVENT

 MOVE INTOUT,HOLDER

 MOVE #TTABLES,D0
 JSR OBJCDRAW

 MOVE.L #TSLIDER,D0
 JSR CALC

 CLR.L D0
 MOVE FILECNT,D0
 MOVE.L #1000,D1
 DIVU D0,D1
 AND.L #$FFFF,D1
 CLR.L D2
 MOVE HOLDER,D2
 DIVU D1,D2
 AND.L #$FFFF,D2

 MOVE.L D2,D0
 ADD #16,D0
 CMP FILECNT,D0
 BCC PAGEBOT

 MOVE D2,FILEY

KINKY JSR SHOWFILE

 CLR SLIDEPOS
 CLR.L D1
 MOVE FILECNT,D1
 CMP #8,D1
 BCS KINKTOP
 SUB #8,D1

 CLR.L D0
 MOVE FILEY,D0
 MULU #1000,D0
 DIVU D1,D0
 AND.L #$FFFF,D0
 MOVE D0,SLIDEPOS

KINKTOP MOVE.L #TTHUMB,D0
 JSR CALC
 CLR.L D3
 MOVE OB_HEIGHT(A0),D3

 MOVE.L #TSLIDER,D0
 JSR CALC

 CLR.L D0
 MOVE OB_HEIGHT(A0),D0
 SUB D3,D0

 MULU SLIDEPOS,D0
 DIVU #1000,D0
 MOVE D0,D2

 MOVE.L #TTHUMB,D0
 JSR CALC
 MOVE D2,OB_Y(A0)

 MOVE #TSLIDER,D0
 JSR OBJCDRAW
 MOVE #TTHUMB,D0
 JSR OBJCDRAW
 JMP TBLEVENT

CALC MULU #OBSIZE,D0
 ADD.L TABLADR,D0
 MOVE.L D0,A0
 RTS

*-----------SHOW ACTIVITY-----------

CLRACT MOVE #3,D0
 LEA ACTS,A0
CACTY2 MOVE.B #'_',(A0)+
 DBF D0,CACTY2
 CLR.B (A0)

 MOVE.L #WORKNUM*OBSIZE,D0
 ADD.L WORKADR,D0
 MOVE.L D0,A0
 MOVE.L OB_SPEC(A0),A0
 MOVE.L #ACTS,(A0)
 RTS

ACTIVITY MOVEM.L A0-A4/D0-D7,-(A7)

 MOVE #3,D0
 LEA ACTS,A0
ACTY2 MOVE.B #'_',(A0)+
 DBF D0,ACTY2
 CLR.B (A0)

 ADD #1,ACTNUMB
 CLR.L D1
 CLR.L D3
 CLR.L ACTS
 LEA ACTS,A0

 MOVE ACTNUMB,D1
 DIVU #10000,D1
 MOVE.L D1,D0
 AND.L #15,D1
 BNE ACT101
 TST D3
 BEQ ACT102
ACT101 ADD.B #'0',D1
 MOVE.B D1,(A0)+
 MOVE #1,D3

ACT102 SWAP D0
 AND.L #$FFFF,D0
 DIVU #1000,D0
 MOVE.L D0,D1
 AND.L #15,D0
 BNE ACT103
 TST D3
 BEQ ACT104
ACT103 ADD.B #'0',D0
 MOVE.B D0,(A0)+
 MOVE #1,D3

ACT104 SWAP D1
 AND.L #$FFFF,D1
 DIVU #100,D1
 MOVE.L D1,D0
 AND.L #15,D1
 BNE ACT105
 TST D3
 BEQ ACT106
ACT105 ADD.B #'0',D1
 MOVE.B D1,(A0)+
 MOVE #1,D3

ACT106 SWAP D0
 AND.L #$FFFF,D0
 DIVU #10,D0
 MOVE.L D0,D1
 AND.L #15,D0
 BNE ACT107
 TST D3
 BEQ ACT108
ACT107 ADD.B #'0',D0
 MOVE.B D0,(A0)+

ACT108 SWAP D1
 AND.L #$FFFF,D1
 AND.L #15,D1
 ADD.B #'0',D1
 MOVE.B D1,(A0)+

 MOVE.L #WORKNUM*OBSIZE,D0
 ADD.L WORKADR,D0
 MOVE.L D0,A0
 MOVE.L OB_SPEC(A0),A0
 MOVE.L #ACTS,(A0)

 MOVE #42,CONTRL ;OBJC_DRAW
 MOVE #6,CONTRL+2
 MOVE #1,CONTRL+4
 MOVE #1,CONTRL+6
 CLR CONTRL+8
 MOVE #WORKNUM,INTIN
 CLR INTIN+2
 MOVE GEMXPOS,INTIN+4
 MOVE GEMYPOS,INTIN+6
 MOVE GEMWIDTH,INTIN+8
 MOVE GEMHOEHE,INTIN+10
 MOVE.L WORKADR,ADDRIN
 DC $A00A
 JSR AES
 DC $A009

 MOVEM.L (A7)+,A0-A4/D0-D7
 RTS

ACTNUMB DC 0
ACTS DC.B '     ',0
 EVEN

*---------------WAIT FOR A KEY-------------

WAIT_KEY MOVE #7,-(A7)
 TRAP #1
 ADD #2,A7
 RTS

*------------FIND FILE NAME FOR TNAME------------

FINDTBLS CLR.B T1FNAME
 CLR FTBLFLAG

 LEA TBLSPEC,A0
 JSR ADDPATH
 JSR F_OPEN
 TST D0
 BMI DISKERR
 MOVE D0,FHANDLE

FTBL100 MOVE FHANDLE,A0
 MOVE.L #48,D0
 LEA DBUFFER,A1
 JSR F_READ
 TST.L D0
 BMI DISKERR
 CMP.L #48,D0
 BNE FTBL101

 LEA DBUFFER+19,A0
FTBL200 CMP.L #DBUFFER,A0
 BEQ FTBL201
 CLR.B (A0)
 SUB.L #1,A0
 CMP.B #' ',(A0)
 BEQ FTBL200
 TST.B (A0)
 BEQ FTBL200

FTBL201 LEA T1FNAME,A2

 LEA DBUFFER,A0
 LEA TNAME,A1
 JSR MATCHER
 TST D0
 BMI FTBL100

FTBL107 MOVE #1,FTBLFLAG

 MOVE.L #19,D0
 LEA DBUFFER+20,A0
FTBL103 CMP.B #' ',(A0)
 BEQ FTBL104
 TST.B (A0)
 BEQ FTBL104
 MOVE.B (A0)+,(A2)+
 DBF D0,FTBL103

FTBL104 LEA DBUFFER,A0
 MOVE.L #19,D0
FTBL105 CMP.B #' ',(A0)
 BEQ FTBL106
 TST.B (A0)
 BEQ FTBL106
 MOVE.B (A0)+,(A2)+
 DBF D0,FTBL105

FTBL106 MOVE.B #'.',(A2)+
 MOVE.B #'T',(A2)+
 MOVE.B #'B',(A2)+
 MOVE.B #'L',(A2)+
 JMP FTBL100

FTBL101 MOVE FHANDLE,A0
 JSR F_CLOSE
 TST D0
 BMI DISKERR

 TST FTBLFLAG
 BEQ FTBL102
 CLR D0
 RTS

FTBL102 MOVE #-1,D0
 RTS

FTBLFLAG DC 0

*------------LOAD FIELDS FOR TABLE------------

LDFIELDS LEA FIELDS,A0
 TST LDINDEX
 BEQ LDF201
 LEA INDXBUFF,A0
LDF201 MOVE.L #RECSIZE*MAXFIELD,D0
 SUB.L #1,D0
LDF202 CLR.B (A0)+
 DBF D0,LDF202

 LEA FLDNAME,A0
 TST LDINDEX
 BEQ LDF200
 LEA INDNAME,A0
LDF200 JSR ADDPATH
 JSR F_OPEN
 TST D0
 BMI DISKERR
 MOVE D0,LHANDLE

 CLR COUNT
 CLR WFLAG
 MOVE.L #FNAMES,FPTR
 TST LDINDEX
 BEQ OP8
 MOVE.L #INAMES,FPTR

OP8 MOVE LHANDLE,A0
 MOVE.L #65,D0
 LEA DBUFFER,A1
 JSR F_READ
 TST D0
 BMI DISKERR
 CMP.L #65,D0
 BEQ OPER2
 TST WFLAG
 BNE OP14

 MOVE LHANDLE,A0
 JSR F_CLOSE
 TST D0
 BMI DISKERR
 MOVE #-1,D0
 RTS

OPER2 LEA DBUFFER+39,A0
OPER3 CMP.L #DBUFFER+20,A0
 BEQ OPER4
 CLR.B (A0)
 SUB.L #1,A0
 CMP.B #' ',(A0)
 BEQ OPER3
 TST.B (A0)
 BEQ OPER3

OPER4 LEA DBUFFER+20,A0
 LEA TNAME,A1
 TST LDINDEX
 BEQ OPER4A
 LEA INNAME,A1
OPER4A JSR MATCHER
 TST D0
 BMI OP8

OPPUS MOVE.L #FIELDS,D1
 TST LDINDEX
 BEQ LDXF102
 MOVE.L #INDXBUFF,D1
LDXF102 CLR.L D0
 MOVE COUNT,D0
 MULU #RECSIZE,D0
 ADD.L D0,D1
 MOVE.L D1,A1

 MOVE.L FPTR,FTABL(A1)
 MOVE.L FPTR,A2
 MOVE.L #19,D0
 LEA DBUFFER+20,A0
LDF103 MOVE.B (A0)+,D1
 TST.B D1
 BEQ LDF104
 CMP.B #' ',D1
 BEQ LDF104
 MOVE.B D1,(A2)+
 ADD.L #1,FPTR
 DBF D0,LDF103

LDF104 CLR.B (A2)
 ADD.L #1,FPTR

 MOVE.L FPTR,FNAME(A1)
 MOVE.L FPTR,FOLDY(A1)

 MOVE.L #19,D0
 LEA DBUFFER,A0
 MOVE.L FPTR,A2
LDF101 MOVE.B (A0)+,D1
 TST.B D1
 BEQ LDF102
 CMP.B #' ',D1
 BEQ LDF102
 MOVE.B D1,(A2)+
 ADD.L #1,FPTR
 DBF D0,LDF101

LDF102 CLR.B (A2)
 ADD.L #1,FPTR

 MOVE.B DBUFFER+40,D0   ;FIELD TYPE
 MOVE D0,FTYPE(A1)

 LEA DBUFFER+42,A0      ;FIELD DISPLACEMENT
 JSR CVAL
 MOVE D1,FDISP(A1)

 LEA DBUFFER+47,A0      ;FIELD SIZE
 JSR CVAL
 MOVE D1,FSIZE(A1)

 LEA DBUFFER+52,A0      ;DECIMAL SIZE
 CLR.L D0
 MOVE.B (A0),D0
 SUB.B #'0',D0
 MOVE D0,FEXTR(A1)

 MOVE #1,WFLAG
 ADD #1,COUNT
 CMP #MAXFIELD,COUNT
 BNE OP8

OP14 MOVE LHANDLE,A0
 JSR F_CLOSE
 TST D0
 BMI DISKERR
 CLR.L D0
 RTS

FPTR DC.L 0
LHANDLE DC.L 0
FLDNAME DC.B 'FIELDS.TBL',0
INDNAME DC.B 'INDEXES.TBL',0
 EVEN

************************************
* CONVERT THE VALUE IN D0 TO ASCII *
* A3 POINTS TO ASCII BUFFER        *
************************************

CONV2DEC CLR D6
 AND.L #$FFFF,D0
 DIVU #1000,D0
 MOVE.L D0,D1
 AND.L #15,D0
 TST D0
 BEQ CONV2A
 ADD.B #'0',D0
 MOVE.B D0,(A3)+
 MOVE #1,D6

CONV2A SWAP D1
 AND.L #$FFFF,D1
 DIVU #100,D1
 MOVE.L D1,D0
 AND.L #15,D1
 TST D1
 BNE CONV2B
 TST D6
 BEQ CONV2C
CONV2B ADD.B #'0',D1
 MOVE.B D1,(A3)+
 MOVE #1,D6

CONV2C SWAP D0
 AND.L #$FFFF,D0
 DIVU #10,D0
 MOVE.L D0,D1
 AND.L #15,D0
 TST D0
 BNE CONV2D
 TST D6
 BEQ CONV2E
CONV2D ADD.B #'0',D0
 MOVE.B D0,(A3)+
 MOVE #1,D6

CONV2E SWAP D1
 AND.L #$FFFF,D1
 AND.L #15,D1
 ADD.B #'0',D1
 MOVE.B D1,(A3)+
 CLR.B (A3)
 RTS

**************************************
* CONVERT 4 DIGIT ASCII TO HEX VALUE *
* A0 POINTS TO ASCII D1 RETURNS VAL  *
**************************************

CVAL CLR.L D0
 CLR.L D1
 MOVE.B (A0)+,D0
 SUB.B #'0',D0
 MULU #1000,D0
 ADD D0,D1
 CLR.L D0
 MOVE.B (A0)+,D0
 SUB.B #'0',D0
 MULU #100,D0
 ADD D0,D1
 CLR.L D0
 MOVE.B (A0)+,D0
 SUB.B #'0',D0
 MULU #10,D0
 ADD D0,D1
 CLR.L D0
 MOVE.B (A0)+,D0
 SUB.B #'0',D0
 ADD D0,D1
 RTS

*----------CALCULATE ROW SIZE------------

CALROW CLR COUNT
 CLR.L ROWSIZE

CAL1 LEA FIELDS,A3
 TST LDINDEX
 BEQ CAL2
 LEA INDXBUFF,A3
CAL2 CLR.L D0
 MOVE COUNT,D0
 MULU #RECSIZE,D0
 ADD.L D0,A3

 TST.L FNAME(A3)
 BEQ CAL3

 CLR.L D0
 MOVE FSIZE(A3),D0
 ADD.L D0,ROWSIZE

 ADD #1,COUNT
 CMP #MAXFIELD,COUNT
 BNE CAL1

CAL3 ADD.L #2,ROWSIZE
 RTS

*-------------------BASIC FILE I/O ROUTINES-----------------

**************************
* OPEN DISK FILE         *
* A0 POINTS TO FILE NAME *
* D0 RETURNS HANDLE      *
**************************

F_OPEN MOVE #0,-(A7)
 MOVE.L A0,-(A7)
 MOVE #$3D,-(A7)
 TRAP #1
 ADD.L #8,A7
 RTS

****************************
* OPEN DISK FILE FOR WRITE *
****************************

F_OPENW MOVE #1,-(A7)
 MOVE.L A0,-(A7)
 MOVE #$3D,-(A7)
 TRAP #1
 ADD.L #8,A7
 RTS

*************************
* READ DISK FILE        *
* A0 = FILE HANDLE      *
* A1 = WHERE TO LOAD IT *
* D0 = NUMBER OF BYTES  *
*************************

F_READ MOVE.L A1,-(A7)
 MOVE.L D0,-(A7)
 MOVE A0,-(A7)
 MOVE #$3F,-(A7)
 TRAP #1
 ADD.L #12,A7
 RTS

*************************
* WRITE TO DISK FILE    *
* A0 = FILE HANDLE      *
* A1 = WHERE TO LOAD IT *
* D0 = NUMBER OF BYTES  *
*************************

F_WRITE MOVE.L A1,-(A7)
 MOVE.L D0,-(A7)
 MOVE A0,-(A7)
 MOVE #$40,-(A7)
 TRAP #1
 ADD.L #12,A7
 RTS

************************************
* MOVE FILE POINTER TO END-OF-FILE *
************************************

F_END MOVE #2,-(A7)
 MOVE A0,-(A7)
 CLR.L -(A7)
 MOVE #$42,-(A7)
 TRAP #1
 ADD.L #10,A7
 RTS

*********************
* SEEK FILE POINTER *
* D0 = OFFSET       *
* A0 = FILE HANDLE  *
*********************

F_SEEK MOVE #1,-(A7)
 MOVE A0,-(A7)
 MOVE.L D0,-(A7)
 MOVE #$42,-(A7)
 TRAP #1
 ADD.L #10,A7
 RTS

*********************
* CLOSE DISK FILE   *
* A0 = FILE HANDLE  *
* D0 = ERROR CODE   *
*********************

F_CLOSE MOVE A0,-(A7)
 MOVE #$3E,-(A7)
 TRAP #1
 ADD.L #4,A7
 RTS

*------------COMPARE STRINGS-----------

MATCHER CMP.L #0,A0
 BEQ MATBAD
 CMP.L #0,A1
 BEQ MATBAD

 MOVE.L A0,MHOLD1
 MOVE.L A1,MHOLD2

XMAT1 CMP.B #' ',(A0)
 BEQ XMAT2
 TST.B (A0)
 BEQ XMAT2
 MOVE.B (A0)+,D0
 CMP.B #'a',D0
 BCS XEM1
 CMP.B #'z'+1,D0
 BCC XEM1
 SUB.B #32,D0

XEM1 MOVE.B (A1)+,D1
 CMP.B #'a',D1
 BCS XEM2
 CMP.B #'z'+1,D1
 BCC XEM2
 SUB.B #32,D1
XEM2 CMP.B D0,D1
 BEQ XMAT1
 JMP XMAT3

XMAT2 CMP.B #' ',(A1)
 BEQ XMAT4
 TST.B (A1)
 BNE XMAT3
XMAT4 MOVE.L MHOLD1,A0
 MOVE.L MHOLD2,A1
 CLR D0
 RTS

XMAT3 MOVE.L MHOLD1,A0
 MOVE.L MHOLD2,A1

MATBAD MOVE #-1,D0
 RTS

MHOLD1 DC.L 0
MHOLD2 DC.L 0

*---------------FIND OBJECT UNDER MOUSE----------------

OBJCFIND MOVE D0,HOLDER

 MOVE #43,CONTRL  ;OBJC_FIND
 MOVE #4,CONTRL+2
 MOVE #1,CONTRL+4
 MOVE #1,CONTRL+6
 CLR CONTRL+8
 MOVE D0,INTIN
 CLR INTIN+2
 MOVE MOUSEX,INTIN+4
 MOVE MOUSEY,INTIN+6
 MOVE.L TREEADR,ADDRIN
 JSR AES

 CLR.L D0
 MOVE HOLDER,D1
 CMP INTOUT,D1
 BEQ OBJCFND
 MOVE #1,D0
OBJCFND RTS

*-------------------WAIT FOR MOUSE DOWN--------------------

WAITMOUSE MOVE #21,CONTRL
 MOVE #3,CONTRL+2
 MOVE #5,CONTRL+4
 CLR CONTRL+6
 CLR CONTRL+8
 MOVE #2,INTIN
 MOVE #1,INTIN+2
 MOVE #1,INTIN+4
 JSR AES
 MOVE INTOUT+2,MOUSEX
 MOVE INTOUT+4,MOUSEY
 MOVE INTOUT,D0
 RTS

MOUSEX DC 0
MOUSEY DC 0

*-----------------WAIT FOR MOUSE UP--------------------

WAITUP MOVE #21,CONTRL
 MOVE #3,CONTRL+2
 MOVE #5,CONTRL+4
 CLR CONTRL+6
 CLR CONTRL+8
 MOVE #1,INTIN
 MOVE #1,INTIN+2
 CLR INTIN+4
 JMP AES

*-----------------------CLEAR SCREEN------------------------

CLS DC $A00A
 MOVE.L #$FFFF,D1
 CMP #2,GRMODE
 BNE CLS2
 CLR.L D1
CLS2 MOVE.L SCREEN,A0
 MOVE.L #8000-1,D0
BEER MOVE.L D1,(A0)+
 DBF D0,BEER
 DC $A009
 RTS

*-----------------------BAD RESOLUTION------------------------

BADREZ MOVE #REZMES,D0
 JSR GADDR
 MOVE.L A0,ADDRIN
 MOVE #52,CONTRL
 MOVE #1,CONTRL+2
 MOVE #1,CONTRL+4
 MOVE #1,CONTRL+6
 CLR CONTRL+8
 MOVE #1,INTIN
 JSR AES
 JMP QUITIT

*------------------------DRAW TREE--------------------------

DRAWTREE MOVE #42,CONTRL ;OBJC_DRAW
 MOVE #6,CONTRL+2
 MOVE #1,CONTRL+4
 MOVE #1,CONTRL+6
 CLR CONTRL+8
 CLR INTIN
 MOVE #80,INTIN+2
 MOVE GEMXPOS,INTIN+4
 MOVE GEMYPOS,INTIN+6
 MOVE GEMWIDTH,INTIN+8
 MOVE GEMHOEHE,INTIN+10
 MOVE.L A0,ADDRIN
 DC $A00A
 JSR AES
 DC $A009
 RTS

*------------------------DRAW OBJECT--------------------------

OBJCDRAW MOVE #42,CONTRL ;OBJC_DRAW
 MOVE #6,CONTRL+2
 MOVE #1,CONTRL+4
 MOVE #1,CONTRL+6
 CLR CONTRL+8
 MOVE D0,INTIN
 CLR INTIN+2
 MOVE GEMXPOS,INTIN+4
 MOVE GEMYPOS,INTIN+6
 MOVE GEMWIDTH,INTIN+8
 MOVE GEMHOEHE,INTIN+10
 MOVE.L TREEADR,ADDRIN
 DC $A00A
 JSR AES
 DC $A009
 RTS

*--------------------------QUIT----------------------------

QUIT CLR.L D0
 MOVE INTOUT,D0
 MULU #OBSIZE,D0
 ADD.L TREEADR,D0
 MOVE.L D0,A0
 MOVE #1,OB_STATE(A0)
 MOVE INTOUT,D0
 JSR OBJCDRAW
 JSR WAITUP

QUITIT MOVE #51,CONTRL  ;FORM_DIAL (SHRINK)
 MOVE #9,CONTRL+2
 MOVE #1,CONTRL+4
 CLR CONTRL+6
 CLR CONTRL+8
 MOVE #2,INTIN
 MOVE #10,INTIN+2
 MOVE #10,INTIN+4
 MOVE #50,INTIN+6
 MOVE #10,INTIN+8
 MOVE DIALX,INTIN+10
 MOVE DIALY,INTIN+12
 MOVE DIALW,INTIN+14
 MOVE DIALH,INTIN+16
 JSR AES

 JSR CLS

 MOVE #121,CONTRL
 MOVE #3,CONTRL+2
 MOVE #1,CONTRL+4
 MOVE #2,CONTRL+6
 MOVE #0,CONTRL+8
 MOVE #1,INTIN
 MOVE #1,INTIN+2
 MOVE #1,INTIN+4
 MOVE.L #GOMENU,ADDRIN
 MOVE.L #INFILE,ADDRIN+4
 JSR AES

 MOVE #101,CONTRL  ;Close Virtual Workstation
 CLR CONTRL+2
 CLR CONTRL+6
 MOVE GRHANDLE,CONTRL+12
 JSR VDI

 MOVE #19,CONTRL  ;Appl_exit
 CLR CONTRL+2
 MOVE #1,CONTRL+4
 CLR CONTRL+6
 CLR CONTRL+8
 JSR AES

 CLR -(A7)  ;P_term
 TRAP #1

GOMENU DC.B 'MENU.PRG',0
INFILE DC.B 0,0
 EVEN

************************
* PROCESS A DIALOG BOX *
* A0=DIALOG TREE       *
* D0=EDIT FIELD        *
************************

FORMDO MOVE.L A0,FORMDIAL
 MOVE D0,FORMEDIT

 MOVE #54,CONTRL   ;FORM_CENTER
 CLR CONTRL+2
 MOVE #5,CONTRL+4
 MOVE #1,CONTRL+6
 CLR CONTRL+8
 MOVE.L FORMDIAL,ADDRIN
 JSR AES
 MOVE INTOUT+2,DIALX
 MOVE INTOUT+4,DIALY
 MOVE INTOUT+6,DIALW
 MOVE INTOUT+8,DIALH

 MOVE #51,CONTRL  ;FORM_DIAL
 MOVE #9,CONTRL+2
 MOVE #1,CONTRL+4
 CLR CONTRL+6
 CLR CONTRL+8
 CLR INTIN
 MOVE #10,INTIN+2
 MOVE #10,INTIN+4
 MOVE #50,INTIN+6
 MOVE #10,INTIN+8
 MOVE INTOUT+2,INTIN+10
 MOVE INTOUT+4,INTIN+12
 MOVE INTOUT+6,INTIN+14
 MOVE INTOUT+8,INTIN+16
 JSR AES

 MOVE #1,INTIN   ;FORM_DIAL (EXPAND)
 JSR AES

 MOVE #42,CONTRL ;OBJC_DRAW
 MOVE #6,CONTRL+2
 MOVE #1,CONTRL+4
 MOVE #1,CONTRL+6
 CLR CONTRL+8
 CLR INTIN
 MOVE #100,INTIN+2
 MOVE GEMXPOS,INTIN+4
 MOVE GEMYPOS,INTIN+6
 MOVE GEMWIDTH,INTIN+8
 MOVE GEMHOEHE,INTIN+10
 MOVE.L FORMDIAL,ADDRIN
 JSR AES

 MOVE #50,CONTRL  ;FORM_DO
 MOVE #1,CONTRL+2
 MOVE #1,CONTRL+4
 MOVE #1,CONTRL+6
 CLR CONTRL+8
 MOVE FORMEDIT,INTIN
 MOVE.L FORMDIAL,ADDRIN
 JSR AES
 MOVE INTOUT,SAVERTRN

 MOVE #51,CONTRL  ;FORM_DIAL (SHRINK)
 MOVE #9,CONTRL+2
 MOVE #1,CONTRL+4
 CLR CONTRL+6
 CLR CONTRL+8
 MOVE #2,INTIN
 MOVE #10,INTIN+2
 MOVE #10,INTIN+4
 MOVE #50,INTIN+6
 MOVE #10,INTIN+8
 MOVE DIALX,INTIN+10
 MOVE DIALY,INTIN+12
 MOVE DIALW,INTIN+14
 MOVE DIALH,INTIN+16
 JSR AES

 MOVE #3,INTIN   ;FORM_DIAL (FREES)
 JSR AES

 CLR.L D0
 MOVE SAVERTRN,D0
 RTS

FORMDIAL DC.L 0
FORMEDIT DC 0

*--------------------SHOW PANEL-------------------

SHOWPANL MOVE.L A0,FORMDIAL
 MOVE D0,FORMEDIT

 DC $A00A

 MOVE #54,CONTRL   ;FORM_CENTER
 CLR CONTRL+2
 MOVE #5,CONTRL+4
 MOVE #1,CONTRL+6
 CLR CONTRL+8
 MOVE.L FORMDIAL,ADDRIN
 JSR AES
 MOVE INTOUT+2,DIALX
 MOVE INTOUT+4,DIALY
 MOVE INTOUT+6,DIALW
 MOVE INTOUT+8,DIALH

 MOVE #73,CONTRL  ;FORM_DIAL
 MOVE #8,CONTRL+2
 MOVE #1,CONTRL+4
 CLR CONTRL+6
 CLR CONTRL+8
 MOVE #10,INTIN
 MOVE #10,INTIN+2
 MOVE #50,INTIN+4
 MOVE #10,INTIN+6
 MOVE INTOUT+2,INTIN+8
 MOVE INTOUT+4,INTIN+10
 MOVE INTOUT+6,INTIN+12
 MOVE INTOUT+8,INTIN+14
 JSR AES

 MOVE #42,CONTRL ;OBJC_DRAW
 MOVE #6,CONTRL+2
 MOVE #1,CONTRL+4
 MOVE #1,CONTRL+6
 CLR CONTRL+8
 CLR INTIN
 MOVE #30,INTIN+2
 MOVE GEMXPOS,INTIN+4
 MOVE GEMYPOS,INTIN+6
 MOVE GEMWIDTH,INTIN+8
 MOVE GEMHOEHE,INTIN+10
 MOVE.L FORMDIAL,ADDRIN
 JSR AES
 DC $A009
 RTS

********************************
* GET ADDRESS OF TREE RESOURCE *
* D0 = TREE NUMBER             *
* A0 RETURNS ADDRESS           *
********************************

GADDR CLR INTIN
 JMP GADDR3

GADDR2 MOVE #5,INTIN

GADDR3 MOVE #112,CONTRL  ;RSRC_GADDR
 MOVE #2,CONTRL+2
 MOVE #1,CONTRL+4
 CLR CONTRL+6
 MOVE #1,CONTRL+8
 MOVE D0,INTIN+2
 JSR AES
 MOVE.L ADDROUT,A0
 RTS

************
* GETEVENT *
************

GT_EVENT MOVE #25,CONTRL
 MOVE #16,CONTRL+2
 MOVE #7,CONTRL+4
 MOVE #1,CONTRL+6
 CLR CONTRL+8
 MOVE #$13,INTIN   ;KEY - MOUSE  - MESAGE
 MOVE #1,INTIN+2
 MOVE #1,INTIN+4
 MOVE #1,INTIN+6
 MOVE.L #EVMESG,ADDRIN
 JSR AES
 MOVE INTOUT,EVWHICH
 MOVE INTOUT+2,EVXPOS
 MOVE INTOUT+4,EVYPOS
 MOVE INTOUT+6,EVBUTN
 MOVE INTOUT+8,EVSTATE
 MOVE INTOUT+10,EVKEY
 MOVE INTOUT+12,EVBSTAT
 MOVE INTOUT+14,EVHEIGH
 RTS

EVMESG DC.L 0,0,0,0,0,0
EVWHICH DC 0
EVXPOS DC 0
EVYPOS DC 0
EVBUTN DC 0
EVSTATE DC 0
EVKEY DC 0
EVBSTAT DC 0
EVHEIGH DC 0

*********************
* SET DRAWING COLOR *
* D0 = COLOR #      *
*********************

GEMCOLR MOVE #17,CONTRL
 CLR CONTRL+2
 MOVE #1,CONTRL+6
 MOVE GRHANDLE,CONTRL+12
 MOVE D0,INTIN
 JMP VDI

****************************
* SET MOUSE IMAGE AND SHOW *
* D0 = IMAGE               *
****************************

SHWMOUSE MOVE #78,CONTRL ;GRAF_MOUSE
 MOVE #1,CONTRL+2
 MOVE #1,CONTRL+4
 MOVE #1,CONTRL+6
 CLR CONTRL+8
 MOVE D0,INTIN ;MOUSE IMAGE
 CLR.L ADDRIN
 JSR AES
 DC $A009  ;SHOW MOUSE
 RTS


*******************************
* INIT ALL GEM TEXT VARIABLES *
*******************************

TEXTINIT CLR D0
 JSR GEMFX
 MOVE #9,D0
 CMP #2,GRMODE
 BNE ELTON
 MOVE #10,D0
ELTON JSR GEMTSIZE
 CLR XSKEW
 CLR YSKEW
 JSR NOCLIP
 MOVE #1,D0
 JMP GEMCOLOR

********************
* SET WRITING MODE *
********************

SETMODE MOVE #32,CONTRL
 CLR CONTRL+2
 MOVE #1,CONTRL+6
 MOVE GRHANDLE,CONTRL+12
 MOVE D0,INTIN
 JMP VDI

*********************
* TURN OFF CLIPPING *
*********************

NOCLIP MOVE #129,CONTRL
 MOVE #2,CONTRL+2
 MOVE #1,CONTRL+6
 MOVE GRHANDLE,CONTRL+12
 CLR INTIN
 JMP VDI

**********************************
* SET EFFECTS FOR GEM TEXT CALLS *
* D0 = EFFECT INFO               *
**********************************

* 0-BOLD 1-INTENSITY 2-ITALICS 3-UNDERLINE 4-OUTLINE 5-SHADOW

GEMFX MOVE #106,CONTRL
 CLR CONTRL+2
 MOVE #1,CONTRL+6
 MOVE GRHANDLE,CONTRL+12
 MOVE D0,INTIN
 JMP VDI

*******************************
* SET SIZE FOR GEM TEXT CALLS *
* D0 = POINT SIZE             *
*******************************

GEMTSIZE MOVE #107,CONTRL
 CLR CONTRL+2
 MOVE #1,CONTRL+6
 MOVE GRHANDLE,CONTRL+12
 MOVE D0,INTIN
 JMP VDI

********************************
* SET COLOR FOR GEM TEXT CALLS *
* D0 = COLOR #                 *
********************************

GEMCOLOR MOVE #22,CONTRL
 CLR CONTRL+2
 MOVE #1,CONTRL+6
 MOVE GRHANDLE,CONTRL+12
 MOVE D0,INTIN
 JMP VDI

************************
* GEM TEXT PRINT SETUP *
* A0 POINTS TO STRING  *
************************

GEMTEXT MOVE XPOS,D0
 LSL #3,D0
 ADD #12,D0
 ADD XSKEW,D0
 MOVE D0,PTSIN
 MOVE YPOS,D0
 LSL #3,D0
 ADD YSKEW,D0
 MOVE D0,PTSIN+2

 LEA INTIN,A1
 CLR.L D1
GEMT101 CLR.L D0
 MOVE.B (A0)+,D0
 TST.B D0
 BEQ GEMT100
 MOVE D0,(A1)+
 ADD #1,D1

 ADD #1,XPOS
 CMP #78,XPOS
 BCS GEMT101
 CLR XPOS
 ADD #1,YPOS
 CMP #24,YPOS
 BCS GEMT101
 CLR YPOS
 JMP GEMT101

GEMT100 MOVE #8,CONTRL
 MOVE #1,CONTRL+2
 MOVE D1,CONTRL+6
 MOVE GRHANDLE,CONTRL+12
 JMP VDI

XSKEW DC 0
YSKEW DC 0
XPOS DC 0
YPOS DC 0

************************
* PRINT HEX WORD IN D0 *
************************

PWORD MOVE D0,-(A7)
 LSR #4,D0
 LSR #4,D0
 AND.L #255,D0
 JSR PRINTHEX
 MOVE (A7)+,D0
 AND.L #255,D0

*************************
* PRINT HEX VALUE IN D0 *
*************************

PRINTHEX MOVE D0,-(A7)
 LSR #4,D0
 AND.L #15,D0
 JSR PRINTONE
 MOVE (A7)+,D0
 AND.L #15,D0

PRINTONE CMP.B #10,D0
 BCC PT1
 ADD.B #'0',D0
 BRA PT2
PT1 SUB.B #10,D0
 ADD.B #'A',D0
PT2 MOVE.B D0,PT3
 LEA PT3,A0
 JMP GEMTEXT
PT3 DC.B ' ',0
 EVEN

*-----------------SYSTEM CALLS-----------------

AES MOVE.L #AESPB,D1  ;CALL AES
 MOVE.L #200,D0
 TRAP #2
 RTS

VDI MOVE.L #VDIPB,D1  ;CALL VDI
 MOVE.L #$73,D0
 TRAP #2
 RTS

*-------------GEM PARAMETER BLOCKS--------------

AESPB DC.L CONTRL,GLOBAL,INTIN,INTOUT,ADDRIN,ADDROUT
VDIPB DC.L CONTRL,INTIN,PTSIN,INTOUT,PTSOUT

GEMXPOS DC 0
GEMYPOS DC 0
GEMWIDTH DC 0
GEMHOEHE DC 0

*--------------GEM RELATED EQUATES-------------------

OBSIZE EQU 24  ;BYTES PER OBJECT STRUCTURE
MAXOB EQU 100  ;MAX NUMBER OF OBJECTS

OB_NEXT EQU 0  ;OBJECT OFFSETS
OB_HEAD EQU 2
OB_TAIL EQU 4
OB_TYPE EQU 6
OB_FLAGS EQU 8
OB_STATE EQU 10
OB_SPEC EQU 12
OB_X EQU 16
OB_Y EQU 18
OB_WIDTH EQU 20
OB_HEIGHT EQU 22

TEDSIZE EQU 28   ;SIZE OF EACH TEDINFO STRUCTURE

TE_PTEXT EQU 0   ;TEDINFO OFFSETS
TE_PTMPLT EQU 4
TE_PVALID EQU 8
TE_FONT EQU 12
TE_RESVD EQU 14
TE_JUST EQU 16
TE_COLOR EQU 18
TE_RESV2 EQU 20
TE_THICK EQU 22
TE_TXLEN EQU 24
TE_TMPLN EQU 26

ICONSIZE EQU 36  ;SIZE OF AN ICON STRUCTURE

IB_PMASK EQU 0
IB_PDATA EQU 4
IB_PTEXT EQU 8
IB_CHAR EQU 12
IB_XCHAR EQU 14
IB_YCHAR EQU 16
IB_XICON EQU 18
IB_YICON EQU 20
IB_WICON EQU 22
IB_HICON EQU 24
IB_XTEXT EQU 26
IB_YTEXT EQU 28
IB_WTEXT EQU 30
IB_HTEXT EQU 32

NAMESIZE EQU 80  ;OBJECT NAME SIZE

*----------NO RESOURCE FILE---------

NORSRC MOVE #52,CONTRL
 MOVE #1,CONTRL+2
 MOVE #1,CONTRL+4
 MOVE #1,CONTRL+6
 CLR CONTRL+8
 MOVE #1,INTIN
 MOVE.L #NORSC,ADDRIN
 JSR AES
 JMP QUITIT

RSCNAME DC.B 'UTIL.RSC',0
 EVEN

NORSC DC.B '[3][Cannot find the|resource file: UTIL.RSC]'
 DC.B '[CONTINUE]',0,0
 EVEN

*-------FATAL ERRORS------

INTERNAL MOVE #INTRNL,D0
 JMP ALERT

NOMEM MOVE #MEMERR,D0
 JMP ALERT

NOTABLE MOVE #TBLERR,D0
 JMP ALERT

TOOBIG MOVE #TBLBIG,D0
 JMP ALERT

DISKFUL MOVE #DISKFULL,D0
 JMP ALERT

*-------NON FATAL ERRORS--------

EXISTS JSR CLS
 MOVE #EXISTNC,D0
 JSR DOALERT
 JSR CLS
 JMP GETEVENT

NOFLDS JSR CLS
 MOVE #NOFIELDS,D0
 JSR DOALERT
 JSR CLS
 JMP GETEVENT

CRAZY MOVE #CRAZ1,D0
 JSR DOALERT
 JSR CLS
 JMP GETEVENT

*-------NO MEMORY ERROR--------

ALERT JSR DOALERT
 CLR GOTO
 JMP QUITIT

DOALERT JSR GADDR2
 MOVE.L A0,ADDRIN
 MOVE #52,CONTRL
 MOVE #1,CONTRL+2
 MOVE #1,CONTRL+4
 MOVE #1,CONTRL+6
 CLR CONTRL+8
 MOVE #1,INTIN
 JMP AES

*--------------------------PATH ROUTINES----------------------------

******************
* LOAD PATH INFO *
******************

GETPATH MOVE.L #39,D0
 LEA PATHNAME,A0
FPATHC CLR.B (A0)+
 DBF D0,FPATHC

 LEA FPATH,A0
 JSR F_OPEN
 TST D0
 BPL FPATHNE

FPTERR MOVE #52,CONTRL
 MOVE #1,CONTRL+2
 MOVE #1,CONTRL+4
 MOVE #1,CONTRL+6
 CLR CONTRL+8
 MOVE #1,INTIN
 MOVE.L #NOPATH,ADDRIN
 JSR AES
 JMP QUITIT

NOPATH DC.B '[3][Cannot find the|PATH.TBL file!]'
 DC.B '[RETURN TO DESKTOP]',0,0
 EVEN

FPATHNE MOVE D0,HANDLE0

 MOVE HANDLE0,A0
 MOVE.L #PATHNAME,A1
 MOVE.L #39,D0
 JSR F_READ

 MOVE.B PATHNAME,D0
 CMP.B #10,D0
 BNE FPATHE

 MOVE.L #38,D0
 LEA PATHNAME,A0
FPATHD MOVE.B 1(A0),(A0)
 ADD.L #1,A0
 DBF D0,FPATHD

FPATHE MOVE HANDLE0,A0
 JMP F_CLOSE

FPATH DC.B 'PATH.TBL',0
 EVEN

HANDLE0 DC 0
HANDLE1 DC 0
HANDLE2 DC 0
HANDLE3 DC 0

*******************************
* ADD PATH TO FILE NAME IN A0 *
*******************************

ADDPATH MOVEM.L A1-A4/D0-D2,-(A7)

 LEA PTHNM,A1
 MOVE.L #39,D0
ADDP100 CLR.B (A1)+
 DBF D0,ADDP100

 MOVE.L #39,D1
 LEA PATHNAME,A1
 LEA PTHNM,A2
ADDP101 MOVE.B (A1)+,D0
 CMP.B #' ',D0
 BEQ ADDP102
 CMP.B #13,D0
 BEQ ADDP102
 TST.B D0
 BEQ ADDP102
 MOVE.B D0,(A2)+
 DBF D1,ADDP101

ADDP102 MOVE.B (A0)+,D0
 CMP.B #13,D0
 BEQ ADDP103
 TST.B D0
 BEQ ADDP103
 MOVE.B D0,(A2)+
 DBF D1,ADDP102

ADDP103 LEA PTHNM,A0
 MOVEM.L (A7)+,A1-A4/D0-D2
 RTS

*------------------------DISK I/O ERROR--------------------------

DISKERR MOVE.L ERRDLG,A1
 ADD.L #ERMES2*OBSIZE,A1
 MOVE.L #NOERRMES,OB_SPEC(A1)

 TST D0
 BEQ DE6

 CLR.L D1
 SUB.L D0,D1
 CMP #18,D1
 BCC DE2

 SUB #1,D1
 ADD #IOERR1,D1
 JMP DEPRINT

DE2 CMP #32,D1
 BCS DE3
 CMP #41,D1
 BCC DE3
 SUB #32,D1
 ADD #IOERR2,D1
 JMP DEPRINT

DE3 CMP #64,D1
 BCS DE4
 CMP #68,D1
 BCC DE4
 SUB #64,D1
 ADD #IOERR5,D1
 JMP DEPRINT

DE4 CMP #46,D1
 BNE DE5
 MOVE #IOERR3,D1
 JMP DEPRINT
DE5 CMP #49,D1
 BNE DE6
 MOVE #IOERR4,D1
 JMP DEPRINT

DE6 MOVE #IOERR6,D1

DEPRINT MOVE.L ERRDLG,A0
 ADD.L #ERMES*OBSIZE,A0

 MULU #OBSIZE,D1
 ADD.L IOERRADR,D1
 MOVE.L D1,A1
 MOVE.L OB_SPEC(A1),OB_SPEC(A0)

 MOVE.L ERRDLG,A0
 ADD.L #ERRETRY*OBSIZE,A0
 CLR OB_STATE(A0)

 MOVE.L ERRDLG,A0
 ADD.L #ERCANCEL*OBSIZE,A0
 CLR OB_STATE(A0)

 CLR D0
 JSR SHWMOUSE

 MOVE.L ERRDLG,A0
 MOVE #-1,D0
 JSR FORMDO

 MOVE #-2,D0
 CMP #ERRETRY,SAVERTRN
 BEQ DE7
 MOVE #-1,D0

DE7 JMP QUITIT

NOERRMES DC.B '---',0
 EVEN

DISKBUFF DC.L 0,0,0,0,0,0,0,0,0,0
 DC.L 0,0,0,0,0,0,0,0,0,0
 DC.L 0,0,0,0,0,0,0,0,0,0
 DC.L 0,0,0,0,0,0,0,0,0,0

*-------------TEXT STRINGS--------------

TEMPNAME DC.B 'TEMPFILE.$$$',0
TMP2NAME DC.B 'TMP2FILE.$$$',0
TMP3NAME DC.B 'TMP3FILE.$$$',0
TBLSPEC DC.B 'TABLES.TBL',0
FLDSPEC DC.B 'FIELDS.TBL',0
INDXSPEC DC.B 'INDEXES.TBL',0
ZEROS DC.B '0',0
 EVEN

ICONS DC ICON1
 DC ICON2
 DC ICON3
 DC ICON4
 DC ICON5
 DC ICON6
 DC ICON7
 DC ICON8

NAMEL DC NAME1
 DC NAME2
 DC NAME3
 DC NAME4
 DC NAME5
 DC NAME6
 DC NAME7
 DC NAME8

TYPEL DC TYPE1
 DC TYPE2
 DC TYPE3
 DC TYPE4
 DC TYPE5
 DC TYPE6
 DC TYPE7
 DC TYPE8

SIZEL DC SIZE1
 DC SIZE2
 DC SIZE3
 DC SIZE4
 DC SIZE5
 DC SIZE6
 DC SIZE7
 DC SIZE8

DECL DC DEC1
 DC DEC2
 DC DEC3
 DC DEC4
 DC DEC5
 DC DEC6
 DC DEC7
 DC DEC8

FLDS DC FIELD1
 DC FIELD2
 DC FIELD3
 DC FIELD4
 DC FIELD5
 DC FIELD6
 DC FIELD7
 DC FIELD8

*-------------GEM PARAMETER BLOCKS--------------

 BSS

GEMSTART DS 1
CONTRL DS 1
OPCODE EQU CONTRL
SINTIN DS 1
SINTOUT DS 1
SADDRIN DS 1
SADDROUT DS.L 5
GLOBAL DS 1
APCOUNT DS 1
APID DS 1
APPRIVATE DS.L 1
APPTREE DS.L 1
AP1RESV DS.L 1
AP2RESV DS.L 1
AP3RESV DS.L 1
AP4RESV DS.L 1
INTIN DS.L 80
PTSIN DS.L 32
INTOUT DS.L 32
PTSOUT DS.L 32
ADDRIN DS.L 32
ADDROUT DS.L 32
GEMEND DS 1

GRHANDLE DS 1
DRHANDLE DS 1

*--------SYSTEM STORAGE--------

INPATH DS.B 128
INSEL DS.B 128
FILENAME DS.B 128
 EVEN

OBJTYPE DS 1
OBJCNT DS 1

DTEDIAL DS.L 1
TXTDIAL DS.L 1
NUMDIAL DS.L 1
SUREADR DS.L 1
DRVADR DS.L 1
BADDADR DS.L 1
IOERRADR DS.L 1
STRINGADR DS.L 1
ERRDLG DS.L 1

PTHNM DS.B 40
PATHNAME DS.B 40
GRMODE DS 1
SCREEN DS.L 1
DIALX DS 1
DIALY DS 1
DIALH DS 1
DIALW DS 1
SAVERTRN DS 1

FHANDLE DS 1
DHANDLE DS 1

MAINADR DS.L 1
NAMEDIAL DS.L 1

BPOINTER DS.L 1

ERASEADR DS.L 1
ERASFLAG DS 1
TRASHADR DS.L 1

FILEY DS 1
FILECNT DS 1
SLIDEPOS DS 1
FLAG DS 1
COUNT DS 1

GOTO DS 1
CNTSTORE DS 1
TBLPTR DS.L 1
POINTER DS.L 1
POINTER2 DS.L 1
ASCDES DS 1
HOLDIT DS 1
SELECTED DS 1
HOLD2 DS 1

CASEFLAG DS 1
NULLFLAG DS 1
TBLOAD DS.L 1

LDINDEX DS 1
GFLAG DS 1
COUNT2 DS 1
BUFFCNT DS.L 1
ROWCNT DS.L 1
ROWSIZE DS.L 1
NEWSIZE DS.L 1
OLDSIZE DS.L 1
WCOUNT DS 1
WFLAG DS 1
HOLDER DS 1
MENUADR DS.L 1
TREEADR DS.L 1
TABLADR DS.L 1
FCOUNT DS 1
SCOUNT DS 1
QUOTFLG DS 1

WORKADR DS.L 1
TBLADR DS.L 1
CHOICDR DS.L 1

TBLED DS.B 20
 EVEN

BIGBUFF DS.L 1
BUFFSIZE DS.L 1

TBLLEN EQU 48
FLDLEN EQU 65
TBLMAX EQU 50
FLDMAX EQU 300

TBLINFO DS.B TBLLEN*TBLMAX
 EVEN
FLDINFO DS.B FLDLEN*FLDMAX
 EVEN
DBUFF DS.B FLDLEN
 EVEN
TNAME DS.B 40
 EVEN
TNAME2 DS.B 40
 EVEN

*---------------------FIELDS TABLE-----------------------

RECSIZE  EQU 34 ;NUMBER OF BYTES IN EACH FIELD RECORD
MAXFIELD EQU 200 ;NUMBER OF FIELDS ALLOWED
FIELDSIZ EQU 65*MAXFIELD

FNAME EQU 0  ; FIELD NAME
FTYPE EQU 4  ; TYPE
FDISP EQU 6  ; DISPLACEMENT
FSIZE EQU 8  ; SIZE
FEXTR EQU 10 ; DECIMAL SIZE
FVALU EQU 12 ; POINTER TO FIELD CONTENTS
FTABL EQU 16 ; TABLE
FNEGT EQU 20 ; NEGATIVE FLAG
FVAL2 EQU 22 ; POINTER TO FIELD2 CONTENTS
FNEG2 EQU 26 ; FIELD 2 NEGATIVE FLAG
FDOIT EQU 28 ; VALID FIELD FOR SORT FLAG
FOLDY EQU 30 ; ORIGINAL FIELD NAME

FNAMES DS.B FIELDSIZ
 EVEN
FNAMES2 DS.B FIELDSIZ
 EVEN
FIELDS DS.B RECSIZE*MAXFIELD
 EVEN
FIELDS2 DS.B RECSIZE*MAXFIELD
 EVEN
INDXBUFF DS.B RECSIZE*MAXFIELD
 EVEN
INAMES DS.B FIELDSIZ
 EVEN

*-----------------MISCELLANEOUS-----------------

BUFSPACE EQU 2048
DBUFFER DS.B BUFSPACE
DBUFFER2 DS.B BUFSPACE
 EVEN

T1NAME  DS.B 40  ;TABLE 1 NAME
INNAME  DS.B 40  ;INDEX NAME
T1FNAME DS.B 40 ;TABLE 1 FILE NAME
INFNAME DS.B 40 ;INDEX FILE NAME
T1FNAME2 DS.B 40
T1FNAME3 DS.B 40
T1FNAME4 DS.B 40
T1FNAME5 DS.B 40
T1FNAME6 DS.B 40
T1FNAME7 DS.B 40

INDXFLAG DS 1

TMPBUFF DS.B 80  ;TEMP FILE NAME HOLDER

NAMES DS.B NAMESIZE*MAXOB
TYPES DS.B 2*MAXOB
DIGIT DS.B 2*MAXOB
XDEC DS.B 2*MAXOB

 EVEN
 DS.B 4000
USTK DS 1

CHOSETBL = 0;  REM ---TREE---
TUPARW = 12;  REM ---OBJECT in TREE #0---
TTABLES = 3;  REM ---OBJECT in TREE #0---
TBL1 = 4;  REM ---OBJECT in TREE #0---
TBL2 = 5;  REM ---OBJECT in TREE #0---
TBL3 = 6;  REM ---OBJECT in TREE #0---
TBL4 = 7;  REM ---OBJECT in TREE #0---
TBL5 = 8;  REM ---OBJECT in TREE #0---
TBL6 = 9;  REM ---OBJECT in TREE #0---
TBL7 = 10;  REM ---OBJECT in TREE #0---
TBL8 = 11;  REM ---OBJECT in TREE #0---
TDNARW = 13;  REM ---OBJECT in TREE #0---
TCANCEL = 17;  REM ---OBJECT in TREE #0---
TTHUMB = 15;  REM ---OBJECT in TREE #0---
TSLIDER = 14;  REM ---OBJECT in TREE #0---
UINFO = 1;  REM ---OBJECT in TREE #0---
UTILMENU = 1;  REM ---TREE---
UMOVE = 4;  REM ---OBJECT in TREE #1---
UCREATE = 5;  REM ---OBJECT in TREE #1---
UDROP = 6;  REM ---OBJECT in TREE #1---
UIMPORT = 9;  REM ---OBJECT in TREE #1---
UEXPORT = 10;  REM ---OBJECT in TREE #1---
UCANCEL = 11;  REM ---OBJECT in TREE #1---
WORKING = 2;  REM ---TREE---
UMODTBL = 7;  REM ---OBJECT in TREE #1---
WORKNUM = 9;  REM ---OBJECT in TREE #2---
ERASE = 3;  REM ---TREE---
ERYES = 4;  REM ---OBJECT in TREE #3---
ERNO = 5;  REM ---OBJECT in TREE #3---
ERCANCL = 6;  REM ---OBJECT in TREE #3---
CHOICES = 4;  REM ---TREE---
TRASH = 5;  REM ---TREE---
TEXTS = 2;  REM ---OBJECT in TREE #4---
NUMBERS = 3;  REM ---OBJECT in TREE #4---
DATE = 4;  REM ---OBJECT in TREE #4---
TABLEBOX = 6;  REM ---TREE---
BOX = 1;  REM ---OBJECT in TREE #6---
NAMETBL = 7;  REM ---TREE---
TBLNAME = 3;  REM ---OBJECT in TREE #7---
CHOIBOX = 1;  REM ---OBJECT in TREE #4---
NEWDATE = 8;  REM ---TREE---
NEWTEXT = 9;  REM ---TREE---
NEWNUM = 10;  REM ---TREE---
DATEA = 3;  REM ---OBJECT in TREE #8---
DATEL = 4;  REM ---OBJECT in TREE #8---
DATES = 5;  REM ---OBJECT in TREE #8---
DATEE = 6;  REM ---OBJECT in TREE #8---
INTVALUE = 2;  REM ---OBJECT in TREE #10---
DECVALUE = 3;  REM ---OBJECT in TREE #10---
FDTNAME = 10;  REM ---OBJECT in TREE #8---
TNAMED = 56;  REM ---OBJECT in TREE #6---
FTXNAME = 5;  REM ---OBJECT in TREE #9---
TEXTNUM = 6;  REM ---OBJECT in TREE #9---
FNMNAME = 4;  REM ---OBJECT in TREE #10---
DIGITS = 5;  REM ---OBJECT in TREE #10---
DECPOINT = 6;  REM ---OBJECT in TREE #10---
CRUPARW = 26;  REM ---OBJECT in TREE #6---
CRDNARW = 29;  REM ---OBJECT in TREE #6---
ICON1 = 3;  REM ---OBJECT in TREE #6---
ICON2 = 9;  REM ---OBJECT in TREE #6---
ICON3 = 15;  REM ---OBJECT in TREE #6---
ICON4 = 21;  REM ---OBJECT in TREE #6---
ICON5 = 31;  REM ---OBJECT in TREE #6---
ICON6 = 37;  REM ---OBJECT in TREE #6---
ICON7 = 43;  REM ---OBJECT in TREE #6---
ICON8 = 49;  REM ---OBJECT in TREE #6---
NAME1 = 4;  REM ---OBJECT in TREE #6---
NAME2 = 10;  REM ---OBJECT in TREE #6---
NAME3 = 16;  REM ---OBJECT in TREE #6---
NAME4 = 22;  REM ---OBJECT in TREE #6---
NAME5 = 32;  REM ---OBJECT in TREE #6---
NAME6 = 38;  REM ---OBJECT in TREE #6---
NAME7 = 44;  REM ---OBJECT in TREE #6---
NAME8 = 50;  REM ---OBJECT in TREE #6---
TYPE1 = 5;  REM ---OBJECT in TREE #6---
TYPE2 = 11;  REM ---OBJECT in TREE #6---
TYPE3 = 17;  REM ---OBJECT in TREE #6---
TYPE4 = 23;  REM ---OBJECT in TREE #6---
TYPE5 = 33;  REM ---OBJECT in TREE #6---
TYPE6 = 39;  REM ---OBJECT in TREE #6---
TYPE7 = 45;  REM ---OBJECT in TREE #6---
TYPE8 = 51;  REM ---OBJECT in TREE #6---
SIZE1 = 6;  REM ---OBJECT in TREE #6---
SIZE2 = 12;  REM ---OBJECT in TREE #6---
SIZE3 = 18;  REM ---OBJECT in TREE #6---
SIZE4 = 24;  REM ---OBJECT in TREE #6---
SIZE5 = 34;  REM ---OBJECT in TREE #6---
SIZE6 = 40;  REM ---OBJECT in TREE #6---
SIZE7 = 46;  REM ---OBJECT in TREE #6---
SIZE8 = 52;  REM ---OBJECT in TREE #6---
DEC1 = 7;  REM ---OBJECT in TREE #6---
DEC2 = 13;  REM ---OBJECT in TREE #6---
DEC3 = 19;  REM ---OBJECT in TREE #6---
DEC4 = 25;  REM ---OBJECT in TREE #6---
DEC5 = 35;  REM ---OBJECT in TREE #6---
DEC6 = 41;  REM ---OBJECT in TREE #6---
DEC7 = 47;  REM ---OBJECT in TREE #6---
DEC8 = 53;  REM ---OBJECT in TREE #6---
BLANK = 54;  REM ---OBJECT in TREE #6---
CRTHUMB = 28;  REM ---OBJECT in TREE #6---
CRSLIDE = 27;  REM ---OBJECT in TREE #6---
FIELD1 = 2;  REM ---OBJECT in TREE #6---
FIELD2 = 8;  REM ---OBJECT in TREE #6---
FIELD3 = 14;  REM ---OBJECT in TREE #6---
FIELD4 = 20;  REM ---OBJECT in TREE #6---
FIELD5 = 30;  REM ---OBJECT in TREE #6---
FIELD6 = 36;  REM ---OBJECT in TREE #6---
FIELD7 = 42;  REM ---OBJECT in TREE #6---
FIELD8 = 48;  REM ---OBJECT in TREE #6---
CANCEL1 = 11;  REM ---OBJECT in TREE #8---
CANCEL2 = 7;  REM ---OBJECT in TREE #9---
CANCEL3 = 7;  REM ---OBJECT in TREE #10---
ARESURE = 11;  REM ---TREE---
SURENO = 6;  REM ---OBJECT in TREE #11---
SUREYES = 5;  REM ---OBJECT in TREE #11---
NEWDRIVE = 12;  REM ---TREE---
DRVNAME = 4;  REM ---OBJECT in TREE #12---
BADSPEC = 13;  REM ---TREE---
DRVCNCL = 6;  REM ---OBJECT in TREE #12---
BADSMES = 5;  REM ---OBJECT in TREE #13---
BADCONT = 4;  REM ---OBJECT in TREE #13---
TRASHICN = 4;  REM ---OBJECT in TREE #5---
GOK = 1;  REM ---OBJECT in TREE #5---
GCANCEL = 2;  REM ---OBJECT in TREE #5---
IOERRS = 14;  REM ---TREE---
IOERR1 = 1;  REM ---OBJECT in TREE #14---
IOERR2 = 18;  REM ---OBJECT in TREE #14---
IOERR3 = 27;  REM ---OBJECT in TREE #14---
IOERR4 = 28;  REM ---OBJECT in TREE #14---
IOERR5 = 29;  REM ---OBJECT in TREE #14---
IOERR6 = 33;  REM ---OBJECT in TREE #14---
MEMERR = 0;  REM ---STRING---
REZMES = 2;  REM ---STRING---
TBLBIG = 3;  REM ---STRING---
TBLERR = 4;  REM ---STRING---
DISKFULL = 5;  REM ---STRING---
NOTBL = 6;  REM ---STRING---
NOFLD = 9;  REM ---STRING---
EXISTNC = 10;  REM ---STRING---
NOFIELDS = 11;  REM ---STRING---
TREE25 = 1;  REM ---STRING---
CRAZ1 = 12;  REM ---STRING---
NONAME = 8;  REM ---STRING---
BADNUM = 7;  REM ---STRING---
FLDEXSTS = 13;  REM ---STRING---
INTRNL = 14;  REM ---STRING---
STRINGS = 15;  REM ---TREE---
ASK1MES = 1;  REM ---OBJECT in TREE #15---
ASK2MES = 2;  REM ---OBJECT in TREE #15---
MODMES1 = 3;  REM ---OBJECT in TREE #15---
IMPMES1 = 4;  REM ---OBJECT in TREE #15---
EXPMES1 = 5;  REM ---OBJECT in TREE #15---
DROPMES1 = 6;  REM ---OBJECT in TREE #15---
MOVEMES1 = 7;  REM ---OBJECT in TREE #15---
DISKERRD = 16;  REM ---TREE---
ERRETRY = 6;  REM ---OBJECT in TREE #16---
ERCANCEL = 7;  REM ---OBJECT in TREE #16---
ERMES = 8;  REM ---OBJECT in TREE #16---
ERMES2 = 9;  REM ---OBJECT in TREE #16---
OBJ0 = 4;  REM ---OBJECT in TREE #16---

 END
