
******************************************************
*                REGENT BASE MAIN MENU               *
* --- A 4GL SQL Database System for the Atari ST --- *
*       (c) 1988 Frank Cohen, Regent Software        *
******************************************************

* 013087 V1.1 Released American Version
* 030487      Fixed 2 Bomb on No Application bug
* 032987 V1.2 AUTOFORM.FRM function added
* 061487 V1.2 Reports, Application, View & Backup icons added
* 071487 V1.2 Fixed GRHANDLE problem (1st Word bugs)
* 020288 V2.0 Fixed GRHANDLE problem again
* 021588 V2.0 Beta testing version
* 011690      Fixed problem of asking for time always in 1990

 TEXT

START MOVE.L A7,A5
 MOVE.L #USTK,A7

 MOVE.L 4(A5),A5
 MOVE.L $C(A5),D0
 ADD.L $14(A5),D0
 ADD.L $1C(A5),D0
 ADD.L #$100,D0
 MOVE.L D0,-(A7)
 MOVE.L A5,-(A7)
 MOVE D0,-(A7)
 MOVE #$4A,-(A7)
 TRAP #1
 ADD.L #12,A7

 MOVE #GEMEND-GEMSTART,D0
 LEA GEMSTART,A0
CLRGEM CLR.B (A0)+
 DBF D0,CLRGEM

*--------------INIT GEM VARIABLES-------------

 CLR.L AP1RESV   ;APPL_INIT
 CLR.L AP2RESV
 CLR.L AP3RESV
 CLR.L AP4RESV
 CLR.L APPTREE
 MOVE #10,CONTRL
 CLR CONTRL+2
 MOVE #1,CONTRL+4
 CLR CONTRL+6
 CLR CONTRL+8
 JSR AES

 MOVE #77,CONTRL  ;GRAF_HANDLE
 CLR CONTRL+2
 MOVE #5,CONTRL+4
 CLR CONTRL+6
 CLR CONTRL+8
 JSR AES
 MOVE INTOUT,DRHANDLE

 MOVE #100,CONTRL ;OPEN_VWORK
 CLR CONTRL+2
 MOVE #11,CONTRL+6
 MOVE DRHANDLE,CONTRL+12
 MOVE #1,INTIN
 MOVE #1,INTIN+2
 MOVE #1,INTIN+4
 MOVE #1,INTIN+6
 MOVE #1,INTIN+8
 MOVE #1,INTIN+10
 MOVE #1,INTIN+12
 MOVE #1,INTIN+14
 MOVE #1,INTIN+16
 MOVE #1,INTIN+18
 MOVE #2,INTIN+20
 JSR VDI
 MOVE CONTRL+12,GRHANDLE

 MOVE #104,OPCODE     ;DETERMINE WORK STORAGE
 MOVE #2,SINTIN       ;OF THE DESKTOP WINDOW
 MOVE #5,SINTOUT
 CLR SADDRIN
 CLR SADDROUT
 CLR INTIN
 MOVE #4,INTIN+2
 JSR AES
 MOVE INTOUT+2,GEMXPOS
 MOVE INTOUT+4,GEMYPOS
 MOVE INTOUT+6,GEMWIDTH
 MOVE INTOUT+8,GEMHOEHE

 CLR D0
 JSR SHWMOUSE

 MOVE #4,-(A7)  ;GRAPHIC MODE?
 TRAP #14
 ADD #2,A7
 MOVE D0,GRMODE
 TST D0
 BEQ BADREZ

 MOVE #3,-(A7)  ;SCREEN LOCATION
 TRAP #14
 ADD #2,A7
 MOVE.L D0,SCREEN

 MOVE.L #DISKBUFF,-(A7)  ;SET UP DISK I/O BLOCK
 MOVE #26,-(A7)
 TRAP #1
 ADD.L #6,A7

 JSR TEXTINIT
 JSR CLS
 JSR GETPATH

*---------GET APPLICATION NAMES-----------

 CLR FILECNT
 CLR FILEY

 LEA NAMES,A0
 MOVE.L #NAMESIZE-1,D0
NEWNMS CLR.B (A0)+
 DBF D0,NEWNMS

 LEA FSPEC,A0
 JSR ADDPATH
 CLR -(A7)        ;F_SFIRST
 MOVE.L A0,-(A7)
 MOVE #78,-(A7)
 TRAP #1
 ADD #8,A7
 TST D0
 BMI ENDNAME

 LEA AUTOFORM+1,A1
 LEA DISKBUFF+30,A0
 MOVE #7,D1
 CLR.L D0
TESTAUTO MOVE.B (A0)+,D0
 CMP.B (A1)+,D0
 BNE NOAUTO
 DBF D1,TESTAUTO
 JMP DOAUTO

NOAUTO LEA NAMES,A4
 LEA DISKBUFF+30,A0
 MOVE #7,D0
NMLOOP CMP.B #'.',(A0)
 BEQ NMEL
 TST.B (A0)
 BEQ NMEL
 CMP.B #' ',(A0)
 BEQ NMEL
 MOVE.B (A0)+,(A4)+
 DBF D0,NMLOOP
 JMP NONO

NMEL MOVE.B #' ',(A4)+
 DBF D0,NMEL

NONO ADD #1,FILECNT

GETLOOP MOVE #79,-(A7)  ;F_SNEXT
 TRAP #1
 ADD #2,A7
 TST D0
 BMI ENDNAME

 LEA AUTOFORM+1,A1
 LEA DISKBUFF+30,A0
 MOVE #7,D1
 CLR.L D0
TESTAUT2 MOVE.B (A0)+,D0
 CMP.B (A1)+,D0
 BNE NOAUT2
 DBF D1,TESTAUT2
 JMP DOAUTO

NOAUT2 CLR.L D0
 MOVE FILECNT,D0
 MULU #8,D0
 ADD.L #NAMES,D0
 MOVE.L D0,A4

 LEA DISKBUFF+30,A0
 MOVE #7,D0
SNMLOOP CMP.B #'.',(A0)
 BEQ NMEL2
 TST.B (A0)
 BEQ NMEL2
 CMP.B #' ',(A0)
 BEQ NMEL2
 MOVE.B (A0)+,(A4)+
 DBF D0,SNMLOOP
 JMP YESYES

NMEL2 MOVE.B #' ',(A4)+
 DBF D0,NMEL2

YESYES ADD #1,FILECNT
 JMP GETLOOP

ENDNAME TST D0

*-----------SORT NAMES----------

NOMORE TST FILECNT
 BEQ ORDFIX

 MOVE.L #NAMES,A0
 MOVE.L A0,A2
 MOVE.L #NAMES,A1
 ADD.L #8,A1
 MOVE.L A1,A3
 CLR FLAG

RECURSE CMP.B #' ',(A1)
 BEQ ORDONE
 TST.B (A1)
 BEQ ORDONE

ORDER TST.B (A0)
 BEQ ORDLE
 MOVE.B (A0)+,D0
 MOVE.B (A1)+,D1
 CMP.B D0,D1
 BEQ ORDER
 BCC ORDLE

 MOVE.L A2,A0
 MOVE.L A3,A1
 MOVE.L #7,D3
SWAPN MOVE.B (A0),D0
 MOVE.B (A1),D1
 MOVE.B D1,(A0)+
 MOVE.B D0,(A1)+
 DBF D3,SWAPN
 MOVE #1,FLAG

ORDLE ADD.L #8,A2
 ADD.L #8,A3
 MOVE.L A2,A0
 MOVE.L A3,A1
 JMP RECURSE

ORDONE TST FLAG
 BNE NOMORE

*----------------LOAD RESOURCE FILE------------------

ORDFIX MOVE #110,CONTRL
 CLR CONTRL+2
 MOVE #1,CONTRL+4
 MOVE #1,CONTRL+6
 CLR CONTRL+8
 MOVE.L #RSCNAME,ADDRIN
 JSR AES
 TST INTOUT
 BEQ NORSRC

 MOVE #MENU,D0
 JSR GADDR
 MOVE.L A0,CHSADR

 MOVE #TIMEDATE,D0
 JSR GADDR
 MOVE.L A0,TIMEADR

*-----------MONOCHROME----------

 MOVE #MONO,D0
 JSR GADDR
 MOVE.L A0,MONOADR

 CMP #2,GRMODE
 BNE HRDROCK

 MOVE.L CHSADR,A0
 ADD.L #SLIDER*OBSIZE,A0
 ADD #2,OB_Y(A0)
 ADD #4,OB_HEIGHT(A0)

 MOVE.L MONOADR,A0
 ADD.L #MSQL*OBSIZE,A0
 MOVE.L CHSADR,A1
 ADD.L #SQLED*OBSIZE,A1
 MOVE.L OB_SPEC(A0),OB_SPEC(A1)
 ADD #6,OB_Y(A1)

 MOVE.L MONOADR,A0
 ADD.L #MFORMS*OBSIZE,A0
 MOVE.L CHSADR,A1
 ADD.L #FORMED*OBSIZE,A1
 MOVE.L OB_SPEC(A0),OB_SPEC(A1)
 ADD #4,OB_Y(A1)

 MOVE.L MONOADR,A0
 ADD.L #MSORT*OBSIZE,A0
 MOVE.L CHSADR,A1
 ADD.L #SORT*OBSIZE,A1
 MOVE.L OB_SPEC(A0),OB_SPEC(A1)
 ADD #2,OB_Y(A1)

 MOVE.L MONOADR,A0
 ADD.L #MUTIL*OBSIZE,A0
 MOVE.L CHSADR,A1
 ADD.L #UTILS*OBSIZE,A1
 MOVE.L OB_SPEC(A0),OB_SPEC(A1)
 ADD #2,OB_Y(A1)

*------------DRAW MENU SCREEN--------------

HRDROCK CLR SLIDEPOS

 JSR DATETIME

 MOVE #54,CONTRL   ;FORM_CENTER
 CLR CONTRL+2
 MOVE #5,CONTRL+4
 MOVE #1,CONTRL+6
 CLR CONTRL+8
 MOVE.L CHSADR,ADDRIN
 JSR AES
 MOVE INTOUT+2,DIALX
 MOVE INTOUT+4,DIALY
 MOVE INTOUT+6,DIALW
 MOVE INTOUT+8,DIALH

 MOVE #51,CONTRL  ;FORM_DIAL
 MOVE #9,CONTRL+2
 MOVE #1,CONTRL+4
 CLR CONTRL+6
 CLR CONTRL+8
 CLR INTIN
 MOVE #10,INTIN+2
 MOVE #10,INTIN+4
 MOVE #50,INTIN+6
 MOVE #10,INTIN+8
 MOVE INTOUT+2,INTIN+10
 MOVE INTOUT+4,INTIN+12
 MOVE INTOUT+6,INTIN+14
 MOVE INTOUT+8,INTIN+16
 JSR AES

 MOVE #1,INTIN   ;FORM_DIAL (EXPAND)
 JSR AES

 MOVE #42,CONTRL ;OBJC_DRAW
 MOVE #6,CONTRL+2
 MOVE #1,CONTRL+4
 MOVE #1,CONTRL+6
 CLR CONTRL+8
 CLR INTIN
 MOVE #100,INTIN+2
 MOVE GEMXPOS,INTIN+4
 MOVE GEMYPOS,INTIN+6
 MOVE GEMWIDTH,INTIN+8
 MOVE GEMHOEHE,INTIN+10
 MOVE.L CHSADR,ADDRIN
 JSR AES

 DC $A00A
 MOVE #28,D0
 JSR GEMTSIZE
 MOVE #5,D0
 JSR GEMFX
 MOVE #2,D0
 JSR SETMODE
 CLR YPOS
 CMP #2,GRMODE
 BNE BANDW1
 MOVE #2,YPOS
BANDW1 MOVE #25,XPOS
 LEA RBASE,A0
 JSR GEMTEXT
 CLR D0
 JSR SETMODE
 JSR TEXTINIT

 JSR SHOWFILE

 DC $A009

*---------------------WAIT FOR EVENT------------------

GETEVENT JSR WAITMOUSE

 MOVE #THUMB,D0
 JSR OBJCFIND
 CMP #THUMB,INTOUT
 BEQ MVTHUMB

 MOVE #SLIDER,D0
 JSR OBJCFIND
 CMP #SLIDER,INTOUT
 BEQ PAGED

 MOVE #UPARROW,D0
 JSR OBJCFIND
 CMP #UPARROW,INTOUT
 BEQ ARROWUP

 MOVE #DNARROW,D0
 JSR OBJCFIND
 CMP #DNARROW,INTOUT
 BEQ ARROWDN

 MOVE #QUIT,D0
 JSR OBJCFIND
 CLR GOTO
 CMP #QUIT,INTOUT
 BEQ QUIT2GEM

 MOVE #SQLED,D0
 JSR OBJCFIND
 MOVE #1,GOTO
 CMP #SQLED,INTOUT
 BEQ QUITZ

 MOVE #FORMED,D0
 JSR OBJCFIND
 MOVE #2,GOTO
 CMP #FORMED,INTOUT
 BEQ QUITZ

 MOVE #SORT,D0
 JSR OBJCFIND
 MOVE #3,GOTO
 CMP #SORT,INTOUT
 BEQ QUITZ

 MOVE #UTILS,D0
 JSR OBJCFIND
 MOVE #4,GOTO
 CMP #UTILS,INTOUT
 BEQ QUITZ

 MOVE #43,CONTRL  ;OBJC_FIND
 MOVE #4,CONTRL+2
 MOVE #1,CONTRL+4
 MOVE #1,CONTRL+6
 CLR CONTRL+8
 MOVE #OFILES,INTIN
 MOVE #100,INTIN+2
 MOVE MOUSEX,INTIN+4
 MOVE MOUSEY,INTIN+6
 MOVE.L CHSADR,ADDRIN
 JSR AES

 CMP #APP1,INTOUT
 BCS GETEVENT
 CMP #APP8+1,INTOUT
 BCC GETEVENT

 CLR.L D0
 MOVE INTOUT,D0
 MULU #OBSIZE,D0
 ADD.L CHSADR,D0
 MOVE.L D0,A0
 MOVE.L OB_SPEC(A0),A1
 MOVE.L TE_PTEXT(A1),A1

 TST.B (A1)
 BEQ GETEVENT
 CMP.B #' ',(A1)
 BEQ GETEVENT

 LEA RUNFILE+1,A2
 CLR D0
TIRED TST.B (A1)
 BEQ USED
 CMP.B #'_',(A1)
 BEQ USED
 CMP.B #' ',(A1)
 BEQ USED
 MOVE.B (A1)+,(A2)+
 ADD #1,D0
 CMP #8,D0
 BNE TIRED

USED MOVE.B #'.',(A2)+
 MOVE.B #'F',(A2)+
 MOVE.B #'R',(A2)+
 MOVE.B #'M',(A2)+
 CLR.B (A2)+
 ADD.B #4,D0
 MOVE.B D0,RUNFILE

 MOVE #1,OB_STATE(A0)
 MOVE INTOUT,D0
 JSR OBJCDRAW

 MOVE #121,CONTRL  ;SHELL_WRITE
 MOVE #3,CONTRL+2
 MOVE #1,CONTRL+4
 MOVE #2,CONTRL+6
 CLR CONTRL+8
 MOVE #1,INTIN
 MOVE #1,INTIN+2
 MOVE #1,INTIN+4
 MOVE.L #BASEPRG,ADDRIN
 MOVE.L #RUNFILE,ADDRIN+4
 JSR AES

 CLR GOTO
 JMP QUITIT

*--------------SHOW APPLICATIONS-------------

SHOWFILE TST FILECNT
 BEQ SHOWDN

 CLR.L D0
 MOVE FILEY,D0
 MULU #8,D0
 ADD.L #NAMES,D0
 MOVE.L D0,A0

 CLR COUNT

TALKING CLR.L D0
 MOVE COUNT,D0
 ADD #APP1,D0
 MULU #OBSIZE,D0
 ADD.L CHSADR,D0
 MOVE.L D0,A1

 MOVE.L OB_SPEC(A1),A1
 MOVE.L TE_PTEXT(A1),A1

 MOVE #7,D0
HEADS MOVE.B (A0)+,(A1)+
 DBF D0,HEADS

 ADD #1,COUNT
 CMP #8,COUNT
 BNE TALKING

 MOVE #1,D0
 JSR SETMODE

 MOVE #APP1,D0
 JSR OBJCDRAW
 MOVE #APP2,D0
 JSR OBJCDRAW
 MOVE #APP3,D0
 JSR OBJCDRAW
 MOVE #APP4,D0
 JSR OBJCDRAW
 MOVE #APP5,D0
 JSR OBJCDRAW
 MOVE #APP6,D0
 JSR OBJCDRAW
 MOVE #APP7,D0
 JSR OBJCDRAW
 MOVE #APP8,D0
 JMP OBJCDRAW

SHOWDN RTS

*--------------SLIDE BAR ACTION-------------

PAGED MOVE.L #81,D1
 CMP #2,GRMODE
 BNE PAGESUN
 MOVE.L #162,D1
PAGESUN MOVE.L #THUMB,D0
 JSR CALC
 CLR.L D0
 MOVE OB_Y(A0),D0
 ADD D1,D0
 CMP MOUSEY,D0
 BCC PAGEUP

PAGEDN CMP #8,FILECNT
 BCS GETEVENT

 CLR.L D0
 MOVE FILEY,D0
 ADD #16,D0
 CMP FILECNT,D0
 BCC PAGEBOT
 ADD #8,FILEY
 JMP KINKY

PAGEBOT CLR.L D0
 MOVE FILECNT,D0
 SUB #8,D0
 MOVE D0,FILEY
 JMP KINKY

PAGEUP CMP #8,FILEY
 BCS PAGETOP
 SUB #8,FILEY
 JMP KINKY
PAGETOP CLR FILEY
 JMP KINKY

ARROWUP TST FILEY
 BEQ GETEVENT
 SUB #1,FILEY
 JMP KINKY

ARROWDN CMP #8,FILECNT
 BCS GETEVENT
 CLR.L D0
 MOVE FILECNT,D0
 SUB #9,D0
 CMP FILEY,D0
 BCS GETEVENT
 ADD #1,FILEY
 JMP KINKY

MVTHUMB MOVE #76,CONTRL  ;GRAF_SLIDBOX  DRAG IT
 MOVE #3,CONTRL+2
 MOVE #1,CONTRL+4
 MOVE #1,CONTRL+6
 CLR CONTRL+8
 MOVE #SLIDER,INTIN
 MOVE #THUMB,INTIN+2
 MOVE #1,INTIN+4
 MOVE.L CHSADR,ADDRIN
 JSR AES

NADA CMP #8,FILECNT
 BCS GETEVENT
 MOVE INTOUT,HOLDER

 MOVE.L #SLIDER,D0
 JSR CALC

 CLR.L D0
 MOVE FILECNT,D0
 MOVE.L #1000,D1
 DIVU D0,D1
 AND.L #$FFFF,D1
 CLR.L D2
 MOVE HOLDER,D2
 DIVU D1,D2
 AND.L #$FFFF,D2

 MOVE.L D2,D0
 ADD #16,D0
 CMP FILECNT,D0
 BCC PAGEBOT

 MOVE D2,FILEY

KINKY JSR SHOWFILE

 CLR SLIDEPOS
 CLR.L D1
 MOVE FILECNT,D1
 CMP #8,D1
 BCS KINKTOP
 SUB #8,D1

 CLR.L D0
 MOVE FILEY,D0
 MULU #1000,D0
 DIVU D1,D0
 MOVE D0,SLIDEPOS

KINKTOP MOVE.L #THUMB,D0
 JSR CALC
 CLR.L D3
 MOVE OB_HEIGHT(A0),D3

 MOVE.L #SLIDER,D0
 JSR CALC
 CLR.L D0
 MOVE OB_HEIGHT(A0),D0
 SUB D3,D0

 MULU SLIDEPOS,D0
 DIVU #1000,D0
 MOVE D0,D2

 MOVE.L #THUMB,D0
 JSR CALC
 MOVE D2,OB_Y(A0)

REPLOT MOVE #SLIDER,D0
 JSR OBJCDRAW
 MOVE #THUMB,D0
 JSR OBJCDRAW
 JMP GETEVENT

CALC MULU #OBSIZE,D0
 ADD.L CHSADR,D0
 MOVE.L D0,A0
 RTS

*---------------FIND OBJECT UNDER MOUSE----------------

OBJCFIND MOVE #43,CONTRL  ;OBJC_FIND
 MOVE #4,CONTRL+2
 MOVE #1,CONTRL+4
 MOVE #1,CONTRL+6
 CLR CONTRL+8
 MOVE D0,INTIN
 CLR INTIN+2
 MOVE MOUSEX,INTIN+4
 MOVE MOUSEY,INTIN+6
 MOVE.L CHSADR,ADDRIN
 JMP AES

*-------------------WAIT FOR MOUSE DOWN--------------------

WAITMOUSE MOVE #21,CONTRL
 MOVE #3,CONTRL+2
 MOVE #5,CONTRL+4
 CLR CONTRL+6
 CLR CONTRL+8
 MOVE #1,INTIN
 MOVE #1,INTIN+2
 MOVE #1,INTIN+4
 JSR AES
 MOVE INTOUT+2,MOUSEX
 MOVE INTOUT+4,MOUSEY
 RTS

MOUSEX DC 0
MOUSEY DC 0

*-----------------WAIT FOR MOUSE UP--------------------

WAITUP MOVE #21,CONTRL
 MOVE #3,CONTRL+2
 MOVE #5,CONTRL+4
 CLR CONTRL+6
 CLR CONTRL+8
 MOVE #1,INTIN
 MOVE #1,INTIN+2
 CLR INTIN+4
 JMP AES

*-----------------------CLEAR SCREEN------------------------

CLS DC $A00A

 MOVE.L #$FFFF,D1
 CMP #2,GRMODE
 BNE CLS2
 CLR.L D1
CLS2 MOVE.L SCREEN,A0
 MOVE.L #8000-1,D0
BEER MOVE.L D1,(A0)+
 DBF D0,BEER
 DC $A009
 RTS

*-----------------------BAD RESOLUTION------------------------

BADREZ MOVE #52,CONTRL
 MOVE #1,CONTRL+2
 MOVE #1,CONTRL+4
 MOVE #1,CONTRL+6
 CLR CONTRL+8
 MOVE #1,INTIN
 MOVE.L #REZMES,ADDRIN
 JSR AES
 JMP QUITZ

REZMES DC.B '[1][You must be in|MEDIUM RESOLUTION|to run this program.]'
 DC.B '[Continue]',0,0
 EVEN

*------------------------DRAW OBJECT--------------------------

OBJCDRAW MOVE #42,CONTRL ;OBJC_DRAW
 MOVE #6,CONTRL+2
 MOVE #1,CONTRL+4
 MOVE #1,CONTRL+6
 CLR CONTRL+8
 MOVE D0,INTIN
 CLR INTIN+2
 MOVE GEMXPOS,INTIN+4
 MOVE GEMYPOS,INTIN+6
 MOVE GEMWIDTH,INTIN+8
 MOVE GEMHOEHE,INTIN+10
 MOVE.L CHSADR,ADDRIN
 JMP AES

*--------------------------QUIT----------------------------

QUIT2GEM MOVE.L CHSADR,D0
 ADD.L #QUIT*24,D0
 MOVE.L D0,A0
 MOVE #1,OB_STATE(A0)
 MOVE #QUIT,D0
 JSR OBJCDRAW

 MOVE #51,CONTRL  ;FORM_DIAL (SHRINK)
 MOVE #9,CONTRL+2
 MOVE #1,CONTRL+4
 CLR CONTRL+6
 CLR CONTRL+8

 MOVE #2,INTIN
 MOVE #1,INTIN+2
 MOVE #1,INTIN+4
 MOVE #2,INTIN+6
 MOVE #2,INTIN+8
 MOVE DIALX,INTIN+10
 MOVE DIALY,INTIN+12
 MOVE DIALW,INTIN+14
 MOVE DIALH,INTIN+16
 JSR AES
 MOVE #3,INTIN   ;FORM_DIAL (FREES)
 JSR AES
 JSR CLS
 JMP QUITGEM

QUITZ CLR.L D0
 MOVE INTOUT,D0
 MULU #24,D0
 ADD.L CHSADR,D0
 MOVE.L D0,A0
 MOVE #1,OB_STATE(A0)

 MOVE INTOUT,D0
 JSR OBJCDRAW

QUITIT MOVE #73,CONTRL ;Graf_growbox
 MOVE #8,CONTRL+2
 MOVE #1,CONTRL+4
 CLR CONTRL+6
 CLR CONTRL+8
 MOVE DIALX,INTIN
 MOVE DIALY,INTIN+2
 MOVE DIALW,INTIN+4
 MOVE DIALH,INTIN+6
 MOVE GEMXPOS,INTIN+8
 MOVE GEMYPOS,INTIN+10
 MOVE GEMWIDTH,INTIN+12
 MOVE GEMHOEHE,INTIN+14
 JSR AES

 MOVE #51,CONTRL  ;FORM_DIAL (finish)
 MOVE #9,CONTRL+2
 MOVE #1,CONTRL+4
 CLR CONTRL+6
 CLR CONTRL+8
 MOVE #3,INTIN
 JSR AES

QUITGEM MOVE #111,CONTRL     ;FREE RESOURCES
 CLR CONTRL+2
 MOVE #1,CONTRL+4
 CLR CONTRL+6
 CLR CONTRL+8
 JSR AES

 TST GOTO
 BEQ NORUN

 MOVE #121,CONTRL
 MOVE #3,CONTRL+2
 MOVE #1,CONTRL+4
 MOVE #2,CONTRL+6
 MOVE #0,CONTRL+8
 MOVE #1,INTIN
 MOVE #1,INTIN+2
 MOVE #1,INTIN+4

 LEA GOSQL,A0
 CMP #1,GOTO
 BEQ RUNIT
 LEA GOFORM,A0
 CMP #2,GOTO
 BEQ RUNIT
 LEA GOSORT,A0
 CMP #3,GOTO
 BEQ RUNIT
 LEA GOUTIL,A0
 CMP #4,GOTO
 BEQ RUNIT
 JMP NORUN

RUNIT JSR ADDPATH
 MOVE.L A0,ADDRIN
 MOVE.L #INFILE,ADDRIN+4
 JSR AES

NORUN MOVE #101,CONTRL  ;Close Virtual Workstation
 CLR CONTRL+2
 CLR CONTRL+6
 MOVE GRHANDLE,CONTRL+12
 JSR VDI

 MOVE #19,CONTRL  ;Appl_exit
 CLR CONTRL+2
 MOVE #1,CONTRL+4
 CLR CONTRL+6
 CLR CONTRL+8
 JSR AES

 CLR -(A7)  ;P_term
 TRAP #1

GOTO DC 0
GOSQL DC.B 'BASE.PRG',0
GOFORM DC.B 'FORMS.PRG',0
GOSORT DC.B 'SORT.PRG',0
GOUTIL DC.B 'UTIL.PRG',0
 EVEN

INFILE DC.B 0,0
 EVEN

DOAUTO MOVE #121,CONTRL  ;SHELL_WRITE
 MOVE #3,CONTRL+2
 MOVE #1,CONTRL+4
 MOVE #2,CONTRL+6
 CLR CONTRL+8
 MOVE #1,INTIN
 MOVE #1,INTIN+2
 MOVE #1,INTIN+4
 LEA BASEPRG,A0
 JSR ADDPATH
 MOVE.L A0,ADDRIN
 MOVE.L #AUTOFORM,ADDRIN+4
 JSR AES
 JMP NORUN

AUTOFORM DC.B 12,'AUTOFORM.FRM',0
 EVEN

************************
* PROCESS A DIALOG BOX *
* A0=DIALOG TREE       *
* D0=EDIT FIELD        *
************************

FORMDO MOVE.L A0,FORMDIAL
 MOVE D0,FORMEDIT

 MOVE #54,CONTRL   ;FORM_CENTER
 CLR CONTRL+2
 MOVE #5,CONTRL+4
 MOVE #1,CONTRL+6
 CLR CONTRL+8
 MOVE.L FORMDIAL,ADDRIN
 JSR AES
 MOVE INTOUT+2,DIALX
 MOVE INTOUT+4,DIALY
 MOVE INTOUT+6,DIALW
 MOVE INTOUT+8,DIALH

 MOVE #51,CONTRL  ;FORM_DIAL
 MOVE #9,CONTRL+2
 MOVE #1,CONTRL+4
 CLR CONTRL+6
 CLR CONTRL+8
 CLR INTIN
 MOVE #10,INTIN+2
 MOVE #10,INTIN+4
 MOVE #50,INTIN+6
 MOVE #10,INTIN+8
 MOVE INTOUT+2,INTIN+10
 MOVE INTOUT+4,INTIN+12
 MOVE INTOUT+6,INTIN+14
 MOVE INTOUT+8,INTIN+16
 JSR AES

 MOVE #1,INTIN   ;FORM_DIAL (EXPAND)
 JSR AES

 MOVE #42,CONTRL ;OBJC_DRAW
 MOVE #6,CONTRL+2
 MOVE #1,CONTRL+4
 MOVE #1,CONTRL+6
 CLR CONTRL+8
 CLR INTIN
 MOVE #100,INTIN+2
 MOVE GEMXPOS,INTIN+4
 MOVE GEMYPOS,INTIN+6
 MOVE GEMWIDTH,INTIN+8
 MOVE GEMHOEHE,INTIN+10
 MOVE.L FORMDIAL,ADDRIN
 JSR AES

 MOVE #50,CONTRL  ;FORM_DO
 MOVE #1,CONTRL+2
 MOVE #1,CONTRL+4
 MOVE #1,CONTRL+6
 CLR CONTRL+8
 MOVE FORMEDIT,INTIN
 MOVE.L FORMDIAL,ADDRIN
 JSR AES
 MOVE INTOUT,SAVERTRN

 MOVE #51,CONTRL  ;FORM_DIAL (SHRINK)
 MOVE #9,CONTRL+2
 MOVE #1,CONTRL+4
 CLR CONTRL+6
 CLR CONTRL+8
 MOVE #2,INTIN
 MOVE #10,INTIN+2
 MOVE #10,INTIN+4
 MOVE #50,INTIN+6
 MOVE #10,INTIN+8
 MOVE DIALX,INTIN+10
 MOVE DIALY,INTIN+12
 MOVE DIALW,INTIN+14
 MOVE DIALH,INTIN+16
 JSR AES

 MOVE #3,INTIN   ;FORM_DIAL (FREES)
 JMP AES

FORMDIAL DC.L 0
FORMEDIT DC 0

********************************
* GET ADDRESS OF TREE RESOURCE *
* D0 = TREE NUMBER             *
* A0 RETURNS ADDRESS           *
********************************

GADDR MOVE #112,CONTRL  ;RSRC_GADDR
 MOVE #2,CONTRL+2
 MOVE #1,CONTRL+4
 CLR CONTRL+6
 MOVE #1,CONTRL+8
 CLR INTIN
 MOVE D0,INTIN+2
 JSR AES
 MOVE.L ADDROUT,A0
 RTS

************
* GETEVENT *
************

GT_EVENT MOVE #25,CONTRL
 MOVE #16,CONTRL+2
 MOVE #7,CONTRL+4
 MOVE #1,CONTRL+6
 CLR CONTRL+8
 MOVE #$13,INTIN   ;KEY - MOUSE  - MESAGE
 MOVE #1,INTIN+2
 MOVE #1,INTIN+4
 MOVE #1,INTIN+6
 MOVE.L #EVMESG,ADDRIN
 JSR AES
 MOVE INTOUT,EVWHICH
 MOVE INTOUT+2,EVXPOS
 MOVE INTOUT+4,EVYPOS
 MOVE INTOUT+6,EVBUTN
 MOVE INTOUT+8,EVSTATE
 MOVE INTOUT+10,EVKEY
 MOVE INTOUT+12,EVBSTAT
 MOVE INTOUT+14,EVHEIGH
 RTS

EVMESG DC.L 0,0,0,0,0,0
EVWHICH DC 0
EVXPOS DC 0
EVYPOS DC 0
EVBUTN DC 0
EVSTATE DC 0
EVKEY DC 0
EVBSTAT DC 0
EVHEIGH DC 0

****************************
* SET MOUSE IMAGE AND SHOW *
* D0 = IMAGE               *
****************************

SHWMOUSE MOVE #78,CONTRL ;GRAF_MOUSE
 MOVE #1,CONTRL+2
 MOVE #1,CONTRL+4
 MOVE #1,CONTRL+6
 CLR CONTRL+8
 MOVE D0,INTIN ;MOUSE IMAGE
 JSR AES
 DC $A009  ;SHOW MOUSE
 RTS

*******************************
* INIT ALL GEM TEXT VARIABLES *
*******************************

TEXTINIT CLR D0
 JSR GEMFX
 MOVE #9,D0
 CMP #2,GRMODE
 BNE ELTON
 MOVE #10,D0
ELTON JSR GEMTSIZE
 CLR XSKEW
 CLR YSKEW
 JSR NOCLIP
 MOVE #1,D0
 JMP GEMCOLOR

********************
* SET WRITING MODE *
********************

SETMODE MOVE #32,CONTRL
 CLR CONTRL+2
 MOVE #1,CONTRL+6
 MOVE GRHANDLE,CONTRL+12
 MOVE D0,INTIN
 JMP VDI

*********************
* TURN OFF CLIPPING *
*********************

NOCLIP MOVE #129,CONTRL
 MOVE #2,CONTRL+2
 MOVE #1,CONTRL+6
 MOVE GRHANDLE,CONTRL+12
 CLR INTIN
 JMP VDI

**********************************
* SET EFFECTS FOR GEM TEXT CALLS *
* D0 = EFFECT INFO               *
**********************************

* 0-BOLD 1-INTENSITY 2-ITALICS 3-UNDERLINE 4-OUTLINE 5-SHADOW

GEMFX MOVE #106,CONTRL
 CLR CONTRL+2
 MOVE #1,CONTRL+6
 MOVE GRHANDLE,CONTRL+12
 MOVE D0,INTIN
 JMP VDI

*******************************
* SET SIZE FOR GEM TEXT CALLS *
* D0 = POINT SIZE             *
*******************************

GEMTSIZE MOVE #107,CONTRL
 CLR CONTRL+2
 MOVE #1,CONTRL+6
 MOVE GRHANDLE,CONTRL+12
 MOVE D0,INTIN
 JMP VDI

********************************
* SET COLOR FOR GEM TEXT CALLS *
* D0 = COLOR #                 *
********************************

GEMCOLOR MOVE #22,CONTRL
 CLR CONTRL+2
 MOVE #1,CONTRL+6
 MOVE GRHANDLE,CONTRL+12
 MOVE D0,INTIN
 JMP VDI

************************
* GEM TEXT PRINT SETUP *
* A0 POINTS TO STRING  *
************************

GEMTEXT MOVE XPOS,D0
 LSL #3,D0
 ADD #12,D0
 ADD XSKEW,D0
 MOVE D0,PTSIN
 MOVE YPOS,D0
 LSL #3,D0
 ADD #29,D0
 ADD YSKEW,D0
 MOVE D0,PTSIN+2

 LEA INTIN,A1
 CLR.L D1
GEMT101 CLR.L D0
 MOVE.B (A0)+,D0
 TST.B D0
 BEQ GEMT100
 MOVE D0,(A1)+
 ADD #1,D1

 ADD #1,XPOS
 CMP #78,XPOS
 BCS GEMT101
 CLR XPOS
 ADD #1,YPOS
 CMP #24,YPOS
 BCS GEMT101
 CLR YPOS
 JMP GEMT101

GEMT100 MOVE #8,CONTRL
 MOVE #1,CONTRL+2
 MOVE D1,CONTRL+6
 MOVE GRHANDLE,CONTRL+12
 JMP VDI

XSKEW DC 0
YSKEW DC 0
XPOS DC 0
YPOS DC 0


AES MOVE.L #AESPB,D1  ;CALL AES
 MOVE.L #$C8,D0
 TRAP #2
 RTS

VDI MOVE.L #VDIPB,D1  ;CALL VDI
 MOVE.L #$73,D0
 TRAP #2
 RTS



*-------------GEM PARAMETER BLOCKS--------------

AESPB DC.L CONTRL,GLOBAL,INTIN,INTOUT,ADDRIN,ADDROUT
VDIPB DC.L CONTRL,INTIN,PTSIN,INTOUT,PTSOUT

*--------------GEM RELATED EQUATES-------------------

OBSIZE EQU 24  ;BYTES PER OBJECT STRUCTURE

OB_NEXT EQU 0  ;OBJECT OFFSETS
OB_HEAD EQU 2
OB_TAIL EQU 4
OB_TYPE EQU 6
OB_FLAGS EQU 8
OB_STATE EQU 10
OB_SPEC EQU 12
OB_X EQU 16
OB_Y EQU 18
OB_WIDTH EQU 20
OB_HEIGHT EQU 22

TEDSIZE EQU 28   ;SIZE OF EACH TEDINFO STRUCTURE

TE_PTEXT EQU 0   ;TEDINFO OFFSETS
TE_PTMPLT EQU 4
TE_PVALID EQU 8
TE_FONT EQU 12
TE_RESVD EQU 14
TE_JUST EQU 16
TE_COLOR EQU 18
TE_RESV2 EQU 20
TE_THICK EQU 22
TE_TXLEN EQU 24
TE_TMPLN EQU 26

*----------NO RESOURCE FILE---------

NORSRC MOVE #52,CONTRL
 MOVE #1,CONTRL+2
 MOVE #1,CONTRL+4
 MOVE #1,CONTRL+6
 CLR CONTRL+8
 MOVE #1,INTIN
 MOVE.L #NORSC,ADDRIN
 JSR AES
 JMP QUITZ

RSCNAME DC.B 'MENU.RSC',0
 EVEN

NORSC DC.B '[3][Cannot find the|resource file: MENU.RSC]'
 DC.B '[CONTINUE]',0,0
 EVEN

*--------------------------PATH ROUTINES----------------------------

******************
* LOAD PATH INFO *
******************

GETPATH MOVE.L #39,D0
 LEA PATHNAME,A0
FPATHC CLR.B (A0)+
 DBF D0,FPATHC

 LEA FPATH,A0
 JSR F_OPEN
 TST D0
 BPL FPATHNE

FPTERR MOVE #52,CONTRL
 MOVE #1,CONTRL+2
 MOVE #1,CONTRL+4
 MOVE #1,CONTRL+6
 CLR CONTRL+8
 MOVE #1,INTIN
 MOVE.L #NOPATH,ADDRIN
 JSR AES
 JMP QUITZ

NOPATH DC.B '[3][Cannot find the|PATH.TBL file!]'
 DC.B '[RETURN TO DESKTOP]',0,0
 EVEN

FPATHNE MOVE D0,HANDLE0

 MOVE HANDLE0,A0
 MOVE.L #PATHNAME,A1
 MOVE.L #39,D0
 JSR F_READ

 MOVE.B PATHNAME,D0
 CMP.B #10,D0
 BNE FPATHE

 MOVE.L #38,D0
 LEA PATHNAME,A0
FPATHD MOVE.B 1(A0),(A0)
 ADD.L #1,A0
 DBF D0,FPATHD

FPATHE MOVE HANDLE0,A0
 JMP F_CLOSE

FPATH DC.B 'PATH.TBL',0
 EVEN
HANDLE0 DC 0
HANDLE1 DC 0

*******************************
* ADD PATH TO FILE NAME IN A0 *
*******************************

ADDPATH MOVEM.L A1-A4/D0-D2,-(A7)

 LEA PTHNM,A1
 MOVE.L #39,D0
ADDP100 CLR.B (A1)+
 DBF D0,ADDP100

 MOVE.L #39,D1
 LEA PATHNAME,A1
 LEA PTHNM,A2
ADDP101 MOVE.B (A1)+,D0
 CMP.B #' ',D0
 BEQ ADDP102
 CMP.B #13,D0
 BEQ ADDP102
 TST.B D0
 BEQ ADDP102
 MOVE.B D0,(A2)+
 DBF D1,ADDP101

ADDP102 MOVE.B (A0)+,D0
 CMP.B #13,D0
 BEQ ADDP103
 TST.B D0
 BEQ ADDP103
 MOVE.B D0,(A2)+
 DBF D1,ADDP102

ADDP103 LEA PTHNM,A0
 MOVEM.L (A7)+,A1-A4/D0-D2
 RTS

**************************
* OPEN DISK FILE         *
* A0 POINTS TO FILE NAME *
* D0 RETURNS HANDLE      *
**************************

F_OPEN MOVE #2,-(A7)
 MOVE.L A0,-(A7)
 MOVE #$3D,-(A7)
 TRAP #1
 ADD.L #8,A7
 RTS

*************************
* READ DISK FILE        *
* A0 = FILE HANDLE      *
* A1 = WHERE TO LOAD IT *
* D0 = NUMBER OF BYTES  *
*************************

F_READ MOVE.L A1,-(A7)
 MOVE.L D0,-(A7)
 MOVE A0,-(A7)
 MOVE #$3F,-(A7)
 TRAP #1
 ADD.L #12,A7
 RTS

*********************
* CLOSE DISK FILE   *
* A0 = FILE HANDLE  *
* D0 = ERROR CODE   *
*********************

F_CLOSE MOVE A0,-(A7)
 MOVE #$3E,-(A7)
 TRAP #1
 ADD.L #4,A7
 RTS

*------------------------DISK I/O ERROR--------------------------

DISKERR TST.L D0
 BEQ GETEVENT

 CLR.L D1
 SUB.L D0,D1
 CMP #18,D1
 BCC DE2

 SUB.L #1,D1
 MULU #22,D1
 ADD.L #ERRORS,D1
 BRA DEPRINT

DE2 CMP #32,D1
 BCS DE3
 CMP #41,D1
 BCC DE3
 SUB #32,D1
 MULU #22,D1
 ADD.L #ERRORS32,D1
 BRA DEPRINT

DE3 CMP #64,D1
 BCS DE4
 CMP #68,D1
 BCC DE4
 SUB #64,D1
 MULU #22,D1
 ADD.L #ERR64,D1
 BRA DEPRINT

DE4 CMP #46,D1
 BNE DE5
 MOVE.L #ERR46,D1
 BRA DEPRINT
DE5 CMP #49,D1
 BNE DE6
 MOVE.L #ERR49,D1
 BRA DEPRINT

DE6 MOVE.L #SERROR,D1

DEPRINT MOVE.L D1,A0
 LEA DISKERR2,A1
 MOVE.L #19,D2
DEPR1 MOVE.B (A0)+,D0
 TST.B D0
 BEQ DEPR2
 MOVE.B D0,(A1)+
 DBF D2,DEPR1

DEPR2 LEA ERRBUT,A0
DEPR3 MOVE.B (A0)+,D0
 TST.B D0
 BEQ DEPR4
 MOVE.B D0,(A1)+
 JMP DEPR3

DEPR4 CLR.B (A1)+
 CLR.B (A1)+

 MOVE #52,CONTRL
 MOVE #1,CONTRL+2
 MOVE #1,CONTRL+4
 MOVE #1,CONTRL+6
 CLR CONTRL+8
 MOVE #1,INTIN
 MOVE.L #DISKERR1,ADDRIN
 JSR AES
 JMP GETEVENT

DISKERR1 DC.B '[1][Disk I/O Error!|'
DISKERR2 DC.L 0,0,0,0,0,0,0,0,0,0,0
 DC.L 0,0,0,0,0,0,0,0,0,0,0
ERRBUT DC.B '][Continue]',0,0
 EVEN

ERRORS0 DC.B 'Ok.',0
 EVEN

ERRORS DC.B 'Fundimental Error    ',0
 DC.B 'Drive not ready      ',0
 DC.B 'Unknown commmand     ',0
 DC.B 'CRC Error            ',0
 DC.B 'Bad Request          ',0
 DC.B 'Seek Error           ',0
 DC.B 'Unknown media        ',0
 DC.B 'Sector not found     ',0
 DC.B 'No paper             ',0
 DC.B 'Write fault          ',0
 DC.B 'Read fault           ',0
 DC.B 'General Error        ',0
 DC.B 'Write protect        ',0
 DC.B 'Media change         ',0
 DC.B 'Unknown device       ',0
 DC.B 'Bad sectors on format',0
 DC.B 'Insert other disk    ',0

ERRORS32 DC.B 'Invalid function     ',0
 DC.B 'File not found       ',0
 DC.B 'Path not found       ',0
 DC.B 'Too many open files  ',0
 DC.B 'Access denied        ',0
 DC.B 'Invalid handle       ',0
 DC.B 'Miscellaneous error  ',0
 DC.B 'Insufficient memory  ',0
 DC.B 'Invalid memory block ',0

ERR46 DC.B 'Invalid Drive        ',0
ERR49 DC.B 'No more files        ',0

ERR64 DC.B 'Range Error          ',0
 DC.B 'Internal Error       ',0
 DC.B 'Invalid program      ',0
 DC.B 'Growth Restrictions  ',0

SERROR DC.B 'Unkown error ',0
 EVEN

*--------------CHANGE DATE AND TIME-----------------

DATETIME MOVE #42,-(A7)  ;GET DATE
 TRAP #1
 ADD #2,A7
 MOVE D0,TIMEHLD

 MOVE.L TIMEADR,A0
 ADD.L #OBSIZE*ODATE,A0
 MOVE.L OB_SPEC(A0),A0
 MOVE.L TE_PTEXT(A0),A0

 MOVE TIMEHLD,D0
 MOVE D0,D1
 LSR.L #5,D1
 AND.L #$0F,D1
 DIVU #10,D1
 ADD.B #'0',D1
 MOVE.B D1,(A0)+
 SWAP D1
 ADD.B #'0',D1
 MOVE.B D1,(A0)+

 MOVE D0,D1
 AND.L #$1F,D1
 DIVU #10,D1
 ADD.B #'0',D1
 MOVE.B D1,(A0)+
 SWAP D1
 ADD.B #'0',D1
 MOVE.B D1,(A0)+

 MOVE D0,D1
 LSR.L #8,D1
 LSR.L #1,D1
 AND.L #$7F,D1
 ADD #80,D1
 DIVU #10,D1
 ADD.B #'0',D1
 MOVE.B D1,(A0)+
 SWAP D1
 ADD.B #'0',D1
 MOVE.B D1,(A0)+

 MOVE.L TIMEADR,A0
 ADD.L #OBSIZE*ODATE,A0
 MOVE.L OB_SPEC(A0),A0
 MOVE.L TE_PTEXT(A0),A0
 CMP.B #'5',5(A0)
 BEQ NEEDED
 CMP.B #'9',4(A0)
 BEQ NONEED
 CMP.B #'8',4(A0)
 BEQ NONEED

NEEDED MOVE #44,-(A7)  ;GET TIME
 TRAP #1
 ADD #2,A7
 MOVE D0,TIMEHLD

 MOVE.L TIMEADR,A0
 ADD.L #OBSIZE*OTIME,A0
 MOVE.L OB_SPEC(A0),A0
 MOVE.L TE_PTEXT(A0),A0

 MOVE TIMEHLD,D0
 MOVE D0,D1
 LSR.L #8,D1
 LSR.L #3,D1
 AND.L #$1F,D1

 MOVE.B #'A',D2
 TST D1
 BNE CTIME4
 MOVE #12,D1
 JMP CTIME2

CTIME4 CMP #13,D1
 BCS CTIME2
 MOVE.B #'P',D2
 SUB #12,D1

CTIME2 DIVU #10,D1
 ADD.B #'0',D1
 MOVE.B D1,(A0)+
 SWAP D1
 ADD.B #'0',D1
 MOVE.B D1,(A0)+

 MOVE D0,D1
 LSR.L #5,D1
 AND.L #$3F,D1
 DIVU #10,D1
 ADD.B #'0',D1
 MOVE.B D1,(A0)+
 SWAP D1
 ADD.B #'0',D1
 MOVE.B D1,(A0)+

 MOVE.B D2,(A0)+

 MOVE.L TIMEADR,A0
 MOVE #OTIME,D0
 JSR FORMDO
 CMP #OOK,SAVERTRN
 BNE QUITIT

 MOVE.L TIMEADR,A0
 ADD.L #OBSIZE*ODATE,A0
 MOVE.L OB_SPEC(A0),A0
 MOVE.L TE_PTEXT(A0),A0

 CLR.L D0

 CLR.L D1
 MOVE.B 2(A0),D1
 SUB.B #'0',D1
 MULU #10,D1
 CLR.L D2
 MOVE.B 3(A0),D2
 SUB.B #'0',D2
 ADD.B D2,D1
 MOVE.B D1,D0

 CLR.L D1
 MOVE.B (A0),D1
 SUB.B #'0',D1
 MULU #10,D1
 CLR.L D2
 MOVE.B 1(A0),D2
 SUB.B #'0',D2
 ADD.B D2,D1
 LSL.L #5,D1
 OR.L D1,D0

 CLR.L D1
 MOVE.B 4(A0),D1
 SUB.B #'8',D1
 MULU #10,D1
 CLR.L D2
 MOVE.B 5(A0),D2
 SUB.B #'0',D2
 ADD.B D2,D1
 LSL.L #8,D1
 LSL.L #1,D1
 OR.L D1,D0

 MOVE D0,-(A7)  ;T_SETDATE
 MOVE #43,-(A7)
 TRAP #1
 ADD #4,A7

 MOVE.L TIMEADR,A0
 ADD.L #OBSIZE*OTIME,A0
 MOVE.L OB_SPEC(A0),A0
 MOVE.L TE_PTEXT(A0),A0

 CLR.L D0

 CLR.L D1
 MOVE.B 2(A0),D1
 SUB.B #'0',D1
 MULU #10,D1
 CLR.L D2
 MOVE.B 3(A0),D2
 SUB.B #'0',D2
 ADD.B D2,D1
 MOVE.B D1,D0
 LSL.L #5,D0

 CLR.L D1
 MOVE.B (A0),D1
 SUB.B #'0',D1
 MULU #10,D1
 CLR.L D2
 MOVE.B 1(A0),D2
 SUB.B #'0',D2
 ADD.B D2,D1

 CMP.B #'P',4(A0)
 BEQ TITS
 CMP.B #'p',4(A0)
 BNE NOTITS
TITS ADD #12,D1

NOTITS LSL.L #8,D1
 LSL.L #3,D1
 OR.L D1,D0

 MOVE D0,-(A7)  ;T_SETTIME
 MOVE #45,-(A7)
 TRAP #1
 ADD #4,A7

NONEED RTS

*--------------TEXT STRINGS------------

RBASE DC.B 'Regent Base 2',0
 EVEN
FSPEC DC.B '*.FRM',0
 EVEN
BASEPRG DC.B 'BASE.PRG',0
 EVEN

*-----------------BLOCK STORAGE AREA--------------

 BSS

GEMSTART DS 1
DISKBUFF DS.B 160
CONTRL DS 1    ; OPCODE
OPCODE EQU CONTRL
SINTIN DS 1
SINTOUT DS 1
SADDRIN DS 1
SADDROUT DS.L 5
GLOBAL DS 1   ; APVERSION
APCOUNT DS 1
APID DS 1
APPRIVATE DS.L 1
APPTREE DS.L 1
AP1RESV DS.L 1
AP2RESV DS.L 1
AP3RESV DS.L 1
AP4RESV DS.L 1
INTIN DS.L 22
PTSIN DS.L 22
INTOUT DS.L 22
PTSOUT DS.L 22
ADDRIN DS.L 22
ADDROUT DS.L 22
GEMEND DS 1

GEMXPOS DS 1
GEMYPOS DS 1
GEMWIDTH DS 1
GEMHOEHE DS 1

GRHANDLE DS 1
DRHANDLE DS 1

MONOADR DS.L 1
PTHNM DS.B 40
PATHNAME DS.B 40
CHSADR DS.L 1
GRMODE DS 1
SCREEN DS.L 1
DIALX DS 1
DIALY DS 1
DIALH DS 1
DIALW DS 1
SAVERTRN DS 1
TIMEADR DS.L 1
TIMEHLD DS 1
SLIDEPOS DS 1
FILECNT DS 1
FILEY DS 1
COUNT DS 1
FLAG DS 1
RUNFILE DS 256
HOLDER DS 1

NAMESIZE EQU 10000
NAMES DS.B NAMESIZE
 EVEN

 DS.B 4000
USTK DS 1

*--------------EQUATES--------------

TIMEDATE = 0;  REM ---TREE---
OTIME = 2;  REM 0---
ODATE = 3;  REM 0---
OOK = 4;  REM 0---
OCANCEL = 5;  REM 0---
MENU = 1;  REM ---TREE---
SQLED = 9;  REM 1---
FORMED = 7;  REM 1---
SORT = 3;  REM 1---
UTILS = 5;  REM 1---
UPARROW = 20;  REM 1---
DNARROW = 21;  REM 1---
SLIDER = 22;  REM 1---
THUMB = 23;  REM 1---
QUIT = 24;  REM 1---
OFILES = 11;  REM 1---
APP1 = 12;  REM 1---
APP2 = 13;  REM 1---
APP3 = 14;  REM 1---
APP4 = 15;  REM 1---
APP5 = 16;  REM 1---
APP6 = 17;  REM 1---
APP7 = 18;  REM 1---
APP8 = 19;  REM 1---
MONO = 2;  REM ---TREE---
MSQL = 1;  REM 2---
MFORMS = 2;  REM 2---
MSORT = 3;  REM 2---
MUTIL = 4;  REM 2---

 END
