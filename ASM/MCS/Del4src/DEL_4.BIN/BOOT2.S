**** MEGA BOOTSECTEUR
**** LE PROGRAMME COMMANCE AU LABEL 'BOOT:' ET FINI A LA FIN
**** JE QUIT EN CHARGENT UNE SECTEUR D'UNE PISTE(TU PEU FAIRE TON CHOIX SUR LA PISTE ET LE SECTEUR...) 

INIT:	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP

	PEA	T1
	MOVE.W	#9,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	
	MOVE.W	#1,-(SP)
	TRAP	#1
	ADDQ.L	#2,SP

	move.l	#$1,-(SP)		* NOMBRE=1 ; FACE = 0
	move.l	#$10000,-(SP)	* PISTE = 0 ; SECTEUR = 1
	move.w	#0,-(SP)		* DRIVE = A
	clr.l	-(SP)		* 0.L

	lea	$4C6,a5		* LIT HIMEM... 
	movea.l	(a5),a5		* C'EST ADIRE UN BUFFER,MAIS UN
	movea.l	a5,a6		* VIRUS NE PEUT PRENDRE DE MEMOIRE 

	move.l	a5,-(SP)		* ZE TAMPON...
	move.w	#$8,-(SP)		* ON LIT...
	trap	#$E		* 
	adda.l	#$14,SP		* 

	tst.w	d0		* ERROR???
	bmi	BEUG1		* OUI,AARRGGHH...
				* NON...
	move.w	#$601C-6,(a5)	* ON RAJOUTE UN BRA AU BOOT...
	adda.l	#$1E-6,a5		* UN PEU + LOIN, 

	lea	BOOT(pc),a4	* ON PREND LE 'BOOT'
	lea	F_BOOT(pc),a3	* ET...
INS:
	move.w	(a4)+,(a5)+	* ON RECOPI
	cmpa.l	a3,a4		* LA FIN?
	blt	INS		* NON, ON CONTINU...

	movea.l	a6,a5		* ZE DEBUT OF ZE BOOT
	move.w	#$FE,d1		* HOP...
	move.w	#$1234,d0		* LA 'MAGICAL VALUE...'

MAGIC:
	sub.w	(a5)+,d0		* LOTSOF CALCUL
	dbf	d1,MAGIC		* ET VOILA... TOUT BOOT =
	move.w	d0,(a5)		* $1234...YEEPPEE


	move.l	#$1,-(SP)		* NE SERION NOUS PAS EN TRAIN D'ECRIRE???
	move.l	#$10000,-(SP)	* ET DANS LE BOOT EN PLUS!!!
	move.w	#0,-(SP)		* 
	clr.l	-(SP)		* 
	move.l	a6,-(SP)		* 
	move.w	#$9,-(SP)		* 
	trap	#$E		* 
	adda.l	#$14,SP		* 

	tst.w	d0		* UNE ERREUR???
	bmi	BEUG2		* OUI...

	PEA	T2
	MOVE.W	#9,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	
	MOVE.W	#1,-(SP)
	TRAP	#1
	ADDQ.L	#2,SP

	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP

	CLR.W	-(SP)	
	TRAP	#1

BEUG1:	PEA	T3
	MOVE.W	#9,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	
	MOVE.W	#1,-(SP)
	TRAP	#1
	ADDQ.L	#2,SP

	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP

	CLR.W	-(SP)
	TRAP	#1

BEUG2:	PEA	T4
	MOVE.W	#9,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	
	MOVE.W	#1,-(SP)
	TRAP	#1
	ADDQ.L	#2,SP
	
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP

	CLR.W	-(SP)
	TRAP	#1

T1:	DC.B	10,13,"VEUILLEZ INSEREZ LA DISQUETTE"
	DC.B	10,13," A BOOT-SECTEUR-EXECUTABILITE...",0,0
T2:	DC.B	10,13,"OKAY! TOUT MARCHE...",0,0
T3:	DC.B	10,13,"BEUG DE LECTURE...",0,0
T4:	DC.B	10,13,"BEUG D'ECRITURE...",0,0
	EVEN

*************** INBOOT *************

BOOT:	CLR.L	$FFFFFA06.W		;LES INTRRUPTS
	CLR.L	$FFFFFA12.W		;GENANTES OU NON!
	CLR.W	$FFFF8240.W

*****************************************************
INIT_FONTS:	
	LEA	TXT(PC),A0
	MOVEQ	#19-1,D0
OCT_S:	NOT.B	(A0)+
	DBRA	D0,OCT_S

	PEA	TXT(PC)
	MOVE.W	#9,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP		

	MOVEQ	#8-1,D0	
	MOVE.L	$44E.W,A0
	LEA	ALL_BUFFER(PC),A1	;TAILLE 4610 OCTS
COD_LIGNE_S:	MOVEQ	#18/2-1,D1		
CHART_S:	MOVE.L	72-8(A0),D3
	MOVEQ	#16-1,D2
BIT_S:	BTST	#0,D3
	BEQ.S	OK_BLOCK
	LEA	INIT_FONTS+2(PC),A2
	MOVE.L	A2,(A1)+		;OK BLOCK
	BRA.S	SUIT_CODE
	
OK_BLOCK:	MOVE.L	#$70700+230*8,(A1)+	;NO BLOCK
SUIT_CODE:	LSR.L	#1,D3
	DBRA	D2,BIT_S
	SUBQ.W	#8,A0
 	DBRA	D1,CHART_S
	ADD.W	#240-8,A0
END_OF_MFNT:	DBRA	D0,COD_LIGNE_S

*****************************************************
	LEA	$70700,A0
	MOVE.W	#(230*214)/4-1,D0
CLR_SCREEN:	CLR.L	(A0)+
	DBRA	D0,CLR_SCREEN
*****************************************************

MAKE_TAB_H:	LEA	ALL_BUFFER+4610(PC),A0
	MOVEQ	#0,D6
	MOVEQ	#57,D0
LAB_1:	MOVEQ	#31,D1
LAB_2:	MOVEQ	#12,D2

LAB_3:	MOVE.W	D2,D3
	LSL.W	#8,D3
	MOVE.W	D3,D5
	
	MOVE.W	D1,D4
	MULU.W	#24,D4
	SUB.W	D4,D3

	CMP.W	D6,D3
	BNE.S	MOD_16P
	
	MOVE.W	D5,(A0)+		
	MOVE.W	D1,(A0)+		
	MOVE.W	D6,(A0)+
	ADDQ.W	#8,D6
	
MOD_16P:	DBF	D2,LAB_3
	DBF	D1,LAB_2
END_TAB_H:	DBF	D0,LAB_1
	MOVEQ	#0,D4
*****************************************************

	LEA	VBL_BOOT(PC),A0	|INIT NOTRES VBL
	MOVE.L	A0,$70.W		|
STOP:	BRA.S	STOP		

****************** START VBL-BOOT ********************
VBL_BOOT:	LEA	$FFFFF8240.W,A6	;METS L'ECRAN EN NOIR
	MOVEQ	#16-1,D0		
CLR_PAL:	CLR.W 	(A6)+
	DBRA	D0,CLR_PAL

	MOVE.W	#1401,D0
WAIT_UP:	DBRA	D0,WAIT_UP		;WAIT BORDER HAUT

	CLR.B	$FFFFF820A.W		;
	MOVEQ	#0,D0		;
	MOVEQ	#4,D1		;
	MOVEQ	#10,D2		;
	MOVEQ	#23,D3		;
	MOVEQ	#32,D5		;
	SUB.W	D4,D5		;
	LEA	$FFFFF820A.W,A0	;
	LEA	$FFFFF8260.W,A1	;
	MOVE.W	A1,(A0)		;
WAIT_SYNC:	DBRA	D2,WAIT_SYNC
	
SYNC:	SUB.B	$FFFFF8209.W,D1
	LSL.L	D1,D1
WAIT_HARD:	DBRA	D3,WAIT_HARD

L_184:	MOVE.W	A1,(A1)	
	ROXL.W	#1,D0	
	MOVE.B	D0,(A1)
	MOVEQ	#27,D1	
WAIT_2:	DBRA	D1,WAIT_2
	MOVE.B	D0,(A0)	
	MOVEQ	#7,D1	
	MOVE.W	A0,(A0)
	MOVEQ	#16-1,D2
	ROXL.W	#1,D0
WAIT_3:	DBRA	D1,WAIT_3	
	DBRA	D4,L_184

SYNC2:	MOVEQ	#40,D1
WAIT_L230:	DBRA	D1,WAIT_L230
	DBRA	D5,SYNC2

AFF_PAL:	MOVE.W	D2,-(A6)
	DBRA	D2,AFF_PAL

	MOVEQ	#12,D1
WAIT_L2220:	DBRA	D1,WAIT_L2220
	ROXL.W	#1,D0

	MOVE.W	#192,D2
L_230:	MOVE.W	A1,(A1)	;GAUCHE
	ROXL.W	#1,D0	;2 NOPS 
	MOVE.B	D0,(A1)
	MOVEQ	#28,D1		|89 NOPS
WAIT1_230:	DBRA	D1,WAIT1_230		|
	MOVE.B	D0,(A0)	;DROITE
	MOVEQ	#0,D1	
	MOVE.W	A0,(A0)
	MOVE.W	#2,D1		|12 NOPS
WAIT2_230:	DBRA	D1,WAIT2_230		|
	MOVE.W	A0,(A1)	;GAUCHE
	MOVEQ	#0,D0
	MOVE.B	D0,(A1)	
	LSL.L	#8,D0
	
****************** GESTION SCROOLING *****************
SCROOL:	LEA	ALL_BUFFER+4610+6*144(PC),A5
	DBRA	D2,L_230

**** FIN FULL ***
	MOVE.L	#$70700,D0
	SUB.W	(A5)+,D0
	MOVE.W	(A5)+,D4
	
	LSR.L	#$8,D0
	LEA	$FFFFF8201.W,A1
	MOVEP.W	D0,(A1)

	LEA	SCROOL+2(PC),A1
CHANGE_DIR:	SUBQ.W	#6,(A1)

DIR:	LEA	$70700+230*26+128-32+8+2-16-48-8-14,A1
	SUB.W	(A5),A1
	MOVE.W	(A5),D0
	LSR	#1,D0
	CLR.L	-230+32(A1)
	
	LEA	ALL_BUFFER(PC),A0
	ADD.W	D0,A0
	
	LEA	CHANGE_DIR+1(PC),A2
	CMP.W	#5*2,D0
	BHS.S	TST_STOP
	CLR.B	(A2)		

TST_STOP:	MOVEQ	#8-1,D0	
BLOCK_S:	MOVEQ	#27,D1
	MOVE.L	(A0),A3

BLOCK_1:	MOVE.L	(A3)+,(A1)+
	MOVE.L	(A3)+,(A1)+
	LEA	230-8(A1),A1
	DBRA	D1,BLOCK_1
	ADD.W	#288*2,A0
	CLR.L	(A6)+
	DBRA	D0,BLOCK_S

	CMPI.B	#$39,$FFFFFC02.W
	BEQ.S	FIN_BOOT	
	RTE

FIN_BOOT:	MOVE.W	#4,-(SP)	;LIRE 1 SECTEUR
	CLR.L	-(SP)	;FACE 0-PISTE 0
	MOVE.W	#2,-(SP)	;SECTEUR 1
	CLR.W	-(SP)	;LECTEUR A
	CLR.L	-(SP)	;
	PEA	$4000.W
	MOVE.W	#8,-(SP)
	TRAP	#14
	LEA	20(SP),SP
	move.w	#$2700,sr
	JMP	$4000.W
	
****DATA****	
TXT:	DC.B	$BB,$BA,$B3,$B6,$AD,$B6,$B0,$AA,$AC
	DC.B	$DF,$BB,$BA,$B2,$B0,$D2,$B6,$A9,$D2,$DF,$00,'GO'
ALL_BUFFER:	
F_BOOT:	END
