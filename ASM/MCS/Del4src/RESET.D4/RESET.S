****************************************
*                                      *
* RESET DEMO OF THE DELIRIOUS DEMO 4   *
*     CODE BY XERCES AND MAGNUM        *
* LAST IMPROVEMENTS : ONE ROUT         *
*                     MORE DOTS        *
*                     LOW BORDER       *
*                     PREDEFINED WAVES *
*                                      *
****************************************


       *       *    *******     *******
       **     **   *       *   *       *
       * *   * *   *           *
       *  * *  *   *           *
       *   *   *   *            *******
       *       *   *                   *
       *       *   *                   *
       *       *   *       *   *       *
       *       *    *******     *******

NB_DOTS	EQU	550	* MAXIMUM (POUR L'INSTANT)

	clr.l	-(sp)
	move.w	#32,-(sp)
	trap	#1
	addq.l	#6,sp
	
	LEA	OLD_PAL(PC),A0

	MOVEM.L	$FF8240,D0-D7
	MOVEM.L	D0-D7,(A0)
	
	CLR.W	$FFFF8260.W

	MOVE.B	#$6,$FFFF8201.W
	MOVE.B	#$0,$FFFF8203.W

	LEA	$60000,A0
	LEA	$60000+160*240,A1
	MOVEQ	#0,D0
BC_EFFACE_SCREEN
	MOVE.L	D0,(A0)+
	CMPA.L	A1,A0
	BLT.S	BC_EFFACE_SCREEN

	LEA	PAL_MAG(PC),A0

	MOVEM.L	(A0),D0-D7
	MOVEM.L	D0-D7,$FF8240

	MOVE.L	#$60000+160*202,A0
	LEA	IMG(PC),A1
	MOVE.W	#40*30-1,D1
AFIM
	MOVE.L	(A1)+,(A0)+
	DBF	D1,AFIM
	
	MOVE.W	#0,$FFFF8240.W

DEB
	LEA	RESET(PC),A0
	LEA	FIN_PRG,A1
	LEA	$40000,A2
RECOP
	MOVE.B	(A0)+,(A2)+
	CMP.L	A0,A1
	BNE.S	RECOP

	MOVE.L	#$31415926,$426
	MOVE.L	#$40000,$42A
WAIT
	BRA	WAIT
	DS.L	200
PILE
	DC.L	0

RESET
	MOVE.W	#$2700,SR
	LEA	PILE(PC),A7
	MOVE.L	A7,USP
	MOVE.B	#$40,$FFFFFA17.W
	MOVE.B	#0,$FFFFFA07.W
	MOVE.B	#0,$FFFFFA09.W
	MOVE.B	#0,$FFFFFA13.W
	MOVE.B	#0,$FFFFFA15.W
	MOVE.B	#0,$FFFFFA0D.W
	MOVE.B	#0,$FFFFFA0B.W

	MOVE.B	#$6,$FF8201
	MOVE.B	#$00,$FF8203

	LEA	FLAG(PC),A0
	TST.W	(A0)
	BEQ.S	PAS_INIT_ZIK
	MOVE.W	#0,(A0)
	MOVEQ	#1,D0
	JSR	ZIK-RESET+$40000
PAS_INIT_ZIK
	LEA	VBL(PC),A0
	MOVE.L 	A0,$70
	MOVE.B	#0,$FFFFFA07.W
	MOVE.B	#0,$FFFFFA09.W
	BCLR	#3,$FFFFFA17.W
	MOVE.B	#0,$FFFFFA09.W
	MOVE.B	#1,$FFFFFA07.W
	MOVE.B	#1,$FFFFFA13.W
	MOVE.B	#0,$FFFFFA21.W
	MOVE.B	#0,$FFFFFA1B.W
	MOVE.W 	#$2300,SR

TEST
	MOVEQ	#0,D0
	MOVE.B	$FFFC02,D0
	CMP.B	#$3B,D0
	BNE.S	ST1
	LEA	PAR11+2(PC),A0
	ADDQ.W	#1,(A0)
	MOVE.W	(A0),D1
	MOVE.L	#$60000+160*217+16,A1
	BSR	AFFIT
	BRA	LOP
FLAG
	DC.W	1
ST1
	CMP.B	#$3C,D0
	BNE.S	ST2
	LEA	PAR11+2(PC),A0
	SUBQ.W	#1,(A0)
	MOVE.W	(A0),D1
	MOVE.L	#$60000+160*217+16,A1
	BSR	AFFIT
	BRA	LOP
ST2
	CMP.B	#$3D,D0
	BNE.S	ST3
	LEA	PAR11+4(PC),A0
	ADDQ.W	#1,(A0)
	MOVE.W	(A0),D1
	MOVE.L	#$60000+160*217+48,A1
	BSR	AFFIT
	BRA	LOP
ST3
	CMP.B	#$3E,D0
	BNE.S	ST4
	LEA	PAR11+4(PC),A0
	SUBQ.W	#1,(A0)
	MOVE.W	(A0),D1
	MOVE.L	#$60000+160*217+48,A1
	BSR	AFFIT
	BRA	LOP
ST4
	CMP.B	#$3F,D0
	BNE.S	ST5
	LEA	PAR21+2(PC),A0
	ADDQ.W	#1,(A0)
	MOVE.W	(A0),D1
	MOVE.L	#$60000+160*217+80,A1
	BSR	AFFIT
	BRA	LOP
ST5
	CMP.B	#$40,D0
	BNE.S	ST6
	LEA	PAR21+2(PC),A0
	SUBQ.W	#1,(A0)
	MOVE.W	(A0),D1
	MOVE.L	#$60000+160*217+80,A1
	BSR	AFFIT
	BRA	LOP
ST6
	CMP.B	#$41,D0
	BNE.S	ST7
	LEA	PAR21+4(PC),A0
	ADDQ.W	#1,(A0)
	MOVE.W	(A0),D1
	MOVE.L	#$60000+160*217+112,A1
	BSR	AFFIT
	BRA	LOP
ST7
	CMP.B	#$42,D0
	BNE.S	ST8
	LEA	PAR21+4(PC),A0
	SUBQ.W	#1,(A0)
	MOVE.W	(A0),D1
	MOVE.L	#$60000+160*217+112,A1
	BSR	AFFIT
	BRA	LOP
ST8
	CMP.B	#$2,D0
	BNE.S	ST9
	LEA	V11+2(PC),A0
	ADDQ.W	#1,(A0)
	MOVE.W	(A0),D1
	MOVE.L	#$60000+160*207+16,A1
	BSR	AFFIT
	BRA	LOP
ST9
	CMP.B	#$3,D0
	BNE.S	ST10
	LEA	V11+2(PC),A0
	SUBQ.W	#1,(A0)
	MOVE.W	(A0),D1
	MOVE.L	#$60000+160*207+16,A1
	BSR	AFFIT
	BRA	LOP
ST10
	CMP.B	#$4,D0
	BNE.S	ST11
	LEA	V12+2(PC),A0
	ADDQ.W	#1,(A0)
	MOVE.W	(A0),D1
	MOVE.L	#$60000+160*207+48,A1
	BSR	AFFIT
	BRA	LOP
ST11
	CMP.B	#$5,D0
	BNE.S	ST12
	LEA	V12+2(PC),A0
	SUBQ.W	#1,(A0)
	MOVE.W	(A0),D1
	MOVE.L	#$60000+160*207+48,A1
	BSR	AFFIT
	BRA	LOP
ST12
	CMP.B	#$6,D0
	BNE.S	ST13
	LEA	V13+2(PC),A0
	ADDQ.W	#1,(A0)
	MOVE.W	(A0),D1
	MOVE.L	#$60000+160*207+80,A1
	BSR	AFFIT
	BRA	LOP
ST13
	CMP.B	#$7,D0
	BNE.S	ST14
	LEA	V13+2(PC),A0
	SUBQ.W	#1,(A0)
	MOVE.W	(A0),D1
	MOVE.L	#$60000+160*207+80,A1
	BSR	AFFIT
	BRA	LOP
ST14
	CMP.B	#$8,D0
	BNE.S	ST15
	LEA	V14+2(PC),A0
	ADDQ.W	#1,(A0)
	MOVE.W	(A0),D1
	MOVE.L	#$60000+160*207+112,A1
	BSR	AFFIT
	BRA	LOP
ST15
	CMP.B	#$9,D0
	BNE.S	ST16
	LEA	V14+2(PC),A0
	SUBQ.W	#1,(A0)
	MOVE.W	(A0),D1
	MOVE.L	#$60000+160*207+112,A1
	BSR	AFFIT
	BRA	LOP
ST16
	CMP.B	#$70,D0
	BGT	SUP_0
	CMP.B	#$67,D0
	BLT	SUP_0
	SUB.W	#$67,D0
	LEA	NUM_COURBE(PC),A0
	CMP.B	(A0),D0
	BEQ	SUP_0
	MOVE.B	D0,(A0)
	LSL.W	#4,D0
	LEA	PREDEF(PC),A0
	ADD.L	D0,A0
	LEA	PAR11+2(PC),A1
	LEA	PAR21+2(PC),A2
	MOVE.L	(A0)+,(A1)
	MOVE.L	(A0)+,(A2)
	LEA	V11+2(PC),A4
	LEA	V12+2(PC),A1
	LEA	V13+2(PC),A2
	LEA	V14+2(PC),A3
	MOVE.W	(A0)+,(A4)
	MOVE.W	(A0)+,(A1)
	MOVE.W	(A0)+,(A2)
	MOVE.W	(A0)+,(A3)
	LEA	BUF_XA(PC),A0
	MOVE.L	#0,(A0)
	MOVE.L	#0,4(A0)
	MOVE.W	V11+2(PC),D1
	MOVE.L	#$60000+160*207+16,A1
	BSR	AFFIT
	MOVE.W	V12+2(PC),D1
	MOVE.L	#$60000+160*207+48,A1
	BSR	AFFIT
	MOVE.W	V13+2(PC),D1
	MOVE.L	#$60000+160*207+80,A1
	BSR	AFFIT
	MOVE.W	V14+2(PC),D1
	MOVE.L	#$60000+160*207+112,A1
	BSR	AFFIT
	MOVE.W	PAR11+2(PC),D1
	MOVE.L	#$60000+160*217+16,A1
	BSR	AFFIT
	MOVE.W	PAR11+4(PC),D1
	MOVE.L	#$60000+160*217+48,A1
	BSR	AFFIT
	MOVE.W	PAR21+2(PC),D1
	MOVE.L	#$60000+160*217+80,A1
	BSR	AFFIT
	MOVE.W	PAR21+4(PC),D1
	MOVE.L	#$60000+160*217+112,A1
	BSR	AFFIT
SUP_0
	BRA	TEST

LOP:	CMP.B	$FFFC02,D0
	BEQ.S	LOP
	BRA	TEST

AFFIT:
	MOVE.L	A1,A2
	MOVEQ	#0,D4
	MOVE.W	D1,D4
	AND.W	#$F000,D4
	DIVU	#$1000,D4
	ADD.W	D4,D4
	ADD.W	D4,D4
	LEA	TABL(PC),A0
	MOVE.L	(A0,D4.W),A0
	MOVE.W	#4,D3
LIG:
	MOVE.B	(A0),(A2)
	MOVE.B	2(A0),2(A2)
	MOVE.B	4(A0),4(A2)
	MOVE.B	6(A0),6(A2)
	ADDQ.W	#8,A0
	LEA	160(A2),A2
	DBF	D3,LIG

	MOVE.L	A1,A2
	MOVEQ	#0,D4
	MOVE.W	D1,D4
	AND.W	#$0F00,D4
	DIVU	#$100,D4
	ADD.W	D4,D4
	ADD.W	D4,D4
	LEA	TABL(PC),A0
	MOVE.L	(A0,D4.W),A0
	MOVE.W	#4,D3
LIG2:
	MOVE.B	(A0),1(A2)
	MOVE.B	2(A0),3(A2)
	MOVE.B	4(A0),5(A2)
	MOVE.B	6(A0),7(A2)
	ADDQ.W	#8,A0
	LEA	160(A2),A2
	DBF	D3,LIG2

	LEA	8(A1),A2
	MOVEQ	#0,D4
	MOVE.W	D1,D4
	AND.W	#$0F0,D4
	DIVU	#$10,D4
	ADD.W	D4,D4
	ADD.W	D4,D4
	LEA	TABL(PC),A0
	MOVE.L	(A0,D4.W),A0
	MOVE.W	#4,D3
LIG3:
	MOVE.B	(A0),(A2)
	MOVE.B	2(A0),2(A2)
	MOVE.B	4(A0),4(A2)
	MOVE.B	6(A0),6(A2)
	ADDQ.W	#8,A0
	LEA	160(A2),A2
	DBF	D3,LIG3
	
	LEA	8(A1),A2
	MOVEQ	#0,D4
	MOVE.W	D1,D4
	AND.W	#$F,D4
	ADD.W	D4,D4
	ADD.W	D4,D4
	LEA	TABL(PC),A0
	MOVE.L	(A0,D4.W),A0
	MOVE.W	#4,D3
LIG4:
	MOVE.B	(A0),1(A2)
	MOVE.B	2(A0),3(A2)
	MOVE.B	4(A0),5(A2)
	MOVE.B	6(A0),7(A2)
	ADDQ.W	#8,A0
	LEA	160(A2),A2
	DBF	D3,LIG4
	RTS

F_KEY	DC.W	0

FIN_VBL
	MOVEM.L	(SP)+,D0-D7/A0-A6
	RTE
VBL
	MOVE.L	A5,USP
	CLR.B	$FFFFFA1B.W
	MOVE.B	#199,$FFFFFA21.W
	MOVE.B	#8,$FFFFFA1B.W
	LEA	HBL(PC),A5
	MOVE.L	A5,$120.W
	MOVE.L	USP,A5

	MOVEM.L	D0-D7/A0-A6,-(SP)
	LEA	PAL_MAG(PC),A0
	MOVEM.L	(A0),D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W
PEK_PAL
	LEA	TAB_PAL(PC),A0
	MOVE.W	(A0)+,$FFFF8242.W
	MOVE.W	(A0)+,$FFFF8244.W
	MOVE.W	(A0)+,$FFFF8248.W
	MOVE.W	(A0)+,$FFFF8250.W

*	JSR	ZIK+$22
PEK_BUF
	LEA	TAB_BUF(PC),A6
	LEA	BUF_1(PC),A0
	ADD.L	(A6),A0
	MOVE.L	A0,A2

	MOVEQ	#0,D0
	REPT	NB_DOTS
	MOVE.L	(A0)+,A1
	MOVE	D0,(A1)
	ENDR

PEK_ECR
	LEA	ADR_ECR(PC),A3
	MOVE.L	(A3),A3
	LEA	TAB_COS(PC),A4
	LEA	TAB_SIN(PC),A5
	LEA	TAB_PUT(PC),A6

	MOVE.L	#$FFF0FFF,D6
	MOVE	#NB_DOTS-1,D7

	MOVEQ	#0,D0
	MOVE.L	D0,D1
	MOVE.L	D0,A0
	MOVE.L	D0,A1
	LEA	BUF_XA(PC),A0
V11
	ADD	#$10,(A0)
V12
	ADD	#$0,2(A0)
V13
	ADD	#$20,4(A0)
V14
	ADD	#$0C,6(A0)

	AND.L	D6,(A0)
	AND.L	D6,4(A0)

	MOVE.L	(A0),D0
	MOVE.L	4(A0),D1

BC_DOTS1
PAR11
	ADD.L	#$800084,D0
PAR21
	ADD.L	#$800084,D1

	MOVE.L	D0,D4
	MOVE.L	D1,D5

	AND.L	D6,D0
	AND.L	D6,D1

	ADD.L	D0,D0	;X
	ADD.L	D1,D1	;Y

	MOVE.W	0(A4,D0.W),D2	;COS
	SWAP	D0
	MOVE.W	0(A4,D0.W),D0	;COS

	MOVE.W	0(A5,D1.W),D3	;SIN
	SWAP	D1
	MOVE.W	0(A5,D1.W),D1	;SIN

	ADD	D2,D0		X
	MOVE	0(A6,D0),A1	;OFFSET
	MOVE	2(A6,D0),D2	;DONNEE

	ADD	D3,D1		Y

	ADD	D1,A1		;OFFSET FINAL

	LEA	0(A3,A1.W),A0	;ADRESSE FINALE
	OR	D2,(A0)		;PLOT
	MOVE.L	A0,(A2)+

	MOVE.L	D4,D0
	MOVE.L	D5,D1

 	DBRA	D7,BC_DOTS1

	LEA	PEK_ECR+2(PC),A0
	LEA	PEK_PAL+2(PC),A1
	LEA	PEK_BUF+2(PC),A2

	ADDQ.W	#4,(A0)
	ADDQ.W	#8,(A1)
	ADDQ.W	#4,(A2)
	CMP.W	#$DE2,(A2)
	BNE.S	PAS_FBUF
	MOVE.W	#$DD2,(A2)
	MOVE.W	#$DB6,(A1)
	MOVE.W	#$51C,(A0)
PAS_FBUF
	JSR	ZIK-RESET+$40000+$22
	MOVEM.L	(SP)+,D0-D7/A0-A6
	RTE

HBL:
	MOVE.W	#$2700,SR
	REPT	121
	NOP
	ENDR
	CLR.B	$FFF820A
	REPT	6
	NOP
	ENDR
	MOVE.B	#2,$FFF820A
	MOVEM.L	D0-D7/A0,-(SP)
	LEA	PAL_IMG(PC),A0
	MOVEM.L	(A0),D0-D7
	MOVEM.L	D0-D7,$FF8240
	MOVEM.L	(SP)+,D0-D7/A0
	MOVE.W	#$2300,SR
	RTE

AD120:	DC.L	0
AD13:	DC.B	0
AD0F:	DC.B	0
AD1B:	DC.B	0
AD0B:	DC.B	0
AD09:	DC.B	0
AD07:	DC.B	0
AD17:	DC.B	0

	EVEN
FNT
	INCBIN	COPYB2.IMG
TABL:
M	SET	0
	REPT	16
	DC.L	FNT+8*M
M	SET	M+5
	ENDR

OLD_PAL
	DCB.W	16,0

PAL:	DC.W	0
	DCB.W	15,$777
TAB_PAL
	DC.W	$777,$333,$444,$555
	DC.W	$555,$777,$333,$444
	DC.W	$444,$555,$777,$333
	DC.W	$333,$444,$555,$777
ADR_ECR
	DC.L	$60000,$60002,$60004,$60006
TAB_BUF
	DC.L	0,BUF_2-BUF_1,BUF_3-BUF_1,BUF_4-BUF_1
F_BUF
BUF_1
	DCB.L	NB_DOTS,$60000
BUF_2
	DCB.L	NB_DOTS,$60000
BUF_3
	DCB.L	NB_DOTS,$60000
BUF_4
	DCB.L	NB_DOTS,$60000
PREDEF
	DC.W	$80,$84,$80,$84
	DC.W	$10,$0,$20,$0C
	
	DC.W	$91,$31,$2E,$30
	DC.W	$FFF1,$F,$FFFD,$18
	
	DC.W	$91,$60,$2E,$30
	DC.W	$FFF1,$F,$FFFD,$18

	DC.W	$91,$60,$91,$61
	DC.W	$0,$12,$FFF1,$18

	DC.W	$104,$100,$104,$100
	DC.W	$10,$10,$10,$10

	DC.W	$F8,$FE,$FE,$104
	DC.W	$10,$10,$10,$10
	
	DC.W	$208,$205,$208,$205
	DC.W	-20,20,-20,20
	
	DC.W	$F1,$50,$52,$50
	DC.W	$E,$12,$FFFD,$FFF0
	
	DC.W	$107,$208,$20D,$208
	DC.W	0,$E,$15,$1E

	DCB.W	8,$0

SAVE_REGISTERS	DS.W	4

SAVE
	DS.L	7
BUF_XA		DC.W	0
BUF_XB		DC.W	0
BUF_YA		DC.W	0
BUF_YB		DC.W	0

TAB_COS		INCBIN	COS_40.TAB
TAB_SIN		INCBIN	SIN_40.TAB
TAB_PUT

A	SET	0
	REPT	20
	DC.W	A,$8000,A,$4000,A,$2000,A,$1000,A,$800,A,$400,A,$200,A,$100,A,$80,A,$40,A,$20,A,$10,A,$8,A,$4,A,2,A,1
	DC.W	A,0
A	SET	A+8
	ENDR
AD70:	DC.L	0
KEY:	DC.B	0
NUM_COURBE
	DC.B	0
	EVEN
PAL_MAG
	DC.W	$0
	DCB.W	15,$777
PAL_IMG
	DC.W	$0000,$0666,$0555,$0777,$0444,$0333,$0222,$0777
	DC.W	$0EEE,$0666,$0DDD,$0CCC,$0444,$0BBB,$0333,$0777
DATE
IMG:	INCBIN	COPYB1.IMG
ZIK
	INCBIN	LAP1.MUS
FIN_PRG