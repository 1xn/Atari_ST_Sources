****************************************************************************
*	GENERATEUR DE CODE POUR SPRITE TRUE COLOR      V1.2B
*	  BY AXEL F.  /   M.C.S.
*	   25/11/93
*
*	    AVEC DEVPAC, CODE 68030, TABULATION A 8
*

	BSR	PASS1			* AFFICHAGE DU SPRITE
	BSR	PASS2			* SAUVEGARDE DU FOND
	BSR	PASS3			* RESTAURATION DU FOND
	BSR	WRITE_TO_DISK	
	CLR.L	-(A7)
	TRAP	#1



PASS1	
	MOVE.L	#DEPART,A0
	LEA	CODE+10,A1
	LEA	DATA,A2
	LEA	TEMPO,A6
	MOVEQ	#0,D1
	MOVE.W	#0,COMPTEUR_OFFSET
	
LINE	BSR	CHECK
	MOVE.L	#X,D0
	MOVEQ	#0,D3			
	MOVEQ	#0,D4			
	MOVEQ	#0,D6	
	MOVEQ	#%11111,D7		
NEXT_PT	SUBQ.L	#1,D0
	BMI.S	NEW_LINE		
	MOVE.W	(A0,D4.W),D5
	BEQ.S	BLANK			
	ADDQ.L	#1,D3
	ADDQ.L	#2,D4
	CMP.W	#26,D3
	BEQ	MAX			
	BRA	NEXT_PT
	
NEW_LINE
	BSR	TRAITE
	ADDQ.L	#1,D1
	CMP.W	#Y,D1
	BEQ	FIN_PASS1
	ADD.W	#LONG-(X*2),COMPTEUR_OFFSET
	ADD.W	#LEN-(X*2),A0
	BRA	LINE
	
BLANK	BSR	TRAITE
	ADDQ.W	#2,COMPTEUR_OFFSET
	ADDQ.W	#2,A0
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	BRA	NEXT_PT
	
MAX	BSR	TRAITE
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	BRA	NEXT_PT
	
FIN_PASS1
	CMP.W	#$D0FC,-4(A1)
	BNE.S	BON
	SUBQ.W	#4,A1
	MOVEQ	#0,D5
	MOVE.W	-(A6),D5
	SUB.L	D5,RESTE

BON	MOVE.W	#$4E75,(A1)+
	MOVE.L	A1,A3
	SUB.L	#CODE,A1
	MOVE.L	A1,LEN_CODE
	MOVE.L	A1,CODE+6
	ADD.L	#48,CODE+6
	SUB.L	#DATA,A2
	MOVE.L	A2,LEN_DATA
	ADDQ.L	#2,LEN_DATA
	MOVE.L	A2,D0
	BEQ.S	RETOUR
	LEA	DATA,A2
COP3	MOVE.B	(A2)+,(A3)+
	SUBQ.L	#1,D0
	BNE.S	COP3
	MOVE.W	#$4E75,(A3)+
RETOUR	RTS	

CHECK	CMP.W	#32*1024-LONG,COMPTEUR_OFFSET
	BLE	POINT0					
	MOVE.W	#$D0FC,(A1)+				
	MOVEQ	#0,D5
	MOVE.W	COMPTEUR_OFFSET,D5
	MOVE.W	D5,(A1)+
	MOVE.W	D5,(A6)+
	ADD.L	D5,RESTE
	MOVE.W	#0,COMPTEUR_OFFSET
	RTS
RESTE	DC.L	0
TEMPO	DS.L	10


TRAITE	LEA	TABLE,A3
	ADD.L	D3,NB_PT
	MOVE.L	(A3,D3.W*4),A3
	JMP	(A3)
	
POINT0	RTS

POINT1	MOVE.W	#$3159,(A1)+			* MOVE.W  (A1)+,XXXX(A0)
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.W	(A0)+,(A2)+
	ADDQ.W	#2,COMPTEUR_OFFSET		
	RTS

POINT2	MOVE.W	#$2159,(A1)+			* MOVE.L  (A1)+,XXXX(A0)
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.L	(A0)+,(A2)+
	ADDQ.W	#4,COMPTEUR_OFFSET		
	RTS

POINT3	MOVE.W	#$2159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.L	(A0)+,(A2)+
	ADDQ.W	#4,COMPTEUR_OFFSET
	MOVE.W	#$3159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.W	(A0)+,(A2)+
	ADDQ.W	#2,COMPTEUR_OFFSET
	RTS
	
POINT4	MOVE.W	#$2159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.L	(A0)+,(A2)+
	ADDQ.W	#4,COMPTEUR_OFFSET
	MOVE.W	#$2159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.L	(A0)+,(A2)+
	ADDQ.W	#4,COMPTEUR_OFFSET
	RTS

POINT5	MOVE.W	#$2159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.L	(A0)+,(A2)+
	ADDQ.W	#4,COMPTEUR_OFFSET
	MOVE.W	#$2159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.L	(A0)+,(A2)+
	ADDQ.W	#4,COMPTEUR_OFFSET
	MOVE.W	#$3159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.W	(A0)+,(A2)+
	ADDQ.W	#2,COMPTEUR_OFFSET
	RTS
	
POINT6	MOVE.W	#$2159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.L	(A0)+,(A2)+
	ADDQ.W	#4,COMPTEUR_OFFSET
	MOVE.W	#$2159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.L	(A0)+,(A2)+
	ADDQ.W	#4,COMPTEUR_OFFSET
	MOVE.W	#$2159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.L	(A0)+,(A2)+
	ADDQ.W	#4,COMPTEUR_OFFSET
	RTS

POINT7	MOVE.W	#$2159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.L	(A0)+,(A2)+
	ADDQ.W	#4,COMPTEUR_OFFSET
	MOVE.W	#$2159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.L	(A0)+,(A2)+
	ADDQ.W	#4,COMPTEUR_OFFSET
	MOVE.W	#$2159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.L	(A0)+,(A2)+
	ADDQ.W	#4,COMPTEUR_OFFSET
	MOVE.W	#$3159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.W	(A0)+,(A2)+
	ADDQ.W	#2,COMPTEUR_OFFSET
	RTS

POINT8	MOVE.W	#$2159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.L	(A0)+,(A2)+
	ADDQ.W	#4,COMPTEUR_OFFSET
	MOVE.W	#$2159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.L	(A0)+,(A2)+
	ADDQ.W	#4,COMPTEUR_OFFSET
	MOVE.W	#$2159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.L	(A0)+,(A2)+
	ADDQ.W	#4,COMPTEUR_OFFSET
	MOVE.W	#$2159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.L	(A0)+,(A2)+
	ADDQ.W	#4,COMPTEUR_OFFSET
	RTS

POINT9	MOVE.W	#$2159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.L	(A0)+,(A2)+
	ADDQ.W	#4,COMPTEUR_OFFSET
	MOVE.W	#$2159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.L	(A0)+,(A2)+
	ADDQ.W	#4,COMPTEUR_OFFSET
	MOVE.W	#$2159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.L	(A0)+,(A2)+
	ADDQ.W	#4,COMPTEUR_OFFSET
	MOVE.W	#$2159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.L	(A0)+,(A2)+
	ADDQ.W	#4,COMPTEUR_OFFSET
	MOVE.W	#$3159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.W	(A0)+,(A2)+
	ADDQ.W	#2,COMPTEUR_OFFSET
	RTS
	
POINT26 ADDQ.W	#1,D6
	BSET	#14,D7
POINT25 ADDQ.W	#1,D6
	BSET	#13,D7
POINT24	ADDQ.W	#1,D6
	BSET	#13,D7
POINT23	ADDQ.W	#1,D6
	BSET	#12,D7
POINT22	ADDQ.W	#1,D6
	BSET	#12,D7
POINT21	ADDQ.W	#1,D6
	BSET	#11,D7
POINT20	ADDQ.W	#1,D6
	BSET	#11,D7
POINT19	ADDQ.W	#1,D6
	BSET	#10,D7
POINT18	ADDQ.W	#1,D6
	BSET	#10,D7
POINT17	ADDQ.W	#1,D6
	BSET	#7,D7
POINT16	ADDQ.W	#1,D6
	BSET	#7,D7
POINT15	ADDQ.W	#1,D6
	BSET	#6,D7
POINT14	ADDQ.W	#1,D6
	BSET	#6,D7
POINT13	ADDQ.W	#1,D6
	BSET	#5,D7
POINT12	ADDQ.W	#1,D6
	BSET	#5,D7	
POINT11	ADDQ.W	#1,D6
	
POINT10	

	BTST	#0,D6
	BNE	PLUS1

	MOVE.W	#$4CD9,(A1)+		* MOVEM.L (A1)+,
	MOVE.W	D7,(A1)+
	MOVE.W	#$48E8,(A1)+		* MOVEM.L   ,XXXX(A0)
	MOVE.W	D7,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.W	D6,D5
	ADD.W	#9,D5
	MOVE.W	D5,D7
COP1	MOVE.W	(A0,D5.W*2),(A2,D5.W*2)
	DBF	D5,COP1
	ADDQ.W	#1,D7
	ADD.W	D7,D7
	ADD.W	D7,A0	
	ADD.W	D7,A2
	ADD.W	D7,COMPTEUR_OFFSET
	BRA	FINI
	
PLUS1	MOVE.W	#$4CD9,(A1)+		* MOVEM.L (A1)+,
	MOVE.W	D7,(A1)+
	MOVE.W	#$48E8,(A1)+		* MOVEM.L   ,XXXX(A0)
	MOVE.W	D7,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.W	D6,D5
	ADDQ.W	#8,D5
	MOVE.W	D5,D7
COP2	MOVE.W	(A0,D5.W*2),(A2,D5.W*2)
	DBF	D5,COP2
	ADDQ.W	#1,D7
	ADD.W	D7,D7
	ADD.W	D7,A0	
	ADD.W	D7,A2
	ADD.W	D7,COMPTEUR_OFFSET
	MOVE.W	#$3159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.W	(A0)+,(A2)+
	ADDQ.W	#2,COMPTEUR_OFFSET
	
FINI	MOVEQ	#0,D6
	MOVEQ	#%11111,D7
	RTS
PASS2
	MOVE.L	LEN_DATA,D0
	ADD.L	LEN_CODE,D0
	ADD.L	#$28,D0
	MOVE.L	D0,BUFFER+8
	MOVE.L	#12,TEMP
	MOVE.L	A3,DEB_SAV

	MOVEQ	#0,D6
	MOVE.L	A3,D7
	LEA	CODE+10,A0
CONVERT	MOVE.W	(A0)+,D1
	CMP.W	#$D0FC,D1			* ADD.W	#XXXX,A0
	BNE.S	NOT_ADD_W
	MOVE.W	D1,(A3)+
	MOVE.W	(A0)+,(A3)+
	ADD.L	#4,TEMP
	BRA	CONVERT

NOT_ADD_W
	CMP.W	#$2159,D1			* MOVE.L (A1)+,XXXX(A0)
	BNE	NOT_MOVE_L
	MOVE.W	#$2328,(A3)+			* MOVE.L XXXX(A0),-(A1)
	MOVE.W	(A0)+,(A3)+
	ADD.L	#4,TEMP
	ADDQ.L	#4,D6
	BRA	CONVERT
NOT_MOVE_L
	CMP.W	#$3159,D1			* MOVE.W (A0)+,XXXX(A1)
	BNE	NOT_MOVE_W
	MOVE.W	#$3328,(A3)+			* MOVE.W XXXX(A0),-(A1)
	MOVE.W	(A0)+,(A3)+
	ADD.L	#4,TEMP
	ADDQ.L	#2,D6
	BRA	CONVERT
NOT_MOVE_W
	CMP.W	#$4CD9,D1
	BNE.S	NOT_MOVEM
	MOVE.W	#$4CE8,(A3)+
	MOVE.W	(A0)+,(A3)+
	MOVE.W	4(A0),(A3)+
	MOVE.W	#$48E1,(A3)+
	MOVE.W	2(A0),D0
	BSR	CONVER
	MOVE.W	D2,(A3)+
	ADD.L	#10,TEMP
	ADDQ.W	#6,A0
	BRA	CONVERT
NOT_MOVEM
	MOVE.W	#$4E75,(A3)+
	MOVE.L	A3,A5
	SUB.L	D7,A5
	MOVE.L	A5,LEN_SAV
	RTS

CONVER	MOVEQ	#0,D4
	MOVEQ	#0,D2
	MOVEQ	#15,D5
SUIV	BTST	D4,D0
	BEQ	VIDE
	BSET	D5,D2
	ADDQ.L	#4,D6
	BRA	GOTO
VIDE	BCLR	D5,D2
GOTO	ADDQ.W	#1,D4
	SUBQ.W	#1,D5
	CMP.W	#-1,D5
	BNE.S	SUIV
	RTS
PASS3
	MOVE.L	LEN_DATA,D0
	ADD.L	LEN_CODE,D0
	ADD.L	LEN_SAV,D0
	ADD.L	#$22,D0
	MOVE.L	D0,BUFFER+14
	
	MOVE.L	A3,D7
	MOVE.L	DEB_SAV,A0
	ADD.L	TEMP,A3
	MOVE.W	#$4E75,(A3)
	MOVE.L	A3,A4
	SUB.L	D7,A4
	MOVE.L	A4,LEN_REST

	
CONV2	MOVE.W	(A0)+,D1
	CMP.W	#$D0FC,D1			* ADD.W	#XXXX,A0
	BNE.S	N_ADD_W
	MOVE.W	-(A6),-(A3)
	MOVE.W	#$90FC,-(A3)			* SUB.W #XXXX,A0
	BRA	CONV2
N_ADD_W	
	CMP.W	#$2328,D1			* MOVE.L XXXX(A0),-(A1)
	BNE.S	N_MOVE_L
	MOVE.W	(A0)+,-(A3)
	MOVE.W	#$2159,-(A3)			* MOVE.L (A1)+,XXXX(A0)
	BRA.S	CONV2
N_MOVE_L
	CMP.W	#$3328,D1			* MOVE.W XXXX(A0),-(A1)
	BNE.S	N_MOVE_W
	MOVE.W	(A0)+,-(A3)
	MOVE.W	#$3159,-(A3)			* MOVE.W (A1)+,XXXX(A0)
	BRA.S	CONV2
N_MOVE_W	
	CMP.W	#$4CE8,D1
	BNE.S	N_MOVEM
	MOVE.L	(A0),-(A3)
	MOVE.W	#$48E8,-(A3)
	MOVE.W	(A0),-(A3)
	MOVE.W	#$4CD9,-(A3)
	ADD.W	#8,A0
	BRA.S	CONV2
N_MOVEM	

	MOVE.L	RESTE,-(A3)
	MOVE.W	#$D1FC,-(A3)			* ADD.L	#XXXXXXXX,A0
	MOVE.L	D6,-(A3)
	MOVE.W	#$93FC,-(A3)			* SUB.L	#XXXXXXXX,A1
	
	RTS
***************************************************************************
WRITE_TO_DISK
	MOVE.W	#0,-(A7)
	PEA	FILENAME
	MOVE.W	#$3C,-(A7)
	TRAP	#1
	ADD.W	#8,A7
	MOVE.L	D0,D7
	
	MOVE.L	#50,D2
	ADD.L	LEN_CODE,D2
	ADD.L	LEN_DATA,D2
	ADD.L	LEN_SAV,D2
	ADD.L	LEN_REST,D2
	PEA	BUFFER
	MOVE.L	D2,-(A7)
	MOVE.W	D7,-(A7)
	MOVE.W	#$40,-(A7)
	TRAP	#1
	ADD.W	#12,A7
	
	MOVE.W	D7,-(A7)
	MOVE.W	#$3E,-(A7)
	TRAP	#1
	ADDQ.W	#4,A7
	RTS
	

FILENAME	DC.B	'SPRTC.BIN',0
		EVEN
		
***************************************************************************
TABLE	DC.L	POINT0,POINT1,POINT2,POINT3,POINT4,POINT5,POINT6
	DC.L	POINT7,POINT8,POINT9,POINT10,POINT11,POINT12,POINT13
	DC.L	POINT14,POINT15,POINT16,POINT17,POINT18,POINT19,POINT20
	DC.L	POINT21,POINT22,POINT23,POINT24,POINT25,POINT26
TEMP		DC.L	0
DEB_SAV		DC.L	0
LEN_SAV		DC.L	0	
LEN_REST	DC.L	0	
LEN_CODE	DC.L	0
LEN_DATA	DC.L	0
COMPTEUR_OFFSET	DC.W	0



ECRAN		INCBIN	D:\HOWTCODE\HTCII\SPRITE\DRAGON.TPI



X	EQU	32		
Y	EQU	32		
LEN	EQU	640		
LONG	EQU	640		

DEPART  EQU	ECRAN+128	

BUFFER		BRA.L	CODE
		BRA.L	SAV_FOND		
		BRA.L	RESTITU			
		
NB_PT		DC.L	0			
		DC.W	X
		DC.W	Y
		DC.W	LONG
		DC.B	"MCS'S SPRITE TC V1.2"
CODE		LEA	BUFFER(PC),A1
		ADD.L	#$FFFFFF,A1
		DS.W	2*X*Y			
DATA		DS.W	2*X*Y			
SAV_FOND					
RESTITU						
						
						

