	SECTION TEXT
	MOVE.L	4(SP),A5
	MOVE.L	$C(A5),D0
	ADD.L	$14(A5),D0
	ADD.L	$1C(A5),D0
	ADD.L	#$1000,D0	stack utrymme
	ADD.L	#$100,D0	base page
	MOVE.L	A5,D1
	ADD.L	D0,D1
	AND.L	#-2,D1
	MOVE.L	D1,SP
	MOVE.L	D0,-(SP)
	MOVE.L	A5,-(SP)
	MOVE.W	D0,-(SP)
	MOVE.W	#$4A,-(SP)
	TRAP	#1
	LEA	12(SP),SP
	TST.L	D0		gick det bra?
	BEQ.B	OK		-> Ja
DONE	CLR.W	-(SP)		Nej, hoppa ur!
	TRAP	#1

OK	MOVE.W	#10,CONTROL
	CLR.W	CONTROL+2
	MOVE.W	#1,CONTROL+4
	CLR.W	CONTROL+6
	CLR.W	CONTROL+8
	BSR	AES
	
	PEA	PATH
	PEA	NAME
	PEA	TITLE
	BSR	FSEL_EXINPUT
	ADD.L	#16,SP
	
	MOVE.W	D0,-(SP)
	PEA	CLRF
	MOVE.W	#9,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP

	TST.W	(SP)+
	BEQ.B	ERROR	->OM AVBRYT

	PEA	PATH
	MOVE.W	#9,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP

	PEA	CLRF
	MOVE.W	#9,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP

	PEA	NAME
	BRA.B	GO_ON

ERROR	PEA	ERR_TEXT

GO_ON	MOVE.W	#9,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP

	MOVE.W	#7,-(SP)
	TRAP	#1
	ADDQ.L	#2,SP

	MOVE.W	#19,CONTROL
	CLR.W	CONTROL+2
	MOVE.W	#1,CONTROL+4
	CLR.W	CONTROL+6
	CLR.W	CONTROL+8
	BSR	AES

	CLR.W	-(SP)
	TRAP	#1

AES	MOVE.L	#AESPB,D1
	MOVE.W	#$C8,D0
	TRAP	#2
	RTS

FSEL_EXINPUT
	LINK	A6,#0
	MOVEM.L	D1-D7/A0-A5,-(SP)
	
	MOVE.W	#91,CONTROL	(#90)
	CLR.W	CONTROL+2
	MOVE.W	#2,CONTROL+4
	MOVE.W	#3,CONTROL+6
	CLR.W	CONTROL+8

	MOVE.L	16(A6),ADDR_IN		PATH
	MOVE.L	12(A6),ADDR_IN+4	FILENAME
	MOVE.L	8(A6),ADDR_IN+8		TITEL
	BSR	AES

	MOVE.W	INT_OUT+2,D0
	
	MOVEM.L	(SP),D1-D7/A0-A5
	UNLK	A6
	RTS

	SECTION DATA
AESPB	DC.L	CONTROL
	DC.L	GLOBAL
	DC.L	INT_IN
	DC.L	INT_OUT
	DC.L	ADDR_IN
	DC.L	ADDR_OUT

TITLE	DC.B	"--- THE TOXIC TWINS ---",0
NAME	DS.B	20
CLRF	DC.B	13,10,0
ERR_TEXT	DC.B	"OGILTIGT VAL",0
PATH	DC.B	"A:\*.*",0
	DS.B	64

	SECTION	BSS
GLOBAL	DS.W	16
CONTROL	DS.W	10
INT_IN	DS.W	128
INT_OUT	DS.W	128
ADDR_IN	DS.L	128
ADDR_OUT	DS.L	128
