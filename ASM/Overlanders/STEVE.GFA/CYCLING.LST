'
' Palette cycling
'
RESERVE 50000                   ! Memory for GFA itself
rez|=XBIOS(4)                   ! Save the basic rez
VOID XBIOS(5,L:-1,L:-1,0)       ! Low Rez
loadpic("CYCLING")
~XBIOS(6,L:pal%)                ! Install the palette
BMOVE pic%,XBIOS(2),32000       ! Display the picture
'
REPEAT
  VSYNC
  VSYNC                         ! Add more VSYNC to get a slower animation speed
  color_out&=CARD{pal%+30}      ! Save the last color (nø15)
  BMOVE pal%+2,pal%+4,28        ! Shift all colors to the right
  CARD{pal%+2}=color_out&       ! Replace the fist color (nø1) by the saved one
  ~XBIOS(6,L:pal%)              ! Install new palette
  '
  KEYTEST c|                    ! Test the keyboard
UNTIL c|=32     ! Quit if space pressed
'
~XBIOS(5,L:-1,L:-1,rez|)        ! Back to the right Rez
~MFREE(pal%)                    ! Clear the pal...
~MFREE(pic%)                    ! ... and pic areas
END
'
PROCEDURE loadpic(image$)
  image$=image$+".PI1"
  pal%=MALLOC(32)
  pic%=MALLOC(32000)
  OPEN "I",#1,image$
  SEEK #1,2                     ! Jump over the Rez coded in the pic
  BGET #1,pal%,32               ! Get the color palette
  BGET #1,pic%,32000            ! Get the picture itself
  CLOSE
RETURN
