0 REM Beispielprogramm
1 REM Support-Ecke Juni/Juli 1991
2 REM Von GÅnther Heidel, 16. Juni 1991
3 REM (C) 1991 Omikron.Software
4 REM
5 Esc$= CHR$(27)
6 PRINT "Dieses Programm fÅhrt einige der VT52-Escape-"
7 PRINT "Sequenzen vor. Hierbei wird zusÑtzlich zu der"
8 PRINT "Funktion auch noch der Code der Sequenz ange-"
9 PRINT "zeigt."
10 PRINT
11 PRINT "Das Programm kann jederzeit durch einen"
12 PRINT "Druck auf eine beliebige Taste angehalten"
13 PRINT "werden. Weiter geht es dann durch noch einen"
14 PRINT "Tastendruck."
15 PRINT
16 PRINT "Zum Starten bitte eine Taste drÅcken."
17 A$= INPUT$(1)
18 Print_("E","","","Bildschirm lîschen")
19 Warte
20 FOR I%L=1 TO 3:Print_("B","","","Cursor nach unten bewegen"): NEXT I%L'   Cursor nach unten bewegen
21 FOR I%L=1 TO 3:Print_("C","","","Cursor nach rechts bewegen"): NEXT I%L'   Cursor nach rechts bewegen
22 FOR I%L=1 TO 3:Print_("A","","","Cursor nach oben bewegen"): NEXT I%L'   Cursor nach oben bewegen
23 FOR I%L=1 TO 3:Print_("D","","","Cursor nach links bewegen"): NEXT I%L'   Cursor nach links bewegen
24 Warte
25 Print_("Y", CHR$(32+8), CHR$(32+0),"Cursor auf Spalte 0, Zeile 8 setzen")' Setzt den Cursor auf Spalte 0, Zeile 8
26 Warte
27 PRINT "Dies ist ein Beispielsatz fÅr das Beispielprogramm.";
28 Warte
29 Print_("Y", CHR$(32+8), CHR$(32+8),"Cursor auf Spalte 8, Zeile 8 setzen")' Setzt den Cursor auf Spalte 8, Zeile 8
30 Warte
31 Print_("K","","","Rest der Zeile lîschen")
32 Warte
33 Print_("Y", CHR$(32+8), CHR$(32+0),"Cursor auf Spalte 0, Zeile 8 setzen")' Setzt den Cursor auf Spalte 0, Zeile 8
34 Warte
35 FOR I%L=1 TO 5: PRINT "Dies ist ein Beispielsatz fÅr das Beispielprogramm.": NEXT I%L
36 Print_("Y", CHR$(32+8), CHR$(32+8),"Cursor auf Spalte 8, Zeile 8 setzen")' Setzt den Cursor auf Spalte 8, Zeile 8
37 Warte
38 Print_("J","","","Rest des Bildschirms lîschen.")
39 Warte
40 Print_("Y", CHR$(32+8), CHR$(32+0),"Cursor auf Spalte 0, Zeile 8 setzen")' Setzt den Cursor auf Spalte 0, Zeile 8
41 Warte
42 FOR I%L=1 TO 5: PRINT "Dies ist ein Beispielsatz fÅr das Beispielprogramm.": NEXT I%L
43 FOR I%L=1 TO 20:Print_("I","","","Cursor eine Spalte nach oben schieben."): NEXT I%L
44 Warte
45 Print_("Y", CHR$(32+7), CHR$(32+0),"Cursor auf Spalte 0, Zeile 7 setzen")' Setzt den Cursor auf Spalte 0, Zeile 7
46 Warte
47 Print_("J","","","Rest des Bildschirms lîschen.")
48 Warte
49 Print_("Y", CHR$(32+8), CHR$(32+0),"Cursor auf Spalte 0, Zeile 8 setzen")' Setzt den Cursor auf Spalte 0, Zeile 8
50 Warte
51 FOR I%L=1 TO 5: PRINT "Dies ist ein Beispielsatz fÅr das Beispielprogramm.": NEXT I%L
52 Print_("Y", CHR$(32+10), CHR$(32+0),"Cursor auf Spalte 0, Zeile 10 setzen")
53 Warte
54 FOR I%L=1 TO 3:Print_("L","","","Eine Zeile einfÅgen."): NEXT I%L
55 Warte
56 FOR I%L=1 TO 3:Print_("M","","","Eine Zeile herauslîschen."): NEXT I%L
57 Warte
58 Print_("Y", CHR$(32+13), CHR$(32+20),"Cursor auf Spalte 20, Zeile 13 stellen.")
59 Print_("d","","","Bildschirm bis zum Cursor lîschen.")
60 Warte
61 Print_("H","","","Cursor in die linke obere Ecke stellen.")
62 Print_("Y", CHR$(32+8), CHR$(32+0),"Cursor auf Spalte 0, Zeile 8 setzen")' Setzt den Cursor auf Spalte 0, Zeile 8
63 Print_("b", CHR$(5),"","Schriftfarbe auf Farbe 5 setzen.")
64 PRINT "Dies ist ein Test."
65 Warte
66 Print_("b", CHR$(1),"","Schriftfarbe auf Farbe 1 setzen.")
67 PRINT "Dies ist ein Test."
68 Warte
69 Print_("c", CHR$(4),"","Hintergrund auf Farbe 4 setzen.")
70 PRINT "Dies ist ein Test."
71 Warte
72 Print_("c", CHR$(0),"","Hintergrund auf Farbe 0 setzen.")
73 PRINT "Dies ist ein Test."
74 Warte
75 Print_("f","","","Cursor ausschalten.")
76 PRINT "Jetzt ist der Cursor aus!"
77 Warte
78 Print_("e","","","Cursor einschalten.")
79 PRINT "Jetzt ist der Cursor an!"
80 Warte
81 Print_("E","","","Bildschirm lîschen.")
82 Warte
83 Print_("Y", CHR$(32+8), CHR$(32+0),"Cursor auf Spalte 0, Zeile 8 setzen")' Setzt den Cursor auf Spalte 0, Zeile 8
84 Print_("p","","","Inversdarstellung einschalten.")
85 PRINT "Dieser Text ist jetzt invers!"
86 Warte
87 Print_("q","","","Inversdarstellung ausschalten.")
88 PRINT "Dieser Text ist wieder normal!"
89 Warte
90 Print_("w","","","Automatischer öberlauf aus.")
91 FOR I%L=1 TO 100: PRINT "Test!";: NEXT I%L
92 Warte
93 Print_("v","","","Automatischer öberlauf ein.")
94 FOR I%L=1 TO 100: PRINT "Test!";: NEXT I%L
95 Warte
96 END
97 '
98 DEF PROC Warte
99 IF INKEY$ <>"" THEN A$= INPUT$(1) ELSE WAIT 1
100 RETURN
101 '
102 ' Diese Prozedur fÅhrt die Åbergeben ESCAPE-Sequenz aus und
103 ' gibt auf dem Bildschirm den PRINT-Befehl aus, der fÅr diese
104 ' Sequenz notwendig ist sowie eine Beschreibung der Funktion.
105 ' Hinweis: Die Prozedur gibt immer ZUERST den Befehl auf dem
106 ' Bildschirm aus und fÅhrt ihn erst dann aus!
107 '
108 DEF PROC Print_(Steuer$,Steuer2$,Steuer3$,Erkl$)
109   PRINT Esc$;"j";' Cursorposition speichern
110   PRINT Esc$;"Y"; CHR$(32+5); CHR$(32+0);' Cursor auf Spalte 5, Zeile 0 setzen
111   PRINT Esc$;"K";' Rest der Zeile lîschen
112   PRINT "Befehl: PRINT CHR$(27);CHR$("; ASC(Steuer$);")";
113   IF Steuer2$<>"" THEN PRINT ";CHR$("; ASC(Steuer2$);")";
114   IF Steuer3$<>"" THEN PRINT ";CHR$("; ASC(Steuer3$);")";
115   PRINT CHR$(10); CHR$(13);Esc$;"K";Erkl$;' Cursor eine Zeile tiefer an linken
116   'Rand bringen und Rest der Zeile lîschen und Erkl$ ausgeben.
117   PRINT Esc$;"k";' Cursor wieder an alte Position setzen
118   Warte
119   PRINT Esc$;Steuer$;' ESCAPE-Seqenz ausfÅhren
120   IF Steuer2$<>"" THEN PRINT Steuer2$;
121   IF Steuer3$<>"" THEN PRINT Steuer3$;
122 RETURN
