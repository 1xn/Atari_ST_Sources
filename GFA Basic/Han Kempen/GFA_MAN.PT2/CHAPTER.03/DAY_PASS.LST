' *** DAY_PASS.LST ***                                2nd GFA-Manual, page 3-12
FUNCTION days_passed(d1$,d2$)
  '
  ' *** Calculate number of days that have passed between two dates
  ' *** Format of dates must be "dd.mm.yyyy"
  '
  LOCAL p,i,d%,j%,m%,r%,z%,days%
  DIM datum$(1)
  datum$(0)=d1$
  datum$(1)=d2$
  FOR i=0 TO 1
    p=INSTR(datum$(i),".")
    d%=VAL(LEFT$(datum$(i),PRED(p)))
    m%=VAL(MID$(datum$(i),SUCC(p)))
    j%=VAL(MID$(datum$(i),SUCC(RINSTR(datum$(i),"."))))
    z%=ADD(MUL(j%,365),d%)
    r%=m%
    IF m%>2
      SUB z%,INT(r%*0.4+2.3)
      INC j%
    ENDIF
    ADD z%,INT(ADD(MUL(m%,31),DIV(PRED(j%),4)))
    IF j%=0
      INC z%
    ENDIF
    IF i<1
      days%=z%
    ENDIF
  NEXT i
  ERASE datum$()
  RETURN ABS(SUB(days%,z%))
ENDFUNC
' *****
'
