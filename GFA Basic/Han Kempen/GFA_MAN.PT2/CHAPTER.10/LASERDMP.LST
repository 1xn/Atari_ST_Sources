' *** LASERDMP.LST ***                                2nd GFA-Manual, page 10-3
> PROCEDURE screendump_atari_laser
  '
  ' *** Screendump from logical screen to Atari Laser-printer
  ' *** Works in High resolution only
  ' *** Couldn't test this Procedure myself, as I don't have this printer
  ' *** By changing the screen-offset and the number of x- and y-pixels, you
  ' ***                         should be able to print any screen-rectangle
  '
  ' *** Standard:  high.res!  x.max&    y.max&  logbase%  char.width&
  '
  LOCAL block$,gl$,k
  IF high.res!
    REPEAT                                ! printer ready?
      EXIT IF GEMDOS(&H11)
      SOUND 1,10,12,4,25
      SOUND 1,10,6,4,25
      SOUND 1,10,12,4,50
      SOUND 1,0
      ALERT 3," Printer| | not ready !!",1," OK | Stop ",k
      IF k=2
        LET stop!=TRUE
      ENDIF
    UNTIL stop!
    '
    IF NOT stop!
      HIDEM
      block$=MKL$(logbase%)
      block$=block$+MKI$(0)+MKI$(0)                        ! screen-offset (0,0)
      block$=block$+MKI$(DIV(SUCC(x.max),char.width))      ! characters/line
      block$=block$+MKI$(SUCC(x.max))+MKI$(SUCC(y.max))    ! x- and y-pixels
      block$=block$+MKI$(0)+MKI$(0)                        ! print-offset (0,0)
      block$=block$+MKI$(2)         ! scaling factor (1=300, 2=150, 4=75 dpi)
      gl$=HEX$(V:block$)            ! hexadecimal block-address
      IF LEN(gl$)<8
        gl$=STRING$(SUB(8,LEN(gl$)),"0")+gl$               ! pad with '0'
      ENDIF
      LPRINT CHR$(27)+"gG"+gl$+CHR$(32)                    ! activate printer
      LPRINT CHR$(12)                                      ! paper feed
    ENDIF
  ELSE
    m$="Sorry, screendump |only possible in |High resolution"
    ALERT 3,m$,1," OK ",k
  ENDIF
RETURN
' *********
'
