' *** SCRNDUMP.LST ***                                2nd GFA-Manual, page 10-3
> PROCEDURE screendump
  '
  ' *** Screendump to Epson-compatible printer
  ' *** Sets the following parameters through Xbios 33 (Setprt):
  ' ***     matrix-printer, High resolution, 960 pixels/line (Epson),
  ' ***     draft, parallel, fanfold paper
  ' *** After setting these parameters, HARDCOPY takes care of the screendump
  ' *** HARDCOPY does not account for the correct height/width ratio
  '
  ' *** Standard:  high.res!
  '
  LOCAL m$,k,stop!
  IF high.res!
    m$="Screendump will last |several minutes,|"
    m$=m$+"abort dump with |<Alternate> <Help>"
    ALERT 1,m$,1," OK | Stop ",k
    IF k=1
      REPEAT                                ! printer on?
        EXIT IF GEMDOS(&H11)
        SOUND 1,10,12,4,25
        SOUND 1,10,6,4,25
        SOUND 1,10,12,4,50
        SOUND 1,0
        ALERT 3," Printer| | not ready !!",1," OK | Stop ",k
        IF k=2
          LET stop!=TRUE
        ENDIF
      UNTIL stop!
      '
      IF NOT stop!
        HIDEM
        ~XBIOS(33,&X100)
        HARDCOPY
      ENDIF
      '
    ENDIF
  ELSE
    m$="Sorry, screendump |only possible in |High resolution"
    ALERT 3,m$,1," OK ",k
  ENDIF
RETURN
' *********
'
