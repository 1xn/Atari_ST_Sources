' *** STR24DMP.LST ***                                2nd GFA-Manual, page 10-4
> PROCEDURE screendump_star24
  '
  ' *** Screendump from logical screen to Star LC24-10 (or compatible printer)
  ' *** Works in High resolution only
  ' *** Each byte is 'enlarged' to 3 bytes (e.g. &X00000001 becomes
  ' ***                                          &X000000000000000000000111)
  '
  ' *** Global:  high.res!  logbase%
  '
  LOCAL m$,k,col,b%,x,p|,b1|,b2|,b3|,n,stop!,c$
  IF high.res!
    m$="Screendump will last |several minutes, |abort dump with <Esc>"
    ALERT 1,m$,1," OK | Stop ",k
    IF k=1
      REPEAT                                ! printer ready?
        EXIT IF GEMDOS(&H11)
        SOUND 1,10,12,4,25
        SOUND 1,10,6,4,25
        SOUND 1,10,12,4,50
        SOUND 1,0
        ALERT 3," Printer| | not ready !!",1," OK | Stop ",k
        IF k=2
          LET stop!=TRUE
        ENDIF
      UNTIL stop!
      '
      IF NOT stop!
        HIDEM
        LPRINT CHR$(27)+"@";              ! initialize printer
        ' *** left margin of 8 spaces (Elite mode!); 24 pixels/line
        ' *** bit-image mode: 120 pixels/inch (double); 2x400 pixels/line
        c$=SPACE$(8)+CHR$(27)+"3"+CHR$(24)+CHR$(27)+"*"+CHR$(33)+CHR$(32)+CHR$(3)
        FOR col=0 TO 79
          b%=ADD(logbase%,col)
          LPRINT c$;
          FOR x=399 DOWNTO 0
            p|=BYTE{ADD(b%,MUL(x,80))}
            IF p|=0                             ! 'white' byte
              OUT 0,0,0,0,0,0,0
            ELSE IF p|=255                      ! 'black' byte
              OUT 0,255,255,255,255,255,255
            ELSE
              CLR b1|,b2|,b3|
              IF BTST(p|,0)
                b1|=7
              ENDIF
              IF BTST(p|,1)
                ADD b1|,56
              ENDIF
              IF BTST(p|,2)
                ADD b1|,192
                b2|=1
              ENDIF
              IF BTST(p|,3)
                ADD b2|,14
              ENDIF
              IF BTST(p|,4)
                ADD b2|,112
              ENDIF
              IF BTST(p|,5)
                ADD b2|,128
                b3|=3
              ENDIF
              IF BTST(p|,6)
                ADD b3|,28
              ENDIF
              IF BTST(p|,7)
                ADD b3|,224
              ENDIF
              OUT 0,b3|,b2|,b1|,b3|,b2|,b1|
            ENDIF
          NEXT x
          LPRINT
          EXIT IF INKEY$=CHR$(27)          ! abort after <Esc>
        NEXT col
        LPRINT CHR$(12);                   ! form feed
        LPRINT CHR$(27)+"@";               ! initialize printer
      ENDIF
    ENDIF
  ELSE
    m$="Sorry, screendump |only possible in |High resolution"
    ALERT 3,m$,1," OK ",k
  ENDIF
RETURN
' *********
'
