' *** ITER_SRT.LST ***                                 2nd GFA-Manual, page 5-7
> PROCEDURE iter_quick_sort(VAR proc())
  '
  ' *** Sort word-array with iterative 'Quick Sort'
  '
  LOCAL stack.dim,last,top,end,begin,i,j,middle,mid.value
  ERASE stack()
  last=PRED(DIM?(proc()))
  stack.dim=2*LOG(last)/LOG(2)
  DIM stack(stack.dim)
  top=2
  stack(1)=0
  stack(2)=last
  WHILE top>0
    end=stack(top)
    begin=stack(SUB(top,1))
    SUB top,2
    WHILE begin<end
      i=begin
      j=end
      middle=DIV(ADD(begin,end),2)
      mid.value=proc(middle)
      WHILE proc(i)<mid.value
        INC i
      WEND
      WHILE proc(j)>mid.value
        DEC j
      WEND
      WHILE i<j-1
        SWAP proc(i),proc(j)
        INC i
        DEC j
        WHILE proc(i)<mid.value
          INC i
        WEND
        WHILE proc(j)>mid.value
          DEC j
        WEND
      WEND
      IF i<=j
        IF i<j
          SWAP proc(i),proc(j)
        ENDIF
        INC i
        DEC j
      ENDIF
      IF j<middle
        INC top
        stack(top)=i
        INC top
        stack(top)=end
        end=j
      ELSE
        INC top
        stack(top)=begin
        INC top
        stack(top)=j
        begin=i
      ENDIF
    WEND
  WEND
RETURN
' *********
'
