' *** MIDI_BUF.LST ***                                2nd GFA-Manual, page 12-2
> PROCEDURE midi_buffer_new(bytes%,VAR old.adr%)
  '
  ' *** Change size of Midi-buffer (default 128 bytes)
  ' *** Return address of original buffer in old.adr%
  ' *** Restore original buffer with Procedure Midi_buffer_restore
  '
  ' *** Global:  midi.buffer|()
  '
  LOCAL ptr%
  ptr%=XBIOS(14,2)
  old.adr%=LPEEK(ptr%)                  ! address of original buffer
  DIM midi.buffer|(bytes%-1)            ! new buffer
  LPOKE ptr%,VARPTR(midi.buffer|(0))    ! address of new buffer
  DPOKE ptr%+4,bytes%                   ! size
  DPOKE ptr%+6,0                        ! buffer-head
  DPOKE ptr%+8,0                        ! buffer-tail
  DPOKE ptr%+10,0                       ! low mark (not used)
  DPOKE ptr%+12,bytes%-1                ! high mark (not used)
RETURN
' ***
> PROCEDURE midi_buffer_restore(midi.buffer.adr%)
  '
  ' *** Restore default Midi-buffer
  ' *** Address was returned by Procedure Midi_buffer_new
  '
  ' *** Global:  midi.buffer|()
  '
  LOCAL adr%,m$,k
  adr%=XBIOS(14,2)                      ! address of current buffer
  LPOKE adr%,midi.buffer.adr%           ! address of default buffer (128 bytes)
  DPOKE adr%+4,128                      ! size
  DPOKE adr%+6,0                        ! buffer-head
  DPOKE adr%+8,0                        ! buffer-tail
  DPOKE adr%+10,0                       ! low mark (not used)
  DPOKE adr%+12,127                     ! high mark (not used)
  ERASE midi.buffer|()                  ! don't need the buffer anymore
RETURN
' *********
'
