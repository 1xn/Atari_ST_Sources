' *** DRIVES.LST ***                                 2nd GFA-Manual, page 11-21
> PROCEDURE drives(VAR drive!())
  '
  ' *** Make array-table drive!() of available drives
  ' *** Drive A available if drive!(0)=TRUE, drive B if drive!(1)=TRUE, etc.
  ' *** If drive!(1)=TRUE: there is an external drive B connected
  ' *** If drive!(1)=FALSE: drive B is "available" as drive A; the operating
  ' ***      system needs drive B for copy-operations from disk (A) to disk (B)
  '
  LOCAL table%,i
  DIM drive!(15)
  SELECT DPEEK(&H4A6)
  CASE 1                        ! drive A (no external drive B)
    drive!(0)=TRUE
  CASE 2                        ! drive A and (external) drive B
    drive!(0)=TRUE
    drive!(1)=TRUE
  ENDSELECT
  table%=BIOS(10)               ! examine other drives (C - P)
  FOR i=2 TO 15
    IF BTST(table%,i)
      drive!(i)=TRUE
    ENDIF
  NEXT i
RETURN
' *******
'
