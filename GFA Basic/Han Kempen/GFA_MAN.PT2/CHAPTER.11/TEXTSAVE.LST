' *** TEXTSAVE.LST ***                               2nd GFA-Manual, page 11-23
> PROCEDURE text_save(file$,n.lines,VAR array$())
  '
  ' *** Save n.lines& of ASCII-text array$() as file$
  ' *** This Procedure uses channel #90 for saving
  '
  LOCAL m$,k,back$
  IF EXIST(file$)
    name$=UPPER$(RIGHT$(file$,LEN(file$)-RINSTR(file$,"\")))
    m$="The file "+name$+" already |exists: delete file or |rename as *.BAK?"
    ALERT 3,m$,0,"Delete|*.BAK",k
    IF k=1
      KILL file$
    ELSE
      p=INSTR(file$,".")
      IF p>0
        back$=LEFT$(file$,p)+"BAK"
      ELSE
        back$=file$+".BAK"
      ENDIF
      IF EXIST(back$)
        KILL back$
      ENDIF
      RENAME file$ AS back$
    ENDIF
  ENDIF
  OPEN "O",#90,file$
  STORE #90,array$(),n.lines
  CLOSE #90
RETURN
' *********
'
