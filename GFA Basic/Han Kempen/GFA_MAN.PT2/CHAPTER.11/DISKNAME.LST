' *** DISKNAME.LST ***                               2nd GFA-Manual, page 11-24
FUNCTION disk_name$(drive$)
  '
  ' *** Return the name of (the disk in) drive$
  '
  ' *** Procedure:  Program_exit
  '
  LOCAL dta.adr%,stat,m$,k
  dta.adr%=FGETDTA()
  stat=FSFIRST(drive$+":\*.*",8)        ! search for disk-name (bit 3)
  IF stat=0
    RETURN CHAR{dta.adr%+30}            ! found it
  ELSE IF stat=-33
    RETURN ""                           ! no name
  ELSE
    m$="   *** Error ***| |in Function Disk_name$ |after FSFIRST"
    ALERT 3,m$,1,"Continue|Exit",k
    IF k=1
      RETURN ""
    ELSE
      @program_exit
    ENDIF
  ENDIF
ENDFUNC
' *****
'
