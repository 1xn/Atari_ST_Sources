' *** FILE_MAL.LST ***                               2nd GFA-Manual, page 11-24
FUNCTION file_load(file$)
  '
  ' *** Reserve memory and load a file (only once!)
  ' *** Returns address of loaded file (0 if not loaded)
  ' *** Don't forget to restore memory again:   ~MFREE(adr%)
  ' ***                                         RESERVE
  '
  ' *** Procedure:  Program_exit
  '
  LOCAL dta.adr%,bytes%,free%,gem%,max%,adr%,m$,k
  dta.adr%=FGETDTA()
  IF EXIST(file$)
    bytes%={dta.adr%+26}                ! file-length
    bytes%=bytes%+256-MOD(bytes%,256)   ! multiple of 256
    free%=FRE()                         ! available RAM
    IF free%>bytes%
      RESERVE -bytes%                   ! give to GEMDOS
      adr%=MALLOC(bytes%)               ! reserved memory now available
      BLOAD file$,adr%                  ! load file
      RETURN adr%                       ! address of loaded file
    ELSE
      m$="   *** Error ***|Not enough memory |avaiable for loading |"
      m$=m$+"file "+RIGHT$(file$,LEN(file$)-RINSTR(file$,"\"))
      ALERT 3,m$,1,"Continue|Exit",k
      IF k=1
        RETURN 0
      ELSE
        @program_exit
      ENDIF
    ENDIF
  ELSE
    m$="   *** Error ***| |File "+RIGHT$(file$,LEN(file$)-RINSTR(file$,"\"))
    m$=m$+"|not found "
    ALERT 3,m$,1,"Continue|Exit",k
    IF k=1
      RETURN 0
    ELSE
      @program_exit
    ENDIF
  ENDIF
ENDFUNC
' *****
'
