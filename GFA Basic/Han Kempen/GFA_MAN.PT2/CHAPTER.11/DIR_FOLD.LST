' *** DIR_FOLD.LST ***                               2nd GFA-Manual, page 11-20
> PROCEDURE dir_folders(path$,ext$,VAR dir.folders$(),last.folder)
  '
  ' *** Put all folders with path path$ and extension ext$ in string-array
  ' *** The index of the last folder is returned in last.folder&
  '
  LOCAL i,search$,dta.adr%,stat
  IF DIM?(dir.folders$())<>0
    ERASE dir.folders$()
  ENDIF
  DIM dir.folders$(99)      ! not more than 100 folders      <<< adapt this >>>
  IF RIGHT$(path$)="\"
    search$=path$+"*."+ext$
  ELSE
    search$=path$+"\*."+ext$
  ENDIF
  dta.adr%=FGETDTA()
  REPEAT                                        ! find first folder
    stat=FSFIRST(search$,16)
  UNTIL stat=-33 OR BTST(BYTE{dta.adr%+21},4)
  IF LEFT$(CHAR{dta.adr%+30})="."            ! ignore "." and ".." in folders
    stat=FSNEXT()
    stat=FSNEXT()
  ENDIF
  IF stat=0
    LET dir.folders$(0)=CHAR{dta.adr%+30}
  ENDIF
  REPEAT
    REPEAT
      stat=FSNEXT()
    UNTIL stat=-33 OR BTST(BYTE{dta.adr%+21},4)
    IF stat=0
      INC i
      LET dir.folders$(i)=CHAR{dta.adr%+30}
    ENDIF
  UNTIL stat<>0
  last.folder=i
RETURN
' *********
'
