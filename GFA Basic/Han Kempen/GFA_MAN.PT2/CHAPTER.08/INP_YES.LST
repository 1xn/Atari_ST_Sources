' *** INP_YES.LST ***                                 2nd GFA-Manual, page 8-13
FUNCTION input_yes_no
  '
  ' *** Choose between yes (TRUE) and no (FALSE)
  ' *** Yes = <y> or <Y> or <Return> or left mouse-button
  ' *** No  = <n> or <N> or right mouse-button
  '
  LOCAL x,y,in$,w1$,w2$,k,yes!
  HIDEM
  x=CRSCOL
  y=CRSLIN
  w1$="(y/n)"
  w2$=SPACE$(LEN(w1$))
  {ADD(XBIOS(14,1),6)}=0                ! clear keyboard-buffer
  PRINT AT(x,y);w1$;
  REPEAT
    PAUSE 25
    SWAP w1$,w2$
    PRINT AT(x,y);w1$;
    k=MOUSEK
    in$=UPPER$(INKEY$)
  UNTIL INSTR("YN"+CHR$(13),in$) OR k>0
  IF in$="Y" OR in$=CHR$(13) OR k=1
    yes!=TRUE
    PRINT AT(x,y);"YES  ";
  ELSE
    yes!=FALSE
    PRINT AT(x,y);"NO   ";
  ENDIF
  WHILE MOUSEK                          ! mouse released?
  WEND
  {ADD(XBIOS(14,1),6)}=0                ! clear keyboard-buffer
  IF yes!
    RETURN TRUE
  ELSE
    RETURN FALSE
  ENDIF
ENDFUNC
' *****
'
