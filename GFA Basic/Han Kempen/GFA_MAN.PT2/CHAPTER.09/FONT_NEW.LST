' *** FONT_NEW.LST ***                                2nd GFA-Manual, page 9-10
> PROCEDURE systemfont_new(font!)
  '
  ' *** Change systemfont for PRINTing on the TOS-screen (font!=TRUE)
  ' *** Use A1_xxxxx.FON-files (4114 bytes) from FONTKIT by Jeremy Hughes
  ' *** Or use any regular Atari-font (4096 bytes)
  ' *** Original systemfont is restored if font!=FALSE
  ' *** Works in High resolution only!
  '
  ' *** Global:  systemfont%  a1_digt% [INLINE-address of new font]
  '
  ' >>> Load a font (4114 or 4096 bytes) in INLINE-line a1_xxxxx% <<<
  '
  LOCAL adr%,m$,k
  '
  IF font!
    IF systemfont%=0
      adr%=L~A-22
      LET systemfont%={adr%}            ! address of systemfont
      SLPOKE adr%,a1_digt%              ! activate new font: A1_DIGT.FON
    ELSE
      m$="*** Error ***|Procedure Systemfont_new |can install not more"
      m$=m$+"|than one new systemfont"
      ALERT 3,m$,1,"EDIT",k
      SLPOKE L~A-22,systemfont%
      EDIT
    ENDIF
  ELSE
    IF systemfont%<>0
      SLPOKE L~A-22,systemfont%        ! restore original systemfont
      LET systemfont%=0
    ENDIF
  ENDIF
RETURN
' *********
'
