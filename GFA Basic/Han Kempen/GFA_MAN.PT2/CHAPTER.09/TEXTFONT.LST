' *** TEXTFONT.LST ***                                2nd GFA-Manual, page 9-10
> PROCEDURE textfont_init
  '
  ' *** Install one or more fonts for use with TEXT
  ' *** Use A1_xxxxx.FON-files (4114 bytes) from FONTKIT by Jeremy Hughes
  ' *** Or use any regular Atari-font (4096 bytes)
  ' *** In this Procedure two fonts are installed, but you can adapt those lines
  ' *** The font-header is put behind the actual font in the INLINE-line
  ' *** Activate a font with: DEFTEXT ,,,,number (where number ò 2)
  ' *** Restore original systemfont (font no. 1) with: DEFTEXT ,,,,1
  '
  ' *** Global:  newfont.adr%()  newheader.adr%()
  '
  ' >>> Load fonts (4114 or 4096 bytes) in: INLINE a1_xxxxx%,4184 <<<
  '
  LOCAL n.fonts,header.adr%,font.adr%,i
  IF DIM?(newfont.adr%())=0
    n.fonts=2                      ! 2 fonts                    <<< adapt! >>>
    DIM newfont.adr%(PRED(n.fonts)),newheader.adr%(PRED(n.fonts))
    newfont.adr%(0)=a1_bauli%           ! font no. 2            <<< adapt! >>>
    newfont.adr%(1)=a1_med%             ! font no. 3            <<< adapt! >>>
    header.adr%={L~A-906}               ! address of systemfont-header
    font.adr%={header.adr%+76}          ! address of systemfont
    FOR i=0 TO PRED(n.fonts)
      newheader.adr%(i)=newfont.adr%(i)+4096  ! header behind font (INLINE-line)
      {header.adr%+84}=newheader.adr%(i)      ! address of next header
      BMOVE header.adr%,newheader.adr%(i),88  ! copy old header
      WORD{newheader.adr%(i)}=i+2             ! font-number
      {newheader.adr%(i)+76}=newfont.adr%(i)  ! font-address
      WORD{newheader.adr%(i)+66}=12           ! bit 0 in flag
      header.adr%=newheader.adr%(i)           ! address of font-header
    NEXT i
    {header.adr%+84}=0                   ! there is no next header
  ENDIF
RETURN
' *********
'
