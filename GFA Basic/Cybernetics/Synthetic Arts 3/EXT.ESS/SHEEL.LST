' -----------------------------------------------------------------------------'
'   - SYNTHETIC ARTS 2 -                              - CYBERNETICS 1992 -     '
'                                                                              '
'  Shell de gestion du fichier 'ART_LGHT.DAT' et de ses sous fichiers   V 2    '
' -----------------------------------------------------------------------------'
'
RESERVE 50000
a%=MALLOC(200000)
work%=MALLOC(200000)
fat%=MALLOC(1344)
IF OR(OR(a%=FALSE,work%=FALSE),fat%=FALSE)
  END                                         ! si pas ouvert on quitte
ENDIF
DIM ssfich$(200)
PRINT AT(1,1);"SELECT MASTER FILE";
FILESELECT "E:\CODE\SYNTHY.ART\CURENT\*.DAT","",dep$
IF dep$<>""
  IF NOT EXIST(dep$)
    init(dep$)
  ENDIF
  REPEAT
    CLS
    PRINT "1....ADD FILE"
    PRINT "2....DIRECTORY"
    PRINT "3....DELETE FILE"
    PRINT "4....SAVE A FILE"
    PRINT "5....INIT FILE"
    PRINT "6....RENAME FILE"
    PRINT
    PRINT "ESC TO QUIT"
    tch&=INP(2)
    CLS
    SELECT tch&
    CASE 49
      PRINT AT(1,1);"SELECT A FILE TO INSERT"
      FILESELECT "E:\CODE\SYNTHY.ART\CURENT\EXT.ESS\*.*","",a$
      IF a$<>""
        OPEN "i",#1,a$
        l%=LOF(#1)
        BGET #1,a%,l%
        CLOSE #1
        IF ODD(l%)
          PRINT AT(1,1);"ODD FILE LENGHT";SPC(20);
          ~INP(2)
        ELSE
          PRINT "Name of slave files (7 characters)";
          INPUT a$
          add(dep$,CHR$(5)+@namf$(a$),a%,l%)
        ENDIF
      ENDIF
    CASE 50
      a$=@select$(dep$)
    CASE 51
      a$=@select$(dep$)
      IF a$<>""
        del(dep$,a$)
      ENDIF
    CASE 52
      a$=@select$(dep$)
      IF a$<>""
        rechfile(a$,l%,lenght%,pos%)
        @load(dep$,a$,work%)
        PRINT AT(1,1);"SELECT NAME OF EXTRACTED FILE";
        FILESELECT "*.*","",a$
        IF a$<>""
          BSAVE a$,work%,lenght%
        ENDIF
      ENDIF
    CASE 53
      PRINT AT(1,1);"ARE YOU SURE: PRESS RETURN TO CONFIRM";
      IF INP(2)=13
        init(dep$)
      ENDIF
    CASE 54
      a$=@select$(dep$)
      IF a$<>""
        PRINT AT(1,1);"NEW NAME (7 CARACTERS)"
        INPUT b$
        ren(dep$,a$,CHR$(5)+@namf$(b$))
      ENDIF
    ENDSELECT
  UNTIL tch&=27
ENDIF
fin
'
> PROCEDURE ren(dep$,a$,b$)
  LOCAL l%,lenght%,pos%
  chargfat(dep$)
  rechfile(a$,l%,lenght%,pos%)
  IF l%<>TRUE
    rechfile(b$,l%,lenght%,lenght%)
    IF l%=TRUE
      OPEN "a",#1,dep$
      SEEK #1,pos%
      OUT% #1,CVL(MID$(b$,1,4))
      OUT% #1,CVL(MID$(b$,5,4))
      CLOSE #1
    ELSE
      PRINT AT(1,1);"THIS FILE ALREADY EXIST";SPC(10);
      ~INP(2)
    ENDIF
  ELSE
    PRINT AT(1,1);"NO FOUND";SPC(30);
    ~INP(2)
  ENDIF
  CLR pos%,lenght%,l%,dep$,a$,b$
RETURN
> PROCEDURE chargfat(fich$)
  OPEN "i",#1,fich$
  BGET #1,fat%,1344
  CLOSE #1
  CLR fich$
RETURN
> PROCEDURE rechfile(a$,VAR l%,lenght%,t%)
  LOCAL i&
  l%=1344
  i&=FALSE
  FOR t%=fat% TO ADD(fat%,1332) STEP 12
    ssfich$(i&)=MKL$({t%})+MKL$({ADD(t%,4)})+" "+STR$({ADD(t%,8)})
    EXIT IF MID$(ssfich$(i&),1,8)=a$
    ADD l%,{ADD(t%,8)}
    INC i&
  NEXT t%
  IF t%<>ADD(fat%,1344)
    lenght%={ADD(t%,8)}
  ELSE
    l%=TRUE
  ENDIF
  SUB t%,fat%
  CLR a$,i&
RETURN
> PROCEDURE add(fich$,a$,adr%,sp%)
  LOCAL lfo%,lenght%,l%,pos2%,pos%,l2%
  chargfat(fich$)
  rechfile(MKL$(FALSE)+MKL$(FALSE),l2%,lenght%,pos2%)
  IF l2%=TRUE
    PRINT AT(1,1);"NO MORE FREE SPACE IN THIS FILE";
    ~INP(2)
  ELSE
    rechfile(a$,l%,lenght%,pos%)
    OPEN "i",#1,fich$
    lfo%=LOF(#1)
    BGET #1,work%,lfo%
    CLOSE #1
    IF l%<>TRUE
      IF SUB(lfo%,ADD(l%,lenght%))
        BMOVE ADD(work%,ADD(l%,lenght%)),ADD(l%,work%),SUB(lfo%,ADD(l%,lenght%))
        BMOVE ADD(work%,ADD(pos%,12)),ADD(pos%,work%),SUB(1332,pos%)
        pos%=SUB(pos2%,12)
      ENDIF
      SUB lfo%,lenght%
    ELSE
      pos%=pos2%
    ENDIF
    BMOVE adr%,ADD(lfo%,work%),sp%
    ADD lfo%,sp%
    {ADD(work%,pos%)}=CVL(MID$(a$,1,4))
    {ADD(work%,ADD(pos%,4))}=CVL(MID$(a$,5,4))
    {ADD(work%,ADD(pos%,8))}=sp%
    BSAVE fich$,work%,lfo%
  ENDIF
  LOCAL lfo%,lenght%,l%,pos2%,pos%,l2%,fich$,a$,adr%,sp%
RETURN
> PROCEDURE del(fich$,a$)
  LOCAL lfo%,lenght%,l%,pos2%,pos%,l2%
  chargfat(fich$)
  rechfile(MKL$(FALSE)+MKL$(FALSE),l2%,lenght%,pos2%)
  IF l2%=TRUE
    PRINT AT(1,1);"NO MORE FREE SPACE IN THIS FILE";
    ~INP(2)
  ELSE
    rechfile(a$,l%,lenght%,pos%)
    OPEN "i",#1,fich$
    lfo%=LOF(#1)
    BGET #1,work%,lfo%
    CLOSE #1
    IF l%<>TRUE
      IF SUB(lfo%,ADD(l%,lenght%))
        BMOVE ADD(work%,ADD(l%,lenght%)),ADD(l%,work%),SUB(lfo%,ADD(l%,lenght%))
        BMOVE ADD(work%,ADD(pos%,12)),ADD(pos%,work%),SUB(1332,pos%)
        pos%=SUB(pos2%,12)
      ENDIF
      SUB lfo%,lenght%
    ELSE
      pos%=pos2%
    ENDIF
    {ADD(work%,pos%)}=FALSE
    {ADD(work%,ADD(pos%,4))}=FALSE
    {ADD(work%,ADD(pos%,8))}=FALSE
    BSAVE fich$,work%,lfo%
  ENDIF
  LOCAL lfo%,lenght%,l%,pos2%,pos%,l2%,fich$,a$
RETURN
> PROCEDURE load(fich$,a$,adr%)
  chargfat(fich$)
  rechfile(a$,l%,lenght%,pos%)
  IF l%<>TRUE
    OPEN "i",#1,fich$
    SEEK #1,l%
    BGET #1,adr%,lenght%
    CLOSE #1
  ELSE
    PRINT AT(1,1);"UNABLE TO FIND THIS FILE";
    ~INP(2)
  ENDIF
RETURN
> PROCEDURE init(fich$)
  LOCAL t&
  OPEN "o",#1,fich$
  FOR t&=FALSE TO 111
    OUT% #1,FALSE
    OUT% #1,FALSE
    OUT% #1,FALSE
  NEXT t&
  CLOSE #1
  CLR t&,fich$
RETURN
> FUNCTION select$(fich$)
LOCAL a$,i&,old&,tch&,l%,lenght%,pos%
chargfat(fich$)
rechfile(MKL$(FALSE)+MKL$(FALSE),l%,lenght%,pos%)
nb&=SUB(DIV(pos%,12),1)
FOR i&=FALSE TO nb&
  PRINT AT(ADD(MUL(DIV(i&,24),16),3),ADD(MOD(i&,24),1));ssfich$(i&);
NEXT i&
i&=FALSE
old&=FALSE
REPEAT
  PRINT AT(ADD(MUL(DIV(i&,24),16),1),ADD(MOD(i&,24),1));">"
  tch&=INP(2)
  PRINT AT(ADD(MUL(DIV(old&,24),16),1),ADD(MOD(old&,24),1));" "
  SELECT tch&
  CASE 208
    SUB i&,i&<nb&
  CASE 200
    ADD i&,i&>FALSE
  CASE 203
    SUB i&,AND(i&>23,24)
  CASE 205
    ADD i&,AND(i&<SUB(nb&,23),24)
  ENDSELECT
  old&=i&
UNTIL OR(tch&=27,tch&=13)
IF tch&=13
  a$=MID$(ssfich$(i&),1,8)
ENDIF
CLR i&,lenght%,pos%,l%,old&,tch&,fich$
RETURN a$
ENDFUNC
'
> PROCEDURE fin
~MFREE(a%)
~MFREE(work%)
~MFREE(fat%)
EDIT
RETURN
> FUNCTION namf$(a$)
a$=TRIM$(UPPER$(MID$(a$,1,7)))
a$=a$+SPACE$(SUB(7,LEN(a$)))
RETURN a$
ENDFUNC
