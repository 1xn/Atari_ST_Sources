' ----------------------------------------------------------------------------
'  GfaBasic INIT ROUTINE 1991-2007 by J.Tobergte (Mr.Coke of Absence.)
' ----------------------------------------------------------------------------
'              *** This is the BITBLT command version ***
'
DIM s_mfdb%(5)
DIM d_mfdb%(5)
DIM par%(8)
'
DIM xpos|(359)
'
RESERVE 5000
IF MALLOC(-1)<96000
  EDIT
ENDIF
'
s_mfdb%(1)=320                         ! breite in pixel [320]
s_mfdb%(2)=200                         ! hoehe in pixel  [200]
s_mfdb%(3)=20                          ! breite in word  [20]
s_mfdb%(4)=0                           ! reserved
s_mfdb%(5)=4                           ! num of bitplanes
d_mfdb%(1)=320                         ! breite in pixel [320]
d_mfdb%(2)=200                         ! hoehe in pixel  [200]
d_mfdb%(3)=20                          ! breite in word  [20]
d_mfdb%(4)=0                           ! reserved
d_mfdb%(5)=4                           ! num of bitplanes
'
m%=MALLOC(96000)
coke%=GEMDOS(&H20,L:0)                 ! supervisor mode
old_screen%=XBIOS(2)
'
ON ERROR GOSUB end
ON BREAK GOSUB end
'
@set_screen
ADD screen%,3840
'
~XBIOS(5,L:screen%,L:screen%,0)        ! set low resolution
'
FOR deg&=0 TO 359
  xpos|(deg&)=112+SINQ(deg&)*112
NEXT deg&
'
CLS
BMOVE screen%,screen%+32000,32000
'
WORD{&HFF8242}=&H777
WORD{&HFF8244}=&H700
WORD{&HFF8246}=&H70
WORD{&HFF8248}=&H7
'
BLOAD "A:\rocks.4pl",screen%+64000
'
deg&=0
deg2&=360-50
deg3&=360-100
deg4&=360-150
'
shift&=0
'
DO
  WORD{&HFFFF8240}=&H0
  VSYNC
  ~XBIOS(5,L:screen%+shift&,L:screen%+shift&,-1)
  ADD shift&,1280
  shift&=shift& MOD 30720
  '
  key|=BYTE{&HFFFFFC02}
  IF key|=15
    WORD{&HFFFF8240}=&H700
  ENDIF
  '
  @rccopy(screen%+65120,0,0,95,6,screen%+shift&,xpos|(deg4&),0,3)
  @rccopy(screen%+65120,0,0,95,6,screen%+30720+shift&,xpos|(deg4&),0,3)
  '
  @rccopy(screen%+64000,192,0,95,6,screen%+shift&,xpos|(deg3&),0,3)
  @rccopy(screen%+64000,192,0,95,6,screen%+30720+shift&,xpos|(deg3&),0,3)
  '
  @rccopy(screen%+64000,96,0,95,6,screen%+shift&,xpos|(deg2&),0,3)
  @rccopy(screen%+64000,96,0,95,6,screen%+30720+shift&,xpos|(deg2&),0,3)
  '
  @rccopy(screen%+64000,0,0,95,6,screen%+shift&,xpos|(deg&),0,3)
  @rccopy(screen%+64000,0,0,95,6,screen%+30720+shift&,xpos|(deg&),0,3)
  '
  ADD deg&,2
  deg&=deg& MOD 360
  '
  ADD deg2&,2
  deg2&=deg2& MOD 360
  '
  ADD deg3&,2
  deg3&=deg3& MOD 360
  '
  ADD deg4&,2
  deg4&=deg4& MOD 360
  '
  EXIT IF key|=57
  '
LOOP
'
GOSUB end
'
' ----------------------------------------------------------------------------
'     FUNCTIONS
' ----------------------------------------------------------------------------
PROCEDURE end
  ~XBIOS(5,L:old_screen%,L:old_screen%,1)         ! set old screen
  ~GEMDOS(&H20,L:coke%)                           ! into user mode
  ~MFREE(m%)                                      ! free memory
  EDIT                                            ! exit
RETURN
'
PROCEDURE set_screen
  screen%=m%+256
  sub%=screen% MOD 256
  SUB screen%,sub%
RETURN
'
PROCEDURE rccopy(s_adr%,sx%,sy%,sw%,sh%,d_adr%,dx%,dy%,mode%)
  s_mfdb%(0)=s_adr%                      ! quell adresse
  d_mfdb%(0)=d_adr%                      ! ziel adresse
  par%(0)=sx%                            ! x1 des quellrasters
  par%(1)=sy%                            ! y1 des quellrasters
  par%(2)=ADD(sx%,sw%)                   ! x2 des quellrasters
  par%(3)=ADD(sy%,sh%)                   ! y2 des quellrasters
  par%(4)=dx%                            ! x1 des zielrasters
  par%(5)=dy%                            ! y1 des zielrasters
  par%(6)=ADD(dx%,sw%)                   ! x2 des zielrasters
  par%(7)=ADD(dy%,sh%)                   ! y2 des zielrasters
  par%(8)=mode%                          ! kopiermodus
  BITBLT s_mfdb%(),d_mfdb%(),par%()      ! nun das eigentliche BLITTEN
RETURN
'
'
