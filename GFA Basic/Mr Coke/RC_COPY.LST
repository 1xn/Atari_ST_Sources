' ----------------------------------------------------------------------------
'  GfaBasic INIT ROUTINE 1991-2007 by J.Tobergte (Mr.Coke of Absence.)
' ----------------------------------------------------------------------------
'              *** This is the RC_COPY command version ***
'
DIM xpos|(359)
'
RESERVE 5000
IF MALLOC(-1)<96000
  EDIT
ENDIF
'
m%=MALLOC(96000)
coke%=GEMDOS(&H20,L:0)              ! supervisor mode
old_screen%=XBIOS(2)
'
ON ERROR GOSUB end
ON BREAK GOSUB end
'
@set_screen
ADD screen%,3840
'
BLOAD "A:\rocks.4pl",screen%+64000
'
~XBIOS(5,L:screen%,L:screen%,0)     ! set low resolution
'
FOR deg&=0 TO 359
  xpos|(deg&)=112+SINQ(deg&)*112
NEXT deg&
'
CLS
BMOVE screen%,screen%+32000,32000
'
WORD{&HFF8242}=&H777
WORD{&HFF8244}=&H700
WORD{&HFF8246}=&H70
WORD{&HFF8248}=&H7
'
deg&=0
deg2&=360-50
deg3&=360-100
deg4&=360-150
'
shift&=0
'
DO
  WORD{&HFFFF8240}=&H0
  VSYNC
  ~XBIOS(5,L:screen%+shift&,L:screen%+shift&,-1)
  ADD shift&,1280
  shift&=shift& MOD 30720
  '
  key|=BYTE{&HFFFFFC02}
  IF key|=15
    WORD{&HFFFF8240}=&H700
  ENDIF
  '
  RC_COPY screen%+65120,0,0,96,7 TO screen%+shift&,xpos|(deg4&),0
  RC_COPY screen%+65120,0,0,96,7 TO screen%+30720+shift&,xpos|(deg4&),0
  '
  RC_COPY screen%+64000,192,0,96,7 TO screen%+shift&,xpos|(deg3&),0
  RC_COPY screen%+64000,192,0,96,7 TO screen%+30720+shift&,xpos|(deg3&),0
  '
  RC_COPY screen%+64000,96,0,96,7 TO screen%+shift&,xpos|(deg2&),0
  RC_COPY screen%+64000,96,0,96,7 TO screen%+30720+shift&,xpos|(deg2&),0
  '
  RC_COPY screen%+64000,0,0,96,7 TO screen%+shift&,xpos|(deg&),0
  RC_COPY screen%+64000,0,0,96,7 TO screen%+30720+shift&,xpos|(deg&),0
  '
  ADD deg&,2
  deg&=deg& MOD 360
  '
  ADD deg2&,2
  deg2&=deg2& MOD 360
  '
  ADD deg3&,2
  deg3&=deg3& MOD 360
  '
  ADD deg4&,2
  deg4&=deg4& MOD 360
  '
  EXIT IF key|=57
  '
LOOP
'
GOSUB end
'
' ----------------------------------------------------------------------------
'     FUNCTIONS
' ----------------------------------------------------------------------------
PROCEDURE end
  ~XBIOS(5,L:old_screen%,L:old_screen%,1)         ! set old screen
  ~GEMDOS(&H20,L:coke%)                           ! into user mode
  ~MFREE(m%)                                      ! free memory
  EDIT                                            ! exit
RETURN
'
PROCEDURE set_screen
  screen%=m%+256
  sub%=screen% MOD 256
  SUB screen%,sub%
RETURN
'
'
'
