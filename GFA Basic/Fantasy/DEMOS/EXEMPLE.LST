.
.Programme principal destin‚ …
.l'animation de sprites en GfA-Basic
.par Jedi of Sector One
.from The Heavy Killers
.
.Rappel: Ce listing doit etre compil‚
.
.
.L…, il faut charger la courbe...
INLINE courbe%,1440
.
....Et l…, la palette !
INLINE pal%,32
.
.Initialisations
.
debut_courbe%=courbe%         !Pour le bouclage
fin_courbe%=courbe%+1440      !la fin de la courbe...
pointeur%=debut_courbe%       !Pointeur sur la courbe
OUT 4,&H12                    !Paralyse la souris...
HIDEM                         !...et l'extermine !
CLS                           !Allo la terre ?
~XBIOS(6,L:pal%)              !C'est + bo en couleur !
VSYNC                         !Jacques Caron aime la
origine%=XBIOS(2)             !crŠme de marron en tube
megatizer%=FRE(0)             !de 78 grammes...
RESERVE megatizer%-32256      !On a besoin de 32 Ko
ON BREAK GOSUB bye            !Beau temps, non ?
ecran1%=XBIOS(2)              !Quoiqu'un peu frais...
nouveau%=MALLOC(32256)        !Le second ‚cran
ecran2%=AND(ADD(nouveau%,255),&HFFFFFF00)
BMOVE ecran1%,ecran2%,32000   !Efface celui-ci
old%=ecran2%                  !Voir ST-Magazine
old2%=ecran1%                 !nø56, pages 64 … 66
.
.Boucle principale
.
DO                            !Tout doux, tout doux
  SWAP ecran1%,ecran2%        !Intervertit...
  ~XBIOS(5,L:ecran1%,L:ecran2%,-1)
  VSYNC                       !Synchronisation
  jedi%=old%                  !C'est l… qu'on efface !
  .
  .[ MERGEZ ICI LA ROUTINE D'EFFACAGE ]
  .
  jedi%=ADD(ecran1%,CARD{pointeur%})
  deca%=CARD{ADD(pointeur%,2)}!C'est ‡a qu'on affiche !
  .
  .Affichage du sprite (d‚calage=deca% ; adresse=jedi%)
  .
  ON deca% GOSUB sprite0,sprite1,sprite2,sprite3,sprite4,sprite5,sprite6,sprite7,sprite8,sprite9,sprite10,sprite11,sprite12,sprite13,sprite14,sprite15
  old%=old2%                  !La routine 'magique'
  old2%=jedi%                 !( cf ST-MAG 56 )
  ADD pointeur%,4             !Position suivante
  IF pointeur%=fin_courbe%    !D‚j… … la fin ?
    pointeur%=debut_courbe%   !->On boucle
  ENDIF                       !!!???!??!?!?!!!?!???!??!?
LOOP UNTIL INP?(2)            !Touche ?
bye                           !Fini
.
.[ MERGEZ ICI LA ROUTINE D'AFFICHAGE ]
.
PROCEDURE bye                 !Comme son nom l'indique
  ~MFREE(nouveau%)            !LibŠre les prisonniers
  RESERVE megatizer%          !Restitue la m‚moire
  ~XBIOS(5,L:origine%,L:origine%,0)
  VSYNC                       !Remet l'‚cran initial
  OUT 4,8                     !Damned ! La souris !!!
  EDIT                        !Aaaaaaaaarrrrrrrrrrgh !!!
RETURN                        !Glibatu makabo ritujakipu
