.*****************************************
.*   SCROLLING VERTICAL (MEMOIRE ECRAN)  *
.*         par DENEB (FANTASY)           *
.*****************************************
.
fre%=FRE(0)                  ! Sauve fre(0)
xb%=XBIOS(2)                 ! Sauve xbios(2)
HIDEM                        ! La souris a disparue !!..
OUT 4,8                      ! On l'a retrouv‚e morte !!
RESERVE fre%-64512*2         ! On r‚serve ... c'est bien
ecc%=MALLOC(64512)           ! On alloue ... c'est mieux
acc%=MALLOC(64512)           ! ...
ec%=(ecc% AND &HFFFFFF00)+256! Ecran1 /256
ac%=(acc% AND &HFFFFFF00)+256! Ecran2 /256
BLOAD "*.neo",ec%+31872      ! Charge image1 (*.neo)
BLOAD "*.neo",ec%-128        ! Charge image2 (*.neo)
~XBIOS(6,L:ec%-124)          ! Restaure palette
BMOVE ec%,ac%+640,64000      ! copie les ‚cran avec 4 ...
ON BREAK GOSUB fin           ! ... pixel de d‚calage (4*160)
.
va%=1280                     ! Initialisation de la vitesse ...
vva%=1280                    ! ... ici 8 pixel (160*8=1280)
SWAP ec%,ac%                 ! Initialisation des ‚crans
DO                           ! Boucle principale
  ADD a%,va%                 ! Position ‚cran
  IF a%>32000 OR a%<1280     ! Tests de d‚bordement
    va%=-va%                 ! Inversement de vitesse
    @swap                    ! Changement d'‚crans
    va%=-va%                 ! Inversement aussi
  ENDIF
  .                          ! Position de la m‚moire video
  VOID XBIOS(5,L:ec%+a%,L:ec%+a%,-1)
  @swap                      ! Echange ‚crans et donn‚es
  VSYNC                      ! La VBL
  EXIT IF PEEK(&HFFFFC02)=57 ! Press space ??
LOOP
@fin                         ! THE END
.
PROCEDURE swap
  SWAP ec%,ac%               ! Echange des ‚crans
  SWAP a%,aa%                ! Echange de la position ‚cran
  SWAP va%,vva%              ! Echange de la vitesse
RETURN
.
PROCEDURE fin
  ~MFREE(ecc%)               ! D‚sallocation
  ~MFREE(acc%)               ! L… aussi ...
  RESERVE fre%               ! Redonne la m‚moire
  EDIT                       ! RETOUR A L'EDITEUR (ET HOP!!)
RETURN
