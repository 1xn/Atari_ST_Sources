.************************************
.* SCROLLING-DIFFERENCIEL DE MOTIFS *
.*      par DENEB (FANTASY)         *
.************************************
.
fre%=FRE(0)                   ! Sauve FRE(0)
xb%=XBIOS(2)                  ! Sauve XBIOS(2)
OUT 4,18                      ! TCHAC !!! on coupe la souris
HIDEM                         ! PLOUF !!! on la cache ...
RESERVE fre%-(6*32256)        ! R‚serve un peu de m‚moire
ecc%=MALLOC(6*32256)          ! Alloue 6 ecrans
ec%=AND(ecc%,&HFFFFFF00)+256  ! Bien positionn‚s
ON BREAK GOSUB fin            ! au cas ou ...
CLS                           ! clrscr;
.
FOR i%=0 TO 15                ! D‚finition de la palette
  READ col%                   ! lit les couleurs
  SETCOLOR i%,col%            ! installe les couleurs
NEXT i%
DATA 0,$10,$20,$30,$40,$50,$60,$70
DATA $70,$70,$70,$0,$0,$0,$0,$f
.
nb%=17                        ! Nombre de plans de scroll
DIM b$(nb%),ecran%(5)         ! Tableau de motifs et ‚crans
FOR i%=0 TO nb%               ! Pr‚calcule des dessins ...
  FOR ii%=0 TO i%*100         ! ... des motifs
    PSET RANDOM(6*i%),RANDOM(i%+1),RANDOM(i%/(nb%/10))
  NEXT ii%
  GET 0,0,i%*6,i%+1,b$(i%)    ! Prise des motifs
NEXT i%
.
FOR i%=0 TO 5                 ! Pr‚calcule des emplacements ...
  CLS                         ! ... des motifs ...
  yy%=10                      ! ... sur 6 ‚crans
  FOR y%=0 TO nb%
    ADD yy%,y%
    FOR x%=0 TO 54
      PUT (x%*6*y%)-c%*y%,yy%,b$(y%),6
    NEXT x%
  NEXT y%
  ecran%(i%)=ec%+i%*32000     ! Sauve adresse ‚cran
  BMOVE xb%,ecran%(i%),32000  ! Copie ‚cran
  INC c%
NEXT i%
'
DO                            ! Boucle principale
  FOR i%=0 TO 5               ! 6 ‚crans !!
    VOID XBIOS(5,L:ecran%(i%),L:ecran%(i%),-1)
    VSYNC                     ! Prochaine VBL
  NEXT i%
LOOP UNTIL PEEK(&HFFFFC02)=57 ! On sort de l… !!!
@fin                          ! Triste fin ...
'
PROCEDURE fin
  VOID XBIOS(5,L:xb%,L:xb%,-1)! Restoration xbios(2)
  ~MFREE(ecc%)                ! D‚sallocation ...
  RESERVE fre%                ! Rend la m‚moire
  OUT 4,8                     ! la souris est l… !!
  EDIT                        ! Et hop … l'‚diteur...
RETURN
