.***********************************************
.* SCROLLING MULTIDIRECTIONNEL (MEMOIRE ECRAN) *
.*            par DENEB (FANTASY)              *
.***********************************************
.
fre%=FRE(0)                   ! Sauve fre(0)
xb%=XBIOS(2)                  ! Sauve adresse ‚cran
OUT 4,18                      ! au revoir petite souris ...
HIDEM                         ! ... elle est deja partie ...
RESERVE fre%-76800*4          ! R‚servation
ecc%=MALLOC(76800*4)          ! allocation
ec%=(ecc% AND &HFFFFFF00)+256 ! M‚moire vid‚o /256
DIM ecran%(3)                 ! Adresse des ‚crans
ON BREAK GOSUB fin            ! au cas ou ...
CLS                           ! On efface l'‚cran (si,si!!)
.
FOR i%=0 TO 15                ! D‚finition de la palette
  READ col%                   ! lecture des couleurs
  SETCOLOR i%,col%            ! installe les couleurs
NEXT i%
DATA $0,$777,$765,$753,$743,$733,$723,$713,$703
DATA $603,$504,$404,$305,$0,$0,$f
.
FOR i%=0 TO 14                ! Pr‚calcule du  motif
  READ z%                     ! Lit valeurs graphiques
  {xb%+i%*160}=z%             ! Installe en m‚moire video
  READ z%                     ! Lit valeurs
  {xb%+i%*160+4}=z%           ! Installe en m‚moire video
NEXT i%
DATA $5200600,$7E00000,$1A780C70,$F881000
DATA $1A8C2CF8,$F043000,$5ABE6CC4,$F027000
DATA $5ABA6CC6,$F007000,$575B6466,$8781F800
DATA $5D22663E,$87C1F800,$2A9C331C,$C3E3FC00
DATA $2D403181,$C1FFFE00,$169F18E1,$E0FFFF00
DATA $B600C7E,$707E7F80,$DC20E02,$70027FFC
DATA $27C0380,$3C003FFC,$8000F8,$1F001FF8
DATA $200020,$7C007E0,$0,$0
.
.
PRINT AT(13,10);"PLEASE WAIT !!!"
FOR i%=0 TO 3                 ! Installe les motifs...
  FOR y%=0 TO 29              ! ...dans les diff‚rents ‚crans
    FOR x%=0 TO 20
      RC_COPY xb%,0,0,16,15 TO ec%+i%*76800,x%*16+i%*4,y%*15
    NEXT x%
  NEXT y%
  ecran%(i%)=ec%+i%*76800     ! Sauve adresse ‚crans
NEXT i%
.
DO                            ! Boucle principale
  FOR i%=0 TO 3               ! Calcul de la courbe
    ADD d%,3                  ! Variable increment‚e
    y%=y%+5*SINQ(d%)          ! Calcul avec SINUS
    IF y%>15                  ! Test de d‚bordement
      y%=y%-15                ! ...Bas...
    ELSE IF y%<0              ! ...
      y%=15+y%                ! ...et haut
    ENDIF
    yy%=MUL(y%,2560)          ! adresse ‚cran /2560
    '                         ! insalle ‚crans suivant i%
    VOID XBIOS(5,L:ecran%(i%)+yy%,L:ecran%(i%)+yy%,-1)
    SETCOLOR 0,0
    VSYNC                     ! Prochaine VBL ...
    SETCOLOR 0,15             ! temps machine en bleu
    EXIT IF PEEK(&HFFFFFC02)=57 !d‚ja la fin ??
  NEXT i%
  EXIT IF PEEK(&HFFFFFC02)=57 ! ben ouais ...
LOOP
@fin
.
PROCEDURE fin
  VOID XBIOS(5,L:xb%,L:xb%,-1)! Restauration xbios(2)
  ~MFREE(ecc%)                ! D‚sallocation
  RESERVE fre%
  OUT 4,8                     ! aahh !!! la souris
  EDIT                        ! Et hop … l'‚diteur
RETURN
