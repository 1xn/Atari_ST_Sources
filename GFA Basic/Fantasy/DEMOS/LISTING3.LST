.
.Animation d'un fond compos‚ d'un scrolling de motifs en GfA
.par Jedi of Sector One from The Heavy Killers ( 14/02/1992 )
.
.N'oubliez pas de charger les INLINEs et de compiler ce source
.
INLINE pal%,32
INLINE courbe%,1440
centrage%=16                  ! D‚calage par rapport au haut...
VSYNC                         ! ...de l'‚cran
FOR a%=0 TO 15
  SETCOLOR a%,0               ! Couleurs en noir
NEXT a%
CLS
HIDEM
SPOKE &HFFFFFC02,&H12
fincourbe%=ADD(courbe%,1440)  ! Fin de la courbe
pntcourbe%=courbe%            ! Pointeur sur la courbe
OPEN "i",#1,"motif.sct"       ! Fichier contenant le motif
sizex%=ADD(SHL(INP(#1),8),INP(#1))
sizey%=ADD(SHL(INP(#1),8),INP(#1))
BGET #1,XBIOS(2),MUL(sizey%,160)
CLOSE #1
GET 0,0,PRED(sizex%),PRED(sizey%),motif$
megatizer%=FRE(0)             ! M‚moire originale
need%=MUL(MUL(320,sizey%),sizex%)
RESERVE SUB(megatizer%,ADD(need%,32255))
ecran1%=XBIOS(2)              ! Premier ‚cran
badaboum%=MALLOC(32255)       ! Pour le second ‚cran
ecran2%=ADD(AND(badaboum%,&HFFFFFF00),256)
buffer%=MALLOC(need%)         ! Pour le pr‚calc
yoka%=MUL(sizey%,160)         ! Hauteur du motif en octets
FOR ph%=0 TO PRED(sizex%)     ! Diff‚rentes phases du pr‚calc
  FOR x%=SUB(ph%,sizex%) TO 319 STEP sizex%
    PUT x%,0,motif$
  NEXT x%
  buf%=ADD(buffer%,MUL(ph%,SHL(yoka%,1)))
  BMOVE ecran1%,buf%,yoka%    ! Ecran -> Buffer
  BMOVE buf%,ADD(buf%,yoka%),yoka%
NEXT ph%
MUL centrage%,160             ! Centrage en octets
CLS
BMOVE ecran1%,ecran2%,32000
~XBIOS(6,L:pal%)              ! Installe la palette
REPEAT
  SWAP ecran1%,ecran2%        ! Sector One recherche des
  ~XBIOS(5,L:-1,L:ecran2%,-1) ! programmeurs en assembleur,
  VSYNC                       ! meme d‚butants...
  gag%=ADD(LONG{pntcourbe%},buffer%)
  ecran%=ADD(centrage%,ecran1%)
  BMOVE gag%,ecran%,yoka%     ! Voir article
  BMOVE gag%,ADD(ecran%,yoka%),yoka%
  BMOVE ecran%,ADD(ecran%,SHL(yoka%,1)),SHL(yoka%,1)
  BMOVE ecran%,ADD(ecran%,SHL(yoka%,2)),SHL(yoka%,1)
  ADD pntcourbe%,4            ! Position suivante
  IF pntcourbe%=fincourbe%    ! Courbe finie ?
    pntcourbe%=courbe%        ! Oui => on repart au d‚but
  ENDIF
UNTIL INKEY$=" "              ! Barre d'espace ???
VSYNC
~XBIOS(5,L:-1,L:XBIOS(3),0)   ! Ecran physique original
VSYNC
~MFREE(badaboum%)             ! LibŠre les diff‚rents buffers
~MFREE(buffer%)
RESERVE megatizer%            ! Ainsi que la m‚moire reserv‚e
SPOKE &HFFFFFC02,8            ! La souris bouge encore !!!
EDIT                          ! La fatale instruction
