.
.Scrolling vertical partiel de deux images au format Degas
.par Jedi of Sector One from The Heavy Killers
.N'oubliez pas que vous pouvez tÇlÇcharger ce listing...
.
HIDEM                           ! Cache la souris...
SPOKE &HFFFFFC02,&H12           ! ...et la bloque
VSYNC                           ! Pour Çviter un scintillement
CLS                             ! Efface l'Çcran !!!
.
.Hauteur de la bande affichÇe
.
hauteur%=150
.
.OrdonnÇe du bord supÇrieur de la bande
.
centrage%=25
.
.Position originale
y%=0
.
megatizer%=FRE(0)             ! MÇmoire originale
MUL hauteur%,160              ! Pour gagner du temps
RESERVE SUB(megatizer%,ADD(96096,hauteur%))
ecran1%=XBIOS(2)              ! Les deux Çcrans...
mega.1_of_sector_one%=MALLOC(32255)
ecran2%=ADD(AND(mega.1_of_sector_one%,&HFFFFFF00),256)
buffer%=MALLOC(ADD(63840,hauteur%))
BMOVE ecran1%,ecran2%,32000   ! Efface l'Çcran 2
OPEN "i",#1,"image1.pi1"      ! Premiäre image
SEEK #1,2                     ! Saute le header
palette$=INPUT$(32,#1)        ! Prend la palette
BGET #1,buffer%,32000         ! L'image
OPEN "i",#2,"image2.pi1"      ! L'autre fichier
SEEK #2,34                    ! Pas besoin de sa palette...
BGET #2,buffer%+32000,32000   ! ...mais de son image
CLOSE                         ! Ferme les fichier
BMOVE buffer%,buffer%+64000,hauteur%-160
~XBIOS(6,L:V:palette$)        ! Met en place la palette
MUL centrage%,160             ! Mieux vaut raisonner en octets
joystick%=ADD(XBIOS(34),61)   ! Adresse du joystick
speed=0                       ! Pour l'inertie
DO                            ! Do, what you do, what you do...
  SWAP ecran1%,ecran2%        ! Reste-t-il encore des...
  ~XBIOS(5,L:-1,L:ecran2%,-1) ! ...lecteurs qui ignorent ce...
  VSYNC                       ! ...qu'est une syncro ???
  joy%=AND(BYTE{joystick%},3) ! Teste le joystick haut ou bas
  IF joy%=2                   ! En bas ???
    ADD speed,0.25            ! Augmente la vitesse
    IF speed>32               ! Mais pas trop quand meme
      speed=32                ! par pas de 32 pixels max
    ENDIF                     ! Sinon on ne verrait rien
  ELSE IF joy%=1              ! En haut ???
    SUB speed,0.25            ! Augmente la vitesse
    IF speed<-32              ! Meme remarque...
      speed=-32               ! ...que prÇcÇdemment
    ENDIF                     ! Endif
  ELSE                        ! Joystick centrÇ
    IF speed>0                ! Ca avance ???
      SUB speed,0.125         ! On ralentit
    ELSE IF speed<0           ! Ca recule ???
      ADD speed,0.125         ! On ralentit
    ENDIF                     ! Re-endif
  ENDIF                       ! Re-re-endif
  ADD y%,ROUND(speed)         ! Position ( 0-400 )
  IF y%<0                     ! Au cas ou on serait...
    ADD y%,400                ! ...sorti des limites...
  ELSE IF y%>399              ! ...on va boucler...
    SUB y%,400                ! ...le scrolling
  ENDIF                       ! Re-re-re-endif
  source%=ADD(buffer%,MUL(y%,160))
  BMOVE source%,ADD(ecran1%,centrage%),hauteur%
LOOP UNTIL INKEY$=" "         ! Barre d'espace => Quitte
~MFREE(mega.1_of_sector_one%) ! Libäre le second Çcran
~MFREE(buffer%)               ! Libäre le gros buffer$
SPOKE &HFFFFFC02,8            ! La souris est vivante !!!
~XBIOS(5,L:-1,L:XBIOS(3),0)   ! MÇmoire-Çcran originale
VSYNC                         ! Syncro
RESERVE megatizer%            ! Restitue la mÇmoire
EDIT                          ! Et Ö ++++++++++++++++++++++++
