.******************************
.*      MOTIFS DE RASTER      *
.*    par  DENEB (FANTASY)    *
.******************************
.Ce programme doit ‚tre compil‚
.
fre%=FRE(0)                   ! Sauve fre(0)
xb%=XBIOS(2)                  ! Sauve adresse ‚cran
RESERVE fre%-10000            ! Reservation
re%=MALLOC(7168)              ! Buffer motif
cob%=MALLOC(720)              ! Buffer courbe
super%=GEMDOS(32,L:0)         ! Superviseur
ON BREAK GOSUB fin            ! Break ...
.
FOR i%=0 TO 15                ! D‚finition de la palette
  READ col%                   ! lecture des couleurs
  SETCOLOR i%,col%            ! installe les couleurs
NEXT i%
DATA $0,$310,$420,$531,$542,$664,$775,$677,$556
DATA $436,$325,$315,$3204,$0,$0,$0
.
FOR i%=0 TO 31                ! Pr‚calcule du  motif
  READ z%                     ! Lit valeurs graphiques
  {xb%+i%*160}=z%             ! Installe en m‚moire video
  READ z%                     ! Lit valeurs
  {xb%+i%*160+4}=z%           ! Installe en m‚moire video
NEXT i%
DATA &h28001C00,&h0,&h6006200,&h1C000000
DATA &h82006D00,&h1E000000,&h8900E500,&h1E000000
DATA &h30003100,&hCE000000,&hD1009E00,&h60000000
DATA &h20000E00,&h70000000,&h2C002800,&h10000000
DATA &hD2004C00,&h3F000000,&h6500E200,&h1F000000
DATA &h9F007E00,&h0,&h4A003800,&h6000000
DATA &h14007200,&hE000000,&h14007200,&hE000000
DATA &h14007200,&hE000000,&h14007200,&hE000000
DATA &h14007200,&hE000000,&h14007200,&hE000000
DATA &h14007200,&hE000000,&h14007200,&hE000000
DATA &h14007200,&hE000000,&hD2004C00,&h3F000000
DATA &h6500E200,&h1F000000,&h9F007E00,&h0
DATA &h14001C00,&h20003C00,&h78006200,&h7E00
DATA &h7D006D00,&h8C00F300,&h7600E500,&h400FB00
DATA &hCF003100,&hFF00,&h2E009E00,&h100FF00
DATA &h5E000E00,&h7E00,&h10002800,&h4003C00
.
FOR y%=0 TO 31                ! Codage du motif ...
  FOR x%=0 TO 7               ! ... par les couleurs.
    u%=PTST(x%,y%)
    CARD{re%+co%}=CARD{&HFFFF8240+u%*2}
    CARD{re%+co%+16}=CARD{&HFFFF8240+u%*2}
    ADD co%,2
  NEXT x%
  ADD co%,16
NEXT y%
FOR i%=0 TO 7                 ! Multiplication ...
  BMOVE re%,re%+i%*1024,1024  ! ... du motif
NEXT i%
.
CLS                           ! Devinez...
FOR i%=0 TO 319               ! Creation du fond:
  ALINE i%,2,i%,199,u%,-1,0   ! Lignes verticales
  IF u%=8                     ! ...
    u%=1                      ! Passionnant, non?
  ELSE                        ! ...
    INC u%
  ENDIF
NEXT i%
.
FOR i%=0 TO 720               ! Creation de la courbe
  cc%=(50+50*SINQ(i%)) MOD 8  ! courbe /8
  BYTE{cob%+i%}=cc%*2         ! remplissage du buffer
NEXT i%
.
POKE &HFFFA09,0               ! Coupe les interruptions
pob%=cob%                     ! Adresse courbe
REPEAT                        ! On y va ...
  IF pob%<cob%+360            ! Teste de cyclage ...
    ADD pob%,1                ! ... de la courbe
  ELSE
    pob%=cob%
  ENDIF
  cou%=pob%                   ! Nouvelle adresse courbe
  max%=cou%+200               ! Nombre de lignes
  w%=BYTE{cou%}               ! Valeur de la courbe
  nt%=re%                     ! Adresse motif
  REPEAT                      ! routine de STAB.
    b|=BYTE{&HFFFF8209}
  UNTIL b|
  VOID SHL(13,-b|)
  VOID 0                      ! On attend la fin ...
  CARD{&HFFFF8240}=0          ! ... de la ligne pour ...
  CARD{&HFFFF8240}=0          ! ... commencer a ...
  CARD{&HFFFF8240}=0          ! ... raster
  CARD{&HFFFF8240}=0          ! ...
  CARD{&HFFFF8240}=0
  .
  DO                          ! Boucle d'une ligne
    LONG{&HFFFF8242}=LONG{w%}
    LONG{&HFFFF8246}=LONG{ADD(w%,4)}
    LONG{&HFFFF824A}=LONG{ADD(w%,8)}
    LONG{&HFFFF824E}=LONG{ADD(w%,12)}
    ADD nt%,32                ! Pointeur du motif
    w%=ADD(BYTE{cou%},nt%)    ! Adresse effective
    INC cou%                  ! INC. courbe
    CARD{&HFFFF825E}=0        ! On attend la ...
    CARD{&HFFFF825E}=0        ! ... la prochaine ...
    CARD{&HFFFF825E}=0        ! ... ligne
    CARD{&HFFFF825E}=0
    CARD{&HFFFF825E}=0
    CARD{&HFFFF825E}=0
    CARD{&HFFFF825E}=0
    VOID 0
    VOID 0
  LOOP UNTIL cou%=max%        ! Test de fin
  VSYNC                       ! Next VBL, please...
UNTIL PEEK(&HFFFFC02)=57      ! Escape
fin                           ! The end
.
PROCEDURE fin
  POKE &HFFFA09,100           ! les interrupts
  SETCOLOR 15,&H777           ! une couleur
  SETCOLOR 0,0                ! une autre
  ~MFREE(re%)                 ! Liberation ...
  ~MFREE(cob%)                ! ... et tout
  RESERVE fre%                ! ...
  ~GEMDOS(32,L:super%)        ! Utilisateur
  EDIT                        ! ben he !!
RETURN
