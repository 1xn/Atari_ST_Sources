.******************************
.*      SCROLL DE RASTER      *
.*    par  DENEB (FANTASY)    *
.******************************
.Ce programme doit ˆtre compil‚
.
CLS
fre%=FRE(0)                  ! Sauve fre(0)
xb%=XBIOS(2)                 ! Sauve adresse ‚cran
RESERVE fre%-10000           ! Reservation
t$="ABCDEFGHIJKLMNOPQRSTUVWXYZ123456789(),."
tx$="  VOICI ENCORE UN TEXTE SUPER INTERESSANT ..."
tx$=tx$+" COMME D'HABITUDE QUOI !!! "
l%=LEN(tx$)                  ! Longueur texte
re%=MALLOC(l%*8*8)           ! Buffer motif
super%=GEMDOS(32,L:0)        ! Superviseur
ON BREAK GOSUB fin           ! Break ...
.
FOR i%=0 TO l%               ! Codage du texte
  PRINT AT(1,1);MID$(tx$,i%,1)
  FOR y%=0 TO 7
    FOR x%=0 TO 7
      IF PTST(x%,y%)         ! Reperage
        CARD{re%+p%}=p%      ! Buffer texte
      ELSE
        CARD{re%+p%}=0
      ENDIF
      ADD p%,2
    NEXT x%
  NEXT y%
NEXT i%
lt%=re%+p%-2                 ! Longueur buffer
.
CLS                          ! he he ...
FOR i%=0 TO 176 STEP 22      ! Dessin de ligne
  ALINE i%,1,i%+23,1,ii%,-1,0
  INC ii%
NEXT i%
FOR i%=0 TO 199              ! Courbe
  RC_COPY xb%,1,1,320,1 TO xb%,40+80*SINQ(i%),200-i%
NEXT i%
.
po%=re%                      ! Adresse du buffer
POKE &HFFFA09,0              ! Coupe les interruptions
DO                           ! On y va !!
  IF po%>lt%                 ! Teste de fin du texte
    po%=re%
  ELSE
    ADD po%,4
  ENDIF
  o%=po%                     ! Position du texte
  max%=o%+16*15              ! Maximum ecran
  .
  REPEAT                     ! Routine de STAB.
    b|=BYTE{&HFFFF8209}
  UNTIL b|
  VOID SHL(13,-b|)
  REPEAT                     ! Ligne ecran
    l%=o% AND &HFFFFFFF0     ! Possition /16
    LONG{&HFFFF8242}=LONG{l%}
    LONG{&HFFFF8246}=LONG{l%+4}
    LONG{&HFFFF824A}=LONG{l%+8}
    LONG{&HFFFF824E}=LONG{l%+12}
    .
    ADD o%,1                 ! Add ligne
    CARD{&HFFFF825E}=0       ! On attent la ...
    CARD{&HFFFF825E}=0       ! ... ligne suivante
    CARD{&HFFFF825E}=0
    CARD{&HFFFF825E}=0
    CARD{&HFFFF825E}=0
    CARD{&HFFFF825E}=0
    CARD{&HFFFF825E}=0
    CARD{&HFFFF825E}=0
    CARD{&HFFFF825E}=0
    CARD{&HFFFF825E}=0
  UNTIL o%>max%              ! Sortie ?
  VSYNC                      ! Vous savez ...
  CARD{&HFFFF8240}=0         ! Ca aussi ...
LOOP UNTIL PEEK(&HFFFFC02)=57! Et ca encore ...
fin
.
PROCEDURE fin
  POKE &HFFFA09,100           ! les interrupts
  SETCOLOR 15,&H777           ! une couleur
  SETCOLOR 0,0                ! une autre
  ~MFREE(re%)                 ! Liberation ...
  RESERVE fre%                ! ...
  ~GEMDOS(32,L:super%)        ! Utilisateur
  EDIT                        ! ben he !!
RETURN

