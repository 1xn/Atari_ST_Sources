' CANOPUS SOFTWARE presents:   NUMEROLOGICAL COMPUTATIONS [0.33]
' Ω1986 by Rufus C.Camphausen
'          Kramatweg 15  1095 JP Amsterdam/NL
' Disc/Letter Communication/Comments are invited
If Xbios(4)<>2 Then
  Alert 3,"This program is designed for HighRes only!",1,"SORRY!",Bull
  Stop
Endif
Gosub Born_wild
Menu Mu$()
Openw 0
Gosub Title
Gosub Screen
On Menu  Gosub Menuloop
Do
  On Menu
Loop
' MAIN PROGRAM PROCEDURES
Procedure Do_it_now
  If Teach Then
    Teach=0
    Gosub Wipe
  Endif
  If Start Then
    Gosub Table
  Endif
  Gosub Tell_me
  Deftext 1,1,0,13
  Print At(R,D);Spc(52)
  Print At(40-Len(Name$)/2,D);Name$
  Deftext 1,2,0,13
  Print At(18,D-1);"    The following combination of letters    "
  Print At(18,D+1);"        has the numerological value"
  Deftext 1,0,0,13
  Gosub Decode_name
  Deftext 1,1,0,13
  Print At(40-Len(Valnum$)/2,D+2);Valnum$
Return
Procedure Tell_me
  Defmouse 4
  R=14
  D=14
  Deftext 1,0,0,13
  Print At(21,D-1);"Type NAME(s) or WORD(s); then RETURN "
  For X=D To D+2
    Print At(R,X);Spc(52)
  Next X
  Print At(R,D);
  Line Input "";N$
  Let Name$=Upper$(N$)
  Defmouse 3
Return
Procedure Decode_name
  Code=0
  Numsum=0
  Valnum$=""
  For C=1 To Len(Name$)
    Test$=Mid$(Name$,C,1)
    Code=Asc(Test$)
    If Code>82 And Code<90 Then
      Sub Code,9
    Endif
    If Code>73 And Code<90 Then
      Sub Code,9
    Endif
    If Code<65 Or Code>90 Then
      Code=64
    Endif
    Numsum=Numsum+(Code-64)
  Next C
  Number$=Str$(Numsum)
  Valnum$=Valnum$+Number$
  Gosub Decode_number
Return
Procedure Decode_number
  Denum=0
  While Len(Number$)>1
    For C=1 To Len(Number$)
      Denum=Denum+Val(Mid$(Number$,C,1))
    Next C
    Valnum$=Str$(Denum)+"<"+Valnum$
    Number$=Str$(Denum)
    Gosub Decode_number
  Wend
Return
' GENERAL ASSISTANCE PROCEDURES
Procedure Title
  Deffill 1,2,4
  Pbox 0,0,639,399
  Deffill 1,1
  Pbox 92,4,548,48
  Deffill 1,0
  Pbox 96,8,544,52
  Deftext 1,0,0,4
  Text 200,16,"CANOPUS SOFTWARE æΩΩ    the ESOTERIC ST"
  Deftext 1,2,0,32
  Text 104,46,"NUMERO-LOGICAL COMPUTATIONS"
  Deftext 1,0,0,13
Return
Procedure Screen
  Box 93,71,547,273
  Box 94,72,546,272
  Box 95,73,545,271
  Deffill 1,0
  Pbox 96,74,544,270
  If Start Then
    Gosub Teach
  Else
    Gosub Table
  Endif
Return
Procedure Table
  Start=0
  Rs=48
  Ds=7
  Deftext 1,0,0,4
  Text 108,84,"The table shows internationally used values for Latin-based languages"
  Deftext 1,1,0,13
  Print At(Rs,Ds);"1 2 3 4 5 6 7 8 9"
  Deftext 1,2,0,13
  Print At(Rs,Ds+1);"A B C D E F G H I"
  Print At(Rs,Ds+2);"J K L M N O P Q R"
  Print At(Rs,Ds+3);"S T U V W X Y Z"
  Box 366,90,522,172
  Box 368,92,520,170
  Deftext 1,0,0,13
Return
Procedure Wipe
  Start=1
  Deffill 1,0
  Pbox 96,74,544,270
Return
Procedure Teach
  Teach=1
  Deftext 1,0,0,13
  Print At(14,6);"This is a new type of numerology combining Qabbalistic"
  Print At(14,7);"Wisdom with Western letters and numbers ( and with ST-"
  Print At(14,8);"speed ) and I've named it - for more than one reason -"
  Print At(14,10);"Once you select INPUT from the FUNCTIONS you can find"
  Print At(14,11);"that it generates interesting results such as these:"
  Deftext 1,1,0,13
  Print At(14,9);"                   MICRO NUMEROLOGY"
  Print At(14,12);"value 1>10     for terms as  ADAM, EVA, SATAN and DNA"
  Print At(14,13);"value 2>11>38  for EMOTIONS, DELIGHT, HEALING, ENERGY"
  Print At(14,14);"      6>42     for KNOWLEDGE,  ASTROLOGY, OUTER SPACE"
  Print At(14,15);"      2>11>56  INTERACTION, PENETRATION,  DEFLORATION"
  Deftext 1,0,0,13
  Print At(14,16);"      [And don't forget to decode your name, too!]æΩΩ"
Return
Procedure Born_wild
  Deflist 0
  Defmouse 3
  Restore
  Start=1
  Dim Mu$(36)
  For I=O To 36
    Read Mu$(I)
    Exit If Mu$(I)="Black Hole"
  Next I
  Mu$(I)=""
  Mu$(I+1)=""
  Dim Can$(4)
  Can$(0)=" Canopusﬂ Esoteric Research:|"
  Can$(1)=" NUMERO-LOGICAL COMPUTATIONS|"
  Can$(2)=" a public domain offering by|"
  Can$(3)=" Rufus æΩΩ Camphausen; Ω1986"
  Inform$=Can$(0)+Can$(1)+Can$(2)+Can$(3)
Return
' PROCEDURES FOR GEM-DROP-DOWN MENU'S
Procedure Menuloop
  If Mu$(Menu(0))="  Information"
    Gosub Inform
  Endif
  If Mu$(Menu(0))=" EDITOR <<<<<"
    Edit
  Endif
  If Mu$(Menu(0))=" Single Input"
    Gosub Do_it_now
  Endif
  If Mu$(Menu(0))=" Tutorial"
    Gosub Wipe
    Gosub Teach
  Endif
  If Mu$(Menu(0))=" QUIT !"
    System
  Endif
Return
Procedure Inform
  Alert 0,Inform$,1,"RETURN",Bull
Return
' DATA
Data DESK,  Information,-----------------, , , , , , ,""
Data Functions, EDITOR <<<<<, Single Input, Tutorial, QUIT !,""
Data Black Hole
