Rem
Rem *********************************
Rem ** driver BJ10 et BJ-200 CANON **
Rem ** pour  fichiers  TGA 210x128 **
Rem **  (C) RAFALE 95 pour ST-MAG  **
Rem *********************************
Rem
Dim Table%(8,8)  !table de matri‡age 8x8 des pixels
Dim Trame%(6)    !tampon de stockage pour 6 pixels verticaux
Rem              !(matrice verticale de 48 x 8 sur l'imprimante)
Dim Tga%(25000)  !tampon d'image TGA 210 x 128 de 100K
Rem
Rem ******* matrices de 8 x 8 *******
Rem *********************************
Rem
Data 255,255,255,255,255,255,255,255
Data &hee,&h77,&hdd,&h77,&hee,&hbb,&hee,&h77
Data &hea,&h57,&hae,&h75,&hea,&h57,&hae,&h75
Data &haa,&h55,&haa,&h55,&haa,&h55,&haa,&h55
Data &h49,&h92,&h25,&h92,&h49,&h92,&h25,&h92
Data &h88,&h22,&h88,&h22,&h88,&h22,&h88,&h22
Data &h80,&h01,&h10,&h04,&h40,&h02,&h20,&h08
Data 0,0,0,0,0,0,0,0
Rem
Rem **********************************
Rem **     programme principal      **
Rem **********************************
Rem
Gosub Lecture_fichier
Gosub Charge_table
Pligne%=0
For T%=1 To 21 !21x6=126 lignes
  Rem
  Gosub Init_print
  Rem
  For Ppixel%=0 To 209  !210 pixels par ligne
    Rem
    Gosub Lecture_buffer
    Gosub Tramage
    Rem
  Next Ppixel%
  Rem
  Gosub Avance_papier
  Pligne%=Pligne%+210*6*3
Next T%
Rem
Rem **********************************
Rem **    proc‚dures de gestion     **
Rem **********************************
Rem
Procedure Init_print
  Print "on imprime les lignes ";T%*6-5;" … ";T%*6
  Out 0,&H1C
  Out 0,&H43
  Out 0,&H42  !mode graphique180/360 dpi
  Rem
  Out 0,4  !0:180 pts par pouce, 4:360 pts par pouce (vertical)
  Out 0,4  !0:180 pts par pouce, 4:360 pts par pouce (horizontal)
  Rem
  Out 0,144 !(A)=144  nombre de points par ligne:  A + (256 x B)
  Out 0,6   !(B)=6    ici, on pr‚voit [210 x 8] points par ligne
  Rem
Return
Rem
Rem ****************************
Rem *initialisation de la table*
Rem *  contenant les matrices  *
Rem ****************************
Rem
Procedure Charge_table
  For I=1 To 8   ! I=code image,
    For J=1 To 8 ! J=matrice 8x8
      Read A
      Table%(I,J)=A
    Next J
  Next I
Return
Rem
Rem ****************************
Rem charger 1 image TGA, 210x128
Rem ****************************
Rem
Procedure Lecture_fichier
  Fileselect "a:\*.TGA","",I$
  If I$<>"" Then
    Bload I$,Varptr(Tga%(1))
  Endif
  Print "chargement correct"
  Print
Return
Rem
Rem ****************************
Rem d‚placement du papier de 48p
Rem ****************************
Rem
Procedure Avance_papier
  Out 0,&H1B
  Out 0,&H4A !interligne graphique
  Out 0,24   !=48/360 pts par pouce
  Rem        !(ou 24/180 en 180dpi)
  Out 0,13   !retour chariot
Return
Rem
Rem ****************************
Rem saisir 1 colonne de 6 pixels
Rem ****************************
Rem
Procedure Lecture_buffer
  I%=1+Ppixel%*3+Pligne%+18
  For J%=1 To 6           !6 lignes d'un coup
    L%=Peek(Varptr(Tga%(1))+I%)   !lecture composante bleue
    M%=Peek(Varptr(Tga%(1))+I%+1) !lecture composante verte
    N%=Peek(Varptr(Tga%(1))+I%+2) !lecture composante rouge
    Trame%(J%)=(L%+M%+N%+96) Div 96  !niveau de gris+lum.
    If Trame%(J%)>7 Then
      Trame%(J%)=7                !correction de saturation
    Endif
    I%=I%+210*3              !offset ligne
  Next J%
Return
Rem
Rem ****************************
Rem tramage 8x8 pts sur 6 pixels
Rem +impression d'1 colonne de 6
Rem pixels (trame 8x8 / 360 dpi)
Rem ****************************
Rem
Procedure Tramage
  For X%=1 To 8           !codage sur 8 octets par pixel
    For Y%=1 To 6         !calcul de tramage sur les 6 pixels
      P%=Trame%(Y%)
      Out 0,Table%(P%,X%) !sortie des octets sur imprimante
    Next Y%
  Next X%
Return
End
