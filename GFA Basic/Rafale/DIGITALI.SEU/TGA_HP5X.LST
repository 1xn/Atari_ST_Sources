Rem
Rem *********************************
Rem ** driver HP500/HP510  Hewlett **
Rem ** pour  fichiers  TGA 210x128 **
Rem **  (C) RAFALE 95 pour ST-MAG  **
Rem *********************************
Rem
Dim Table%(8,8)  !table de matri‡age 8x8 des pixels
Dim Tga%(25000)  !tampon d'image TGA 210 x 128 de 100K
Rem
Rem ******* matrices de 8 x 8 *******
Rem *********************************
Rem
Data 255,255,255,255,255,255,255,255
Data &hee,&h77,&hdd,&h77,&hee,&hbb,&hee,&h77
Data &hea,&h57,&hae,&h75,&hea,&h57,&hae,&h75
Data &haa,&h55,&haa,&h55,&haa,&h55,&haa,&h55
Data &h49,&h92,&h25,&h92,&h49,&h92,&h25,&h92
Data &h88,&h22,&h88,&h22,&h88,&h22,&h88,&h22
Data &h80,&h01,&h10,&h04,&h40,&h02,&h20,&h08
Data 0,0,0,0,0,0,0,0
Rem
Rem **********************************
Rem **     programme principal      **
Rem **********************************
Rem
Gosub Lecture_fichier
Gosub Charge_table
Pligne%=0 !pointeur dans le fichier TGA
Gosub Init_print
For T%=1 To 128 !128 lignes
  For S%=1 To 8
    Rem
    Gosub Init_ligne
    Rem
    For Ppixel%=0 To 209  !210 pixels par ligne
      Rem
      Gosub Imprime
      Rem
    Next Ppixel%
  Next S%
  Pligne%=Pligne%+210*3
  Print "ligne ";T%;" transmise"
Next T%
Gosub Ejection
Rem
Rem **********************************
Rem **    proc‚dures de gestion     **
Rem **********************************
Rem
Procedure Init_print
  Rem
  Out 0,&H1B  !impression de gauche … droite
  Out 0,&H26
  Out 0,&H6B
  Out 0,&H30
  Out 0,&H57
  Rem
  Out 0,&HA   !saut de ligne au d‚marrage
  Out 0,&HD   !....avec retour du chariot
  Out 0,&HA   !idem
  Out 0,&HD   !idem
  Rem
  Out 0,&H1B  !mode graphique 300 points par pouce
  Out 0,&H2A
  Out 0,&H74
  Out 0,&H33  ! "3" \
  Out 0,&H30  ! "0"  > soit 300 ppp
  Out 0,&H30  ! "0" /
  Out 0,&H52
  Rem
Return
Rem
Procedure Init_ligne
  Rem
  Out 0,&H1B  !nombre d'octets … envoyer
  Out 0,&H2A
  Out 0,&H62
  Out 0,&H32  ! "2" \
  Out 0,&H31  ! "1"  > soit 210 octets
  Out 0,&H30  ! "0" /
  Out 0,&H57
  Rem
Return
Rem
Procedure Ejection
  Rem
  Print "impression du fichier TGA 210x128 termin‚e !"
  Out 0,&HC   !‚jection du papier
  Rem
Return
Rem
Rem ****************************
Rem *initialisation de la table*
Rem *  contenant les matrices  *
Rem ****************************
Rem
Procedure Charge_table
  For I=1 To 8   ! I=code image,
    For J=1 To 8 ! J=matrice 8x8
      Read A
      Table%(I,J)=A
    Next J
  Next I
Return
Rem
Rem ****************************
Rem charger 1 image TGA, 210x128
Rem ****************************
Rem
Procedure Lecture_fichier
  Fileselect "a:\*.TGA","",I$
  If I$<>"" Then
    Bload I$,Varptr(Tga%(1))
  Endif
  Print "chargement correct"
  Print
Return
Rem
Rem ****************************
Rem  saisir un pixel du fichier
Rem    conversion RVB => mono
Rem  puis impression de l'octet
Rem ****************************
Rem
Procedure Imprime
  I%=1+Ppixel%*3+Pligne%+18
  L%=Peek(Varptr(Tga%(1))+I%)   !lecture composante bleue
  M%=Peek(Varptr(Tga%(1))+I%+1) !lecture composante verte
  N%=Peek(Varptr(Tga%(1))+I%+2) !lecture composante rouge
  Point%=(L%+M%+N%+96) Div 96   !niveau de gris+lum.
  If Point%>8 Then
    Point%=8                    !correction de saturation
  Endif
  Out 0,Table%(Point%,S%)     !sortie sur imprimante
Return
End
