'
'  Dateivergleicher  V 1.0
'
' von Wolfram R”sler  29/11/87 - 29/12/87
'
'   GfA-Basic 2.0
'
'  KOMMENTIERTES SOURCE-LISTING
'
DEFLIST 0
DEFMOUSE 2
@anfg
DIM ausgabe!(6)   !  ausgabe!  enth„lt die Informationen, welche Menupunkte
FOR j%=1 TO 6     !  im AUSGABE-Menu angew„hlt sind
  READ a!
  ausgabe!(j%)=a!
NEXT j%
DATA -1,0,0,0,-1,-1
DEFMOUSE 0
@menueleiste     ! Menuleiste initialisieren
@haken           ! die H„kchen im AUSGABE-Menu
@anz             ! die ausgew„hlten Dateien neben der Menuleiste anzeigen
ON MENU  GOSUB menu_auswahl
ON BREAK CONT
ON ERROR GOSUB er
haupt:
DO
  ON MENU
LOOP
PROCEDURE menu_auswahl  ! Auswahl aus der Menuleiste:
  LOCAL sel%,dummy%,dat$
  sel%=MENU(0)          ! sel% ist die Nummer des gew„hlten Menupunktes
  IF sel%=1             ! Info
    ALERT 0,"Dateivergleicher  Version 1.0 |               |vergleicht 2 Dateien und gibt|alle Unterschiede aus.",1,"Ok.",dummy%
    ALERT 0,"Autor: Wolfram R”sler|Augustastr. 44-46|5100 Aachen|Tel. 0241/504290",1,"Ok.",dummy%
  ENDIF
  IF sel%>=15 AND sel%<=20    ! Falls eine Auswahl im AUSGABE-Menu erfolgt ist,
    '                         ! nur die entsprechende Flag gesetzt.
    ausgabe!(sel%-14)=NOT ausgabe!(sel%-14)
    IF sel%=18
      ausgabe!(5)=FALSE
    ENDIF
    IF sel%=19
      ausgabe!(4)=FALSE
    ENDIF
    @haken
  ENDIF
  IF sel%=11       ! Auswahl der 1. Datei
    FILESELECT "a:\*.*","",dat$
    IF dat$<>""
      datei1$=dat$
    ENDIF
    @anz
  ENDIF
  IF sel%=12       ! Auswahl der 2. Datei
    FILESELECT "a:\*.*","",dat$
    IF dat$<>""
      datei2$=dat$
    ENDIF
    @anz
  ENDIF
  IF sel%=23       ! Aufruf des eigentlichen Hauptprogramms
    @vergl
  ENDIF
  IF sel%=24       ! Prg. beenden
    ALERT 3,"Programm beenden ?",1,"Ja|Nein",dummy%
    IF dummy%=1
      @ende
    ENDIF
  ENDIF
  MENU OFF
RETURN
PROCEDURE ende
  MENU OFF
  MENU KILL
  OPENW 0
  END
RETURN
PROCEDURE anz   ! Anzeige der Dateien neben der Menuleiste
  CLOSEW 0      ! Hiermit wird die Menuzeile beschreibbar gemacht
  DEFTEXT 1,0,0,4
  TEXT 350,6,SPACE$(80)
  TEXT 350,15,SPACE$(80)
  TEXT 350,6,"Datei 1: "+datei1$
  TEXT 350,15,"Datei 2: "+datei2$
  IF datei1$="" OR datei2$=""  ! Wenn eine er Dateien nicht ausgew„hlt ist,
    MENU 23,2                  ! gibt es nichts zu vergleichen, dh. der Menu-
  ELSE                         ! punkt dazu wird hell dargestellt.
    MENU 23,3
  ENDIF
  OPENW 0
RETURN
PROCEDURE haken
  LOCAL j%
  FOR j%=15 TO 20
    IF ausgabe!(j%-14)=TRUE
      MENU j%,1       ! H„kchen machen
    ELSE
      MENU j%,0       ! H„kchen l”schen
    ENDIF
  NEXT j%
RETURN
PROCEDURE vergl   ! das eigentliche Hauptprogramm
  LOCAL j%,len1%,len2%,lmin%,i1%,i2%,diff%
  DEFTEXT 1,0,0,13
  DEFMOUSE 2
  IF ausgabe!(4)=TRUE OR ausgabe!(5)=TRUE OR anfg!=TRUE
    CLS   ! Bildschirm l”schen, wenn das Anfangsbild noch drauf ist, oder
    '     ! wenn die ganze Datei oder die Untershiede ausgegeben werden sollen
    anfg!=FALSE  ! dh. das Anfangsbild ist nun nicht mehr auf dem Bildschirm
  ENDIF
  OPEN "I",#1,datei1$
  OPEN "I",#2,datei2$
  len1%=LOF(#1)  ! L„nge der Dateien
  len2%=LOF(#2)
  lmin%=len1%    ! lmin% ist die kleinste der beiden L„ngen und bestimmt,
  IF len2%<len1% ! wieviele Bytes verglichen werden
    lmin%=len2%
  ENDIF
  FOR j%=1 TO lmin%
    i1%=INP(#1)
    i2%=INP(#2)
    IF ausgabe!(4)=TRUE     ! Ausgabe der 1. Datei ?
      IF ausgabe!(1)=TRUE       ! auf Bildschirm ?
        PRINT CHR$(i1%);
      ENDIF
      IF ausgabe!(2)=TRUE       ! auf Drucker ?
        LPRINT CHR$(i1%);
      ENDIF
    ENDIF
    IF i1%<>i2%   ! wenn Unterschied gefunden
      INC diff%   ! diff% : Anzahl der Unterschiede
      IF ausgabe!(5)=TRUE       ! Ausgabe der Unterschiede ?
        IF ausgabe!(1)=TRUE            ! auf Bildschirm ?
          PRINT "Byte #";j%;" : Datei 1 : ";CHR$(i1%);" (";i1%;")","Datei 2 : ";CHR$(i2%);" (";i2%;")"
        ENDIF
        IF ausgabe!(2)=TRUE            ! auf Drucker ?
          LPRINT "Byte #";j%;" : Datei 1 : ";CHR$(i1%);" (";i1%;")","Datei 2 : ";CHR$(i2%);" (";i2%;")"
        ENDIF
      ENDIF
    ENDIF
    IF MOUSEK=1  ! linke Maustaste: anhalten
      REPEAT
        DEFMOUSE 2
      UNTIL MOUSEK=0
    ENDIF
    EXIT IF MOUSEK=2  ! rechte Maustaste: abbrechen
  NEXT j%
  IF MOUSEK=2  ! abgebrochen ?
    PRINT "* Abbruch   bei byte #";j%;" *"
  ENDIF
  DEFMOUSE 0
  CLOSE #2
  CLOSE #1
  IF ausgabe!(6)     ! Rapport ausgeben ?
    IF ausgabe!(1)        ! auf Bildschirm ?
      PRINT
      PRINT "   Datei 1 : ";datei1$,"L„nge : ";len1%;" bytes"
      PRINT "   Datei 2 : ";datei2$,"L„nge : ";len2%;" bytes"
      PRINT "   L„ngendifferenz : ";ABS(len1%-len2%);" bytes"
      PRINT
      PRINT "   ";diff%;" Unterschiede","= ";INT((diff%/j%)*10000)/100;" %"
      PRINT "   ";j%-diff%;" šbereinstimmungen","= ";INT(((j%-diff%)/j%)*10000)/100;" %"
      PRINT
    ENDIF
    IF ausgabe!(2)       ! auf Drucker ?
      LPRINT
      LPRINT "   Datei 1 : ";datei1$,"L„nge : ";len1%;" bytes"
      LPRINT "   Datei 2 : ";datei2$,"L„nge : ";len2%;" bytes"
      LPRINT "   L„ngendifferenz : ";ABS(len1%-len2%);" bytes"
      LPRINT
      LPRINT "   ";diff%;" Unterschiede","= ";INT((diff%/j%)*10000)/100;" %"
      LPRINT "   ";j%-diff%;" šbereinstimmungen","= ";INT(((j%-diff%)/j%)*10000)/100;" %"
      LPRINT
    ENDIF
  ENDIF
RETURN
PROCEDURE er   ! Fehlerbehandlung
  LOCAL in%
  ON ERROR GOSUB er
  ALERT 3,"Fehler #"+STR$(ERR)+" ist aufgetreten ! ",3,"Nochmal|Weiter|Abbruch",in%
  IF in%=1
    RESUME
  ENDIF
  IF in%=2
    RESUME NEXT
  ENDIF
  IF in%=3
    RESUME haupt
  ENDIF
RETURN
PROCEDURE anfg   ! Titelbild
  LOCAL j%
  FOR j%=1 TO 150 STEP 10
    ELLIPSE 320,200,3*j%,200-2*j%
  NEXT j%
  DEFTEXT 1,16,0,32
  TEXT 0,380,640," Ein pd-Programm von Wolfram R”sler "
  anfg!=TRUE
RETURN
PROCEDURE menueleiste
  LOCAL leiste$,i%
  CLOSEW 0
  DIM feld$(50)
  FOR i%=0 TO 50
    READ leiste$
    EXIT IF leiste$="<ende>"   !!!!!  <ende> muž letzter Eintrag sein  !!!!!
    IF leiste$<>"" AND leiste$<>"-"
      leiste$=" "+leiste$+" "
    ENDIF
    feld$(i%)=leiste$
  NEXT i%
  MENU feld$()
  OPENW 0
  MENU 2,2  ! Strich unter Info
  MENU 17,2  ! Strich im Ausgabe-Menu
  MENU 23,2  ! anfangen (wird erst aktiviert, wenn 2 Dateien angew„hlt sind)
RETURN
DATA ,Dateivergleicher Version 1.0 vom 29.12.87     in GfA-Basic    pd-Software,-------------------------------------------------------------------------,-,-,-,-,-,-,""
DATA Datei,Datei 1,Datei 2,""
DATA Ausgabe, Bildschirm, Drucker,--------------, Datei 1, Unterschiede, Rapport,""
DATA Programm,anfangen,beenden,"",""
DATA <ende>
PROCEDURE psave
  LOCAL file$
  ' Psave-Modul
  ' Aufrufen im Direktmodus mit @psave
  ' von WR 5/9/87
  FILESELECT "\*.BAS","",file$
  IF file$<>""
    PSAVE file$
  ENDIF
RETURN
