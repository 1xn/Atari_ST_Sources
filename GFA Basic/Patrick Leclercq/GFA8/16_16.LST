'
' ********************************************
' *                                          *
' *  G‚n‚rateur de sprites 16*16 pr‚d‚cal‚s  *
' *  … utiliser avec la routine sprite.lst   *
' *                                          *
' ********************************************
'
debut:
'
' On appel le s‚lecteur de fichier pour charger
' l'image o— se trouve le sprite 16*16 .Cette
' image doit etre au format DEGAS non compress‚e
' ( c'est-…-dire .PI1 ET NON .PC1 )
'
FILESELECT "a:*.pi1","",n$ ! on sauvegarde le nom de l'image dans N$
IF n$=""  ! si pas d'image selectionn‚e
  END     ! alors on revient … l'‚diteur
ENDIF     ! sinon on continue
'
image$=SPACE$(32000) ! on reserve 32000 octets pour l'image
pal$=SPACE$(32)      ! on reserve 32 octets pour les couleurs
'
OPEN "i",#1,n$       ! on ouvre le fichier image
'
SEEK #1,2            ! on se place 2 octets plus loin dans le fichier
BGET #1,V:pal$,32    ! pour attraper la palette
'
BGET #1,V:image$,32000 ! on charge l'image dans IMAGE$
'
CLOSE                  ! on ferme le fichier
'
VOID XBIOS(6,L:V:pal$) ! on met les couleurs de l'image
'
BMOVE V:image$,XBIOS(3),32000 ! on affiche l'image … l'‚cran
'
GET 0,0,16,16,a$ ! on prend le sprite de 16*16 dans A$
'
CLS ! on efface l'‚cran
'
sprite$=SPACE$(5000) ! reserve de la m‚moire pour le sprite
sprite%=V:sprite$    ! quer l'on va pr‚d‚cal‚
'
cpt%=0               ! on efface quelques variables
e1%=XBIOS(3)         ! on fixe une fois pour toute l'adresse de l'‚cran
'
' ********************************************
' *                                          *
' *    la routine qui suit pose le sprite    *
' *   dans les 16 positions du pred‚calage   *
' *   et le code dans un fichier trŠs peu    *
' * gourmand en m‚moire ceci pour un double  *
' * avantage : un gain important de vitesse  *
' * d'‚x‚cution et une place m‚moire moindre *
' *                                          *
' ********************************************
'
FOR j%=0 TO 15 ! de 0 … 15 pour les 16 positions
  '
  CLS ! on efface l'‚cran a chaque nouveau d‚calage
  '
  PUT j%,0,a$ ! on pose le sprite decal‚
  '
  FOR i%=0 TO 2400 STEP 160 ! 2400/160=15 avec la position 0
    '                       ! on a donc 16 positions
    '
    sp%=ADD(sprite%,cpt%)   ! on fait ces additions une bonne fois
    ec%=ADD(e1%,i%)         ! pout toutes pour augmenter la vitesse
    '                       ! du codage du sprite
    '
    '
    LONG{sp%}=LONG{ec%}     ! on transfert 4 octets de l'‚cran
    LONG{ADD(sp%,4)}=LONG{ADD(ec%,4)} ! repr‚sent‚ par la variable ec%
    LONG{ADD(sp%,8)}=LONG{ADD(ec%,8)} ! vers le fichier du sprite repr‚sent‚
    LONG{ADD(sp%,12)}=LONG{ADD(ec%,12)} ! par la variable sp%
    '
    '           ! on a transf‚r‚ 4 fois 4 octets ( 16 octets )
    ADD cpt%,16 ! on va ensuite se d‚placer dans le fichier
    '           ! du sprite de ces meme 16 octets
    '
  NEXT i%       ! on boucle ici pour le 16 lignes du sprite
  '
NEXT j% ! on boucle cette fois pour les 16 positions diff‚rentes
'
'
'
CLS ! on efface l'‚cran
PRINT "Entrez le nom du fichier pour la"
PRINT "  sauvegarde s'il vous plait .  "
PRINT "Vous retrouverez le fichier avec 3 "
PRINT "   lettres supl‚mentaires que je   "
PRINT " rajoute volontairement : nom.inl "
'
title:
INPUT nom$          ! on sauvegarde le nom
IF nom$=""          ! si pas de nom
  GOTO title        ! alors on redemande
ENDIF               ! sinon on continu
IF LEN(nom$)>8      ! on v‚rifit s'il n'est sup‚rieur … 8 lettres
  CLS               ! on efface l'‚cran
  PRINT CHR$(7)     ! le bruit de la cloche
  '
  PRINT "Votre nom est compos‚ de plus" ! petit message
  PRINT "  de 8 lettres . Recommencer  "
  '
  GOTO title ! on revient a l'‚tiquette TITLE
  '
ENDIF
'
IF EXIST(nom$+".INL") ! On v‚rifit si le fichier n'‚xiste pas
  CLS
  PRINT "Attention vous avez d‚ja un fichier"
  PRINT "  qui s'appele ainsi .Voulez vous  "
  PRINT "changez le nom de votre fichier ou "
  PRINT " continuer ( O/N )"
  INPUT r$
  IF r$="O" OR r$="o"
    CLS
    PRINT "Nouveau nom"
    GOTO title
  ENDIF
ENDIF
'
BSAVE nom$+".inl",V:sprite$,5000 ! on sauvegarde le fichier
'
CLS
'
PRINT " Voulez vous recodez un autre "
PRINT " sprite ( O/N ) ? "
INPUT r$
IF r$="O" OR r$="o"
  CLS
  GOTO debut
ELSE
  CLS
  END
ENDIF
'
