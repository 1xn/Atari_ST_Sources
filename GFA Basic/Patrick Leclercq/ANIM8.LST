' ***************
' *  ANIM8.GFA  *
' ***************
RESERVE 50000        ! RESERVATION MEMOIRE POUR LE GFA
'
CLS                  ! EFFACEMENT ECRAN
@dessin_animation    ! DESSIN OBJET GRAPHIQUE
CLS                  ! EFFACEMENT ECRAN
VOID INP(2)          ! ATTENTE CLAVIER
@anim9               ! EXECUTION DE L'ANIMATION
VOID INP(2)          ! ATTENTE CLAVIER
END                  ! FIN DU PROGRAMME
'
' ***************************
' *  DESSIN DE L'ANIMATION  *
' ***************************
PROCEDURE dessin_animation
  DEFFILL 2           ! FIXER COULEUR DE TRACE
  PBOX 0,0,9,9        ! AFFICHAGE RECTANGLE PLEIN
  GET 0,0,9,9,objet$  ! STOCKAGE IMAGE EN MEMOIRE
RETURN
'
' ******************************
' *  EXECUTION DE L'ANIMATION  *
' ******************************
PROCEDURE anim9
  LOCAL px%,py%
  LOCAL x0%,y0%
  LOCAL adr1%,adr2%
  LOCAL sauve$
  '
  px%=0                          ! POS X DEBUT
  py%=100                        ! POS Y DEBUT
  SGET sauve$
  x0%=px%                        ! X0 DEBUT
  y0%=py%                        ! Y0 DEBUT
  FOR i%=1 TO 100                ! DEBUT BOUCLE
    adr1%=VARPTR(sauve$)+y0%*160 ! ADR BLOC DE LIGNES
    adr2%=XBIOS(2)+y0%*160       ! ADR LIGNES ECRAN
    VSYNC                        ! ATTENTE SYNCHRO VIDEO
    BMOVE adr1%,adr2%,1600       ! COPIE BLOC DE LIGNES
    PUT px%,py%,objet$           ! AFF ANIMATION
    x0%=px%                      ! CALCUL X0
    y0%=py%                      ! CALCUL Y0
    INC px%                      ! MODIF. POS X
  NEXT i%                        ! FIN BOUCLE
RETURN
