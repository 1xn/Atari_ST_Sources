' *******************************************
' *  INTERPRETEUR D'AVENTURES TEXTE         *
' *  TYPE LOGICIEL DONT VOUS ETES LE HEROS  *
' *******************************************
' *  START MICRO MAGAZINE nø3               *
' *******************************************
' *  Ecrit en GFA Basic 3.xx                *
' *******************************************
' *  (C) 1992 PATRICK LECLERCQ              *
' *  (C) 1992 ARTIPRESSE                    *
' *******************************************
' *  REMARQUE: Ce logiciel n'est pr‚vus que *
' *  pour la basse r‚solution.              *
' *******************************************
RESERVE 50000
DIM textes$(300)
' ---
DIM paragraphe%(100,9)
' paragraphe%(n%,1)= Num‚ro texte principale n%
' paragraphe%(n%,2)= Nombre de choix
' paragraphe%(n%,3)= Num‚ro texte choix 1
' paragraphe%(n%,4)= nouveau paragraphe si choix 1
' paragraphe%(n%,5)= Num‚ro texte choix 2
' paragraphe%(n%,6)= nouveau paragraphe si choix 2
' paragraphe%(n%,7)= Num‚ro texte choix 3
' paragraphe%(n%,8)= nouveau paragraphe si choix 3
' ---
DIM lignes$(20)
'
' ------------------------------
' ******************************
' *  INITIALISATIONS DIVERSES  *
' ******************************
DEFMOUSE 0      ! SOURIS EN FORME DE FLECHE
GRAPHMODE 2     ! AFFICHAGE TRANSPARENT DES LETTRES
@charge_aventure
@aff_ecran_jeu
' -------------------
' ****************************
' *  BOUCLE SANS FIN DE JEU  *
' ****************************
' *  La sortie se fait dans  *
' *  le corps du programme   *
' *  avec l'instruction EDIT *
' ****************************
jeu%=1
DO
  jeu%=@exec_paragraphe(jeu%)
LOOP
END
'
' ***************************
' *  EFFACEMENT DE L'ECRAN  *
' ***************************
' *  c%: Couleur ‚cran      *
' ***************************
PROCEDURE cls_ecran(c%)
  LOCAL px2%,py2%
  '
  SELECT XBIOS(4)  ! DETERMINATION RESOLUTION
  CASE 0           ! TEST BASSE RESOLUTION
    px2%=319
    py2%=199
  CASE 1           ! TEST MOYENNE RESOLUTION
    px2%=639
    py2%=199
  CASE 2           ! TEST HAUTE RESOLUTION
    px2%=639
    py2%=399
  ENDSELECT
  DEFFILL c%         ! COULEUR ECRAN
  PBOX 0,0,px2%,py2% ! EFFACE ECRAN
RETURN
'
' ------------------------------------
' ************************************
' *  AFFICHAGE D'UN RECTANGLE PLEIN  *
' ************************************
' *  px%: Position X du rectangle    *
' *  py%: Position Y du rectagle     *
' *  tx%: Largeur du rectangle       *
' *  ty%: Hauteur du rectangle       *
' *  c% : Couleur de remplissage     *
' ************************************
PROCEDURE rect(px%,py%,tx%,ty%,c%)
  LOCAL px2%,py2%
  '
  px2%=px%+tx%-1
  py2%=py%+ty%-1
  DEFFILL c%              ! COULEUR RECTANGLE
  PBOX px%,py%,px2%,py2%  ! DESSIN RECTANGLE
RETURN
'
' -------------------------------
' *******************************
' *  AFFICHAGE D'UN CADRE VIDE  *
' *******************************
' *  px%:   Position X cadre    *
' *  py%:   Position Y cadre    *
' *  tx%:   Largeur cadre       *
' *  ty%:   Hauteur cadre       *
' *  fond%: Couleur de fond     *
' *  cadre%:Couleur du contour  *
' *******************************
PROCEDURE cadre(px%,py%,tx%,ty%,fond%,cadre%)
  LOCAL px2%,py2%
  '
  px2%=px%+tx%-1
  py2%=py%+ty%-1
  DEFFILL fond%
  PBOX px%,py%,px2%,py2%  ! COULEUR FOND
  COLOR cadre%            ! DESSIN FOND
  BOX px%,py%,px2%,py2%   ! COULEUR CADRE
RETURN                    ! DESSIN CADRE
'
' --------------------------------------------
' ********************************************
' *  AFFICHAGE D'UN TEXTE DANS UN RECTANGLE  *
' ********************************************
PROCEDURE draw_texte(px%,py%,tx%,ty%,m$,f%,c%,t%)
  LOCAL x_texte%,y_texte%
  '
  x_texte%=px%+((tx%-LEN(m$)*8)/2)
  y_texte%=py%+7+((ty%-8)/2)
  @cadre(px%,py%,tx%,ty%,f%,c%)
  TEXT x_texte%,y_texte%,m$
RETURN
'
' -------------------------
' *************************
' *  ATTENTE CLIC SOURIS  *
' *************************
PROCEDURE att_clic(VAR xm%,ym%)
  LOCAL km%
  '
  DO                   ! DEBUT BOUCLE
    MOUSE xm%,ym%,km%  ! CONSULTATION SOURIS
    EXIT IF km%<>0     ! TEST SI CLIC
  LOOP                 ! FIN BOUCLE
RETURN
'
' ----------------------------------------
' ****************************************
' *  MEMORISATION D'UN TEXTE EN MEMOIRE  *
' ****************************************
FUNCTION memorise_texte(t$)
  INC nb_textes%
  textes$(nb_textes%)=t$
  RETURN nb_textes%
ENDFUNC
'
' -----------------------------------------
' *****************************************
' *  CHARGEMENT DE L'AVENTURE EN MEMOIRE  *
' *****************************************
PROCEDURE charge_aventure
  LOCAL n%
  LOCAL num_texte%
  LOCAL mess$
  LOCAL t$
  '
  nb_textes%=0
  DO
    READ n%           ! LECTURE NUMERO PARAGRAPHE
    EXIT IF n%=-1     ! TEST SI FIN AVENTURE
    ' ---
    mess$=""          ! EFFACEMENT MESSAGE
    DO                ! BOUCLE DE LECTEUR DU MESSAGE
      READ t$         ! LECTURE LIGNE DE TEXTE
      EXIT IF t$="*"  ! TEST SI FIN TEXTE PRINCIPAL
      mess$=mess$+t$  ! AJOUT TEXTE PRINCIPAL
    LOOP              ! FIN BOUCLE LECTURE TEXTE
    ' STOCKAGE TEXTE EN MEMOIRE
    num_texte%=@memorise_texte(mess$)
    paragraphe%(n%,1)=num_texte%
    ' ---
    nb_options%=0
    DO
      READ texte_choix$
      EXIT IF texte_choix$="*"
      num_texte%=@memorise_texte(texte_choix$)
      INC nb_options%
      READ new_paragraphe%
      ' ----
      SELECT nb_options%
      CASE 1
        paragraphe%(n%,3)=num_texte%
        paragraphe%(n%,4)=new_paragraphe%
      CASE 2
        paragraphe%(n%,5)=num_texte%
        paragraphe%(n%,6)=new_paragraphe%
      CASE 3
        paragraphe%(n%,7)=num_texte%
        paragraphe%(n%,8)=new_paragraphe%
      ENDSELECT
    LOOP
    paragraphe%(n%,2)=nb_options%
  LOOP
RETURN
'
' ---------------------------------
' *********************************
' *  AFFICHAGE DE L'ECRAN DE JEU  *
' *********************************
PROCEDURE aff_ecran_jeu
  @cls_ecran(5)
  @draw_texte(10,8,67,13,"INFOS",2,1,0)
  @draw_texte(87,8,67,13,"SAUVER",2,1,0)
  @draw_texte(164,8,67,13,"CHARGER",2,1,0)
  @draw_texte(241,8,67,13,"QUITTER",2,1,0)
  @cadre(5,28,310,120,8,1)
  @cadre(5,155,310,36,8,1)
RETURN
'
' --------------------------
' **************************
' *  FORMATAGE D'UN TEXTE  *
' **************************
PROCEDURE format_texte(m$,taille%)
  LOCAL tampon$,p%,c$,ligne$
  '
  ' ---
  nb_lignes%=0
  tampon$=""
  ligne$=""
  FOR p%=1 TO 20
    lignes$(p%)=""
  NEXT p%
  ' ----
  ' *************************
  ' *  BOUCLE DE DECOUPAGE  *
  ' *************************
  FOR p%=1 TO LEN(m$)
    c$=MID$(m$,p%,1)
    IF c$<>" "
      tampon$=tampon$+c$
    ENDIF
    ' ----
    IF c$=" "
      IF LEN(ligne$)+1+LEN(tampon$)<=taille%
        IF ligne$=""
          ligne$=tampon$
          tampon$=""
        ELSE
          ligne$=ligne$+" "+tampon$
          tampon$=""
        ENDIF
      ELSE
        INC nb_lignes%
        lignes$(nb_lignes%)=ligne$
        ligne$=""
        IF tampon$<>""
          tampon$=tampon$+" "
        ENDIF
      ENDIF
    ENDIF
  NEXT p%
  ' ----
  ' **********************
  ' *  FIN DU DECOUPAGE  *
  ' **********************
  IF (ligne$="") AND (tampon$<>"")
    INC nb_lignes%
    lignes$(nb_lignes%)=tampon$
    tampon$=""
  ENDIF
  ' ----
  IF (tampon$<>"") AND (ligne$<>"")
    IF LEN(ligne$)+1+LEN(tampon$)<=taille%
      INC nb_lignes%
      lignes$(nb_lignes%)=ligne$+" "+tampon$
      ligne$=""
      tampon$=""
    ELSE
      INC nb_lignes%
      lignes$(nb_lignes%)=ligne$
      ligne$=""
      INC nb_lignes%
      lignes$(nb_lignes%)=tampon$
      tampon$=""
    ENDIF
  ENDIF
  '
RETURN
'
' ----------------------------------
' **********************************
' *  AFFICHAGE D'UN TEXTE FORMATE  *
' **********************************
PROCEDURE aff_format(px%,py%)
  LOCAL y%,l%
  '
  y%=py%
  FOR l%=1 TO nb_lignes%
    TEXT px%,y%,lignes$(l%)
    ADD y%,9
  NEXT l%
RETURN
'
' ------------------
' ******************
' *  OPTION INFOS  *
' ******************
PROCEDURE exec_infos
  LOCAL m$,b%
  '
  m$="DEMONSTRATION 'UN LOGICIEL |"
  m$=m$+"DONT VOUS ETES LE HEROS'. |"
  m$=m$+"-------------------------|"
  m$=m$+"(C) Patrick Leclercq |"
  ALERT 0,m$,1,"OUI",b%
RETURN
'
' -------------------
' *******************
' *  OPTION SAUVER  *
' *******************
PROCEDURE exec_sauver
RETURN
'
' --------------------
' ********************
' *  OPTION CHARGER  *
' ********************
PROCEDURE exec_charger
RETURN
'
' ********************
' *  OPTION QUITTER  *
' ********************
PROCEDURE exec_quitter
  LOCAL m$,b%
  '
  m$="Voulez-vous vraiment |"
  m$=m$+"quitter ce jeu ?"
  ALERT 2,m$,1,"Oui|Non",b%
  IF b%=1
    EDIT     ! QUITTER LE PRG
  ENDIF
RETURN
'
' ------------------------------------------
' ******************************************
' *  EXECUTION DES FONCTIONS DU PROGRAMME  *
' ******************************************
PROCEDURE exec_fonction(f%)
  SELECT f%
  CASE 4
    @exec_infos
  CASE 5
    @exec_sauver
  CASE 6
    @exec_charger
  CASE 7
    @exec_quitter
  ENDSELECT
RETURN
'
' ********************************************
' *  LECTURE DU NB DE CHOIX D'UN PARAGRAPHE  *
' ********************************************
FUNCTION get_nb_choix(p%)
  LOCAL nb_choix%
  '
  nb_choix%=paragraphe%(p%,2)
  RETURN nb_choix%
ENDFUNC
'
' -----------------------------------
' ***********************************
' *  LECTURE DU NUMERO D'UN TEXTE   *
' *  CORRESPONDANT AU CHOIX choix%  *
' *  DU PARAGRAPHE p%.              *
' ***********************************
FUNCTION get_texte_choix(p%,choix%)
  LOCAL num%
  '
  SELECT choix%
  CASE 1
    num%=paragraphe%(p%,3)
  CASE 2
    num%=paragraphe%(p%,5)
  CASE 3
    num%=paragraphe%(p%,7)
  ENDSELECT
  RETURN num%
ENDFUNC
'
' -----------------------------------
' ***********************************
' *  LECTURE DU NOUVEAU PARAGRAPHE  *
' *  CORRESPONDANT AU CHOIX choix%  *
' *  DU PARAGRAPHE p%.              *
' ***********************************
FUNCTION get_new_paragraphe(p%,choix%)
  LOCAL new%
  '
  SELECT choix%
  CASE 1
    new%=paragraphe%(p%,4)
  CASE 2
    new%=paragraphe%(p%,6)
  CASE 3
    new%=paragraphe%(p%,8)
  ENDSELECT
  RETURN new%
ENDFUNC
'
' -------------------------------
' *******************************
' *  AFFICHAGE D'UN PARAGRAPHE  *
' *******************************
PROCEDURE aff_paragraphe(p%)
  LOCAL num_texte%,mess$
  LOCAL x_choix%,y_choix%
  LOCAL choix$
  LOCAL i%
  '
  num_texte%=paragraphe%(p%,1) ! LECTURE NUM TEXTE
  mess$=textes$(num_texte%)    ! LECTURE TEXTE
  @format_texte(mess$,36)      ! FORMATAGE TEXTE
  ' ----
  @cadre(5,28,310,120,8,1)     ! EFFACEMENT CADRE
  @aff_format(17,40)           ! AFFICHAGE TEXTE
  ' ----
  @cadre(5,155,310,36,8,1)
  nb_choix%=@get_nb_choix(p%)
  FOR i%=1 TO nb_choix%
    num_texte%=@get_texte_choix(p%,i%)
    choix$=textes$(num_texte%)
    x_choix%=(320-LEN(choix$)*8)/2
    y_choix%=165+(i%-1)*10
    TEXT x_choix%,y_choix%,choix$
  NEXT i%
RETURN
'
' -------------------------------
' *******************************
' *  TEST D'UNE ZONE GRAPHIQUE  *
' *******************************
FUNCTION tstzone(x%,y%,px%,py%,tx%,ty%)
  LOCAL px2%,py2%
  LOCAL rep%
  '
  px2%=px%+tx%-1
  py2%=py%+ty%-1
  r%=FALSE
  IF (x%>=px%) AND (x%<=px2%)
    IF (y%>=py%) AND (y%<=py2%)
      rep%=TRUE
    ENDIF
  ENDIF
  RETURN rep%
ENDFUNC
'
' ---------------------------------------
' ***************************************
' *  IDENTIFICATION DE LA ZONE CLIQUEE  *
' *  PAR LE JOUEUR                      *
' ***************************************
FUNCTION identif_clic(xm%,ym%)
  LOCAL rep%
  '
  rep%=0
  IF @tstzone(xm%,ym%,5,158,310,9)
    rep%=1
  ELSE IF @tstzone(xm%,ym%,5,167,310,9)
    rep%=2
  ELSE IF @tstzone(xm%,ym%,5,180,310,9)
    rep%=3
  ELSE IF @tstzone(xm%,ym%,10,8,67,13)
    rep%=4
  ELSE IF @tstzone(xm%,ym%,87,8,67,13)
    rep%=5
  ELSE IF @tstzone(xm%,ym%,164,8,67,13)
    rep%=6
  ELSE IF @tstzone(xm%,ym%,241,8,67,13)
    rep%=7
  ENDIF
  RETURN rep%
ENDFUNC
'
' -------------------------------
' *******************************
' *  EXECUTION D'UN PARAGRAPHE  *
' *******************************
FUNCTION exec_paragraphe(p%)
  LOCAL xm%,ym%
  LOCAL clic%
  LOCAL nb_choix%
  '
  HIDEM                 ! EFFACEMENT SOURIS
  @aff_paragraphe(p%)   ! AFF PARAGRAPHE
  SHOWM                 ! AFF SOURIS
  nb_choix%=@get_nb_choix(p%)
  ' ----
  DO
    DO
      @att_clic(xm%,ym%)  ! ATTENTE CLIC
      clic%=@identif_clic(xm%,ym%)
      EXIT IF clic%<>0
    LOOP
    ' *****************************************
    ' *  TEST SI APPEL D'UNE FONCTION DU PRG  *
    ' *****************************************
    SELECT clic%
    CASE 4,5,6,7
      @exec_fonction(clic%)
    ENDSELECT
    ' *********************************
    ' *  SORTIE SI CLIC SUR UN CHOIX  *
    ' *********************************
    EXIT IF clic%<=nb_choix%
  LOOP
  RETURN @get_new_paragraphe(p%,clic%)
ENDFUNC
'
' -------------------------------
' *******************************
' *  DESCRIPTION DE L'AVENTURE  *
' *******************************
' ----
DATA 1
DATA "Apr‚s un long voyage fatiguant et dangereux, "
DATA "vous revenez … votre ville natale si belle "
DATA "en savourant le plaisir de vous retrouvez "
DATA "enfin chez vous. Le matin est superbe et les "
DATA "gens affair‚s comme … l'habitude. Votre gorge "
DATA "est dess‚ch‚e par le long trajet … dos de "
DATA "cheval et vous avez soif."
DATA *
DATA "Rentrer directement … la maison",15
DATA "Aller boire un verre",2
DATA *
' ----
DATA 2
DATA "D'un pas alerte, vous vous dirigez dans votre "
DATA "taverne habituel et entrez en saluant "
DATA "amicalement le patron. Contrairement … son "
DATA "habitude, il ne r‚pond pas … votre salut et "
DATA "sert les clients sans ‚mettre la moindre "
DATA "plaisanterie. Votre attention est attir‚ par "
DATA "un homme en rouge assis dans le fond de la "
DATA "taverne qui semble surveiller tous le monde. "
DATA *
DATA "Discutter avec le patron",3
DATA "Parler … l'homme en rouge",6
DATA "Surveiller l'homme en rouge",14
DATA *
' ----
DATA 3
DATA "Vous levez pour aller parler avec le patron. "
DATA "Il vous salue rapidement d'un signe de la tˆte "
DATA "et vous demande ce que vous voulez boire."
DATA *
DATA "Commander … boire",4
DATA "Parler de l'homme en rouge",5
DATA *
' ----
DATA 4
DATA "Il prend votre commande et vous apporte ce que "
DATA "vous avez demand‚ sans faire de remarque "
DATA "sarcastique, ce qui vous aurait parus "
DATA "impossible il y a seulement 2 mois."
DATA *
DATA "Faire une remarque sur son laconisme",5
DATA "Parler de l'homme en rouge",5
DATA "Sortir de la taverne",15
DATA *
' ----
DATA 5
DATA "Il blˆmit en entendant vos propos et fais "
DATA "semblant de ne pas avoir entendus. L'homme en "
DATA "rouge se lŠve et se dirige vers vous d'un pas "
DATA "d‚cid‚. Les buveurs s'‚cartent "
DATA "respectueusement sur son passage."
DATA *
DATA "Parler avec lui",6
DATA "Continuer de parler",7
DATA "Quitter la taverne",9
DATA *
' ----
DATA 6
DATA "C'est un homme de taille moyenne aux cheveux "
DATA "courts, aux doigts orn‚s de bijoux et qui porte "
DATA "un curieux m‚daillon circulaire en cuivre "
DATA "rouge. D'un ton sec, il vous demande qui vous "
DATA "ˆtes et quelle est la raison de votre pr‚sence "
DATA "dans cette ville."
DATA *
DATA "R‚v‚ler votre identit‚",8
DATA "Ne rien lui dire",7
DATA "Quitter la taverne",9
DATA *
' ----
DATA 7
DATA "L'homme en rouge lance un ordre dans une langue "
DATA "rocailleuse aux accents ‚tranges. Trois hommes "
DATA "portant des ‚p‚es courbes se lŠvent brutalement "
DATA "et tentent de vous encercler."
DATA *
DATA "Fuir",9
DATA "Tenter de discuter",13
DATA "Combattre",10
DATA *
' ----
DATA 8
DATA "Il sursaute et lance un ordre dans une langue "
DATA "rocailleuse. Trois buveurs se lŠvent, d‚gainent "
DATA "des ‚p‚es courbes et s'approchent prudemment de "
DATA "vous."
DATA *
DATA "Fuir",9
DATA "Tenter de discuter",13
DATA "Combattre",10
DATA *
' ----
DATA 9
DATA "Vous vous dirigez vers la porte de la taverne "
DATA "lorsque le ciel vous tombe sur la tˆte. "
DATA "Quelqu'un vient de vous frapper violemment sur "
DATA "la nuque. Vous perdez connaissance en attendant "
DATA "l'homme en rouge parler de l'‚lu et d'une "
DATA "promesse de r‚compense."
DATA *
DATA "Suite de l'aventure",18
DATA *
' ----
DATA 10
DATA "Vous d‚gainez votre fidŠle ‚p‚e et affrontez "
DATA "vos adversaires qui se d‚brouillent bien malgr‚ "
DATA "leurs armes inhabituelles. Vous vous retrouvez "
DATA "vite accul‚ dans un coin de la taverne. L'homme "
DATA "en rouge vous ordonne de vous rendre."
DATA *
DATA "Ob‚ir",12
DATA "Continuer le combat",11
DATA *
' ----
DATA 11
DATA "L'homme en rouge fait un geste rapide de la "
DATA "main tous en pronon‡ant une phrase "
DATA "incompr‚hensible. Une douleur intol‚rable "
DATA "‚clate dans votre tˆte, vous lƒchez votre arme "
DATA "et tombez sur le sol en g‚missant de douleur. "
DATA "Vous perdez connaissance au moment o— l'homme "
DATA "en rouge parle du plaisir de l'‚lu et d'une "
DATA "forte r‚compense."
DATA *
DATA "Suite de l'aventure",18
DATA *
' ----
DATA 12
DATA "Les hommes d'armes vous d‚sarment et vous "
DATA "ligotent. L'homme en rouge vous explique que "
DATA "vous allez bient“t connaŒtre votre destin en "
DATA "rencontrant l'‚lu de Zirma."
DATA *
DATA "Suite de l'aventure",18
DATA *
' ----
DATA 13
DATA "L'homme en rouge vous explique qu'il est un "
DATA "prˆtre de Zirma la lune rouge et que l'‚lu de "
DATA "Zirma auras plaisir … converser avec lui. Les "
DATA "trois hommes l'‚coutent tous en tripotant "
DATA "nerveusement leurs armes."
DATA *
DATA "Accepter de le suivre",12
DATA "Refuser",11
DATA "Tenter de fuir",9
DATA *
' ----
DATA 14
DATA "Malgr‚ la discr‚tion dont vous faites preuve, "
DATA "il s'aper‡oit de votre surveillance, se lŠve "
DATA "et se dirige vers vous."
DATA *
DATA "Parler avec lui",6
DATA "Quitter la taverne",9
DATA *
' ----
DATA 15
DATA "Alors que vous vous dirigez vers votre demeure, "
DATA "vous remarquez que la population s'‚carte avec "
DATA "respect devant des hommes vˆtus de rouges et "
DATA "portant sur la poitrine le symbole d'une lune "
DATA "en m‚tal rouge. Une patrouille d'hommes en "
DATA "armes accompagn‚ d'un des homme en rouge vous "
DATA "arrˆte et vous demande de montrer votre symbole "
DATA "symbole d'all‚geance, symbole dont vous n'avez "
DATA "jamais entendus parler."
DATA *
DATA "Raconter une histoire quelconque",16
DATA "Fuir",11
DATA "Combattre",17
DATA *
' ----
DATA 16
DATA "L'homme en rouge fait un geste ‚trange et vous "
DATA "tombez ‚vanouis sur le sol sans avoir put "
DATA "faire le moindre geste."
DATA *
DATA "Suite de l'aventure",18
DATA *
' ----
DATA 17
DATA "Les hommes d'armes sont trop nombreux et vous "
DATA "succombez sous leurs coups. C'est la fin de "
DATA "votre aventure."
DATA *
DATA "Recommencer le jeu",1
DATA *
' ----
DATA 18
DATA "Voici la fin de cette aventure, c'est … vous "
DATA "de la continuer et r‚pondre … quelques "
DATA "questions. Qui sont les hommes en rouges ? "
DATA "est l'‚lu ? Que s'est-il pass‚ en ville "
DATA "pendant votre absence ?. Si vous ‚crivez "
DATA "une belle aventure … partir de ce noyau, "
DATA "envoyez-la … START MICRO MAGAZINE. Les "
DATA "meilleurs sc‚narios seront mis sur la "
DATA "disquette et gagneront un "
DATA "abonnement gratuit."
DATA *
DATA "Recommencer le jeu",1
DATA *
' ----
' **********************************
' *  INDICATEUR DE FIN D'AVENTURE  *
' **********************************
DATA -1
