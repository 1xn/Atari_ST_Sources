' ****************
' *  ANIM10.GFA  *
' ****************
RESERVE 50000        ! RESERVATION MEMOIRE POUR LE GFA
'
CLS                  ! EFFACEMENT ECRAN
@dessin_animation    ! DESSIN OBJET GRAPHIQUE
CLS                  ! EFFACEMENT ECRAN
VOID INP(2)          ! ATTENTE CLAVIER
@anim10              ! EXECUTION DE L'ANIMATION
VOID INP(2)          ! ATTENTE CLAVIER
END                  ! FIN DU PROGRAMME
'
' ***************************
' *  DESSIN DE L'ANIMATION  *
' ***************************
PROCEDURE dessin_animation
  DEFFILL 2           ! FIXER COULEUR DE TRACE
  PBOX 0,0,9,9        ! AFFICHAGE RECTANGLE PLEIN
  GET 0,0,9,9,objet$  ! STOCKAGE IMAGE EN MEMOIRE
RETURN
'
'
PROCEDURE aff_anim(px%,py%)
  LOCAL adr1%,adr2%
  '
  adr1%=VARPTR(sauve$)+y0%*160 ! ADR BLOC DE LIGNES
  adr2%=XBIOS(2)+y0%*160       ! ADR LIGNES ECRAN
  VSYNC                        ! ATTENTE SYNCHRO VIDEO
  BMOVE adr1%,adr2%,1600       ! COPIE BLOC DE LIGNES
  PUT px%,py%,objet$           ! AFF ANIMATION
  x0%=px%                      ! CALCUL X0
  y0%=py%                      ! CALCUL Y0
RETURN
'
' ******************************
' *  EXECUTION DE L'ANIMATION  *
' ******************************
PROCEDURE anim10
  LOCAL px%,py%
  '
  SGET sauve$
  px%=0                   ! POS X DEBUT
  py%=50                  ! POS Y DEBUT
  x0%=px%                 ! X0 DEBUT
  y0%=py%                 ! Y0 DEBUT
  DO
    FOR i%=1 TO 45        ! MOUVEMENT VERS LE BAS
      @aff_anim(px%,py%)
      ADD px%,5           ! MODIF. POS X
      ADD py%,2
    NEXT i%               ! FIN BOUCLE
    FOR i%=1 TO 45        ! MOUVEMENT VERS LE HAUT
      @aff_anim(px%,py%)
      SUB px%,5
      SUB py%,2
    NEXT i%
  LOOP
RETURN
