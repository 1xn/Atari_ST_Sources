' *******************************
' *  EXEMPLE D'UTILISATION DES  *
' *  POLICES DE CARACTERES      *
' *******************************
' *  Programme AFFTEXTE.GFA     *
' *******************************
RESERVE 50000                ! MEMOIRE POUR GFA BASIC
DIM police$(255)             ! STOCKAGE DES CARACTERES
@cls_ecran(0)                ! EFFACEMENT ECRAN
@charge_police("FONT1.CAR")  ! CHARGEMENT POLICE
@test                        ! AFFICHAGE TEXTE
VOID INP(2)                  ! ATTENTE CLAVIER
END                          ! FIN PROGRAMME
'
' ************************************
' *  AFFICHAGE D'UN TEXTE D'EXEMPLE  *
' ************************************
PROCEDURE test
  @def_couleur_texte(1)
  affmessage(10,10,"ABCDEFGHIJKLMNOPQRSTUVW")
  @affmessage(10,30,"XYZ")
  @affmessage(10,50,"abcdefghijklmnopqrstuvwxyz")
  @affmessage(10,70,"‚Š‡…ˆƒ‹Œ")
  @affmessage(10,90,"01234566789")
  @affmessage(10,110,"'.,?!;()+-/*$'")
RETURN
'
' ********************************
' *  EFFACEMENT DE L'ECRAN DANS  *
' *  UNE COULEUR QUELCONQUE      *
' ********************************
PROCEDURE cls_ecran(c%)
  DEFFILL c%
  SELECT XBIOS(4)      ! DETERMINATION RESOLUTION
  CASE 0
    PBOX 0,0,319,199   ! EFFACEMENT BASSE RESOLUTION
  CASE 1
    PBOX 0,0,639,199   ! EFFACEMENT MOYENNE RESOLUTION
  CASE 2
    PBOX 0,0,639,399   ! EFFACEMENT HAUTE RESOLUTION
  ENDSELECT
RETURN
'
' *******************************************
' *  MODIFICATION DE LA COULEUR D'ECRITURE  *
' *******************************************
' *  0= Texte couleur 0                     *
' *  1= Texte couleur 15                    *
' *******************************************
PROCEDURE def_couleur_texte(n%)
  couleur_texte%=n%
RETURN
'
' ***********************************
' *  LECTURE LONGUEUR D'UNE LETTRE  *
' ***********************************
FUNCTION get_tx(v$)
  RETURN (1+INT{VARPTR(v$)})
ENDFUNC
'
'
' *********************************
' *  AFFICHAGE D'UN MESSAGE AVEC  *
' *  LA POLICE COURANTE           *
' *********************************
PROCEDURE affmessage(px%,py%,m$)
  LOCAL c$,i%,nc%,lx%
  LOCAL op%
  '
  IF couleur_texte%=0
    op%=1
  ELSE
    op%=13
  ENDIF
  FOR i%=1 TO LEN(m$)      ! DEBUT BOUCLE AFFICHAGE
    c$=MID$(m$,i%,1)       ! LECTURE CARACTERE
    nc%=ASC(c$)            ! CALCUL NUMERO LETTRE
    IF police$(nc%)<>""    !TEST SI LETTRE EXISTANTE
      PUT px%,py%,police$(nc%),op% !AFFICHAGE LETTRE
      lx%=@get_tx(police$(nc%))   !LARGEUR  LETTRE
      ADD px%,lx%   !POSITION PROCHAIN AFFICHAGE
    ENDIF
  NEXT i%
RETURN
'
' ******************************
' *  CHARGEMENT D'UNE POLICE   *
' *  DE CARACTERES EN MEMOIRE  *
' ******************************
PROCEDURE charge_police(nom$)
  LOCAL n%
  '
  OPEN "I",#1,nom$               ! OUVERTURE FICHIER
  DO                             ! DEBUT BOUCLE
    BGET #1,VARPTR(n%),4         ! LECTURE CODE CAR
    EXIT IF n%=-1                ! TEST SI FIN DE CAR
    BGET #1,VARPTR(taille%),4    ! LECTURE TAILLE CAR
    police$(n%)=SPACE$(taille%)  ! RESERVATION MEMOIRE
    adr_car%=VARPTR(police$(n%)) ! ADRESSE CAR
    BGET #1,adr_car%,taille%     ! CHARGEMENT IMAGE CAR
  LOOP                           ! FIN BOUCLE
  CLOSE #1                       ! FERMETURE FICHIER
RETURN
