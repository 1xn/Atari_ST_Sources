' ****************
' *  ANIM11.GFA  *
' ****************
RESERVE 50000        ! RESERVATION MEMOIRE POUR LE GFA
'
CLS                  ! EFFACEMENT ECRAN
@dessin_animation    ! DESSIN OBJET GRAPHIQUE
CLS                  ! EFFACEMENT ECRAN
VOID INP(2)          ! ATTENTE CLAVIER
@anim11              ! EXECUTION DE L'ANIMATION
VOID INP(2)          ! ATTENTE CLAVIER
END                  ! FIN DU PROGRAMME
'
' ***************************
' *  DESSIN DE L'ANIMATION  *
' ***************************
PROCEDURE dessin_animation
  DEFFILL 2           ! FIXER COULEUR DE TRACE
  PBOX 0,0,9,9        ! AFFICHAGE RECTANGLE PLEIN
  GET 0,0,9,9,objet$  ! STOCKAGE IMAGE EN MEMOIRE
RETURN
'
'
PROCEDURE aff_anim(px%,py%)
  LOCAL adr1%,adr2%
  '
  adr1%=VARPTR(sauve$)+y0%*160 ! ADR BLOC DE LIGNES
  adr2%=XBIOS(2)+y0%*160       ! ADR LIGNES ECRAN
  VSYNC                        ! ATTENTE SYNCHRO VIDEO
  BMOVE adr1%,adr2%,1600       ! COPIE BLOC DE LIGNES
  PUT px%,py%,objet$           ! AFF ANIMATION
  x0%=px%                      ! CALCUL X0
  y0%=py%                      ! CALCUL Y0
RETURN
'
' ******************************
' *  EXECUTION DE L'ANIMATION  *
' ******************************
PROCEDURE anim11
  LOCAL px%,py%
  '
  SGET sauve$
  READ px%              ! POS X DEBUT
  READ py%              ! POS Y DEBUT
  x0%=px%               ! X0 DEBUT
  y0%=py%               ! Y0 DEBUT
  DO
    READ px%
    EXIT IF px%=-1
    READ py%
    @aff_anim(px%,py%)  ! AFF. IMAGE ANIMATION
  LOOP
RETURN
'
DATA 100,100
DATA 102,102
DATA 104,104
DATA 106,106
DATA 108,108
DATA 110,110
DATA 110,112
DATA 110,115
DATA 110,118
DATA 110,120
DATA -1
