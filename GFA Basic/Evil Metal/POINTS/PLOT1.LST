'
' Routine de point par Pignard Arnaud
' Evil Metal/DUNE
'
' Ne pas oublier de charger les INLINE !!!
'
INLINE deca%,640
INLINE v%,640
'
@init
CLS
'
' Nombre de point
'
nb&=100
DIM x&(nb&),y|(nb&)
'
' Des points n'importe ou
'
FOR i&=0 TO nb&
  x&(i&)=RANDOM(319)
  y|(i&)=RANDOM(199)
NEXT i&
'
' Quelques couleurs blanches
'
VSYNC
SETCOLOR 0,&H0
SETCOLOR 1,&H700
SETCOLOR 2,&H777
SETCOLOR 3,&H777
SETCOLOR 15,&H777
'
' On affiche nos points sur le plan 1
'
FOR i&=0 TO nb&
  @plot(x&(i&),y|(i&),2)
NEXT i&
'
~INP(2)                                 ! On attend 1 touche
'
' On efface nos points
'
FOR i&=0 TO nb&
  @efface(x&(i&),y|(i&),2)
NEXT i&
'
~INP(2)                                 ! On attend 1 touche
'
@fin                                    ! On quitte proprement !!!
'
> PROCEDURE init
  '
  s%=GEMDOS(32,L:0)                     ! On passe en mode superviseur
  '
  xb%=XBIOS(2)                          ! On sauve l'adresse de l'‚cran
  rez%=XBIOS(4)                         ! On sauve la resolution
  ~XBIOS(5,L:-1,L:-1,0)                 ! On passe en basse r‚solution
  '                                     ! Buffer pour la palette
  INLINE pal%,32
  '                                     ! Il faut mieux sauvegarder ca aussi !
  INLINE sauv%,4
  LONG{sauv%}=LONG{&H484}
  '
  BMOVE &HFFFF8240,pal%,32              ! On copie la palette dans notre buffer
  '
  VSYNC                                 ! Attente de Vbl
  '
RETURN
'
> PROCEDURE plot(x&,y|,plan|)
  '
  addecran%=ADD(ADD(xb%,ADD(CARD{ADD(deca%,ADD(x&,x&))},MUL(y|,160))),plan|)
  CARD{addecran%}=CARD{ADD(v%,ADD(x&,x&))} OR CARD{addecran%}
  '
RETURN
'
> PROCEDURE efface(x&,y|,plan|)
  '
  addecran%=ADD(ADD(xb%,ADD(CARD{ADD(deca%,ADD(x&,x&))},MUL(y|,160))),plan|)
  CARD{addecran%}=0
  '
RETURN
'
> PROCEDURE fin
  VSYNC                                         ! Attente de Vbl
  LONG{&H484}=LONG{sauv%}                       ! On restore les donn‚es
  ~XBIOS(5,L:xb%,L:xb%,-1)                      ! de bases
  ~XBIOS(5,L:-1,L:-1,rez%)
  BMOVE pal%,&HFFFF8240,32
  '
  VOID GEMDOS(32,L:s%)                          ! Passe en mode User
  '
  EDIT                                          ! On revient sous l'‚diteur
RETURN
'
