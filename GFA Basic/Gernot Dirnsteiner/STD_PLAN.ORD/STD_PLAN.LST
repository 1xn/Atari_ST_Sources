'                   Stundenplanerstellung
'
'           G.Dirnsteiner, Falkenweg 7, 8313 Vilsbiburg
'
'           ******* Version vom 5.10.88  GFA 2.02 ***********
'
' ***************************************************************************
Aut.inf$="** STUNDENPLANDRUCK P6/7 ** |    Gernot Dirnsteiner  |        Falkenweg 7|      8313 VILSBIBURG"
Deffill 1,2,12
Pbox 0,0,639,399
Deftext 1,0,0,13
@Gong5
Alert 0,Aut.inf$,1,"Weiter|Desktop",Dum%
If Dum%=2
  End
Endif
Deffill 0
If Exist("FAECHER.INF")<>0
  Open "I",#1,"FAECHER.INF"
  Input #1,Fachbezeichnung1$,Fachbezeichnung2$
  Close #1
Else
  Deffill 1,2,18
  Pbox 0,0,639,399
  @Shadowbox(40,100,600,150,10)
  Text 50,130,"Datei FAECHER.INF nicht vorhanden --> Default$= Biologie,Chemie"
  @Shadowbox(260,200,370,250,7)
  Text 270,240,"W E I T E R"
  Deftext 1,0,0,6
  Text 265,210,"Abbruch: Esc"
  Text 290,220,"sonst"
  @Gong5
  Repeat
    Taste$=Inkey$
  Until Taste$>"" Or Mousek<>0
  If Taste$=Chr$(27)
    @E.xit
  Endif
  Fachbezeichnung1$="Biologie"
  Fachbezeichnung2$="Chemie"
Endif
Deffill 0
Cls
' ********************  Hauptprogramm ***************************************
'
Spa%=6
Reihe%=10
Sx%=30    ! Startkoord.
Sy%=30
Br%=100   ! Boxbreite
Ho%=28    ! Boxhîhe
H.br%=Br%  ! Boxbreite und
H.ho%=Ho%   ! -hîhe fÅr H.copy
X1%=6*Br%+Sx%
Y1%=10*Ho%+Sy%
Faktor%=8 ! Offsetfaktor, 8 fÅr Normalschrift(13), 4  fÅr 6 und 4
H.textgr%=13
Le.textgr%=6
Zeit.textgr%=13
Deftext 1,0,0,6
Dim Boxtext$(12),Tastenbelegg$(12)
Dim Tag$(Spa%),Zeit$(Reihe%),Feld(Spa%,Reihe%),Feld$(Spa%,Reihe%)
Dim Klasse$(8,11),Fach$(8,11),Anf.fach$(8,11),Lehrer$(8,11),Raum$(8,11),Abk$(11)
Dim Klasse1$(8,11),Fach1$(8,11),Anf.fach1$(8,11),Lehrer1$(8,11),Raum1$(8,11),Abk1$(11)
Dim Klasse2$(8,11),Fach2$(8,11),Anf.fach2$(8,11),Lehrer2$(8,11),Raum2$(8,11),Abk2$(11)
Dim Klasse3$(8,11),Fach3$(8,11),Anf.fach3$(8,11),Lehrer3$(8,11),Raum3$(8,11),Abk3$(11)
Dim Planzeit$(10)
'
Dim X%(10),Y%(10),T$(10),L%(16),I$(16),V%(10) ! FÅr Dialogbox
'
' ---------- MenÅleiste ------------------
@Mausbox(8,365,52,30,12)
Erstflag%=1
'
' ---------------------------------------
@Hauptprozedur
'
'
'
' **********************************************************************
Procedure Hauptprozedur
  Do
    '
    Taste$=Inkey$
    If Taste$<>""
      @Tastenproc
    Endif
    '
    If (Mousey>330 And Mousek=1) Or Neubildflag%=0
      @Mausbox(8,365,52,30,12)
    Endif
    ' -----------------------------------
    If Mousek=1 And Ueb_flag%=1
      If Mousey<110
        Sy_cnt%=0
      Else
        If Mousey>130 And Mousey<240
          Sy_cnt%=1
        Else
          If Mousey>250
            Sy_cnt%=2
          Else
            Goto Ueb_ende
          Endif
        Endif
      Endif
      Repeat
      Until Mousek=0
      @Proc5
      Ueb_ende:
    Endif
    ' -------------------------------------
    @Wahlfeld(Sx%,Sy%,Br%,Ho%,Reihe%,Spa%)
    If Sp%<6 And Sp%>=0 And Re%<=9 And Re%>=0
      If Sp%=5   ! wenn abk.spalte
        Sp5flag%=1
      Else
        Sp5flag%=0
      Endif
      If Re%<>6
        @Dialog
        Put 0,365,Bild2$
      Endif
      @Feldzuweisung
      If X%>Sx% And X%<Sx%+Br%*Spa% And Y%>Sy% And Y%<Sy%+Ho%*Reihe%
        If Re%<>6  ! wenn nicht Mittag: normalisieren
          Deffill 1,1
          Graphmode 3 ! gewÑhlte Box reinvertieren
          Pbox Sx%+Br%*Sp%+1,Sy%+Ho%*Re%+1,Sx%+Br%+Br%*Sp%-1,Sy%+Ho%+Ho%*Re%-1
          Graphmode 1
        Endif
      Endif
      @Feldeintrag
    Endif
    '
  Loop
Return
'
'
' ********************  Einzelprozeduren **************************************
'
' *******  Wahlboxfeld, 2 dimensional ; Version vom 11.3.88  ******************
' *******   G.Dirnsteiner, 8313 Vilsbiburg, Falkenweg 7  **********************
'
Procedure Wahlfeld(Sx%,Sy%,Br%,Ho%,Reihe%,Spa%)
  '
  '  Text 400,380,"Dimensionierung= "+Str$(Reihe%)+","+Str$(Spa%)
  ' +++ öbergabeparameter: Sx%,Sy% = Koordinaten Boxfeldbeginn/links-oben +++
  ' +++ Br%,Ho% = Einzelboxbreite bzw.-hîhe                               +++
  ' +++ Reihe%, Spa% = Anzahl der waagr.Reihen bzw. senkr. Spalten        +++
  ' ++ RÅckgabeparameter: Boxflag%(Re/Sp)
  '
  '
  If Neubildflag%=0 ! nur anfangs zeichnen
    '
    Restore Zeit
    Deftext 1,0,0,Zeit.textgr%
    For I%=0 To Reihe%-1
      Read Zeit$(I%)
      Read Planzeit$(I%)
      Text Sx%-20,Sy%+Ho%/1.5+Ho%*I%,Zeit$(I%)
    Next I%
    Deftext 1,0,0,H.textgr%
    '
    '
    If Br%>90 And Abk.flag%=0
      Restore Tag.voll
    Else
      Restore Tag.abk.
      Abk.flag%=0
    Endif
    '
    For U%=0 To Spa%-1
      Read Tag$(U%)
      Offs%=Br%-Faktor%*Len(Tag$(U%))
      Text Sx%+Offs%/2+Br%*U%,Sy%-10,Tag$(U%)
    Next U%
    '
    '
    For I%=0 To Reihe%-1
      For U%=0 To Spa%-1
        ' ----------------------------------
        If I%=6              ! Mittagspause
          Deffill 1,2,2
        Else
          Deffill 1,0
        Endif
        ' ---------------------------------------
        Pbox Sx%+Br%*U%,Sy%+Ho%*I%,Sx%+Br%+Br%*U%,Sy%+Ho%+Ho%*I%
        Deftext 1,0,0,13
      Next U%
    Next I%
    '
    @Regenerieren
    Box 5,3,Sx%+Br%*Spa%+3,Sy%+Ho%*Reihe%+3 ! Rahmen um Stundenplan
    '
    Get 0,365,639,399,Bild2$
  Endif
  Neubildflag%=1   ! nicht mehr zeichnen
  I%=0
  '
  Deffill 1,1
  Do
    '
    Taste$=Inkey$
    If Taste$<>""
      @Tastenproc
    Endif
    '
    Mouse X%,Y%,K%
    Sp%=Int((X%-Sx%)/Br%)
    Re%=Int((Y%-Sy%)/Ho%)
    '
    ' ------ GrîûenverÑnderung --------------
    While X%>Sx%+Br%*Spa%-5 And X%<Sx%+Br%*Spa%+10 And Y%>Sy%+Ho%*Reihe%-5 And Y%<Sy%+Ho%*Reihe%+10
      '
      Taste$=Inkey$
      If Taste$<>""
        @Tastenproc
      Endif
      '
      Mouse X%,Y%,K%
      Defmouse 4
      Pause 6
      If K%=1
        @Plangroesse
      Endif
    Wend
    ' ----------------------------------
    '
    If X%>Sx% And X%<Sx%+Br%*Spa% And Y%>Sy% And Y%<Sy%+Ho%*Reihe%
      Defmouse 3
      Pause 2
      If K%=1
        If Re%<>6
          Graphmode 3
          Pbox Sx%+Br%*Sp%+1,Sy%+Ho%*Re%+1,Sx%+Br%+Br%*Sp%-1,Sy%+Ho%+Ho%*Re%-1
          Graphmode 1
        Endif
        Deftext 1,0,0,13
        '
        Deffill 0,0            ! unten lîschen
        Pbox 0,360,639,399
        '
        If Re%<>6
          If Sp%>4
            Text 30,377,Zeit$(Re%)+Tag$(Sp%)+" :"
          Else
            Text 30,377,Tag$(Sp%)+","+Zeit$(Re%)+"Stunde:"
          Endif
        Else
          Text 30,377,"Mittagspause"
          @Gong5
          Put 0,365,Bild2$
        Endif
        '
        '
        Showm
        Pause 7
      Endif
    Else
      Defmouse 0
      Pause 2
    Endif
    Exit If K%=1
  Loop
  '
Return
' *********************************************************************
Procedure Feldzuweisung
  Klasse$(Sp%,Re%)=I$(0)
  Fach$(Sp%,Re%)=I$(1)
  Anf.fach$(Sp%,Re%)=I$(1)
  Raum$(Sp%,Re%)=I$(2)
  Lehrer$(Sp%,Re%)=I$(3)
  On Sy_cnt% Gosub Plan1,Plan2,Plan3  ! Auch bei 3 Stundenpl. Ñndern
Return
' *********************************************************************
Procedure Feldeintrag
  Deftext 1,0,0,H.textgr%
  Deffill 0
  If Lehrer$(Sp%,Re%)>"" And Ueb_flag%=0
    Shft%=2.5
  Else
    Shft%=3.5
  Endif
  Fuell$=" "
  If Kurzflag%=1
    Fuell$=""
  Endif
  If Raum$(Sp%,Re%)=""
    Haupt$=Klasse$(Sp%,Re%)+Fuell$+Fach$(Sp%,Re%)
  Else
    Haupt$=Klasse$(Sp%,Re%)+" "+Fach$(Sp%,Re%)+"  "+Raum$(Sp%,Re%)
  Endif
  If Ueb_flag%=1
    Haupt$=Klasse$(Sp%,Re%)+" "+Left$(Lehrer$(Sp%,Re%),3)
  Endif
  If Bel_flag%=1 And Ueb_flag%=0
    Haupt$=Klasse$(Sp%,Re%)
  Endif
  If Re%<>6
    Pbox Sx%+Br%*Sp%+1,Sy%+Ho%*Re%+1,Sx%+Br%+Br%*Sp%-1,Sy%+Ho%+Ho%*Re%-1
    Offs%=Br%-Faktor%*Len(Haupt$)
    Text Sx%+Offs%/2+Br%*Sp%,Sy%-Ho%/Shft%+Ho%*(Re%+1),Haupt$
    Deftext 1,0,0,Le.textgr%
    Offs%=Br%-Faktor%*Len(Lehrer$(Sp%,Re%))
    '
    If Ueb_flag%=0 Or Bel_flag%=1
      Text Sx%+Offs%/2+Br%*Sp%,Sy%-3+Ho%*(Re%+1),Lehrer$(Sp%,Re%)
    Endif
    '
    Deftext 1,0,0,13
    '
  Endif
  Deftext 1,0,0,H.textgr%
Return
' *********************************************************************
Procedure Plangroesse
  While Mousek=1
    Mouse X%,Y%,K%
    If Y%>=350
      Y%=350
    Endif
    If X%<>X1% Or Y%<>Y1% And (X1%<>0 And Y1%<>0)
      Graphmode 3
      Box 4,2,X%,Y%
      Box 4,2,X1%,Y1%
      Graphmode 1
    Endif
    X1%=X%
    Y1%=Y%
  Wend
  X1%=0
  Y1%=0
  Defmouse 0
  Br%=Int((X%-Sx%)/6)
  Ho%=Int((Y%-Sy%)/10)
  H.br%=Br%  ! Boxbreite und
  H.ho%=Ho%  ! -hîhe fÅr H.copy
  ' *
  @Textgroesse
  ' *
  If Ueb_flag%=0
    Cls
  Else
    Deffill 0
  Endif
  Neubildflag%=0  ! neu zeichnen
  @Hauptprozedur
Return
' ********************************************************************
Procedure Textgroesse
  If Br%<65
    H.textgr%=6
    Faktor%=8
  Else
    If Br%<32
      H.textgr%=4
      Faktor%=4
      Le.textgr%=4
    Else
      H.textgr%=13
    Endif
  Endif
  If Ho%<25
    Zeit.textgr%=6
    H.textgr%=6
    Faktor%=8
    Le.textgr%=4
    If Ho%<15
      Zeit.textgr%=4
      H.textgr%=4
      Faktor%=4
    Endif
  Else
    Zeit.textgr%=13
    Le.textgr%=6
  Endif
Return
' *********************************************************************
Procedure Regenerieren
  '
  Neubildflag%=0
  Put 0,365,Bild2$
  For Sp%=0 To Spa%-1
    For Re%=0 To Reihe%-1
      If Br%<40
        Kurzflag%=1
        Fach$(Sp%,Re%)=Left$(Fach$(Sp%,Re%),3)
        If Br%<25
          Fach$(Sp%,Re%)=Left$(Fach$(Sp%,Re%),2)
        Endif
        If Br%<10
          Fach$(Sp%,Re%)=Left$(Fach$(Sp%,Re%),1)
        Endif
      Endif
      If Kurzflag%=0
        Fach$(Sp%,Re%)=Anf.fach$(Sp%,Re%)
      Endif
      Deftext 1,0,0,H.textgr%
      Deffill 0
      If Lehrer$(Sp%,Re%)>"" And Ueb_flag%=0
        Shft%=2.5
      Else
        Shft%=3.5
      Endif
      If Raum$(Sp%,Re%)=""
        Haupt$=Klasse$(Sp%,Re%)+" "+Fach$(Sp%,Re%)
      Else
        Haupt$=Klasse$(Sp%,Re%)+" "+Fach$(Sp%,Re%)+"  "+Raum$(Sp%,Re%)
      Endif
      '
      If Ueb_flag%=1 Or Bel_flag%=1
        Haupt$=Klasse$(Sp%,Re%)+" "+Left$(Lehrer$(Sp%,Re%),3)
      Endif
      If Bel_flag%=1 And Ueb_flag%=0
        Haupt$=Klasse$(Sp%,Re%)
      Endif
      '
      If Re%<>6
        Pbox Sx%+Br%*Sp%+1,Sy%+Ho%*Re%+1,Sx%+Br%+Br%*Sp%-1,Sy%+Ho%+Ho%*Re%-1
        Offs%=Br%-Faktor%*Len(Haupt$)
        Text Sx%+Offs%/2+Br%*Sp%,Sy%-Ho%/Shft%+Ho%*(Re%+1),Haupt$
        Deftext 1,0,0,Le.textgr%
        Offs%=Br%-Faktor%*Len(Lehrer$(Sp%,Re%))
        '
        If Ueb_flag%=0 Or Bel_flag%=1
          Text Sx%+Offs%/2+Br%*Sp%,Sy%-3+Ho%*(Re%+1),Lehrer$(Sp%,Re%)
        Endif
        '
        Deftext 1,0,0,13
      Endif
    Next Re%
  Next Sp%
Return
' *********************************************************************
Procedure Dialog
  ' **********************************************************************
  ' *        Dialogartige Eingabeprozedur  in GfA V 2.0                  *
  ' *                     3.8.1987                                       *
  ' **********************************************************************
  '
  '
  If Sp5flag%=0
    Restore Dialogdaten1
  Else
    Restore Dialogdaten2
  Endif
  '
  I$(0)=Klasse$(Sp%,Re%)
  I$(1)=Fach$(Sp%,Re%)
  I$(1)=Anf.fach$(Sp%,Re%)
  I$(2)=Raum$(Sp%,Re%)
  I$(3)=Lehrer$(Sp%,Re%)
  '
  For I%=0 To 3        ! ++++ Zeilenanzahl +++
    Read X%(I%),Y%(I%),T$(I%),L%(I%),V%(I%)
    ' I$(i%)=""
  Next I%
  '
  '
  Rem +++ Data: Zeilenort(X%),Zeilennummer(Y%),vorggeg.Text(T$),ZeilenlÑnge(L%),++
  Rem +++ zuletzt Datentyp(V%):  0= Alphanumerisch; 1= nur Zahl: 2= Zahl und /  ++
  Rem +++ beliebige Festlegung des Datentyps in --> Procedure E.ins_char(K%) +++
  Dialogdaten1:
  Data 0,0,"Klasse:",5,0
  Data 0,1,"  Fach:",15,0
  Data 0,2,"  Raum:",3,0
  Data 0,3,"Lehrer:",15,0
  Dialogdaten2:
  Data 0,0,"Erkl.1:",11,0
  Data 0,1,"",0,0
  Data 0,2,"",0,0
  Data 0,3,"Erkl.2:",15,0
  '
  '
  @Eingabe(3,200,340,1)! N%(Zeilenzahl-1), X%, Y%(X/Y-Koord. der Box), F%
  Gosub Schnittstelle
Return
' ---------------------------
Procedure Eingabe(N%,X%,Y%,F%)
  Vdisys 38
  Cb%=Dpeek(Ptsout+4)
  Ch%=Dpeek(Ptsout+6)
  Lh%=Dpeek(Ptsout+2)
  Ll%=Ch%-Lh%
  Insflg!=True
  Spec$=Chr$(8)+Chr$(13)+Chr$(27)
  Sp.scan$=Chr$(&H48)+Chr$(&H4B)+Chr$(&H50)+Chr$(&H4D)+Chr$(&H52)
  Spr$=Mki$(0)+Mki$(Lh%)+Mki$(-1)+Mki$(1)+Mki$(0)
  For I%=1 To Ch%
    Spr$=Spr$+Mkl$(&H8000)
  Next I%
  Spr$=Left$(Spr$+String$(74,0),74)
  U$=String$(100,"_")
  Dim Tx%(N%),Ty%(N%)
  Mx%=0
  My%=0
  For I%=0 To N%
    Tx%(I%)=X%+Cb%*(X%(I%)+Len(T$(I%)))
    Ty%(I%)=Y%+Ch%*Y%(I%)
    Mx%=Max(Mx%,Tx%(I%)+Cb%*L%(I%))
    My%=Max(My%,Ty%(I%))
  Next I%
  If F%
    Get X%-10,Y%-10-Ch%,Mx%+10,My%+10,Temp$
  Endif
  Deffill 1,0
  Color 1
  Pbox X%-10,Y%-10-Ch%,Mx%+10,My%+10
  Box X%-5,Y%-5-Ch%,Mx%+5,My%+5
  ' oder deffill ,2,1 pbox ...
  For I%=0 To N%
    T$=T$(I%)+Left$(I$(I%)+U$,L%(I%))
    Text X%+X%(I%)*Cb%,Y%+Y%(I%)*Ch%,T$
  Next I%
  E%=0
  T$=I$(E%)
  C%=0
  Repeat
    @E.curson
    Repeat
      Mouse Mox%,Moy%,Mok%
      K$=Inkey$
    Until Len(K$) Or Mok%
    ' --- ErgÑnzung: Eingabe-Ende durch Mausknopf 2 oder F10 --------
    If Mok%=2 Or (Asc(Right$(K$))=68 And K$<>Chr$(68))
      @E.ok
    Endif
    ' ---------------------------------------------------------------
    @E.cursoff
    If K$<>""
      If Len(K$)=1
        @E.do_char(Asc(K$))
      Else
        @E.do_scan(Asc(Right$(K$)))
      Endif
    Endif
    If Mok%
      If Mox%>=X% And Moy%>=Y%-Ch%
        If Mox%<=Mx% And Moy%<My%
          @E.do_mouse
        Endif
      Endif
    Endif
  Until E%>N% Or E%<0
  If F%
    Put X%-10,Y%-10-Ch%,Temp$
  Endif
  Erase Tx%()
  Erase Ty%()
Return
Procedure E.dsp.ln
  If Len(T$)>L%(E%)
    Out 2,7
    T$=Left$(T$,L%(E%))
  Endif
  C%=Min(C%,Len(T$),L%(E%))
  Text Tx%,Ty%,Left$(T$+U$,L%(E%))
Return
Procedure E.curson
  Tx%=Tx%(E%)
  Ty%=Ty%(E%)
  Sprite Spr$,Tx%+C%*Cb%,Ty%
Return
Procedure E.cursoff
  Sprite Spr$
Return
Procedure E.ins_char(K%)
  Do
    Exit If V%(E%)=1 And Instr("0123456789",Chr$(K%))=0
    Exit If V%(E%)=2 And Instr("0123456789/()-",Chr$(K%))=0
    If Insflg! Or C%=Len(T$)
      T$=Left$(T$,C%)+Chr$(K%)+Mid$(T$,C%+1)
    Else
      Mid$(T$,C%)=Chr$(K%)
    Endif
    Inc C%
    @E.dsp.ln
    Goto E.insx
  Loop
  Out 2,7
  E.insx:
Return
Procedure E.do_char(K%)
  V%=Instr(Spec$,Chr$(K%))
  If V%
    On V% Gosub E.backs,E.enter,E.esc
  Else
    @E.ins_char(K%)
  Endif
Return
Procedure E.backs
  If C%>0
    T$=Left$(T$,C%-1)+Mid$(T$,C%+1)
    Dec C%
    @E.dsp.ln
  Endif
Return
Procedure E.enter
  I$(E%)=T$
  Inc E%
  T$=I$(E%)
  C%=0
Return
Procedure E.esc
  T$=""
  C%=0
  @E.dsp.ln
Return
' +++++++++++++++++++++++++
Procedure E.do_scan(K%)
  V%=Instr(Sp.scan$,Chr$(K%))
  If V%
    On V% Gosub E.up,E.lft,E.dwn,E.rgt,E.insert,E.ok
  Else
  Endif
Return
' -------------- ErgÑnzung SchnellbestÑtigung---------
Procedure E.ok ! wenn Maustaste 2 gedrÅckt
  @E.enter     ! Zeile sofort Åbernehmen (auch ohne Enter)
  E%=N%+1      ! Abbruchbedingung erfÅllt
Return
' ---------------------------------------------------
Procedure E.up
  I$(E%)=T$
  If E%
    Dec E%
  Else
    E%=N%
  Endif
  T$=I$(E%)
  C%=Len(T$)
Return
Procedure E.dwn
  I$(E%)=T$
  If E%<N%
    Inc E%
  Else
    E%=0
  Endif
  T$=I$(E%)
  C%=Len(T$)
Return
Procedure E.lft
  If C%
    Dec C%
  Endif
Return
Procedure E.rgt
  If C%<Len(T$)
    Inc C%
  Endif
Return
Procedure E.insert
  Insflg!=Not Insflg!
Return
Procedure E.do_mouse
  Qx%=(Mox%-X%)/Cb%
  Qy%=(Moy%-Y%)/Ch%+1
  I%=0
  Repeat
    If Qy%=Y%(I%)
      Q%=Qx%-X%(I%)-Len(T$(I%))
      If (Q% And 255)<=L%(I%)
        Goto E.dom.ok
      Endif
    Endif
    Inc I%
  Until I%>N%
  If 0
    E.dom.ok:
    I$(E%)=T$
    E%=I%
    T$=I$(E%)
    C%=Min(Q%,Len(T$))
    @E.curson
    @E.dsp.ln
  Endif
Return
Rem ************** Schnittstelle zu Eigenprogrammen ***************+
Procedure Schnittstelle
  For I%=0 To 2        ! Originalbeispiel zum Bildschirm-Ausdruck der 3
    ' Print T$(I%)'I$(I%)! eingegebenen Datenzeilen untereinander
    ' I$(I%)=""          ! T$ ist der Kopftext (vorgegeben)
  Next I%              ! I$ ist der eingegebene Text; setzt man I$="",
  Print                ! so ist anschlieûend die Box wieder frei !
Return
'
'
'
'
' ---------------------------------------------------------------------
Procedure Druckerroutine
  ' **************************************************************************
  ' ****************   Color-P6-Teilhardcopy- Proc.Komplex  ******************
  ' ****************  mit Druckerkontrolle,Abbruch mit ESC  ******************
  ' ****************  Gernot Dirnsteier, Falkenweg 7        ******************
  ' ****************         8313 Vilsbiburg                ******************
  ' ********************* Version vom 20.5.88 /1.00  *************************
  ' ------- Maûangaben fÅr Drucker etc. --------
  Rand%=0         ! Randabst.(Hardcopy-EinrÅcken) -Voreinstellg.(*0.5 mm)
  Start.y%=0      ! Startkoordinaten Harcopybild
  Start.x%=0      !  links/oben
  If Ueb_flag%=1
    Start.y%=Sy%-17 ! Startkoordinaten Harcopybild bei Dreierplan
    Start.x%=0      !  links/oben
  Endif
  Schluss.x%=6*H.br%+Sx%+6  ! Endkoordinaten
  Schluss.y%=10*H.ho%+Sy%+6 ! rechts/unten
  '
  ' ------------- Druckeranweisungen ---------------------------
  Spaltenfaktor%=2  ! fÅr einfache Dichte=1, doppelte Dichte=2 ...
  Sn%=6      ! fÅr 24-Nadel-Druck DD! (einfache Dichte->3 doppelte Dichte->6 ..)
  Modus%=39  ! ...180 DPI !.. ( 33 = 120 DPI, 40 = 360 DPI )
  '
  Spaltenzahl%=Schluss.x%-Start.x%
  Zeilenzahl%=Int((Schluss.y%-Start.y%+11)/12)
  N2%=Spaltenzahl%*Spaltenfaktor%/256
  N1%=Spaltenzahl%*Spaltenfaktor%-(N2%*256)
  @H.ardcopy
Return
'
' *********** Routine Hardcopy fÅr NEC P6  12-Punkt: ********************
' *********** Abtastung, Auswertung , Hardcopy  *************************
Procedure H.ardcopy
  ' ------- Farbwarnung -----
  If Farbflag1%       ! wenn Farbe gewÅnscht...dann Flag setzen!!
    Farbwarnflag%=1   ! Warnflag% (wieder) setzen
  Endif
  If Farbwarnflag%=1
    Pause 5
    @Gong5
    Alert 1,"  FARBMODUS eingestellt!| |Bitte ggf. Farbband einlegen|und AnschlagstÑrke reduzieren!",1,"O.K.",Dummy%
    Farbwarnflag%=0
  Endif
  ' --------------------------
  @Hardcopyhinweis
  '
  '
  '
  E.xitflag%=0
  Hidem
  Zeile$=Space$(Sn%*Spaltenzahl%)
  G$=Chr$(27)+"$"+Chr$(Rand%)+Chr$(0)+Chr$(27)+"*"+Chr$(Modus%)+Chr$(N1%)+Chr$(N2%)
  '
  '
  Open "",#99,"LST:"
  For K%=0 To Zeilenzahl%-1
    Exit If E.xitflag%=1
    Drx%=Varptr(Zeile$)
    '
    '
    For I%=0 To Spaltenzahl%-1
      ' --------
      @Drucker_online(0)
      ' ---------
      If Inkey$=Chr$(27)
        Gosub Abbruch
      Endif
      Exit If E.xitflag%=1
      ' -----
      Suz1%=0
      Suz2%=0
      Suz3%=0
      For M%=0 To 11
        Z1%=0
        Z2%=0
        Z3%=0
        P%=Point(Start.x%+I%,Start.y%+K%*12+M%)*12-M%
        If P%=12
          Z1%=192
          Z2%=0
          Z3%=0
        Endif
        If P%=11
          Z1%=48
          Z2%=0
          Z3%=0
          Goto Frag.ende
        Endif
        If P%=10
          Z1%=12
          Z2%=0
          Z3%=0
          Goto Frag.ende
        Endif
        If P%=9
          Z1%=3
          Z2%=0
          Z3%=0
          Goto Frag.ende
        Endif
        If P%=8
          Z1%=0
          Z2%=192
          Z3%=0
          Goto Frag.ende
        Endif
        If P%=7
          Z1%=0
          Z2%=48
          Z3%=0
          Goto Frag.ende
        Endif
        If P%=6
          Z1%=0
          Z2%=12
          Z3%=0
          Goto Frag.ende
        Endif
        If P%=5
          Z1%=0
          Z2%=3
          Z3%=0
        Endif
        If P%=4
          Z1%=0
          Z2%=0
          Z3%=192
          Goto Frag.ende
        Endif
        If P%=3
          Z1%=0
          Z2%=0
          Z3%=48
          Goto Frag.ende
        Endif
        If P%=2
          Z1%=0
          Z2%=0
          Z3%=12
          Goto Frag.ende
        Endif
        If P%=1
          Z1%=0
          Z2%=0
          Z3%=3
        Endif
        Frag.ende:
        Suz1%=Suz1%+Z1%
        Suz2%=Suz2%+Z2%
        Suz3%=Suz3%+Z3%
      Next M%
      ' ---------------------------------------------
      For Pi%=1 To Spaltenfaktor%   ! fÅr doppelte Dichte, sonst ohne For-next
        Poke Drx%,Suz1%
        Inc Drx%
        Poke Drx%,Suz2%
        Inc Drx%
        Poke Drx%,Suz3%
        Inc Drx%
      Next Pi%
      ' ----------------------------------------------
      '
      '
    Next I%
    '
    '
    If Farbflag%=0
      If K%=3
        Print #99,Fa$;                ! Farbe
      Endif
    Endif
    '
    Exit If E.xitflag%=1
    '
    Print #99,G$;Zeile$;Chr$(13);
    Print #99,Chr$(27);"J";Chr$(24);
    Pause 10
  Next K%
  '
  '
  Close #99
  ' +++ Papiertransport, Zeilenspeicher lîschen, Drucker in Grundstellung +++++++
  '
  '
  If Rausflag%=0
    Lprint Chr$(24);       ! Zeilenpuffer leeren
    Lprint Chr$(28);"@";   ! Drucker-RÅckstellung
    Lprint Chr$(27);"A";Chr$(Et.abst%) ! Transport + Z.speicherlîschen
  Endif
  Rausflag%=0
  '
  Sput Screen$
Return
' -----------------------------------------------------------------
Procedure Drucker_online(Don%)
  If Out?(Don%)=False
    Repeat
      @Gong5
      Alert 3," DRUCKER NICHT ONLINE: | | e i n s c h a l t e n !",1," A H A|Abbruch",Onl%
      If Onl%=2
        E.xitflag%=1
        Rausflag%=1
        Pause 5
      Endif
    Until Out?(Don%)<>0 Or E.xitflag%=1
  Endif
Return
' ----------------------------------------------------------------
Procedure Druckerkontrolle
  Rem
  Rem ++++++++ Druckerabfrage ++++++++++++
  Druckerwarnung:
  If Out?(0)=False
    Rem +++++++ Pfiff 3 (mittelhoch)+++++++++
    For Pfiff=1 To 3
      Sound 1,15,#30
      Sound 2,15,#16
      Wave 3,3,4,1500
      Pause 10
      Wave 0,0,0
    Next Pfiff
    Rem +++++++++++++++++++++++++++++++++++++
    Alert 3,"   Schalten Sie den  | |DRUCKER EIN bzw. ONLINE !",1,"DRUCKEN|Abbruch",Abbruchabfrage%
  Endif
  If Abbruchabfrage%=2
    Abbruchabfrage%=0
    E.xitflag%=1
    Druckabbruch%=1
    Goto Druckerkontroll_ende
  Else
    Druckabbruch%=0
  Endif
  If Out?(0)=0
    Goto Druckerwarnung
  Endif
  Druckerkontroll_ende:
Return
Rem *************************************************************
' ------------------------------------------------------------------
Procedure Abbruch
  Alert 2,"Drucken abbrechen ?",1,"JA|NEIN",Dummy%
  If Dummy%=1
    Showm
    While Out?(0)=False
      @Drucker_online(0)
    Wend
    E.xitflag%=1
    ' ************************************************************************
    For I%=1 To 2
      @Gong5
    Next I%
    ' ************************************************************************
  Endif
Return
' ************************************************************************
Procedure E.xit
  @Gong5
  Alert 2,"PROGRAMM BEENDEN ?",2,"EXIT|Weiter",Dummy%
  If Dummy%=1
    If Out?(0)=True
      Lprint Chr$(24)       ! Zeilenpuffer leeren
      Lprint Chr$(28);"@"   ! Drucker-RÅckstellung, wenn online
    Endif
    Cls
    End
  Endif
Return
' ************************************************************************
Procedure Gong5
  Sound 1,15,10,5
  Wave 1,1,9,25000
  Pause 15
Return
' *************** Farbeinstellung; Åber on..gosub wÑhlen! *****************
' -------------------------- Farbe -------------------------
Procedure Fa1
  Fa$=Chr$(27)+"r"+Chr$(0)     ! schwarz
  Farbflag1%=0
Return
Procedure Fa2
  Farbflag1%=1
  Fa$=Chr$(27)+"r"+Chr$(1)     ! rot
Return
Procedure Fa3
  Farbflag1%=1
  Fa$=Chr$(27)+"r"+Chr$(5)     ! orange
Return
Procedure Fa4
  Farbflag1%=1
  Fa$=Chr$(27)+"r"+Chr$(2)     ! blau
Return
Procedure Fa5
  Farbflag1%=1
  Fa$=Chr$(27)+"r"+Chr$(3)     ! violett
Return
Procedure Fa6
  Farbflag1%=1
  Fa$=Chr$(27)+"r"+Chr$(6)     ! grÅn
Return
Procedure Fa7
  Farbflag1%=1
  Fa$=Chr$(27)+"r"+Chr$(4)     ! gelb
Return
Procedure Fa8
  Farbflag1%=1
  Fa$=Chr$(27)+"r"+Chr$(7)     ! braun
Return
' ******************************************************************
' **************** Auswahlboxen mit Mausklick anwÑhlen, **********************
'                 wenn eine regelmÑûige Boxreihe vorliegt
'
' öbergabepar.:  Lx%,Ly%,Ho%,Br% (=1.Box),Bz%(=Boxanzahl)
'
Procedure Mausbox(Lx%,Ly%,Br%,Ho%,Bz%)
  Local X%,Y%,K%
  Rx%=Lx%+Br%
  Ry%=Ly%+Ho%
  '
  Restore Tastenbelegung
  If Erstflag%=0
    For Bo%=0 To Bz%-1
      Read Tastenbelegg$(Bo%)
      T.offs%=(Br%-6*Len(Tastenbelegg$(Bo%)))/2
      Deftext 1,1,0,4
      Text Lx%+Bo%*Br%+T.offs%,Ry%-20,Tastenbelegg$(Bo%)
    Next Bo%
  Endif
  Restore Boxtext
  '
  Do
    '
    Taste$=Inkey$
    If Taste$<>""
      @Tastenproc
    Endif
    '
    Mouse X%,Y%,K%
    For Bo%=0 To Bz%-1
      ' ++++++++++++++++++ fÅr waagrechte Boxreihe +++++++++++++++++++++
      If Erstflag%=0
        If Bo%=0 Or Bo%=6 Or Bo%=8 Or Bo%=9
          Box Lx%+Bo%*Br%+1,Ly%+1,Rx%+Bo%*Br%-1,Ry%-1
        Endif
        If Bo%=10
          Box Lx%+Bo%*Br%+2,Ly%+2,Rx%+Bo%*Br%-2,Ry%-2
        Endif
        Box Lx%+Bo%*Br%,Ly%,Rx%+Bo%*Br%,Ry%
        Read Boxtext$(Bo%)
        B.offs%=(Br%-6*Len(Boxtext$(Bo%)))/2
        Deftext 1,0,0,4
        Text Lx%+Bo%*Br%+B.offs%,Ry%-7,Boxtext$(Bo%)
      Else
        Put 0,365,Bild2$
      Endif
      If X%>Lx%+Br%*Bo% And X%<Rx%+Br%*Bo% And Y%>Ly% And Y%<Ry% And K%=1
        Graphmode 3
        Pbox Lx%+Bo%*Br%,Ly%,Rx%+Bo%*Br%,Ry%
        Repeat
        Until Mousek=0
        Pause 5
        Pbox Lx%+Bo%*Br%,Ly%,Rx%+Bo%*Br%,Ry%
        Graphmode 1
        On Bo%+1 Gosub Proc1,Proc2,Proc3,Proc4,Proc5,Proc6,Proc7,Proc8,Proc9,Proc10,Proc11,Proc12
        '
        '
      Endif
    Next Bo%
    Exit If K%=1 Or Neubildflag%=0
  Loop
  '
Return
' *****************************************************************************
Procedure F.ileselect
  Datei$=""
  '
  Repeat
    Fileselect "\*.SPL","",Datei$
  Until Datei$<>"\" Or Datei$<>""
  '
Return
' *****************************************************************************
' *********************************************************************
Procedure Auto.extender
  Extender$="spl"
  If Instr(Datei$,".")=0
    Datei$=Datei$+"."+Extender$
  Endif
  ' If Right$(Datei$,3)<>Extender$
  ' P_os=Instr(Datei$,".")
  ' Datei$=Left$(Datei$,P_os)+Extender$
  ' Endif
Return
' *********************************************************************
' ****************** Daten laden ********************
Procedure Laden
  If Datei$<>""
    If Exist(Datei$)=0
      Alert 2," Diese Datei  | |existiert nicht !",1,"O.K.",Dummy%
      Datei$=""
    Else
      Open "I",#1,Datei$
      Input #1,Kontroll$
      If Kontroll$<>"Stundenplan"
        @Gong5
        @Gong5
        Alert 3," Das ist doch|   K E I N | Stundenplan !",1,"O.K.",Dum%
        Goto Laden_ende
      Endif
      For I%=0 To Reihe%-1
        For U%=0 To Spa%-1
          Input #1,Klasse$(U%,I%),Fach$(U%,I%),Lehrer$(U%,I%),Raum$(U%,I%)
          Anf.fach$(U%,I%)=Fach$(U%,I%)
        Next U%
        Input #1,Abk$(I%)
      Next I%
      Laden_ende:
      Close #1
    Endif
  Endif
  ' --------- RÅckstellung auf Ausgangs-Format ---------
  Sx%=30    ! Startkoord.
  If Ueb_flag%=0 ! Wenn keine Åberlagerung
    Sy%=30
    Br%=100   ! Boxbreite
    Ho%=28    ! Boxhîhe
    H.br%=Br%  ! Boxbreite und
    H.ho%=Ho%   ! -hîhe fÅr H.copy
    X1%=6*Br%+Sx%
    Y1%=10*Ho%+Sy%
    Faktor%=8 ! Offsetfaktor, 8 fÅr Normalschrift(13), 4  fÅr 6 und 4
    H.textgr%=13
    Le.textgr%=6
    Zeit.textgr%=13
  Else
    Br%=97
    Ho%=9
    H.br%=Br%  ! Boxbreite und
    H.ho%=Ho%   ! -hîhe fÅr H.copy
    On Sy_cnt% Gosub Plan1,Plan2,Plan3
  Endif
  ' -------------------------------------------------
  Put 150,5,Bild$
Return
' **************** Daten speichern ******************
Procedure Backup
  If Exist(Datei$)
    Extlage%=Instr(1,Datei$,".")
    Backupname$=Left$(Datei$,Extlage%)+"BAK"
    If Exist(Backupname$)
      Kill Backupname$
    Endif
    Altername$=Datei$
    Name Datei$ As Backupname$
  Endif
Return
'
Procedure Speichern
  If Datei$>"" And Datei$<>"\"
    Open "O",#1,Datei$
    Write #1,"Stundenplan"
    For I%=0 To Reihe%-1
      For U%=0 To Spa%-1
        Write #1,Klasse$(U%,I%),Fach$(U%,I%),Lehrer$(U%,I%),Raum$(U%,I%)
      Next U%
      Write #1,Abk$(I%)
    Next I%
    Close #1
  Endif
Return
' *****************************************************************************
Procedure Tastenproc
  '
  ' ---------- Plannummerwahl bei Dreierplan ----------
  '
  If Ueb_flag%=1  ! Wenn öberlagerungsmodus
    If Taste$="1"
      Sy_cnt%=0
      @Proc5
    Endif
    If Taste$="2"
      Sy_cnt%=1
      @Proc5
    Endif
    If Taste$="3"
      Sy_cnt%=2
      @Proc5
    Endif
  Endif
  '
  '
  ' ------- Esc , ClrHome und  Funktionstastenabfrage --------
  '
  If Asc(Right$(Taste$))=27    ! Exit
    @Proc1
  Endif
  If Asc(Right$(Taste$))=119 And Taste$<>"w"  ! wenn Control+Clr --> Disk lîschen
    @F.ilekill
  Endif
  If Asc(Right$(Taste$))=71 And Taste$<>"G"   ! Wenn Clr -->Speicher lîschen
    @Proc2
    @Hauptprozedur
  Endif
  If Asc(Right$(Taste$))=59 And Taste$<>";"   ! F1:Regenerieren"
    Ueb_flag%=0
    Neubildflag%=0
    @Hauptprozedur
  Endif
  '
  If Asc(Right$(Taste$))=60 And Taste$<>"<"  ! F2:Verteilen
    @Proc4
  Endif
  '
  If Asc(Right$(Taste$))=61 And Taste$<>"="   ! F3: Dreierplan
    @Proc5
  Endif
  If Asc(Right$(Taste$))=62 And Taste$<>">"   ! F4: Belegplan,Lehrerz.
    @Proc6
  Endif
  If Asc(Right$(Taste$))=63 And Taste$<>"?"   ! F5: Standardgrîûe
    @Proc7
  Endif
  If Asc(Right$(Taste$))=64 And Taste$<>"@"   ! F6: Privatplan
    @Proc8
  Endif
  If Asc(Right$(Taste$))=65 And Taste$<>"A"   ! F7: Laden
    @Proc9
  Endif
  If Asc(Right$(Taste$))=66 And Taste$<>"B"   ! F8: Speichern
    @Proc10
  Endif
  If Asc(Right$(Taste$))=67 And Taste$<>"C"   ! F9: Hardcopy
    @Proc11
  Endif
  If Asc(Right$(Taste$))=68 And Taste$<>"D"   ! F10: Lprint/Gabi
    @Proc12
  Endif
Return
' *****************************************************************************
Procedure Proc1
  @Gong5
  Alert 3,"Sind alle|  DATEN |gesichert ?",1,"JA|NEIN",Dum%
  If Dum%=1
    @E.xit
  Endif
Return
Procedure Proc2
  @Gong5
  Alert 2,"Wirklich alles lîschen ?",2,"lîschen|Abbruch",Dummy%
  If Dummy%=1
    For I%=0 To Reihe%-1
      For U%=0 To Spa%-1
        Klasse$(U%,I%)=""
        Fach$(U%,I%)=""
        Anf.fach$(U%,I%)=""
        Lehrer$(U%,I%)=""
        Raum$(U%,I%)=""
      Next U%
      Abk$(I%)=""
    Next I%
    Neubildflag%=0
    '
    On Sy_cnt% Gosub Plan1,Plan2,Plan3 ! auch Zwischenspeicher lîschen
    '
    ' hier nicht @Hauptprozedur,sondern nur bei Tastenabfrage !!
  Endif
Return
Procedure Proc3
  Neubildflag%=0
Return
Procedure Proc4
  ' -----------------------------------------------------------------
  If Ueb_flag%=1 And (Sy_cnt%=1 Or Sy_cnt%=2)
    @Gong5
    Alert 1,"Erst PLAN 3 aktivieren !",1,"A H A",Dum%
  Else
    If Ueb_flag%=1 And Sy_cnt%=3
      Sy_cnt%=2
      @Verteilung
    Endif
  Endif
  If Ueb_flag%=0
    @Gong5
    Alert 1," | Vergleichsmodus (F3)    |      n i c h t |        aktiv",1,"A H A",Dum%
  Endif
  ' ------------------------------------------------------------------
Return
Procedure Proc5   ! ---------->  öberlagern
  Bel_flag%=0
  '
  If Firstueb%=0
    @Plan1
    Sy_cnt%=0
    Deffill 0
    Pbox 0,0,639,365
    Firstueb%=1
  Endif
  '
  '
  Sy%=20
  Ueb_flag%=1
  Br%=97   ! Boxbreite
  Ho%=9    ! Boxhîhe
  H.br%=Br%  ! Boxbreite und
  H.ho%=Ho%   ! -hîhe fÅr H.copy
  If Sy_cnt%=3
    Sy_cnt%=0
  Endif
  If Sy_cnt%=0
    Sy%=Sy%+11*Ho%*Sy_cnt%
  Else
    Sy%=Sy%+11*Ho%*Sy_cnt%+20*Sy_cnt%
  Endif
  '
  Inc Sy_cnt%
  '
  On Sy_cnt% Gosub Neuplan1,Neuplan2,Neuplan3
  '
  Deftext 1,0,0,6
  Deffill 0
  Pbox 0,Sy%-24,Sx%+12*Br%+6,Sy%+10*Ho%+6
  Neubildflag%=0
  @Textgroesse
  Deffill 0
  Pbox 618,0,639,365
  Deffill 1
  Pbox 618,Sy%,637,Sy%+11*Ho%-10
  '
  Deftext 1,0,0,13
  Text 624,40+120*(Sy_cnt%-1),Sy_cnt%
  '
  Neubildflag%=0
  @Hauptprozedur
  Ueb_flag%=0
Return
Procedure Proc6
  Bel_flag%=1
  Ueb_flag%=0
  Firstueb%=0
  Sx%=30    ! Startkoord.
  Sy%=30
  Br%=55   ! Boxbreite
  Ho%=30   ! Boxhîhe
  H.br%=Br%  ! Boxbreite und
  H.ho%=Ho%   ! -hîhe fÅr H.copy
  Faktor%=8 ! Offsetfaktor, 8 fÅr Normalschrift(13), 4  fÅr 6 und 4
  H.textgr%=6
  Le.textgr%=6
  Zeit.textgr%=6
  Abk.flag%=1 !---> Restore Tag.abk.
  Cls
  Neubildflag%=0  !
  @Hauptprozedur
  Bel_flag%=0
Return
Procedure Proc7    ! wieder Standardgrîûe
  Firstueb%=0
  Bel_flag%=0
  Ueb_flag%=0
  Sy_cnt%=0
  Kurzflag%=0
  Sx%=30    ! Startkoord.
  Sy%=30
  Br%=100   ! Boxbreite
  Ho%=28    ! Boxhîhe
  H.br%=Br%  ! Boxbreite und
  H.ho%=Ho%   ! -hîhe fÅr H.copy
  Faktor%=8 ! Offsetfaktor, 8 fÅr Normalschrift(13), 4  fÅr 6 und 4
  H.textgr%=13
  Le.textgr%=6
  Zeit.textgr%=13
  Cls
  Neubildflag%=0
  @Hauptprozedur
Return
Procedure Proc8
  Ueb_flag%=0
  Firstueb%=0
  Sx%=30
  Sy%=85
  Br%=80   ! Boxbreite
  Ho%=24   ! Boxhîhe
  H.br%=Br%  ! Boxbreite und
  H.ho%=Ho%   ! -hîhe fÅr H.copy
  Faktor%=8 ! Offsetfaktor, 8 fÅr Normalschrift(13), 4  fÅr 6 und 4
  '  H.textgr%=13
  '  Le.textgr%=6
  '  Zeit.textgr%=6
  @Textgroesse
  Abk.flag%=1 !---> Restore Tag.abk.
  Cls
  Neubildflag%=0  !
  Deftext 1,0,0,6
  For I%=1 To 2
    For Ii%=0 To 4
      Text 35+97*Ii%,27*I%,"Kl.___:___"
    Next Ii%
  Next I%
  Box 30,7,30+6*Br%,60
  @Hauptprozedur
Return
Procedure Proc9
  @Diskhinweis
  Deftext 1,0,0,13
  Text 250,38," L  A  D  E  N "
  @F.ileselect
  If Datei$>"" And Datei$<>"\"
    @Laden
    If Ueb_flag%=0
      Cls
    Endif
    Neubildflag%=0
    @Hauptprozedur
  Endif
  Put 150,5,Bild$
  Datei$=""
Return
Procedure Proc10
  @Diskhinweis
  Deftext 1,0,0,13
  Text 230,38," S P E I C H E R N  "
  @F.ileselect
  If Datei$>"" And Datei$<>"\"
    @Backup
    @Auto.extender
    @Speichern
  Endif
  Put 150,5,Bild$
Return
Procedure Proc11
  @Druckerroutine
  K%=1
Return
Procedure Proc12
  @Grossausdruck
Return
'
' *********************************************************************
Procedure F.ilekill
  @Diskhinweis
  Text 240,38," L  ô  S C H E  N "
  Fileselect "\*.SPL","",F.kill$
  '
  If F.kill$<>"\" And F.kill$>""
    K.illwarnung$="Datei "+F.kill$+"|wirklich lîschen ?"
    Alert 2,K.illwarnung$,2,"JA|NEIN",Dummy
    If Dummy=1
      Kill F.kill$
    Endif
  Endif
  Put 150,5,Bild$
  '
Return
' *********************************************************************
'
Procedure Diskhinweis
  Get 150,5,500,60,Bild$
  Deffill 0
  Pbox 155,12,485,55
  Box 157,15,482,50
  Box 159,17,480,48
Return
' ****************************************************************************
Procedure Plan1
  For I%=0 To Reihe%-1
    For U%=0 To Spa%-1
      Klasse1$(U%,I%)=Klasse$(U%,I%)!
      Fach1$(U%,I%)=Fach$(U%,I%)!
      Raum1$(U%,I%)=Raum$(U%,I%)!
      Lehrer1$(U%,I%)=Lehrer$(U%,I%)!
      Anf.fach1$(U%,I%)=Anf.fach$(U%,I%)
      Abk1$(U%)=Abk$(U%)
    Next U%
  Next I%
Return
Procedure Plan2
  For I%=0 To Reihe%-1
    For U%=0 To Spa%-1
      Klasse2$(U%,I%)=Klasse$(U%,I%)!
      Fach2$(U%,I%)=Fach$(U%,I%)!
      Raum2$(U%,I%)=Raum$(U%,I%)!
      Lehrer2$(U%,I%)=Lehrer$(U%,I%)!
      Anf.fach2$(U%,I%)=Anf.fach$(U%,I%)
      Abk2$(U%)=Abk$(U%)
    Next U%
  Next I%
Return
Procedure Plan3
  For I%=0 To Reihe%-1
    For U%=0 To Spa%-1
      Klasse3$(U%,I%)=Klasse$(U%,I%)!
      Fach3$(U%,I%)=Fach$(U%,I%)!
      Raum3$(U%,I%)=Raum$(U%,I%)!
      Lehrer3$(U%,I%)=Lehrer$(U%,I%)!
      Anf.fach3$(U%,I%)=Anf.fach$(U%,I%)
      Abk3$(U%)=Abk$(U%)
    Next U%
  Next I%
Return
' ------------------
Procedure Neuplan1
  For I%=0 To Reihe%-1
    For U%=0 To Spa%-1
      Klasse$(U%,I%)=Klasse1$(U%,I%)!
      Fach$(U%,I%)=Fach1$(U%,I%)!
      Raum$(U%,I%)=Raum1$(U%,I%)!
      Lehrer$(U%,I%)=Lehrer1$(U%,I%)!
      Anf.fach$(U%,I%)=Anf.fach1$(U%,I%)
      Abk$(U%)=Abk1$(U%)
    Next U%
  Next I%
Return
Procedure Neuplan2
  For I%=0 To Reihe%-1
    For U%=0 To Spa%-1
      Klasse$(U%,I%)=Klasse2$(U%,I%)!
      Fach$(U%,I%)=Fach2$(U%,I%)!
      Raum$(U%,I%)=Raum$(U%,I%)!
      Lehrer$(U%,I%)=Lehrer2$(U%,I%)!
      Anf.fach$(U%,I%)=Anf.fach2$(U%,I%)
      Abk$(U%)=Abk2$(U%)
    Next U%
  Next I%
Return
Procedure Neuplan3
  For I%=0 To Reihe%-1
    For U%=0 To Spa%-1
      Klasse$(U%,I%)=Klasse3$(U%,I%)!
      Fach$(U%,I%)=Fach3$(U%,I%)!
      Raum$(U%,I%)=Raum3$(U%,I%)!
      Lehrer$(U%,I%)=Lehrer3$(U%,I%)!
      Anf.fach$(U%,I%)=Anf.fach3$(U%,I%)
      Abk$(U%)=Abk3$(U%)
    Next U%
  Next I%
Return
' **********************************************************************
Procedure Verteilung
  @Gong5
  ' --------------------
  Fachbezeichnungen$=Upper$(Fachbezeichnung1$+"|"+Fachbezeichnung2$)
  Alert 2," Fachselektion fÅr die | |ö B E R L A G E R U N G",1,"NICHTS|"+Fachbezeichnungen$,Dum%
  If Dum%=1
    Fachkontrollflag%=0
  Endif
  If Dum%=2
    Fachkontrollflag%=1
    Fachkontroll1$=Left$(Fachbezeichnung1$,2)
    Fachkontroll2$=Left$(Fachbezeichnung1$,2)
  Endif
  If Dum%=3
    Fachkontrollflag%=1
    Fachkontroll1$=Left$(Fachbezeichnung2$,2)
    Fachkontroll2$=Left$(Fachbezeichnung2$,2)
  Endif
  ' ---------------------
  For I%=0 To Reihe%-1
    For U%=0 To Spa%-1
      @Fachkontrolle
      If Klasse1$(U%,I%)>"" And Left$(Klasse1$(U%,I%))<>"*" And (Klasse2$(U%,I%)="" Or Left$(Klasse2$(U%,I%))="*" Or Left$(Klasse2$(U%,I%),2)<>Fachkontroll2$)
        ' ------
        If Left$(Fach1$(U%,I%),2)=Fachkontroll1$
          ' -------
          If Klasse3$(U%,I%)="" Or Klasse3$(U%,I%)="*****"
            Klasse3$(U%,I%)=Klasse1$(U%,I%)
            Fach3$(U%,I%)=Fach1$(U%,I%)
            Raum3$(U%,I%)=Raum1$(U%,I%)
            Lehrer3$(U%,I%)=Lehrer1$(U%,I%)
            Anf.fach3$(U%,I%)=Anf.fach1$(U%,I%)
            Abk3$(U%)=Abk1$(U%)
          Endif
        Endif
      Endif
      '
      If (Klasse1$(U%,I%)="" Or Left$(Klasse1$(U%,I%))="*" Or Left$(Klasse1$(U%,I%),2)<>Fachkontroll1$) And Klasse2$(U%,I%)>"" And Left$(Klasse2$(U%,I%))<>"*"
        ' ------
        If Left$(Fach2$(U%,I%),2)=Fachkontroll2$
          ' --------
          If Klasse3$(U%,I%)="" Or Klasse3$(U%,I%)="*****"
            Klasse3$(U%,I%)=Klasse2$(U%,I%)
            Fach3$(U%,I%)=Fach2$(U%,I%)
            Raum3$(U%,I%)=Raum2$(U%,I%)
            Lehrer3$(U%,I%)=Lehrer2$(U%,I%)
            Anf.fach3$(U%,I%)=Anf.fach2$(U%,I%)
            Abk3$(U%)=Abk2$(U%)
          Endif
        Endif
      Endif
      If (Klasse1$(U%,I%)>"" And Left$(Klasse1$(U%,I%))<>"*") And (Klasse2$(U%,I%)>"" And Left$(Klasse2$(U%,I%))<>"*")
        ' If Klasse3$(U%,I%)="" Or Klasse3$(U%,I%)="*****"
        If Left$(Fach1$(U%,I%),2)=Fachkontroll1$ And Left$(Fach2$(U%,I%),2)=Fachkontroll2$
          Klasse3$(U%,I%)="*****"
          Fach3$(U%,I%)=""
          Raum3$(U%,I%)=""
          Lehrer3$(U%,I%)=""
          Anf.fach3$(U%,I%)=""
          Abk3$(U%)=""
        Endif
      Endif
    Next U%
  Next I%
  Fachkontrollflag%=0  ! keine Fachbegrenzung mehr
  @Proc5
Return
' **********************************************************************
Procedure Fachkontrolle
  If Fachkontrollflag%=0
    Fachkontroll1$=Left$(Fach1$(U%,I%),2)
    Fachkontroll2$=Left$(Fach2$(U%,I%),2)
  Endif
Return
' **********************************************************************
Procedure Grossausdruck
  Alert 2,"KLASSEN-Plan| oder|FACHRAUM-Plan ?",2,"KLASSE|FACHRAUM",Wahl%
  Sget Screen$
  @Gong5
  Alert 2," GABRIELE 9009 seriell  |        oder|   NEC P6/7 parallel ?       ",1,"GABI|NEC P6/7|Abbruch",Dum%
  If Dum%=1
    Gabi_flag%=1
    Nec7_flag%=0
    Nec6_flag%=0
    Planhinweis1$="1. Stundenplan auf A3-Marken|"
    Planhinweis2$="2. Hîheneinst. auf Zeilenmitte|"
    Planhinweis3$="3. - Umlaute z.Zt. vermeiden -|"
    Planhinweis4$="4. GABI 9009 online schalten !"
    Planhinweis$=Planhinweis1$+Planhinweis2$+Planhinweis3$+Planhinweis4$
    Alert 1,Planhinweis$,1,"A H A|Abbruch",Dum%
    Einrueck%=7
    @Gong5
    Kanal$="Aux:"
    Open "O",#1,Kanal$  ! seriellen Kanal îffnen !
  Endif
  If Dum%=2
    Alert 1,"    Haben Sie   |den breiten  NEC P7|       oder|den schmalen NEC P6 ?",2," P 7 | P 6 |Abbruch",Ddum%
    If Ddum%=3
      Goto Ende
    Endif
    If Ddum%=2
      Nec6_flag%=1
    Endif
    If Ddum%=1
      Nec7_flag%=1
    Endif
    Gabi_flag%=0
    @Drucker_online(0)
  Endif
  If Dum%=3
    Goto Ende
  Endif
  If E.xitflag%=1
    Goto Ende
  Endif
  @Druckhinweis
  '
  '
  For Re%=0 To 9
    Planzeile$=Planzeit$(Re%)+"  "
    For Sp%=0 To 6
      If Wahl%=1
        Planstring$=Fach$(Sp%,Re%)+"  "+Lehrer$(Sp%,Re%)  ! Klassenplan
      Else
        Planstring$=Klasse$(Sp%,Re%)+"  "+Lehrer$(Sp%,Re%) ! Fachraumplan
      Endif
      If Sp%<5
        If Nec6_flag%=1
          Leerstring$=Space$((11-Len(Planstring$))/2)
        Else
          Leerstring$=Space$((17-Len(Planstring$))/2)
        Endif
      Else
        Leerstring$=" "
      Endif
      Planblock$=Leerstring$+Planstring$+Leerstring$
      Planzeile$=Planzeile$+Planblock$
    Next Sp%
    If Gabi_flag%=1
      @Drucker_online(1)
      ' ------- Druckereinstellg. Gabi-------------
      If Kanal$="Aux:" And Re%>0
        Print #1,Chr$(27);Chr$(5);Chr$(24) ! ca 12 mm Zeilenvorschub
      Endif
      ' ---------------------------------------
      If Re%=7
        Print #1,Chr$(27);Chr$(10);   ! insges. 1,5 Z.RÅckschritt zur Korrektur
        Print #1,Chr$(27);Chr$(68);
      Endif
      If Re%<>6
        Print #1,Chr$(27);"W";Space$(Einrueck%)+Planzeile$  !Fett,EingerÅckt
      Endif
    Endif
    '
    If Nec7_flag%=1 Or Nec6_flag%=1
      @Drucker_online(0)
      If Re%<>6
        Lprint Space$(Einrueck%)+Planzeile$  !Fett,EingerÅckt
        Lprint
      Endif
    Endif
    '
    Planzeile$=""
  Next Re%
  If Gabi_flag%=1
    Close #1
    Gabi_flag%=0
  Endif
  Ende:
  Kanal$=""
  E.xitflag%=0
  Cnt%=0
  Sput Screen$
Return
' --------------------------------------------------------------------------
Procedure Druckhinweis
  Sget Screen$
  Deffill 0
  Pbox 140,365,500,395
  Box 140,365,500,395
  Box 142,367,498,393
  Text 220,385,"DatenÅbertragung lÑuft"
Return
Procedure Hardcopyhinweis
  @Gong5
  Sget Screen$
  Deffill 0
  Pbox 140,365,500,395
  Box 140,365,500,395
  Box 142,367,498,393
  Text 180,385,"Hardcopy lÑuft -- Abbruch mit 'Esc'"
Return
' ****************************************************************************
' ******************* Procedure fÅr Schatten-Boxen ***************************
'
Procedure Shadowbox(Xsl%,Ysl%,Xsr%,Ysr%,Schattenbr.%)
  Deffill 1,1,1
  Pbox Xsl%+Schattenbr.%,Ysl%+Schattenbr.%,Xsr%+Schattenbr.%,Ysr%+Schattenbr.%
  Deffill ,0,0
  Pbox Xsl%,Ysl%,Xsr%,Ysr%
Return
'
' ****************************************************************************
' ***************************************************************************
Tag.voll:
Data Montag,Dienstag,Mittwoch,Donnerstag,Freitag,Info.
Tag.abk.:
Data Mo.,Di.,Mi.,Do.,Fr.,Inf.
Zeit:
Data 1., 8.00- 8.45,2., 8.45- 9.30,3., 9.30-10.15,4.,10.35-11.20,5.,11.20-12.05
Data 6.,12.05-12.50,,,7.,13.45-14.30,8.,14.30-15.15,9.,15.15-16.00
Tastenbelegung:
Data Esc,^Clr,F1,F2,F3,F4,F5,F6,F7,F8,F9,F10
Boxtext:
Data EXIT,CLEAR,REGEN.,VERTLG.,PL1/2/3,BELEGPL.,N O R M,PRIVAT,LADEN,-> DISK,H.COPY,LPRINT
