'
' proceduren um das eintreffen von daten an der MIDI-Schnittstelle zu Åberwachen
' und ggf. auszugeben.
' analog dazu die datenausgabe Åber die MIDI-Schnittstelle
'
' 10.04.1991  Sandro Lucifora  fÅr  TOS
'
EVERY 4 GOSUB midi_input                ! alle 1.2 Sekunden procedure anspringen
ON MENU KEY GOSUB taste                 ! bei tastendruck in procedure
REPEAT                                  ! wiederhole solange, ..,
  ON MENU
UNTIL raus!                             ! ... bis raus!=TRUE
EDIT                                    ! editor
'
PROCEDURE taste
  '  PRINT MENU(13),BYTE(MENU(14)),SHR(MENU(14),8)
  IF SHR(MENU(14),8)=49                 ! taste 'n' oder 'N'
    CLS                                 ! bildschirm lîschen
    PRINT "Bitte den gewÅnschten Text eingeben (max. 125 Zeichen) :"
    FORM INPUT 125,midi_ausgabe$        ! eingabe auf 125 Zeichen begrenzt
    IF midi_ausgabe$<>""                ! wenn etwas eingegeben ...
      OPEN "o",#1,"mid:"                ! datenkanal îffnen
      PRINT #1,midi_ausgabe$            ! string Åber datenkanal ausgeben
      CLOSE #1                          ! datenkanal schlieûen
      PRINT "Daten Åbermittelt !"       ! information ausgeben
    ELSE                                ! ... ansonsten ...
      PRINT "Daten nicht Åbermittelt !" ! information ausgeben
    ENDIF
  ELSE IF SHR(MENU(14),8)=97            ! UNDO
    raus!=TRUE                          ! flag setzen
  ENDIF
RETURN
PROCEDURE midi_input
  midi_input$=INPMID$                   ! inhalt aus dem MIDI-puffer Åbernehmen
  IF LEN(midi_input$)>0                 ! wenn daten vorhanden ...
    CLS                                 ! bildschirmlîschen
    daten$=midi_input$
    ALERT 3,"Es sind Daten Åber die |  MIDI-Schnittstelle |      gekommen.",1," Zeigen | Weiter ",wahl| ! information ausgeben
    IF wahl|=1                          ! wenn zeigen gewÑhlt
      PRINT AT(1,1);"Folgende Daten sind gekommen :" !\
      PRINT daten$                                   ! > daten ausgeben
      PRINT AT(1,10);"Gesamte LÑnge : ";LEN(daten$)  !/
    ENDIF
  ENDIF
RETURN
