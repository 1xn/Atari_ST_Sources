'
' Programm um eine einen bestimmten String in einem Programm zu finden und
' hierfÅr ein Listing zum Patchen erstellen
'
' 08.11.1992  Sandro Lucifora  fÅr  TOS
'
FILESELECT #"Bitte Programm auswÑhlen","*.PRG","",wahl$   ! datei auswÑhlen
IF wahl$<>""
  @main_menu(wahl$)
ENDIF
'
PROCEDURE main_menu(pfad$)
  LOCAL file_laenge%,adr%,rueck!
  '
  text$="Test"+SPACE$(96)                                 ! text, nachdem gesucht werden soll, erstellen
  '
  OPEN "i",#1,pfad$                                       ! datei îffnen
  file_laenge%=LOF(#1)                                    ! lÑnge ermitteln
  CLOSE #1                                                ! datei schlieûen
  '
  IF MALLOC(-1)>=file_laenge%                             ! prÅfen ob genug speicherplatz in einem block
    adr%=MALLOC(file_laenge%)                             ! adresse des reservierten speicher
    BLOAD wahl$,adr%                                      ! datei in den speicher laden
    @text_suchen(text$,adr%,file_laenge%,find_adr%,rueck!)! text suchen
    IF rueck!
      @patch_lst(SUB(find_adr%,adr%),wahl$)               ! patch-File schreiben
    ELSE
      ALERT 3,"|Leider die Stelle zum Patchen|nicht gefunden !",1," Aha ",wahl%
    ENDIF
    ~MFREE(adr%)                                          ! speicher freigeben
  ELSE
    ALERT 1," |Leider kein Speicherblock in|benîtigter Grîûe vorhanden !",1,"  ENDE  ",wahl%
  ENDIF
RETURN
PROCEDURE text_suchen(text$,ab_adr%,laenge%,VAR find_adr%,rueck!)
  ' text$       = Text nach dem gesucht werden soll
  ' ab_adr%     = Adresse ab der gesucht werden soll
  ' laenge%     = LÑnge der zu durchsuchenden Datei
  '
  LOCAL i%,pos%
  pos%=1
  FOR i%=0 TO laenge%
    PRINT AT(1,1);"Suche bei Byte ";laenge%-i%;"  "
    '    OUT 5,PEEK(ab_adr%+i%)
    IF pos%=1
      IF LEFT$(text$)=CHR$(PEEK(ab_adr%+i%))
        find$=CHR$(PEEK(ab_adr%+i%))
        INC pos%
        find_adr%=ab_adr%+i%
      ENDIF
    ELSE
      IF MID$(text$,pos%,1)=CHR$(PEEK(ab_adr%+i%))
        find$=find$+CHR$(PEEK(ab_adr%+i%))
        INC pos%
        rueck!=TRUE
      ELSE
        IF pos%<LEN(text$)
          CLR find$,find_adr%
          pos%=1
          rueck!=FALSE
        ENDIF
      ENDIF
    ENDIF
  NEXT i%
RETURN
PROCEDURE patch_lst(byte%,pprg$)
  LOCAL wahl$,prg_name$
  pos%=RINSTR(pprg$,"\")
  prg_name$=RIGHT$(pprg$,LEN(pprg$)-pos%)
  FILESELECT #"Bitte Namen fÅr Patch-Prg wÑhlen","*.LST","",wahl$
  IF wahl$<>""
    OPEN "o",#2,wahl$
    PRINT #2,"' Patchprogramm fÅr "+pprg$
    PRINT #2,"' erstellt am "+DATE$+" mit MAKEPAT.PRG von"
    PRINT #2,"' Sandro Lucifora  und der Zeitschrift  TOS"
    PRINT #2,"'"
    PRINT #2,"FILESELECT #"+CHR$(34)+"Bitte Programm auswÑhlen"+CHR$(34)+","+CHR$(34)+"*.PRG"+CHR$(34)+","+CHR$(34)+prg_name$+CHR$(34)+",wahl$"
    PRINT #2,"IF wahl$<>"""
    PRINT #2,"  OPEN "+CHR$(34)+"U"+CHR$(34)+"#1,wahl$"
    PRINT #2,"  SEEK #1,";byte%
    PRINT #2,"' PRINT #1,"+CHR$(34)+CHR$(34)+";  ! >>>Hier kommt der Text hin<<<"
    PRINT #2,"  CLOSE #1"
    PRINT #2,"ENDIF"
    CLOSE #2
  ENDIF
RETURN
