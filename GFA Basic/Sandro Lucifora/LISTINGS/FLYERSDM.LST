@init_var                       ! allgemeine variablen setzen
@init_rsc                       ! resourcen anmelden
@put_rsc
@main
' ------------------------------ INIT
PROCEDURE init_var
  s_x%=WORK_OUT(0)
  s_y%=WORK_OUT(1)
  '
  akt_pfad$=CHR$(GEMDOS(25)+65)+":"+DIR$(0)+"\"  !aktueller pfad
  '
  ~FORM_DIAL(3,0,0,0,0,0,0,WORK_OUT(0),WORK_OUT(1))     ! desktop zeinen
  '
RETURN
PROCEDURE init_rsc
  IF RSRC_LOAD(akt_pfad$+"FLYERS.RSC")=0
    ALERT 3," |Konnte das RSC-File nicht laden.",1," hmmm ",wahl%
    @ende
  ENDIF
  '
  '  Resource Datei Indizes fÅr FLY_DIAL
  LET fly_dial_demo&=0       !  Formular/Dialog
  LET fd_button&=1       !  IMAGE in Baum FLY_DIAL_DEMO
  LET ok&=9       !  BUTTON in Baum FLY_DIAL_DEMO
  ' RESOURCE ENDE
  '
  ~RSRC_GADDR(0,fly_dial_demo&,fd_adr%)
RETURN
' ------------------------------ PROGRAMM
PROCEDURE put_rsc
  LOCAL dialog_x%,dialog_y%,dialog_w%,dialog_h%,wahl$,i%
  '
  ~FORM_CENTER(fd_adr%,dialog_x%,dialog_y%,dialog_w%,dialog_h%)       ! Dialog zentrieren
  '
  ~OBJC_DRAW(fd_adr%,0,7,dialog_x%,dialog_y%,dialog_w%,dialog_h%)     ! Object zeichnen
RETURN
PROCEDURE main
  REPEAT
    rueck&=FORM_DO(fd_adr%,0)
    SELECT rueck&
    CASE fd_button&
      '      STOP
      @flyers(fd_adr%)
    ENDSELECT
  UNTIL rueck&=ok&
  @ende
RETURN
'
' ------------------------------
' FLYERS ROUTINE
'
' Procedure um einen Dialog "fliegen zu lassen"
'
' 18.10.1992   Sandro Lucifora  fÅr  TOS
'
PROCEDURE flyers(tree_adr%)
  LOCAL dx%,dy%,dw%,dh%
  '
  ~WIND_UPDATE(1)
  ~WIND_UPDATE(3)
  '
  DEFMOUSE 4                                    ! Maus als Hand
  '
  dx%=OB_X(tree_adr%,0)                         !\
  dy%=OB_Y(tree_adr%,0)                         ! \ Dialog-
  dw%=OB_W(tree_adr%,0)                         ! /     Koordinaten
  dh%=OB_H(tree_adr%,0)                         !/
  '
  ~WIND_GET(0,4,desk_x%,desk_y%,desk_w%,desk_h%)! Ausmaûe des Desktop holen (WIND 0)
  ~GRAF_DRAGBOX(dw%,dh%,dx%,dy%,desk_x%,desk_y%,desk_w%,desk_h%,lx%,ly%) ! Rahmen zeichnen
  ~FORM_DIAL(3,0,0,0,0,dx%,dy%,dw%,dh%)         ! alte Dialogbox weg
  '
  OB_X(tree_adr%,0)=lx%                         ! neue x-Koordiante fÅr die Dialogbox
  OB_Y(tree_adr%,0)=ly%                         ! neue y-Koordiante fÅr die Dialogbox
  '
  ~OBJC_DRAW(tree_adr%,0,7,lx%,ly%,dw%,dh%)     ! Object neu zeichnen
  '
  ~WIND_UPDATE(2)
  ~WIND_UPDATE(0)
  '
  DEFMOUSE 0                                    ! Maus als Pfeil
RETURN
' ------------------------------
'
' ------------------------------ ENDE
PROCEDURE ende
  ~RSRC_FREE()                                  ! resource freigeben
  RESERVE
  EDIT                                          ! und zurÅck zum editor ...
RETURN
