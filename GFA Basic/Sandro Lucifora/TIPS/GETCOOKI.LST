'
' Function um einen bestimmten Cookie und dessen Wert zu ermitteln
' Procedure um alle Cookies und deren Werte auszugeben
'
' 23.09.1992   Sandro Lucifora   fÅr   TOS
'
@print_cookies
PRINT
IF @get_cookie("_CPU",cwert%)
  PRINT "Der Cookie >>_CPU<< hat den Wert ";cwert%
  PRINT "Der Prozessor ist ein ";68000+cwert%
ENDIF
'
FUNCTION get_cookie(such$,VAR cookie_wert%)
  LOCAL adr%,find$
  '
  adr%=LPEEK(1440)               ! Adresse des Cookie-Jar
  '
  IF adr%                        ! Cookie-Jar vorhanden
    '
    DO WHILE {adr%}<>0           ! Solange suchen bis zum  Nullcookie...
      find$=MKL$({adr%})
      ADD adr%,8
      '
      IF such$=find$             ! ... oder der Eintrag gefunden ist...
        cookie_wert%={SUB(adr%,4)}
        RETURN TRUE              ! Dann TRUE zurÅck
      ENDIF
      '
    LOOP
    '
  ELSE
    RETURN FALSE                 ! Keine Cookie gefunden, dann FALSE zurÅck
  ENDIF
ENDFUNC
PROCEDURE print_cookies
  LOCAL adr%,find$
  '
  adr%=LPEEK(1440)               ! Adresse des Cookie-Jar
  '
  IF adr%                        ! Cookie-Jar vorhanden
    DO WHILE {adr%}<>0           ! Solange ausgeben, bis zum Nullcookie
      '
      find$=MKL$({adr%})
      ADD adr%,8
      PRINT find$,{SUB(adr%,4)},BIN$({SUB(adr%,4)},32)
      '
    LOOP
  ENDIF
RETURN
