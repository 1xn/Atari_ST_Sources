.he      \d                Program: ANAGRAM1                   Page #
.pl60
.lr6
.n3
.ll80
REM Program "Anagram1"
'
esc$=CHR$(27)
rtn$=CHR$(13)
maxlines|=20
'
REPEAT
  CLS
  linesleft|=maxlines|
  abort!=FALSE
  INPUT "String";my_string$
  CLS
  '
  GOSUB perm(my_string$,"")
  '
  PRINT AT(1,23);"Press <Return> to restart. "
  PRINT "Press <Escape> to exit. "
  '
UNTIL @wotnext$=esc$
'
CLS
END
'
'
FUNCTION wotnext$
  LOCAL reply$
  '
  REPEAT
    reply$=INKEY$
  UNTIL (reply$=esc$ OR reply$=rtn$)
  '
  RETURN reply$
ENDFUNC
'
'
FUNCTION del_char$(f_string$,f_strlen%,posn%)
  LOCAL new_str$
  '
  new_str$=LEFT$(f_string$,posn%-1)
  new_str$=new_str$+RIGHT$(f_string$,f_strlen%-posn%)
  '
  RETURN new_str$
ENDFUNC
'
'
PROCEDURE perm(string_a.in$,string_b.in$)
  LOCAL string_a.out$,string_b.out$,strlen_a.in%,chosen$,i%
  '
  strlen_a.in%=LEN(string_a.in$)
  '
  IF strlen_a.in%<2 THEN
    PRINT string_b.in$+string_a.in$
    DEC linesleft|
    '
    IF linesleft|<1 THEN
      PRINT AT(1,23);"Press <Return> to continue."
      PRINT "Press <Escape> to abort."
      abort!=(@wotnext$=esc$)
      CLS
      linesleft|=maxlines|
    ENDIF
    '
  ELSE
    i%=1
    '
    DO UNTIL abort!
      chosen$=MID$(string_a.in$,i%,1)
      string_b.out$=string_b.in$+chosen$
      string_a.out$=@del_char$(string_a.in$,strlen_a.in%,i%)
      GOSUB perm(string_a.out$,string_b.out$)
      INC i%
      '
    LOOP UNTIL (i%>strlen_a.in%)
    '
  ENDIF
  '
RETURN
