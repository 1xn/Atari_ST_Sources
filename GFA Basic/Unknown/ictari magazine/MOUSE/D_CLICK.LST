' This is a function to let you react to doubleclicks, even
' if you're not using GEM calls at the time. A bonus is that
' you can set the time between clicks by adjusting parameter
' wait in 1/1000 seconds. It responds to both mousekeys.
' An example:
'
DO
  MOUSE mx,my,mk
  IF mk
    shift|=BYTE(BIOS(11,-1))    ! how about Shift,Contrl or Alt-keys?
    IF @d_click(20,mk)          ! TRUE: doubleclick
      PRINT "doubleclick : ";
    ELSE
      PRINT "single click: ";
      ' test for dragging actions here: WHILE MOUSEK ... ect
    ENDIF
    PRINT mk'';"shift: ";BIN$(shift|)
  ENDIF
LOOP UNTIL LEN(INKEY$)
EDIT
'
FUNCTION d_click(wait,mbutton)
  LOCAL dc_time#,dc_click!
  CLR dc_click!
  dc_time#=ADD(TIMER,wait)      ! <18 is too fast, 20 is usually correct
  WHILE TIMER<dc_time#          ! dc_time# is a FLOAT
  WEND                          ! waiting loop for mouse to clear
  IF MOUSEK<>mbutton            ! still depressed? --> user wants to drag
    dc_time#=ADD(TIMER,wait)    ! or is he doubleclicking?
    WHILE TIMER<dc_time#
      dc_click!=(MOUSEK=mbutton)
      EXIT IF dc_click!
    WEND
  ENDIF
  IF dc_click!
    WHILE MOUSEK                ! clear mousebutton
    WEND
    RETURN TRUE
  ELSE
    RETURN FALSE
  ENDIF
ENDFUNC
