' Below the settings for GfA I always use. With my compiler it is
' ESSENTIAL to tell it to OPTION BASE 0 (arrays start with element
' zero). If I don't, the compiler I use (v3.5) will initiate (unlike
' my editor) at OPTION BASE 1, leaving me with a lot of bombs..
'
OPTION BASE 0     ! 1st element of array is number ZERO
DEFWRD "a-z"      ! all variables with no suffix are 2-byte signed WORDS
DEFLIST 4         ! show commands in CAPS, variables in lowercase
'
' How to use PABLO proportional fonts. This routine loads
' a FTO font into a GfA string array. There are not many
' Pablo fonts in my collection, but perhaps you have some..
' Anyway, here's how to do it:
'
ON ERROR GOSUB rd_error         ! in case of  "End of file.."
FILESELECT #"Load PABLO font","\*.fto","",fsel$
IF AND(fsel$>"",RIGHT$(fsel$)<>"\")
  a$=SPACE$(8)
  OPEN "i",#1,fsel$
  BGET #1,V:a$,8
  IF LEFT$(UPPER$(a$),8)<>"PICASSO "    ! check file format
    CLOSE #1
    ALERT 1,"|This is no Pablo font.. ",1," Ok ",n
  ELSE
    LINE INPUT #1,a$    ! rest of magic: PICASSO SYSTEM FONT:
    LINE INPUT #1,fontname$
    LINE INPUT #1,a$
    bigw=VAL(a$)
    LINE INPUT #1,a$
    bigh=VAL(a$)
    blen=2*((bigw+15) DIV 16)*bigh  ! blocklength
    IF blen
      ERASE prop_ch$()
      ~FRE(0)
      DIM prop_ch$(255)
      CLR i,c
      FOR i=0 TO 255
        LINE INPUT #1,a$
        IF a$<>"0"              ! if its a character: "40"
          prop_ch$(i)=SPACE$(blen+6)
          adr%=V:prop_ch$(i)
          BGET #1,adr%,blen+6
          CARD{adr%}=PRED(CARD{adr%})           ! adjust width and height
          CARD{adr%+2}=PRED(CARD{adr%+2})       ! for GfA blocks
          ~INP&(#1)             ! dummy word closes definition
          INC c                 ! number of loaded characters
        ENDIF
      NEXT i
    ENDIF
  close_file:
    CLOSE #1
    IF c>0                      ! there WAS something loaded?
      @prop_text(10,40,"The quick brown fox jumps over the lazy dog.")
      '  PRINT @prop_len("The quick brown fox jumps over the lazy dog.")
      ~INP(2)
    ENDIF
  ENDIF
ENDIF
EDIT
'
PROCEDURE rd_error
  RESUME close_file
RETURN
'
PROCEDURE prop_text(tx,ty,tekst$)       ! writes proportional string
  LOCAL a,c
  FOR c=0 TO LEN(tekst$)-1
    a=PEEK(V:tekst$+c)
    IF prop_ch$(a)>""
      PUT tx,ty,prop_ch$(a)
      ADD tx,SUCC(CARD{V:prop_ch$(a)})
    ENDIF
    EXIT IF tx>WORK_OUT(0)
  NEXT c
RETURN
'
FUNCTION prop_len(tekst$)               ! calculates width in pixels for string
  $F&
  LOCAL a,c,l
  FOR c=0 TO LEN(tekst$)-1
    a=PEEK(V:tekst$+c)
    IF prop_ch$(a)>""
      ADD l,SUCC(CARD{V:prop_ch$(a)})
    ENDIF
  NEXT c
  RETURN l
ENDFUNC
