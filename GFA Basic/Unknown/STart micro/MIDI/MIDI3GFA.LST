' ------------------------------------------------------------------------------
'                          EXEMPLE DE PROGRAMMATION MIDI
'                                 en basic GFA
' ------------------------------------------------------------------------------
'
HIDEM                    ! Cache la souris
'
DIM note$(11)            ! On cr‚e un tableau de 0 … 11, soit 12 notes.
DIM sequence(10000)      ! Et un autre pour la sequence de notes … enregistrer.
.
FOR i|=0 TO 11           ! On lit le nom des notes dans les datas.
  READ note$(i|)
NEXT i|
'
DATA DO   ,DO#  ,RE   ,RE#  ,MI   ,FA   ,FA#  ,SOL  ,SOL# ,LA  ,LA#  , SI
'
'
@boucle                  ! Va … la boucle principal.
'
> PROCEDURE boucle
  ' ---------------------------- boucle du programme ---------------------------
  @affiche_text
  '
  DO
    '
    IF INKEY$=CHR$(13)
      '
      @enregistre
      @efface_text
      @affiche_text
      '
    ENDIF
    '
    IF INKEY$=" "
      '
      IF sequence(j%)<1
        ALERT 1," AVANT DE JOUER|ENREGISTREZ D'ABORD|QUELQUES NOTES.",1,"OK",a
        @enregistre
      ENDIF
      '
      @joue
      @efface_text
      @affiche_text
      '
    ENDIF
    '
  LOOP UNTIL INKEY$=CHR$(27)        ! Fin du programme si escape est press‚.
  '
  SHOWM                             ! Montre la souris.
  EDIT                              ! Retourne … l'editeur du GFA.
  '
RETURN
'
> PROCEDURE affiche_text
  '
  PRINT AT(1,1);" PRESSEZ RETURN POUR ENREGISTRER. "
  PRINT AT(1,2);" PRESSEZ ESPACE POUR JOUER. "
  PRINT AT(1,3);" PRESSEZ ESCAPE POUR QUITTER. "
  '
RETURN
> PROCEDURE efface_text
  '
  PRINT AT(1,1);"                                                             "
  PRINT AT(1,2);"                                                             "
  PRINT AT(1,3);"                                                             "
  PRINT AT(1,4);"                                                             "
  '
RETURN
'
' ---------------------------- PROCEDURES MIDI ---------------------------------
> PROCEDURE enregistre
  '
  @efface_text
  '
  PRINT AT(1,1);" ENREGISTREMENT, JOUEZ SVP.                    "
  '
  REPEAT
    a|=INP(3)
  UNTIL (a| AND &HF0)=144                     ! Si on re‡oit un NOTE ON
  '                                           ! On peut commencer d'enregistrer.
  sequence(0)=a|
  '
  PRINT AT(1,1);" PRESSEZ ESPACE POUR STOPER. "
  '
  FOR j%=1 TO 10000
    '
    DO                                      ! Boucle tant qu'il n'y … pas
    LOOP UNTIL INP?(3)                      ! de donn‚e dans le buffer MIDI.
    '
    sequence(j%)=INP(3)                     ! On sauvegarde la donn‚e MIDI.
    '
    IF (sequence(j%-1) AND &HF0)=144        ! Si NOTE ON commande pr‚c‚dente
      @affiche_notes                         ! On affiche la note.
    ENDIF
    '
    EXIT IF INKEY$=" "                          ! Sortie si espace est press‚.
    '
  NEXT j%
  '
RETURN
> PROCEDURE joue
  '
  @efface_text
  '
  PRINT AT(1,1);" JE JOUE LES NOTES, PRESSEZ ESPACE POUR STOPER. "
  '
  OUT 3,sequence(0)
  FOR j%=1 TO 10000
    '
    t%=TIMER                    ! Petite temporisation pour ˆtre dans le rythme.
    '
    DO
    LOOP UNTIL TIMER>t%+2
    '
    IF sequence(j%)<129
      IF (sequence(j%-1) AND &HF0)=144             ! Si on a jou‚ un NOTE ON.
        @affiche_notes
      ENDIF
      OUT 3,sequence(j%),sequence(j%+1),sequence(j%+2) ! On joue la note
      j%=j%+2
    ELSE
      OUT 3,sequence(j%)
    ENDIF
    EXIT IF INKEY$=" "          ! Sortie si espace est press‚,
  NEXT j%
  ' ou si sequence plus petit que 1.
  @efface_text
  '
RETURN
> PROCEDURE affiche_notes
  '
  PRINT AT(1,4);" Je joue un ";note$(MOD(sequence(j%),12))! On affiche la note
  '                                                       ! et son octave.
  PRINT AT(18,4);"OCTAVE: ";INT(sequence(j%)/12-2);"       "
  '
RETURN
