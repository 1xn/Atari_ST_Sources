'
'        CIRC&SPH.BCC\CIRCLES\B-SPIRAL.LST  (Draw spiral by angle theta)
'
' +----------------------------------------------------------------------------+
' |         This routine was written by Professor of VIRTUAL INFINITY,         |
' |      and is part of the "Programmer`s Circle & Sphere Routine Library"     |
' |                                                                            |
' |      The routine has been implemented in GFA 3.5e & ST low resolution,     |
' |  and may be re-implemented in other languages & resolutions **providing**  |
' | you have read & agree with the conditions listed in CIRC&SPH.BCC\INTRO.TXT |
' +----------------------------------------------------------------------------+
'
' (See also A-CIRC1.LST)
' This uses the "angle method" to draw a spiral or 'spiral ellipse' at the
' specified origin with specified radiuses.  The angle (theta) loops from 0 to
' 2*PI at the specified rate (step).  At each point the radiuses are incremented
' by the specified amounts to create the spiral effect.
'
' This routine`s calculation is **entirely** realtime.
'
CLS
'
x_abs&=160                    ! Absolute horizontal origin
y_abs&=100                    ! Absolute vertical origin
x_rad=30                      ! Initial X radius
y_rad=10                      ! Initial Y radius
step=0.02                     ! Theta increment step
x_incrad=0.02                 ! X radius increment step
y_incrad=0.05                 ! Y radius increment step
loop&=3                       ! Number of loops
'
FOR theta=0 TO loop&*2*PI STEP step
  x&=COS(theta)*x_rad
  y&=SIN(theta)*y_rad
  clip(x_abs&+x&,y_abs&+y&,15)
  '
  ADD x_rad,x_incrad
  ADD y_rad,y_incrad
NEXT theta
'
PRINT CHR$(7);
~INP(2)
EDIT
'
'
'
PROCEDURE clip(x&,y&,col&)
  IF x&>=0 AND x&<320 AND y&>=0 AND y&<200
    PSET x&,y&,col&
  ENDIF
RETURN
