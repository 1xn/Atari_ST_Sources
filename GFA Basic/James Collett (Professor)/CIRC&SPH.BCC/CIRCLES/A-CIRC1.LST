'
'         CIRC&SPH.BCC\CIRCLES\A-CIRC1.LST  (Draw circle by angle theta)
'
' +----------------------------------------------------------------------------+
' |         This routine was written by Professor of VIRTUAL INFINITY,         |
' |      and is part of the "Programmer`s Circle & Sphere Routine Library"     |
' |                                                                            |
' |      The routine has been implemented in GFA 3.5e & ST low resolution,     |
' |  and may be re-implemented in other languages & resolutions **providing**  |
' | you have read & agree with the conditions listed in CIRC&SPH.BCC\INTRO.TXT |
' +----------------------------------------------------------------------------+
'
' This uses the "angle method" to draw a circle or ellipse at the specified
' origin with specified radiuses.  The angle (theta) loops from 0 to 2*PI,
' through the four arcs, incrementing at the specified rate (step): as step
' decrements so more points are plotted; as step increments so less points are
' plotted.
'
' A disadvantage of this method over the "axis method" (C-CIRC2.LST) is speed,
' this method is slower than the "axis method".  However an advantage of this
' method ("angle method") is spirals can easily be drawn (B-SPIRAL.LST).
'
' This routine`s calculation is **entirely** realtime.
'
CLS
'
x_abs&=160                    ! Absolute horizontal origin
y_abs&=100                    ! Absolute vertical origin
x_rad&=100                    ! X radius
y_rad&=50                     ! Y radius
step=0.01                     ! Theta increment step
'
FOR theta=0 TO 2*PI STEP step
  x&=COS(theta)*x_rad&
  y&=SIN(theta)*y_rad&
  clip(x_abs&+x&,y_abs&+y&,15)
NEXT theta
'
PRINT CHR$(7);
~INP(2)
EDIT
'
'
'
PROCEDURE clip(x&,y&,col&)
  IF x&>=0 AND x&<320 AND y&>=0 AND y&<200
    PSET x&,y&,col&
  ENDIF
RETURN
