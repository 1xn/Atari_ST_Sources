' Demo on how to change a selected button back to normal
' Displays a simple dialog with two choises, EXIT & SHOW
' SHOW will display a new dialog and the EXIT button in FORM2
' will be unselected at every time the FORM2 object-tree is drawn.
'
' NB! This is not a very good example of GEM programming. It is merely
' to be used as a guide on how the OBJC_CHANGE works.
'
'
' Resource file indices for TEST. */
form1&=0        ! First dialog-box
button_1&=1     ! Show-button
button_2&=2     ! Exit button
form2&=1        ! Second dialog box
button_3&=3     ! Exit button (dialog box 2)
'
' **** CHANGE THIS FOR YOUR OWN NEEDS!!!! ****
rsc.file$="h:\rsc.tmp\test.rsc"
'
ap_id&=APPL_INIT()
IF RSRC_LOAD(rsc.file$)=0
  ALERT 1,"RSC file not found!",1,"Argh",e|
  ~APPL_EXIT()
  END
ENDIF
'
~RSRC_GADDR(0,form1&,form1%)            ! Get rsc_address
~FORM_CENTER(form1%,x&,y&,w&,h&)        ! find its center coords
~FORM_DIAL(0,x&,y&,w&,h&,x&,y&,w&,h&)   ! Reserve screen space
DO
  ~OBJC_DRAW(form1%,0,1,x&,y&,w&,h&)       ! Draw it on screen
  a&=FORM_DO(form1%,0)                  ! wait for an event
  SELECT a&
    '
  CASE button_1&                        ! SHOW-button pressed
    '
    ~FORM_DIAL(3,x&,y&,w&,h&,x&,y&,w&,h&)       ! Release previous reserved space
    ~RSRC_GADDR(0,form2&,form2%)                ! Get rsc_address to dialog 2
    ~FORM_CENTER(form2%,x2&,y2&,w2&,h2&)        ! Get it coords
    ~FORM_DIAL(0,x2&,y2&,w2&,h2&,x2&,y2&,w2&,h2&)       ! Reserve screen space for it
    ~OBJC_DRAW(form2%,0,1,x2&,y2&,w2&,h2&) ! Draw it
    a&=FORM_DO(form2%,0)                        ! Wait for event of any kind
    '
    '
    ' Here is the lines that changes a selected button from "selected"
    ' to unselected. Syntax is:
    '
    ' OBJC_CHANGE(tree,object,0,x,y,w,h,new_stat,redraw_flag)
    '
    ' tree    = tree to work on
    ' obect   = object to change
    ' 0       = reserved
    ' x,y,w,h = coords of clipping rectangle (use coords from FORM_CENTER at normal)
    ' new_stat= new object status
    ' redraw_f= redraw flag (0 - don't redraw/1 - redraw object)
    '
    ~OBJC_CHANGE(form1%,button_1&,0,x&,y&,w&,h&,0,1)    ! Change "selected" object back to normal (unselected) in dialog bo 1
    ~OBJC_CHANGE(form2%,button_3&,0,x2&,y2&,w2&,h2&,0,1)    ! Do the same with exit button in dialog box 2
    '
    ~FORM_DIAL(3,x2&,y2&,w2&,h2&,x2&,y2&,w2&,h2&)       ! Release screen space for dialog box 2
    ~FORM_DIAL(0,x&,y&,w&,h&,x&,y&,w&,h&)               ! Reserve space for dialog 1
    '
  CASE button_2&                                ! Exit selected
    exit!=TRUE
  ENDSELECT
LOOP UNTIL exit!
~RSRC_FREE()                                    ! Free RSC from mem
~APPL_EXIT()                                    ! exit cleanly..
EDIT
