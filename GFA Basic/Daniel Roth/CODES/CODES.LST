' Codes ...
' Ω 1986 sinccom software
' geschrieben von Daniel Roth
' BrÅcker Mauspfad 448
' D-5000 Kîln 91
'
' Literatur:
' Luftschlîsser und Hirngespinste
' von Albrecht Beutelspacher
' Vieweg, ISBN 3-528-08957-1 (bitte nachprÅfen)
'
' Druckroutine von BANANA COMPANY
'
Deffn Zen$(A$)=Space$(Fn Cen(A$))+A$
Deffn Cen(X$)=Int((80-Len(X$))/2)
'
Max=7
Dim Y(Max),A$(Max)
Dim Z$(9),Z(12)
Dim Ean$(2,9)
Dim Eanzei$(5,9)
'
Repeat
  Gosub Menu
Until Wahl=Max+1
End
'
Procedure Init
  Cls
  Print
  Print Fn Zen$("Codes ...")
  Print Fn Zen$("Ω 1986 sinccom software")
  Print Fn Zen$("geschrieben von Daniel Roth")
  Print Fn Zen$("EntschlÅsselung verschiedener PrÅfcodierungen")
  Restore
  For I=0 To Max
    Read Y(I),A$(I)
  Next I
  Data 128,"Codezahl ÅberprÅfen - System 1"
  Data 160,"Gewichtungen ermitteln - System 1"
  Data 192,"Codezahl ÅberprÅfen - System 2"
  Data 224,"Gewichtungen ermitteln - System 2"
  Data 256,"ISBN-Code ÅberprÅfen"
  Data 288,"EAN-Code zeichnen"
  Data 320,"Information"
  Data 352,"Ende"
  Sys=1
  For I=0 To 2
    For J=0 To 9
      Read Ean$(I,J)
    Next J
  Next I
  Data 0001101,0011001,0010011,0111101,0100011
  Data 0110001,0101111,0111011,0110111,0001011
  Data 0100111,0110011,0011011,0100001,0011101
  Data 0111001,0000101,0010001,0001001,0010111
  Data 1110010,1100110,1101100,1000010,1011100
  Data 1001110,1010000,1000100,1001000,1110100
  For I=0 To 9
    For J=0 To 5
      Read Eanzei$(J,I)
    Next J
  Next I
  Data a,a,a,a,a,a
  Data a,a,b,a,b,b
  Data a,a,b,b,a,b
  Data a,a,b,b,b,a
  Data a,b,a,a,b,b
  Data a,b,b,a,a,b
  Data a,b,b,b,a,a
  Data a,b,a,b,a,b
  Data a,b,a,b,b,a
  Data a,b,b,a,b,a
Return
'
Procedure Menu
  Gosub Init
  Deftext 1,0,0,13
  For I=0 To Max
    Print At(Fn Cen(A$(I)),8+2*I);A$(I)
  Next I
  '
  Wahl=0
  Repeat
    Y=Mousey
    For I=0 To Max
      If Y>Y(I)-16 And Y<Y(I)
        Print Chr$(27);"p";
        Print At(Fn Cen(A$(I)),8+2*I);A$(I)
        Print Chr$(27);"q";
        Wahl=I+1
        Repeat
          K=Mousek
          Y=Mousey
        Until K=1 Or Y<Y(I)-16 Or Y>Y(I)
        If K<>1
          Print At(Fn Cen(A$(I)),8+2*I);A$(I)
          Wahl=0
        Endif
      Endif
    Next I
  Until Mousek=1 And Wahl<>0
  On Wahl Gosub Pruefsys1,Gewsys1,Pruefsys2,Gewsys2,Isbn,Ean,Information
Return
'
Procedure Pruefsys1
  Cls
  Print Fn Zen$(A$((Sys-1)*2))
  Print
  Input "Nummer mit oder ohne PrÅfziffer: ",Nr$
  If Len(Nr$)<>0
    Print
    Input "Welche beiden Gewichtungen sollen alternieren: ",G1,G2
    Print
    Gew=G1
    G=G1+G2
    Quer=0
    For I=1 To Len(Nr$)
      Quer$=Str$(Val(Mid$(Nr$,I,1))*Gew)
      Gew=G-Gew
      If Sys=1
        For J=1 To Len(Quer$)
          Quer=Quer+Val(Mid$(Quer$,J,1))
        Next J
      Else
        Quer=Quer+Val(Quer$)
      Endif
    Next I
    Pz=(Int(Quer/10)+1)*10-Quer
    Taste$=""
    If Pz=10
      Print "Wollten Sie die PrÅfziffer ermitteln (j/n) ? ";
      Repeat
        Taste$=Inkey$
      Until Taste$="j" Or Taste$="n"
      Print Taste$
      Print
      If Taste$="n"
        Print "Die Nummer wurde korrekt eingegeben!"
      Endif
    Endif
    If Pz<>10 Or Taste$="j"
      If Taste$<>"j"
        Print "Entweder haben Sie die Nummer falsch eingegeben, oder Sie wollten"
        Print "die PrÅfziffer ermitteln!"
        Print
      Endif
      If Pz=10
        Pz=0
      Endif
      Print "Die PrÅfziffer lautet: ";Pz,"Quersumme: ";Quer
      Print
      Print "Die komplette Nummer lautet daher: ";Nr$;Pz
    Endif
    Repeat
    Until Inkey$<>"" Or Mousek<>0
  Endif
Return
'
Procedure Gewsys1
  Cls
  Print Fn Zen$(A$(1+(Sys-1)*2))
  Print
  Input "Geben Sie die komplette Nummer ein: ",Nr$
  If Len(Nr$)<>0
    Print
    Print "Es werden nur Gewichtungen grîûer als 0 und kleiner als 10 berechnet!"
    Print
    Pzk=Val(Right$(Nr$,1))
    Nr$=Left$(Nr$,Len(Nr$)-1)
    Aus!=False
    For G1=1 To 9
      For G2=1 To 9
        If G2<>G1
          G=G1+G2
          Gew=G1
          Quer=0
          For I=1 To Len(Nr$)
            Quer$=Str$(Val(Mid$(Nr$,I,1))*Gew)
            Gew=G-Gew
            If Sys=1
              For J=1 To Len(Quer$)
                Quer=Quer+Val(Mid$(Quer$,J,1))
              Next J
            Else
              Quer=Quer+Val(Quer$)
            Endif
          Next I
          Pz=(Int(Quer/10)+1)*10-Quer
          If Pz=10
            Pz=0
          Endif
          If Pz=Pzk
            Print "Gewichtungen ";G1;",";G2;" sind mîglich.   "
            Print "Soll weitergesucht werden (j/n) ? "
            Print Chr$(27);"A";
            Repeat
              Taste$=Inkey$
            Until Taste$="j" Or Taste$="n"
            If Taste$="n"
              Aus!=True
            Endif
          Endif
        Endif
        Exit If Aus!
      Next G2
      Exit If Aus!
    Next G1
  Endif
Return
'
Procedure Pruefsys2
  Sys=2
  Gosub Pruefsys1
  Sys=1
Return
'
Procedure Gewsys2
  Sys=2
  Gosub Gewsys1
  Sys=1
Return
'
Procedure Isbn
  Cls
  Print Fn Zen$(A$(4))
  Print
  Input "Geben Sie den ISBN-Code ein: ",Isbn$
  If Len(Isbn$)<>0
    Z=9
    For I=1 To Len(Isbn$)
      If Mid$(Isbn$,I,1)<>"-"
        Z$(Z)=Mid$(Isbn$,I,1)
        Z=Z-1
      Endif
    Next I
    If Z$(0)="x" Or Z$(0)="X"
      Z$(0)="10"
    Endif
    S=0
    For I=0 To 9
      S=S+(I+1)*Val(Z$(I))
    Next I
    Print
    Print "Der ISBN-Code wurde ";
    If S/11<>Int(S/11) Or Z<>-1
      Print "nicht ";
    Else
      Print Chr$(7);
    Endif
    Print "korrekt eingegeben!"
    Repeat
    Until Inkey$<>"" Or Mousek<>0
  Endif
Return
'
Procedure Ean
  Cls
  Print Fn Zen$(A$(5))
  Print
  Input "Geben Sie den EAN-Code ein: ",Ean$
  Print
  If Len(Ean$)=13
    Offsetx=10
    Offsety=5
    Pzk=Val(Right$(Ean$,1))
    Eanop$=Left$(Ean$,Len(Ean$)-1)
    Gew=1
    G=4
    Quer=0
    For I=1 To Len(Eanop$)
      Quer$=Str$(Val(Mid$(Eanop$,I,1))*Gew)
      Gew=G-Gew
      Quer=Quer+Val(Quer$)
    Next I
    Pz=(Int(Quer/10)+1)*10-Quer
    If Pz=10
      Pz=0
    Endif
    If Pz<>Pzk
      Print "Der EAN-Code wurde falsch eingegeben!"
    Else
      Cls
      Deftext 1,0,0,6
      Text Offsetx-10,Offsety+58,Left$(Ean$,1)
      Text Offsetx+3,Offsety+58,43,Mid$(Ean$,2,6)
      Text Offsetx+49,Offsety+58,43,Right$(Ean$,6)
      For I=0 To 12
        Z(I)=Val(Mid$(Ean$,I+1,1))
      Next I
      Line Offsetx,Offsety,Offsetx,Offsety+55
      Line Offsetx+2,Offsety,Offsetx+2,Offsety+55
      Offsetx=Offsetx-5
      For I=1 To 6
        Code$=Ean$(Asc(Eanzei$(I-1,Z(0)))-Asc("a"),Z(I))
        For J=1 To Len(Code$)
          If Mid$(Code$,J,1)="1"
            Line Offsetx+J+I*7,Offsety,Offsetx+J+I*7,Offsety+50
          Endif
        Next J
      Next I
      Line Offsetx+51,Offsety,Offsetx+51,Offsety+55
      Line Offsetx+53,Offsety,Offsetx+53,Offsety+55
      Offsetx=Offsetx+5
      For I=7 To 12
        Code$=Ean$(2,Z(I))
        For J=1 To Len(Code$)
          If Mid$(Code$,J,1)="1"
            Line Offsetx+J+I*7,Offsety,Offsetx+J+I*7,Offsety+50
          Endif
        Next J
      Next I
      Line Offsetx+92,Offsety,Offsetx+92,Offsety+55
      Line Offsetx+94,Offsety,Offsetx+94,Offsety+55
    Endif
  Else
    If Len(Ean$)=8
      Offsetx=10
      Offsety=5
      Pzk=Val(Right$(Ean$,1))
      Eanop$=Left$(Ean$,Len(Ean$)-1)
      Gew=3
      G=4
      Quer=0
      For I=1 To Len(Eanop$)
        Quer$=Str$(Val(Mid$(Eanop$,I,1))*Gew)
        Gew=G-Gew
        Quer=Quer+Val(Quer$)
      Next I
      Pz=(Int(Quer/10)+1)*10-Quer
      If Pz=10
        Pz=0
      Endif
      If Pz<>Pzk
        Print "Der EAN-Code wurde falsch eingegeben!"
      Else
        Cls
        Deftext 1,0,0,6
        Text Offsetx+2,Offsety+48,30,Left$(Ean$,4)
        Text Offsetx+35,Offsety+48,30,Right$(Ean$,4)
        For I=0 To 7
          Z(I)=Val(Mid$(Ean$,I+1,1))
        Next I
        Line Offsetx,Offsety,Offsetx,Offsety+45
        Line Offsetx+2,Offsety,Offsetx+2,Offsety+45
        Offsetx=Offsetx-5
        For I=1 To 8
          If I=5
            Offsetx=Offsetx+5
            Line Offsetx+32,Offsety,Offsetx+32,Offsety+45
            Line Offsetx+34,Offsety,Offsetx+34,Offsety+45
          Endif
          Code$=Ean$(Int((I-1)/4)*2,Z(I-1))
          For J=1 To Len(Code$)
            If Mid$(Code$,J,1)="1"
              Line Offsetx+J+I*7,Offsety,Offsetx+J+I*7,Offsety+40
            Endif
          Next J
        Next I
        Line Offsetx+64,Offsety,Offsetx+64,Offsety+45
        Line Offsetx+66,Offsety,Offsetx+66,Offsety+45
      Endif
    Endif
  Endif
  If (Len(Ean$)=8 Or Len(Ean$)=13) And Pz=Pzk
    Print At(1,7);"Herstellungsland: ";
    He$=Left$(Ean$,2)
    If He$>="00" And He$<="09"
      Print "USA, Canada"
    Endif
    If He$>="30" And He$<="37"
      Print "Frankreich"
    Endif
    If He$>="40" And He$<="43"
      Print "Bundesrepublik Deutschland"
    Endif
    If He$="49"
      Print "Japan"
    Endif
    If He$="50"
      Print "Groûbritannien"
    Endif
    If He$="54"
      Print "Belgien"
    Endif
    If He$="57"
      Print "DÑnemark"
    Endif
    If He$="73"
      Print "Schweden"
    Endif
    If He$="76"
      Print "Schweiz"
    Endif
    If He$="80" Or He$="81"
      Print "Italien"
    Endif
    If He$="87"
      Print "Niederlande"
    Endif
    If He$="90" Or He$="91"
      Print "ôsterreich"
    Endif
  Endif
  If Len(Ean$)=8 Or Len(Ean$)=13
    Alert 2,"Code auf Drucker ausgeben ?",1,"Ja|Nein",Druckit%
    If Druckit%=1
      Druckit%=0
      Gosub Ean_drucken
    Endif
    Repeat
    Until Inkey$<>"" Or Mousek<>0
  Endif
Return
'
Procedure Information
  Restore Info
  Start=0
  Read Text$
  While Text$<>"ende"
    If Text$="cls"
      If Start=1
        Repeat
        Until Inkey$<>"" Or Mousek<>0
      Endif
      Cls
    Else
      Print Fn Zen$(Text$)
    Endif
    Read Text$
    Start=1
  Wend
  Repeat
  Until Inkey$<>"" Or Mousek<>0
  Info:
  Data cls
  Data ""
  Data "Codes ..."
  Data "Ω 1986 sinccom software"
  Data "geschrieben von Daniel Roth"
  Data "EntschlÅsselung verschiedener PrÅfcodierungen"
  Data ""
  Data "Mit Hilfe des ersten Menupunktes kann man zum einen eine Zahl mit einer"
  Data "PrÅfziffer versehen, zum anderen kann man prÅfen, ob eine Zahl mit"
  Data "einer PrÅfziffer korrekt eingegeben wurde. Dabei muss man zusÑtzlich"
  Data "zwei alternierende Gewichtungen eingeben. Dies ist nîtig, um auch zu"
  Data "identifizieren, ob zwei Ziffern vertauscht wurden."
  Data "Die benutzten Algorithmen sind relativ einfach, da sie nur fehlererkennend"
  Data "sind und nicht fehlerkorrigierend. Sie sind nachzulesen in:"
  Data "Luftschlîsser und Hirngespinste"
  Data "von Albrecht Beutelspacher"
  Data "Vieweg, ISBN 3-528-08957-1 (bitte nachprÅfen)"
  Data ""
  Data "Mit dem zweiten Menupunkt lassen sich aus einer eingegebenen mit einer"
  Data "PrÅfziffer versehenen Zahl die alternierenden Gewichtungen ermitteln."
  Data "Der dritte und vierte Menupunkt entspricht den beiden Vorangegangenen,"
  Data "wobei hier aber ein anderes Codiersystem verwandt wurde."
  Data "Diese Codierungen werden z.B. benutzt bei Kontonummern oder"
  Data "Artikelnumerierungen."
  Data cls
  Data ""
  Data "Mit dem fÅnften Menupunkt kann man prÅfen, ob eine ISBN"
  Data "(Internationale Standard Buch Nummer) korrekt eingegeben wurde."
  Data ""
  Data "Der interessanteste Menupunkt ist wohl der sechste. EAN steht"
  Data "nÑmlich fÅr EuropÑische Artikel Numerierung. Dabei handelt es sich"
  Data "um den auf nahezu allen Nahrungsmitteln aufgedruckten Strichcode."
  Data "Hier kann man wahlweise die acht- oder 13-stellige EAN-Nummer"
  Data "eingeben, welche dann in den Strichcode umgewandelt wird."
  Data "Diese Nummer steht unter jedem Strichcode. Auch hier hat die letzte"
  Data "Ziffer die Funktion einer PrÅfziffer. Sie wird errechnet nach dem"
  Data "zweiten System mit den alternierenden Gewichtungen 1 und 3, bzw."
  Data "3 und 1 bei der achtstelligen EAN-Nummer."
  Data ""
  Data "Viel Spaû!"
  Data ende
Return
'
Procedure Ean_drucken
  ' --------------------------------------------------------------------
  ' eine ErgÑnzung zu 'Codes' von D.Roth
  ' Copyright 6. Juni 1987 by BANANA COMPANY, fÅr NEC P6,
  ' aber sehr leicht an EPSON FX 80 anzupassen
  ' --------------------------------------------------------------------
  ' NIE kînnte jemand darauf kommen, dies zu miûbrauchen ...
  ' --------------------------------------------------------------------
  '
  Local X%,Y%,M,X1,X2,Y1,Y2,A,County,B$,Help%,Spalte%
  Print At(2,21);"Erst Rechnen!"
  X1=0
  X2=120
  Y1=0
  Y2=60
  '
  If Len(Ean$)=13
    X2=120
  Else
    X2=90
  Endif
  County=(Y2-Y1) Div 8-(((Y2-Y1) Mod 8)<>0)
  Dim B$(County)
  '
  ' Doofes aber einfaches Bestimmen der zu druckenden chr$()
  '
  For X%=X1 To X2
    For Y%=Y1 To Y1+County*8 Step 8
      Spalte%=0
      For Help%=0 To 7
        If Point(X%,Y%+Help%)
          Draw X%+120,Y%+Help%         ! Nur damit man was vom rechnen sieht
          Spalte%=Spalte%+2^(7-Help%)
        Endif
      Next Help%
      B$((Y%-Y1)/8)=B$((Y%-Y1)/8)+Chr$(Spalte%)+Chr$(Spalte%)+Chr$(Spalte%)
    Next Y%
  Next X%
  Print At(2,23);
  Input "Wieviele Zeilen mit 2 Aufklebern soll ich drucken ";Aufkleb
  Bytes=3*(X2-X1)+1
  '  Lprint Chr$(27);"O";      ! for NEC P6: Cancel Skip-over-perforation function
  For Auf=1 To Aufkleb
    For M=1 To County
      ' --------------------- Ein Absatz reicht för EPSON aus ! ---------
      For K=0 To 1
        Lprint Chr$(27);"Z";Chr$(Bytes Mod 256);Chr$(Bytes Div 256);
        ' ^ Epson Graphmode 240 dpi 8 Pin Graphics/same for P6
        Lprint B$(M);
        Lprint "  ";
      Next K
      ' --------------------- zum Beispiel bis hier ---------------------
      '
      Lprint Chr$(27);"J";Chr$(1);Chr$(13);
      ' ^ P6: advance paper for 1/180 inch and return to start of line
      For K=0 To 1
        Lprint Chr$(27);"Z";Chr$(Bytes Mod 256);Chr$(Bytes Div 256);
        ' ^ Epson Graphmode 240 dpi 8 Pin Graphics/same for P6
        Lprint B$(M);
        Lprint "  ";
      Next K
      Lprint Chr$(27);"J";Chr$(1);Chr$(13);
      For K=0 To 1
        Lprint Chr$(27);"Z";Chr$(Bytes Mod 256);Chr$(Bytes Div 256);
        ' ^ Epson Graphmode 240 dpi 8 Pin Graphics/same for P6
        Lprint B$(M);
        Lprint "  ";
      Next K
      ' -----------  ab hier wird es wieder för beide gebraucht ---------
      '
      Lprint Chr$(27);"J";Chr$(22);Chr$(13);       ! for EPSON FX 80: change
      ' ^ NEC P6:advance paper for 22/180 inch     ! advance to 24/216 inch
    Next M
    Lprint                     ! je nach Aufklebergrôûe selbst einstellen
    Lprint
    Lprint
    Lprint
  Next Auf
  Lprint Chr$(27);"@";
  Erase B$()
Return
