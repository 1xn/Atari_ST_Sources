' 2ndSTar 1.31
' Programm zur Simulation von bedeckungsverÑnderlichen DoppelSTernen
' Ω 1987 sinccom software
' (Public Domain)
'
' geschrieben von Daniel Roth
' BrÅcker Mauspfad 448
' D-5000 Kîln 91
'
' VerbesserungsvorschlÑge, weitere Anregungen,
' sowie Aufmerksammachen auf Fehler sind erwÅnscht!
'
' Resource-Parameter
'
Param%=0
Hell1%=8
Hell2%=11
Radius1%=12
Radius2%=13
Abstand%=14
Winkel%=15
Graphik%=16
Los%=17
Beispiel%=18
Schluss%=19
'
Deffn Rad(Deg)=Deg*Pi/180
Deffn Arcos(X)=-Atn(X/Sqr(1-X*X))+Pi/2
Deffn Me(M)=10^((M+14.18)/-2.5)
Deffn Em(E)=-2.5*Log(E)/Log(10)-14.18
Deffn Zen$(A$)=Space$(Int((80-Len(A$))/2))+A$
Dim Helligkeiten(314)
Dim Zweiter%(8065)
Zweiter_adr%=256*((Varptr(Zweiter%(0))+255) Div 256)
Erster_adr%=Xbios(3)
'
Gosub Rsrc_load("2ndstar.rsc")
Gosub Rsrc_gtree(Param%,*Baum%)
Gosub Form_center(Baum%)
X%=Dpeek(Baum%+16)
Y%=Dpeek(Baum%+18)
B%=Dpeek(Baum%+20)
H%=Dpeek(Baum%+22)
Gosub Sstate(Baum%,Graphik%,1)
Gosub Stext(Baum%,Hell1%,"     0")
Gosub Stext(Baum%,Hell2%,"     0")
Gosub Stext(Baum%,Radius1%,"     0")
Gosub Stext(Baum%,Radius2%,"     0")
Gosub Stext(Baum%,Abstand%,"     0")
Gosub Stext(Baum%,Winkel%,"  0")
'
Nochmal=-1
Repeat
  Cls
  Gosub Auswahl
  If Nochmal
    Gosub 3.modell
  Endif
Until Not Nochmal
Gosub Rsrc_free
End
'
Procedure Auswahl
  Gosub Form_dial(1,10,10,0,0,X%,Y%,B%,H%)
  Repeat
    Gosub Objc_draw(Baum%,0,8,X%,Y%,B%,H%)
    Gosub Form_do(Baum%,Hell1%)
    Ex%=Dpeek(Gintout)
    Gosub Sstate(Baum%,Ex%,0)
    If Ex%=Schluss% Then
      Nochmal=0
    Else
      If Ex%=Beispiel% Then
        Gosub Stext(Baum%,Hell1%,"     1")
        Gosub Stext(Baum%,Hell2%,"     3")
        Gosub Stext(Baum%,Radius1%,"     3")
        Gosub Stext(Baum%,Radius2%,"     1")
        Gosub Stext(Baum%,Abstand%,"     7")
        Gosub Stext(Baum%,Winkel%," 70")
      Else
        Gosub Gstate(Baum%,Graphik%,*Status%)
        Gosub Gtext(Baum%,Hell1%,*Hell1$)
        Gosub Gtext(Baum%,Hell2%,*Hell2$)
        Gosub Gtext(Baum%,Radius1%,*Radius1$)
        Gosub Gtext(Baum%,Radius2%,*Radius2$)
        Gosub Gtext(Baum%,Abstand%,*Abstand$)
        Gosub Gtext(Baum%,Winkel%,*Winkel$)
        Mag1=Val(Hell1$)
        Mag2=Val(Hell2$)
        R1=Val(Radius1$)
        R2=Val(Radius2$)
        R=Val(Abstand$)
        Wi=Val(Winkel$)
        Ok=Mag1*Mag2*R1*R2
        If R1+R2>R And Ok<>0
          Alert 2,"FÅr Kontaktsysteme|ist die Berechnung|nicht korrekt!",1,"Abbruch|Trotzdem",Taste%
          If Taste%=1
            Ok=0
          Endif
        Endif
        If Status%=1
          Graphik$="j"
        Else
          Graphik$="n"
        Endif
      Endif
    Endif
  Until Ex%=Schluss% Or (Ex%=Los% And Ok<>0)
Return
'
Procedure 3.modell
  Cls
  ' grîûeren Stern in die Mitte setzen
  If R1<R2
    Swap R1,R2
    Swap Mag1,Mag2
  Endif
  Rs=R*Cos(Fn Rad(Wi))
  ' Vergrîûerungsfaktor errechnen
  Blup=310/(R+R2)
  If Blup*(Rs+R2)>96
    Blup=96/(Rs+R2)
  Endif
  If Blup*R1>96
    Blup=96/R1
  Endif
  Print Fn Zen$("Die Lichtkurve wird berechnet!")
  If Graphik$="j"
    Void Xbios(5,L:Zweiter_adr%,L:-1,-1)
  Endif
  E1=Fn Me(Mag1)
  E2=Fn Me(Mag2)
  Hellgetr=Fn Em(E1+E2)
  If Mag2<Mag1
    F=1
  Else
    If Mag2>Mag1
      F=7
    Else
      F=4
    Endif
  Endif
  R1=R1*Blup
  R2=R2*Blup
  R12=R1*R1
  R22=R2*R2
  T%=0
  Absmin=-10000000000
  For Ph=Pi To 3*Pi Step 0.02
    Dmx=Blup*R*Cos(Ph)
    Dmy=-Blup*Rs*Sin(Ph)
    Dmxs=Sqr(Dmx*Dmx+Dmy*Dmy)
    If Abs(Dmxs)<R1+R2 And Abs(Dmxs)>R1-R2
      La=Abs((R22-R12)/2/Dmxs-Dmxs/2)
      Mu=Sqr(R12-La*La)
      A=Fn Arcos(La/R1)*R12-Mu*La
      La=Abs(Dmxs)-La
      A=A+Fn Arcos(La/R2)*R22-Mu*La
    Else
      If Abs(Dmxs)<R1-R2
        A=Pi*R22
      Else
        A=0
      Endif
    Endif
    If Sgn(Sin(Ph))=1
      Egesamt=E1+E2-A/(Pi*R22)*E2
    Else
      Egesamt=E1+E2-A/(Pi*R12)*E1
    Endif
    Mgesamt=Fn Em(Egesamt)
    Helligkeiten(T%)=Mgesamt
    Absmin=Max(Mgesamt,Absmin)
    Add T%,1
  Next Ph
  Taste=0
  If (Absmin-Hellgetr)==0
    If Graphik$="j"
      Void Xbios(5,L:Erster_adr%,L:Erster_adr%,-1)
    Endif
    Alert 1,"Es fand keine|Bedeckung statt!",1,"OK|Trotzdem",Taste
    If Graphik$="j"
      Void Xbios(5,L:Zweiter_adr%,L:-1,-1)
    Endif
  Endif
  If Taste<>1
    If Taste=0 Then
      Faktor%=80/(Absmin-Hellgetr)
    Else
      Faktor%=80
    Endif
    Gosub Zeichnen(Faktor%)
    If Graphik$<>"j"
      For T%=0 To 314
        Plot 162+T%,100-(Hellgetr-Helligkeiten(T%))*Faktor%
      Next T%
    Else
      T%=0
      Aa%=1
      Modus%=3
      Grenze=R1+R2+(R1+R2)/10
      Deffill 1,2,8-F
      Pcircle 320,300,R1
      Get 320-R1,300-R1,320+R1,300+R1,Stern1$
      Deffill 1,2,F
      Pcircle 320-Blup*R,300,R2
      Get 320-Blup*R-R2,300-R2,320-Blup*R+R2,301+R2,Stern2$
      For Ph=Pi To 3*Pi Step 0.02
        Dmx=Blup*R*Cos(Ph)
        Dmy=-Blup*Rs*Sin(Ph)
        Dmxs=Sqr(Dmx*Dmx+Dmy*Dmy)
        If Aa%<>0
          Put 320-R1,300-R1,Stern1$,Modus%
          Aa%=0
        Endif
        If Sgn(Sin(Ph))=1
          Put 320+Dmx-R2,300+Dmy-R2,Stern2$,Modus%
          If Abs(Dmx)<Grenze
            Deffill 1,2,8-F
            Pcircle 320,300,R1
            Aa%=1
          Endif
        Else
          If Abs(Dmx)<Grenze
            Put 320-R1,300-R1,Stern1$,Modus%
            Deffill 1,2,F
            Pcircle 320+Dmx,300+Dmy,R2
            Aa%=1
          Else
            Put 320+Dmx-R2,300+Dmy-R2,Stern2$,Modus%
          Endif
        Endif
        Plot 162+T%,100-(Hellgetr-Helligkeiten(T%))*Faktor%
        Add T%,1
        Bmove Zweiter_adr%,Erster_adr%,32000
        Deffill 0,1
        Pbox 320+Dmx-R2,300+Dmy-R2,321+Dmx+R2,301+Dmy+R2
        If Abs(Dmx)<Grenze
          Pbox 320-R1,300-R1,320+R1,300+R1
        Endif
      Next Ph
    Endif
    Repeat
    Until Inkey$<>"" Or Mousek<>0
  Endif
  Void Xbios(5,L:Erster_adr%,L:Erster_adr%,-1)
Return
'
Procedure Zeichnen(Faktor%)
  Local X%,Y%,Dm
  Cls
  Deftext 1,0,0,13
  Text 224,16,"2  STar  von Daniel Roth"
  Deftext 1,0,0,6
  Text 232,10,"ND"
  Print At(62,4);Using "Stern 1: ##.## mag",Mag1
  Print At(62,5);Using "Stern 2: ##.## mag",Mag2
  Print At(62,6);"Gesamthelligkeit:"
  Print At(62,7);Using "##.## mag",Hellgetr
  Print At(62,8);"RadienverhÑltnis:"
  Print At(62,9);Using "R1/R2=##.##",R1/R2
  Print At(62,10);"Neigungswinkel:"
  Print At(62,11);Using "###.# ¯",Wi
  Deftext 1,0,900,6
  Text 98,180,"m in mag"
  Defline 1,1,0,1
  Line 97,100,97,70
  Defline 1,1,0,0
  Deftext 1,0,0,6
  Dm=-80/Faktor%
  For Y%=0 To 160 Step 40
    Line 162,40+Y%,477,40+Y%
    Zahl$=Str$(Int(Dm*100+0.5)/100)
    If Sgn(Dm)=1
      Zahl$="+"+Zahl$
    Else
      If Sgn(Dm)=0
        Zahl$=" "+Zahl$
      Endif
    Endif
    Text 114,43+Y%,Zahl$
    Dm=Dm+40/Faktor%
  Next Y%
  For X%=0 To 315 Step 45
    Line 162+X%,40,162+X%,200
  Next X%
Return
'
' Include fÅr Dialog-Handling
'
Procedure Objc_draw(Tree%,Start%,Depth%,X%,Y%,B%,H%)
  Lpoke Addrin,Tree%
  Dpoke Gintin,Start%
  Dpoke Gintin+2,Depth%
  Dpoke Gintin+4,X%
  Dpoke Gintin+6,Y%
  Dpoke Gintin+8,B%
  Dpoke Gintin+10,H%
  Gemsys 42
Return
Procedure Form_do(Tree%,Start%)
  Lpoke Addrin,Tree%
  Dpoke Gintin,Start%
  Gemsys 50
Return
Procedure Form_dial(F%,X%,Y%,B%,H%,Xb%,Yb%,Bb%,Hb%)
  Dpoke Gintin,F%
  Dpoke Gintin+2,X%
  Dpoke Gintin+4,Y%
  Dpoke Gintin+6,B%
  Dpoke Gintin+8,H%
  Dpoke Gintin+10,Xb%
  Dpoke Gintin+12,Yb%
  Dpoke Gintin+14,Bb%
  Dpoke Gintin+16,Hb%
  Gemsys 51
Return
Procedure Form_center(Tree%)
  Lpoke Addrin,Tree%
  Gemsys 54
Return
Procedure Rsrc_load(Nam$)
  Nam$=Nam$+Chr$(0)
  Lpoke Addrin,Varptr(Nam$)
  Gemsys 110
Return
Procedure Rsrc_free
  Gemsys 111
Return
Procedure Rsrc_gaddr(Type%,Index%)
  Dpoke Gintin,Type%
  Dpoke Gintin+2,Index%
  Gemsys 112
Return
Procedure Rsrc_gtree(Index_%,Tree.%)
  Lpoke Gintin,Index_%
  Gemsys 112
  *Tree.%=Lpeek(Addrout)
Return
'
Procedure Gstate(T_%,N_%,X.%)
  *X.%=Dpeek(T_%+24*N_%+10)
Return
'
Procedure Sstate(T_%,N_%,X_%)
  Dpoke T_%+24*N_%+10,X_%
Return
'
Procedure Gtext(T_%,N_%,X.%)
  Local X_$
  X_$=Space$(100)
  T_%=Lpeek(Lpeek(T_%+24*N_%+12))
  Bmove T_%,Varptr(X_$),100
  *X.%=Left$(X_$,Instr(X_$,Chr$(0))-1)
Return
'
Procedure Stext(T_%,N_%,X_$)
  X_$=X_$+Chr$(0)
  T_%=Lpeek(T_%+24*N_%+12)
  Bmove Varptr(X_$),Lpeek(T_%),Min(Len(X_$),Dpeek(T_%+24)-1)
Return
'
