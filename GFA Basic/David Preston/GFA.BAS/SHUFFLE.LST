DEFINT "a-z"
DIM deck(51)
RANDOMIZE TIMER
restart:
CLS
PRINT "Shuffling demonstration - GFA Basic 3.5"
PRINT "By David Preston for ICTARI"
PRINT
PRINT
reset_deck
t=TIMER
deal_1
PRINT "Loop...until method took ";(TIMER-t)/50;" seconds"
PRINT
PRINT
reset_deck
t=TIMER
deal_2
PRINT "Swapping method took ";(TIMER-t)/50;" seconds"
PRINT
PRINT
PRINT
PRINT "Press <Esc> to quit or any other key to repeat"
DO
  k$=INKEY$
LOOP UNTIL k$<>""
IF ASC(k$)<>27 THEN
  GOTO restart
ENDIF
STOP
'
PROCEDURE reset_deck
  LOCAL card
  FOR card=0 TO 51
    deck(card)=card
  NEXT card
RETURN
'
PROCEDURE deal_1
  LOCAL card,this_card
  FOR card=0 TO 51
    DO
      this_card=INT(RND*52)
    LOOP UNTIL deck(this_card)<>-1
    deck(this_card)=-1
    PRINT this_card;" ";
  NEXT card
  PRINT
  PRINT
RETURN
'
PROCEDURE deal_2
  LOCAL card,other_card
  FOR card=0 TO 51
    other_card=INT(RND*52)
    SWAP deck(card),deck(other_card)
  NEXT card
  FOR card=0 TO 51
    PRINT deck(card);" ";
  NEXT card
  PRINT
  PRINT
RETURN
