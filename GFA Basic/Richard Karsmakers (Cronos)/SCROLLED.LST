' Scroll Message Editor V1.3
'
' Software written by Richard Karsmakers
' Actual scroll developed by Jos Schilders
'
' This program is Shareware
'
' This program must be used with GfA Basic V2.0
'
@Checkres
@Init
Do
  @Getkey
  If A=187
    @Load
  Endif
  If A=188
    @Save
  Endif
  If A=189
    @Edit
  Endif
  If A=190
    @Scroll
  Endif
  If A=191
    @New
  Endif
  If A=192
    @Message
  Endif
  If A=196
    System
  Endif
Loop
Procedure Checkres
  Res=Xbios(4) !Which is the resolution?
  If Res<>2 !No high res?
    Deftext 1,17,0,32
    Setcolor 1,0,0,0
    Text 70,70,"ONLY SM12x!"
    Label12:
    For X=0 To 7
      Setcolor 0,7,7,X
      Pause 0.0001
    Next X
    For X=7 Downto 0
      Setcolor 0,7,7,X
      Pause 0.0001
    Next X
    Goto Label12
  Endif
Return !If high res, continue execution
Procedure Init
  Spoke 16745024,0
  Deffill 1,2,24
  Pbox 0,0,639,399
  Deffill 1,0
  Pbox 50,10,590,50
  Deftext 1,1,0,32
  Text 112,42,"Scroll Message Editor V1.3"
  Pbox 20,70,620,385
  Deftext 1,1,0,13
  Text 154,100,"PLEASE SELECT ONE OF THE FOLLOWING OPTIONS:"
  Deftext 1,16,0,13
  Text 114,140,"         Load String         F 1"
  Text 114,160,"         Save String         F 2"
  Text 114,180,"         Edit                F 3"
  Text 114,200,"         Scroll              F 4"
  Text 114,220,"         New                 F 5"
  Text 114,240,"         Save Message File   F 6"
  Text 114,260,"         Quit                F10"
  Deftext 1,1,0,13
  Text 126,350,"Scrolling Design and Development by Jos Schilders"
  Text 190,370,"Programming by Richard Karsmakers"
  Deftext 1,0,0,13
  Pbox 0,286,640,305
  ' String data for the default scrolling text
  A$=" Welcome to the Scroll Message Editor V1.3, written in November 1986 by Richard Karsmakers "
  A$=A$+"for ST NEWS Volume 1 Issue 6. Thanks to Jos Schilders, who designed the original scrolling routine. "
  A$=A$+"His routine only had to be improved a bit (like adding synchronisation and integer variables). "
  A$=A$+"With the use of this program, it is possible to create scrolling messages that can easily be MERGEd into "
  A$=A$+"your own programs.   Simply call the procedure and that's all. It's all extremely flexible and simple.  This program belongs "
  A$=A$+"to the Shareware, and you can freely use the MERGEd in subroutine in your own (commercial) programs. "
  A$=A$+"If you want to extend your gratitude to me, you can do that by thanking me (or The ACC¿) in your "
  A$=A$+"program using the scrolling routine, or paying any amount of money on my giro account: "
  A$=A$+"Number 5060326 t.n.v. Richard Karsmakers, Helmond, The Netherlands.    "
  A$=A$+"A short user manual:  * Pressing any key quits the scroll * Pressing F1 in 'Edit' or 'New' mode "
  A$=A$+"enables you to type in an ASCII code (which DOES NOT appear on the screen) for a character "
  A$=A$+"that cannot be gained through the keyboard * Hitting F10 quits most modes * Hitting return closes "
  A$=A$+"down the 'New' mode * Pressing the left and right arrow keys scrolls the text in 'Edit' mode * "
  A$=A$+"Texts may not be longer than 32766 characters (32 Kb) * When in 'Edit' mode, the character to "
  A$=A$+"the rightmost side of the screen is changed when hitting an alphanumeral key * When "
  A$=A$+"SAVEing a text string, another file using the same name is automatically deleted * This is also the case "
  A$=A$+"when you are SAVEing a 'message file' * 'Quit' returns to the desktop, but if you want to examine "
  A$=A$+"the programming techniques I used, you can do this by pressing ALTERNATE, SHIFT and CONTROL "
  A$=A$+"simultaneously in the GfA Basic interpreter (not the run-only interpreter!) * "
  A$=A$+" I hope you will like this small (but handy?!) programming tool.............            "
  @Scroll
Return
Procedure Load
  Showm
  Alert 3,"The string in memory|will be lost!",1,"OK|SHIT!",A
  If A=2
    Goto Label1
  Endif
  A$=""
  Fileselect "\*.SCR","",Lo$
  If Lo$="" !Avoid bus error
    Goto Label1
  Endif
  If Exist(Lo$)=0 !Avoid File not found error
    Goto Label1
  Endif
  Open "I",#1,Lo$
  Input #1,Q
  Q=Q/255
  For X=0 To Q
    Line Input #1,Buf$ !Line input is used because the string might use ','
    A$=A$+Buf$
  Next X
  Close #1
  Label1:
  Alert 0,"READY!",1,"OK",Dummy
  Hidem
  @Scroll
Return
Procedure Save
  Showm
  Fileselect "\*.SCR","",Sa$
  If Sa$="" !Avoid bus error
    Goto Label2
  Endif
  If Exist(Sa$)=-1 !Avoid file exists error
    Kill Sa$
  Endif
  Q=Len(A$)
  Open "O",#1,Sa$
  Print #1,Q
  Print #1,A$
  Close #1
  Label2:
  Alert 0,"READY!",1,"OK",Dummy
  Hidem
Return
Procedure Scroll
  C=0
  Hidem
  Print At(1,19);Space$(80)
  L=Len(A$)
  Do
    C=(C+1) Mod L
    Print At(80,19);Mid$(A$,C,1)
    For I%=1 To 8
      Get 1,288,639,303,B$
      Put 0,288,B$
      Vsync
    Next I%
    Exit If Inp?(2)
  Loop
  Showm
  Pause 10
  Print At(1,19);Space$(80)
  A=Inp(2)
Return
Procedure Getkey
  A=Inp(2)
Return
Procedure New
  Alert 3,"The string in memory|will be lost!",1,"OK|SHIT!",U
  If U=2
    Goto Label10
  Endif
  A$=" "
  X=0
  Do
    @Getkey
    If A=13 !RETURN key (closes down mode)
      A$=A$+" "
      A$=A$+Chr$(A)
    Endif
    Exit If A=13
    If A=187 !Function key 1?
      @Getcode
      Goto Label4
    Endif
    If A=8 And X>0 !backspace check
      X=X-1
      Buffer$=A$
      A$=Left$(Buffer$,Len(Buffer$)-1)
      Print At(40,19);"  ";
      Get 0,288,639,303,B$
      Put 8,288,B$
      If Len(A$)>39
        Print At(1,19);Mid$(A$,Len(A$)-39);
      Endif
      Print At(1,19);" ";
      Goto Label3
    Endif
    If A=34 !Disable quote (gives mess in message file)
      Goto Label3
    Endif
    If A=156 !Enable pound sign (œ)
      Goto Label4
    Endif
    If A<32 Or A>126
      Goto Label3
    Endif
    Label4:
    A$=A$+Chr$(A)
    Get 8,288,639,303,B$
    Put 0,288,B$
    Print At(40,19);Chr$(A);
    Inc X
    Exit If X>32766 !String too long?
    Label3:
  Loop
  Pause 10
  Print At(1,19);Space$(80)
  Label10:
Return
Procedure Getcode
  Alert 2,"Please type in|the Character code|you want after|hitting Return",1,"OK",Dummy
  Label5:
  @Getkey
  If A<48 Or A>57
    Goto Label5
  Endif
  A=A-48
  D=A*100
  Label6:
  @Getkey
  If A<48 Or A>57
    Goto Label6
  Endif
  A=A-48
  E=A*10
  Label7:
  @Getkey
  If A<48 Or A>57
    Goto Label7
  Endif
  A=A-48
  F=A
  A=D+E+F
Return
Procedure Edit
  C=0
  Hidem
  Print At(1,19);Space$(80)
  L=Len(A$)
  Do
    @Getkey
    Exit If A=196
    If A=203
      C=C-1
      If C<0
        C=0
        Goto Label8
      Endif
      Get 0,288,639,303,B$
      Print At(1,19);" "
      Put 8,288,B$
      If C>79
        Print At(1,19);Mid$(A$,C-79,1)
      Endif
      Goto Label8
    Endif
    If A=205
      C=C+1
      If C>Len(A$)
        C=C-1
        Goto Label8
      Endif
      Get 8,288,639,303,B$
      Put 0,288,B$
      Print At(80,19);Mid$(A$,C,1)
      Goto Label8
    Endif
    If A=187
      @Getcode
      Mid$(A$,C,1)=Chr$(A)
      Print At(80,19);Mid$(A$,C,1)
    Endif
    If A<32 Or A>126
      Goto Label8
    Endif
    Mid$(A$,C,1)=Chr$(A)
    Print At(80,19);Mid$(A$,C,1)
    Label8:
  Loop
  Pause 10
  Print At(1,19);Space$(80)
  A=Inp(2)
Return
Procedure Message
  Showm
  Fileselect "\*.LST","MESSAGE.LST",Me$
  If Me$=""
    Goto Label11
  Endif
  If Exist(Me$)=-1 !Avoid File exist error
    Kill Me$
  Endif
  Open "O",#1,Me$
  Print #1,"' This procedure can be MERGEd into your own programs"
  Print #1,"' a$ = the string containing the text"
  For X=1 To Len(A$) Step 60
    Print #1,"a$=a$+";Chr$(34);Mid$(A$,X,60);Chr$(34)
  Next X
  Print #1,"@Scroll"
  Print #1,"Procedure Scroll"
  Print #1,"C=0"
  Print #1,"Hidem"
  Print #1,"Print At(1,19);Space$(80)"
  Print #1,"L=Len(A$)"
  Print #1,"Do"
  Print #1,"C=(C+1) Mod L"
  Print #1,"Print At(80,19);Mid$(A$,C,1)"
  Print #1,"For I%=1 To 8"
  Print #1,"Get 1,288,639,303,B$"
  Print #1,"Put 0,288,B$"
  Print #1,"Vsync"
  Print #1,"Next I%"
  Print #1,"Exit If Inp?(2)"
  Print #1,"Loop"
  Print #1,"Showm"
  Print #1,"Pause 10"
  Print #1,"Print At(1,19);Space$(80)"
  Print #1,"A=Inp(2)"
  Print #1,"Return"
  Close #1
  Label11:
  Hidem
Return
' End of program!
