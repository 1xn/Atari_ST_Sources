'                       **   UNDERGROUND ADVENTURE   **
'                    Change to GFA V2.0  Basic    update 15/11/95
'                    change to GFA V3.6                  06/01/96
GOSUB initdata
GOSUB initdis
GOSUB readdata
GOSUB assign
CLS
GOSUB setprgname
back=1
GOSUB control
END
' ----------------------------------------------------------------------
'                 verb subroutines
> PROCEDURE go
  IF no$<>"" AND no=0
    m=33
    GOTO 119
  ENDIF
  IF no>28 OR no<21
    m=34
    GOTO 119
  ENDIF
  IF no>24
    no=no-4
  ENDIF
  no=no-21
  IF cp=3 AND gf=1
    GOSUB lockin
    gf=0
    m=0
    GOTO 119
  ENDIF
  IF no<4 AND pd=1
    m=1
    mr=23
    GOSUB dismess
    back=2
    GOTO 120
  ENDIF
  IF p%(cp,no)=0
    m=3
    GOTO 119
  ENDIF
  IF cp=53 AND no=1
    GOSUB crevice
    GOTO 118
  ENDIF
  cp=p%(cp,no)
  118:
  back=1
  GOTO 120 !     Back to control
  119:
  mr=23
  GOSUB dismess
  back=0
  120:
RETURN !  noun not recognised
> PROCEDURE get
  IF no=0
    GOSUB notrec
    GOTO 139
  ENDIF
  GOSUB bottles
  IF ob%(no)=-1
    m=35
    GOTO 139
  ENDIF
  IF ob%(no)<>cp
    m=36
    GOTO 139
  ENDIF
  IF (no=18 OR no=39) AND ob%(19)<>-1
    m=37
    GOTO 139
  ENDIF
  IF no=39 AND ob%(19)=-1
    ob%(39)=0
    no=52
    ob%(19)=0
    DEC zz
    GOTO 138
  ENDIF
  IF no=18 AND ob%(19)=-1
    ob%(18)=0
    no=51
    ob%(19)=0
    DEC zz
    GOTO 138
  ENDIF
  IF no=1 OR no=3 OR no=6 OR no=9 OR no=11 OR no=17
    m=78
    GOTO 139
  ENDIF
  IF no=20 OR no=29 OR no=30 OR no=31 OR no=32 OR no=35 OR no=36
    m=79
    GOTO 139
  ENDIF
  IF no=40 OR no=41 OR no=43 OR no=49
    m=38
    GOTO 139
  ENDIF
  IF no=8 OR no=50
    m=39
    GOTO 139
  ENDIF
  IF zz>4
    m=40
    GOTO 139
  ENDIF
  IF no=12 AND cp=10
    p%(10,3)=0
    pos$(10,0)=mess$(42)
    @curpos
  ENDIF
  IF no=15 AND sc=0
    m=43
    GOTO 139
  ENDIF
  138:
  INC zz
  m=48
  ob%(no)=-1
  @desobj
  139:
  mr=23
  GOSUB dismess
  back=0
RETURN !'  noun not recognised
> PROCEDURE loo
  back=1
RETURN !'  back to control
> PROCEDURE inv
  mr=23
  m=44
  @dismess
  gs=0
  zz=0
  text$=""
  FOR k=1 TO lo
    IF ob%(k)=-1
      text$=text$+obj$(k)+", "
      INC gs
      INC zz
    ENDIF
  NEXT k
  row=24
  @strout
  IF gs=0
    m=45
    mr=24
    @dismess
  ENDIF
  PAUSE 200
  back=0
RETURN !'  back to what now
> PROCEDURE sco
  mr=23
  m=80
  @dismess
  back=0
RETURN !'  back to what now
> PROCEDURE dro
  IF no=0
    GOSUB notrec
    GOTO 219
  ENDIF
  GOSUB bottles
  IF ob%(no)<>-1
    m=81
    GOTO 219
  ENDIF
  IF no=19
    m=46
    ob%(19)=0
    ob%(50)=cp
    GOTO 218
  ENDIF
  IF no=51
    m=46
    ob%(51)=0
    ob%(50)=cp
    GOTO 218
  ENDIF
  IF no=52
    m=46
    ob%(52)=0
    ob%(50)=cp
    GOTO 218
  ENDIF
  IF no=16
    m=47
    ob%(16)=0
    GOTO 218
  ENDIF
  IF no=46
    ob%(no)=0
    ob%(45)=cp
    m=48
    GOTO 218
  ENDIF
  IF no<>12 OR cp<>10
    GOTO 214
  ENDIF
  m=49
  ob%(12)=cp
  p%(10,3)=14
  pos$(10,0)=mess$(50)
  GOSUB curpos
  GOTO 218
  214:
  mr=23
  m=48
  GOSUB dismess
  ob%(no)=cp
  IF tb=1
    mr=24
    m=51
    tb=0
    ob%(9)=(INT(RND(0.5)*41+1))
    GOSUB dismess
    m=0
  ENDIF
  218:
  DEC zz
  GOSUB desobj
  219:
  mr=23
  GOSUB dismess
  back=0
RETURN !'  back to what now
> PROCEDURE hel
  FOR k=23 TO 25
    mr=k
    m=k+59
    GOSUB dismess
  NEXT k
  back=0
RETURN !'  back to what now
> PROCEDURE qui
  mr=23
  m=99
  @dismess ! '   do you want to save game
  @yesno
  IF y=1
    @savegame
  ENDIF
  mr=24
  m=100
  GOSUB dismess ! '   are you sure
  GOSUB yesno
  IF y=1
    GOSUB done
  ENDIF
  back=0
RETURN !'  back to what now
> PROCEDURE cro
  IF no=0
    m=63
    GOTO 279
  ENDIF
  IF (cp<>15) AND (cp<>10)
    m=64
    GOTO 279
  ENDIF
  IF no<>1 AND no<>6 AND no<>12
    m=65
    GOTO 279
  ENDIF
  m=66
  279:
  mr=23
  GOSUB dismess
  back=0
RETURN !'  back to what now
> PROCEDURE ope
  IF no=0
    m=67
    GOTO 319
  ENDIF
  IF cp<>60 AND cp<>3
    m=68
    GOTO 319
  ENDIF
  IF cp=60
    GOTO 314
  ENDIF
  IF gf=1
    m=69
    GOTO 319
  ENDIF
  IF ob%(42)<>-1
    m=70
    GOTO 319
  ENDIF
  gf=1
  p%(3,0)=2
  mr=23
  m=71
  GOSUB dismess
  back=3
  GOTO 320 !' out
  314:
  IF df=1
    m=69
    GOTO 319
  ENDIF
  IF ob%(33)<>-1
    m=72
    GOTO 319
  ENDIF
  m=73
  319:
  mr=23
  GOSUB dismess
  back=0
  320:
RETURN !  back to what now
> PROCEDURE clo
  IF no=0
    m=74
    GOTO 339
  ENDIF
  IF no<>32 AND no<>35
    m=75
    GOTO 339
  ENDIF
  IF cp=3
    GOTO 334
  ENDIF
  IF df=0
    m=76
    GOTO 339
  ENDIF
  m=48
  p%(60,1)=0
  df=0
  pos$(60,0)=mess$(77)
  GOSUB curpos
  GOTO 339
  334:
  IF gf=0
    m=76
    GOTO 339
  ENDIF
  m=85
  339:
  mr=23
  GOSUB dismess
  back=0
RETURN !  back to what now
> PROCEDURE eat
  IF no=0
    GOSUB notrec
    GOTO 359
  ENDIF
  GOSUB bottles
  IF ob%(no)<>-1 AND ob%(no)<>cp
    m=36
    GOTO 359
  ENDIF
  IF no<>10
    m=53
    GOTO 359
  ENDIF
  ob%(10)=0
  m=54
  DEC zz
  GOSUB desobj
  359:
  mr=23
  GOSUB dismess
  back=0
RETURN !'  back to what now
> PROCEDURE fee
  IF no=0
    m=34
    GOTO 379
  ENDIF
  IF no<>9
    m=55
    GOTO 379
  ENDIF
  IF ob%(10)<>-1
    m=56
    GOTO 379
  ENDIF
  GOSUB off
  379:
  mr=23
  GOSUB dismess
  back=0
RETURN !'  back to what now
> PROCEDURE dri
  IF no=0
    GOSUB notrec
    GOTO 399
  ENDIF
  GOSUB bottles
  IF no<>51 AND no<>52
    m=57
    GOTO 399
  ENDIF
  IF no=51
    m=58
    ob%(51)=0
    ob%(19)=-1
    GOTO 398
  ENDIF
  m=59
  ob%(52)=0
  ob%(19)=-1
  398:
  GOSUB desobj
  399:
  mr=23
  GOSUB dismess
  back=0
RETURN !'  back to what now
> PROCEDURE off
  IF no=0
    m=86
    GOTO 419
  ENDIF
  GOSUB bottles
  IF ob%(no)<>-1
    m=87
    GOTO 419
  ENDIF
  IF no=10
    GOTO 414
  ENDIF
  IF no<>52
    m=88
    GOTO 419
  ENDIF
  IF cp<>50
    m=89
    GOTO 419
  ENDIF
  ob%(52)=0
  ob%(19)=-1
  p%(50,3)=55
  pos$(50,0)=mess$(90)
  ob%(29)=0
  m=92
  mr=23
  GOSUB dismess
  m=93
  mr=24
  GOSUB dismess
  GOTO 418
  414:
  IF cp<>27
    m=91
    GOTO 419
  ENDIF
  m=94
  mr=23
  GOSUB dismess
  m=95
  mr=24
  GOSUB dismess
  ob%(10)=0
  DEC zz
  p%(27,0)=28
  pos$(27,0)=mess$(96)
  tb=1
  418:
  back=1
  GOTO 420
  419:
  mr=23
  GOSUB dismess
  back=0
  420:
RETURN !'  back to what now
> PROCEDURE wav
  IF no=0
    GOSUB notrec
    GOTO 439
  ENDIF
  GOSUB bottles
  IF ob%(no)<>-1 AND ob%(no)<>cp
    m=81
    GOTO 439
  ENDIF
  IF no<>2
    m=97
    GOTO 439
  ENDIF
  IF cp<>15
    m=97
    GOTO 439
  ENDIF
  IF br=1
    m=98
    GOTO 439
  ENDIF
  ob%(6)=cp
  p%(15,2)=16
  p%(15,1)=17
  pos$(15,0)=mess$(103)
  m=102
  br=1
  mr=23
  GOSUB dismess
  back=1
  GOTO 440
  439:
  mr=23
  GOSUB dismess
  back=0
  440:
RETURN !'  back to what now
> PROCEDURE cut
  GOSUB cho
RETURN
> PROCEDURE cho
  IF no=0
    GOSUB notrec
    GOTO 479
  ENDIF
  GOSUB bottles
  IF ob%(no)<>-1 AND ob%(no)<>cp
    m=52
    GOTO 479
  ENDIF
  IF no<>3 AND no<>15 AND no<>5 AND no<>12 AND no<>32
    m=104
    GOTO 479
  ENDIF
  IF ob%(4)<>-1
    m=105
    GOTO 479
  ENDIF
  IF no<>3 AND no<>12
    m=106
    GOTO 479
  ENDIF
  IF no=3
    GOTO 477
  ENDIF
  ob%(12)=0
  ob%(53)=-1
  m=112
  mr=23
  GOSUB dismess
  m=113
  mr=24
  GOSUB dismess
  m=0
  GOTO 479
  477:
  p%(21,2)=22
  pos$(21,0)=mess$(108)
  obj$(3)=mess$(109)
  m=107
  mr=23
  GOSUB dismess
  back=1
  GOTO 480
  479:
  mr=23
  GOSUB dismess
  back=0
  480:
RETURN ! '  back to what now
> PROCEDURE cli
  IF no<>3 AND no<>5 AND no<>13
    m=114
    GOTO 499
  ENDIF
  IF no=3
    m=115
    GOTO 499
  ENDIF
  IF no=5
    GOTO 490
  ENDIF
  IF ob%(13)<>cp
    m=116
    GOTO 499
  ENDIF
  IF cp<>45 AND cp<>47
    m=117
    GOTO 499
  ENDIF
  IF tb=1
    m=11
    ob%(13)=0
    p%(45,1)=0
    cp=45
    GOTO 499
  ENDIF
  IF cp=45
    ob%(13)=47
    cp=47
    GOTO 498
  ENDIF
  ob%(13)=45
  cp=45
  GOTO 498
  490:
  IF ob%(5)<>cp
    m=116
    GOTO 499
  ENDIF
  IF cp<>35 AND cp<>36
    m=118
    GOTO 499
  ENDIF
  obj$(5)=" A rope Hanging down the deep drop"
  IF cp=35
    ob%(5)=36
    cp=36
    GOTO 498
  ENDIF
  ob%(5)=35
  cp=35
  498:
  back=1
  GOTO 500 !'  back to control
  499:
  mr=23
  @dismess
  back=0
  500:
RETURN !'  back to what now
> PROCEDURE lig
  IF no=0
    @notrec
    GOTO 519
  ENDIF
  GOSUB bottles
  IF ob%(no)<>-1 AND ob%(no)<>cp
    m=127
    GOTO 519
  ENDIF
  IF ob%(44)<>-1
    m=119
    GOTO 519
  ENDIF
  IF no<>45 AND no<>7
    m=121
    GOTO 519
  ENDIF
  IF no=7
    GOTO 510
  ENDIF
  IF ob%(46)=-1
    m=120
    GOTO 519
  ENDIF
  ob%(46)=-1
  ob%(45)=0
  pd=0
  m=48
  GOTO 519
  510:
  IF ob%(7)=-1
    m=122
    mr=23
    GOSUB dismess
    back=2
    GOTO 520
  ENDIF
  IF cp<>4
    m=123
    GOTO 517
  ENDIF
  p%(4,3)=5
  pos$(4,0)=mess$(125)
  m=124
  517:
  ob%(7)=0
  DEC zz
  back=1
  GOTO 520
  519:
  back=0
  520:
  mr=23
  GOSUB dismess
RETURN ! '  back to what now
> PROCEDURE att
  IF no=0
    m=126
    GOTO 539
  ENDIF
  GOSUB bottles
  IF ob%(no)<>-1 AND ob%(no)<>cp
    m=61
    GOTO 539
  ENDIF
  IF no<>9 AND no<>11 AND no<>29 AND no<>30 AND no<>31
    m=128
    GOTO 539
  ENDIF
  m=129
  539:
  mr=23
  GOSUB dismess
  back=0
RETURN ! '  back to what now
> PROCEDURE kil
  IF no=0
    m=130
    GOTO 559
  ENDIF
  GOSUB att
  m=0
  559:
  mr=23
  @dismess
  back=0
RETURN !'  back to what now
> PROCEDURE hit
  IF no=0
    m=60
    GOTO 579
  ENDIF
  @bottles
  IF ob%(no)<>-1 AND ob%(no)<>cp
    m=61
    GOTO 579
  ENDIF
  m=62
  579:
  mr=23
  @dismess
  back=0
RETURN !'  back to what now
> PROCEDURE mak
  IF no=0
    m=171
    GOTO 599
  ENDIF
  IF no<>13
    m=131
    GOTO 599
  ENDIF
  IF ob%(53)<>-1 OR ob%(14)<>-1 OR ob%(4)<>-1
    m=132
    GOTO 599
  ENDIF
  ob%(13)=-1
  ob%(14)=0
  ob%(53)=0
  zz=zz-1
  m=133
  599:
  mr=23
  GOSUB dismess
  back=0
RETURN !'  back to what now
> PROCEDURE ref
  IF no=0
    m=134
    GOTO 619
  ENDIF
  GOSUB bottles
  IF ob%(16)<>-1
    m=173
    GOTO 619
  ENDIF
  IF no<>47
    m=34
    GOTO 619
  ENDIF
  IF cp<>93
    m=172
    GOTO 619
  ENDIF
  IF sc=1
    m=98
    GOTO 619
  ENDIF
  ob%(15)=cp
  obj$(14)=obj$(14)+" Drawn Aside"
  p%(93,0)=95
  pos$(93,0)=mess$(136)
  m=135
  sc=1
  mr=23
  GOSUB dismess
  back=1
  GOTO 620
  619:
  mr=23
  GOSUB dismess
  back=0
  620:
RETURN !'  back to what now
> PROCEDURE oil
  IF no=0
    m=137
    GOTO 639
  ENDIF
  IF ob%(51)<>-1
    m=138
    GOTO 639
  ENDIF
  IF cp<>79
    m=139
    GOTO 639
  ENDIF
  IF no<>17
    m=140
    ob%(51)=0
    ob%(19)=-1
    GOTO 639
  ENDIF
  ob%(51)=0
  ob%(19)=-1
  p%(79,2)=80
  p%(79,3)=81
  pos$(79,0)=mess$(142)
  m=141
  mr=23
  GOSUB dismess
  back=1
  GOTO 640
  639:
  mr=23
  GOSUB dismess
  back=0
  640:
RETURN ! '  back to what now
> PROCEDURE sta
  IF no=0
    GOSUB notrec
    GOTO 659
  ENDIF
  GOSUB bottles
  IF ob%(no)<>-1 AND ob%(no)<>cp
    m=127
    GOTO 659
  ENDIF
  IF ob%(38)<>-1
    m=143
    GOTO 659
  ENDIF
  IF no<>30
    row=23
    column=10
    text$="The  "+obj$(no)+" Is not Impressed "
    GOSUB strout
    m=0
    GOTO 659
  ENDIF
  p%(84,2)=86
  p%(84,3)=85
  obj$(30)=mess$(146)
  pos$(84,0)=mess$(147)
  m=144
  mr=23
  GOSUB dismess
  m=145
  mr=24
  GOSUB dismess
  back=1
  GOTO 660
  659:
  mr=23
  GOSUB dismess
  back=0
  660:
RETURN !'  back to what now
> PROCEDURE spr
  IF no=0
    GOSUB notrec
    GOTO 679
  ENDIF
  GOSUB bottles
  IF ob%(no)<>-1 AND ob%(no)<>cp
    m=127
    GOTO 679
  ENDIF
  IF ob%(34)<>-1
    m=148
    GOTO 679
  ENDIF
  IF no<>31
    m=149
    GOTO 679
  ENDIF
  p%(74,3)=75
  obj$(31)=mess$(152)
  pos$(74,0)=mess$(151)
  m=150
  mr=23
  GOSUB dismess
  back=1
  GOTO 680
  679:
  mr=23
  GOSUB dismess
  back=0
  680:
RETURN !'  back to what now
> PROCEDURE thr
  IF no=0
    m=153
    GOTO 699
  ENDIF
  IF no<>33 AND no<>4
    GOSUB 200
    m=0
    GOTO 699
  ENDIF
  IF ob%(4)<>-1
    m=81
    GOTO 699
  ENDIF
  IF no=4 AND np=0
    GOSUB dro
    m=0
    GOTO 699
  ENDIF
  IF no=4 AND np=1
    GOSUB fight
    m=0
    GOTO 699
  ENDIF
  IF cp<>60
    m=154
    GOTO 697
  ENDIF
  p%(60,0)=61
  p%(60,3)=65
  pos$(60,0)=mess$(157)
  df=1
  m=155
  mr=23
  @dismess
  m=156
  mr=24
  @dismess
  m=0
  697:
  ob%(33)=0
  DEC zz
  mr=23
  @dismess
  back=1
  GOTO 700
  699:
  mr=23
  @dismess
  back=0
  700:
RETURN !'  back to what now
> PROCEDURE rub
  IF no=0
    GOSUB notrec
    GOTO 719
  ENDIF
  @bottles
  IF ob%(no)<>-1 AND ob%(no)<>cp
    m=158
    GOTO 719
  ENDIF
  m=162
  mr=23
  @dismess
  m=163
  mr=24
  @dismess
  m=0
  719:
  mr=23
  @dismess
  back=0
RETURN !'  back to what now
> PROCEDURE rea
  IF no=0
    @notrec
    GOTO 739
  ENDIF
  @bottles
  IF no<>48
    m=160
    GOTO 739
  ENDIF
  IF ob%(48)<>-1
    m=161
    GOTO 739
  ENDIF
  m=162
  mr=23
  @dismess
  m=163
  mr=24
  @dismess
  m=0
  739:
  mr=23
  @dismess
  back=0
RETURN !'  back to what now
> PROCEDURE exa
  IF no=0
    GOSUB notrec
    GOTO 759
  ENDIF
  IF no=43 OR no=10 OR no=6
    m=174
    GOTO 759
  ENDIF
  @bottles
  IF ob%(no)<>-1 AND ob%(no)<>cp
    m=36
    GOTO 759
  ENDIF
  IF no=2 OR no=16 OR no=33 OR no=37 OR no=38
    m=164
    GOTO 759
  ENDIF
  row=23
  column=10
  text$=mess$(165)+obj$(no)
  GOSUB strout
  m=0
  759:
  mr=23
  GOSUB dismess
  back=0
RETURN !'  back to what now
> PROCEDURE jum
  IF cp=15 OR cp=10 OR cp=45
    m=175
    mr=23
    GOSUB dismess
    m=6
    mr=24
    GOSUB dismess
    back=2
    GOTO 780
  ENDIF
  m=166
  mr=23
  GOSUB dismess
  back=0
  780:
RETURN !'  back to what now
> PROCEDURE bre
  IF no=0
    m=167
    GOTO 799
  ENDIF
  GOSUB bottles
  IF ob%(no)<>-1 AND ob%(no)<>cp
    m=52
    GOTO 799
  ENDIF
  m=168
  799:
  mr=23
  GOSUB dismess
  back=0
RETURN !'  back to what now
> PROCEDURE pus
  IF no=0
    m=169
    GOTO 819
  ENDIF
  GOSUB bottles
  IF ob%(no)<>-1 AND ob%(no)<>cp
    m=52
    GOTO 819
  ENDIF
  IF cp<>79
    m=170
    GOTO 819
  ENDIF
  m=73
  819:
  mr=23
  GOSUB dismess
  back=0
RETURN !'  back to what now
> PROCEDURE sav  !   save game
  SHOWM
  REPEAT
    FILESELECT "\*.*","",fl$
    IF fl$<>""
      file_name$=fl$
      b=1
      IF EXIST(file_name$)=TRUE
        ALERT 3," File name conflict|Overwrite existing version ?|Do you want to do this?",2,"  Yes  |  No  ",b
        IF b=1
          f=1
        ENDIF
      ENDIF
      IF b=1
        @savegame
        m=111
        mr=23
        GOSUB dismess
        back=0
      ENDIF
    ENDIF
  UNTIL b=1 OR fl$=""
RETURN
> PROCEDURE loa  !   load game
  REPEAT
    FILESELECT "\*.*","",fl$
  UNTIL EXIST(fl$)=TRUE OR fl$=""
  IF fl$<>""
    file_name$=fl$
    @loadgame
  ENDIF
  back=1
RETURN
' -------------------  control Loop module
> PROCEDURE control
  DO
    endloop=0
    DO  !      Startloop
      IF back=1
        @move
        IF tb=1
          ob%(9)=cp
          mr=22
          m=10
          @dismess
        ENDIF
      ENDIF
      GOSUB whatnow
      GOSUB recogn
      IF vb>9 AND no$=""
        m=12
        mr=25
        GOSUB dismess
        back=0
        endloop=-1
      ENDIF
      EXIT IF endloop
      IF (no$<>"" AND vb=1 AND no=0)
        m=13
        mr=25
        GOSUB dismess
        back=0
        endloop=-1
      ENDIF
      EXIT IF endloop
      ON vb GOSUB go,get,loo,inv,sco,dro,hel,qui,cro,get,ope,clo,eat,fee,dri,off,wav,cut,cho,cli,lig,att,kil,hit,mak,ref,oil,sta,spr,thr,rub,rea,exa,jum,bre,pus,sav,loa
      IF back=2
        GOSUB dead
      ENDIF
      IF back=3
        GOSUB won
      ENDIF
    LOOP
  LOOP
RETURN
> PROCEDURE whatnow   ! Keyboard Input
  row=20
  column=2
  length=20
  text1$="What Now ?   "
  style2=0
  text2$=""
  GOSUB string
  com$=text2$
  GOSUB clrmes
RETURN
> PROCEDURE string
  IF column>1 AND column<80 AND row>1 AND row<25
    text$=text1$
    style1=0
    textstyle=style1
    GOSUB strout
    IF LEN(text2$)>length
      text2$=LEFT$(text2$,length)
    ENDIF
    text$=text2$+STRING$(length-LEN(text2$)," ")
    textstyle=style2
    column=column+LEN(text1$)
    GOSUB strout
    column=column+LEN(text2$)
    '                        *     character input   *
    rtrn=0
    DO
      GOSUB nxtchar
      '                        *     character choice      *
      ON char(letter) GOSUB ret,bspc,chrs,del,hlp,nor,sou,eas,wes
      EXIT IF rtrn
    LOOP
  ENDIF
RETURN
> PROCEDURE nxtchar ! * Get next character *
  LET letter=219
  GOSUB chrout
  LET letter=INP(2)
  IF letter>31 AND letter<127
    GOSUB chrout
  ENDIF
RETURN
' -------------------------------------------------
> PROCEDURE ret  ! Return key
  IF LEN(text2$)>0
    rtrn=-1
  ENDIF
RETURN
> PROCEDURE bspc  !   backspace
  IF LEN(text2$)>0
    text2$=LEFT$(text2$,LEN(text2$)-1)
    LET letter=32
    GOSUB chrout
    column=column-1
  ENDIF
RETURN
> PROCEDURE chrs  !  Legal characters
  IF (letter>96 AND letter<122)
    LET letter=letter-32
  ENDIF
  lft$=LEFT$(text2$,column-1)
  text2$=lft$+CHR$(letter)+RIGHT$(text2$,LEN(text2$)-LEN(lft$))
  GOSUB chrout
  IF LEN(text2$)=length
    rtrn=-1
  ELSE
    column=column+1
  ENDIF
RETURN
> PROCEDURE del   !  Delete key
  @bspc
RETURN
> PROCEDURE hlp   ! Help key
  text2$="HELP"
  rtrn=-1
RETURN
> PROCEDURE nor
  text2$="N"
  rtrn=-1
RETURN
> PROCEDURE sou
  text2$="S"
  rtrn=-1
RETURN
> PROCEDURE eas
  text2$="E"
  rtrn=-1
RETURN
> PROCEDURE wes
  text2$="W"
  rtrn=-1
RETURN
' -------------------------------------------------
> PROCEDURE recogn  ! Recognise verb and noun
  no$=""
  vb$=""
  vb=0
  n1$="" !     '    * init var *
  lc=LEN(com$)
  FOR h=1 TO lc
    IF MID$(com$,h,1)<>" "
      vb$=vb$+MID$(com$,h,1) ! ' *   get vb$   *
    ENDIF
    IF MID$(com$,h,1)=" "
      h=lc
    ENDIF
  NEXT h
  v1$=vb$
  vb$=LEFT$(v1$,3) !        '    *   chop to 3 char *
  FOR i=1 TO nv
    IF vb$(i)=vb$
      vb=i
    ENDIF
  NEXT i
  IF vb<>0
    GOTO 2750   !'  *  got vb No.  *
  ELSE
    vb=1
    no$=vb$
    GOTO 2760 !    '     * no verb  imp GO  set str to noun *
  ENDIF
  2750:
  IF LEN(v1$)+1>=LEN(com$)
    GOTO 2780  !    '  * only 1 word a verb *
  ENDIF
  no$=RIGHT$(com$,LEN(com$)-(LEN(v1$)+1)) !'  * get noun   *
  '  Alternative ?   no$=mid$(com$,(len(v1$)+1),3)  '  * get noun   *
  2760:
  n1$=no$
  no$=LEFT$(n1$,3)  ! '  *  chop to 3 char  *
  FOR i=1 TO nn
    EXIT IF no$(i)=no$
  NEXT i
  IF i<nn+1
    no=i
  ELSE
    no=0
  ENDIF
  2780:
RETURN
> PROCEDURE notrec  ! Noun not recognised
  IF no=0
    text$=" What's a  "+n1$
    row=23
    GOSUB strout
  ENDIF
  m=33
  mr=24
  GOSUB dismess
  m=0
RETURN
> PROCEDURE move
  IF ob%(46)<>-1 AND (cp>4 AND cp<100)
    m=2
    GOSUB dismess
    pd=1
    GOTO 1285
  ENDIF
  pd=0
  GOSUB curpos
  IF (cp=42 AND tb=1 AND p%(42,1)=0)
    GOSUB panther
  ENDIF
  GOSUB desobj
  IF (cp=3 AND gf=0)
    m=4
    mr=23
    GOSUB dismess ! 'gate shut
  ENDIF
  fl=0
  GOSUB desdir
  IF np=1
    GOSUB gargoyl
  ENDIF
  IF (cp>20 AND cp<88) AND (RANDOM(10)>9)
    np=1
    GOSUB gargoyl
  ENDIF
  IF cp<>69
    GOTO 1285
  ENDIF
  IF p%(69,3)=70
    GOTO 1285
  ENDIF
  IF ob%(15)<>-1
    m=7
    GOTO 1285 !'  cant pass
  ENDIF
  p%(69,3)=70
  ob%(15)=0
  DEC zz
  pos$(69,0)=mess$(9)  ! walk past cold
  ob%(20)=0
  GOSUB curpos
  m=8
  1285:
  mr=23
  GOSUB dismess
RETURN
> PROCEDURE crevice  !  Crevice
  oc=o
  FOR i=1 TO lo
    IF ob%(i)=-1
      oc=oc+1
    ENDIF
  NEXT i
  IF oc>1
    m=30
    back=0
    GOTO 5590
  ENDIF
  IF ob%(37)<>-1
    m=31
    back=0
    GOTO 5590
  ENDIF
  cp=100
  m=32
  back=1
  5590:
  mr=23
  GOSUB dismess
RETURN
> PROCEDURE lockin  !  Gate locked
  GOSUB clrmes
  FOR k=15 TO 17
    mr=k+7
    m=k
    @dismess
  NEXT k
  PAUSE 400
  p%(3,0)=0
  back=0
  GOSUB clrmes
RETURN
> PROCEDURE bottles  !  Bottles and torches
  IF no=45 AND ob%(46)=-1
    no=46
  ENDIF
  IF no=19 AND ob%(51)=-1
    no=51
  ENDIF
  IF no=19 AND ob%(52)=-1
    no=52
  ENDIF
  IF no=18 AND ob%(51)=-1
    no=51
  ENDIF
  IF no=39 AND ob%(52)=-1
    no=52
  ENDIF
RETURN
' --------------------------------------------------
> PROCEDURE panther
  mr=24
  m=19
  GOSUB dismess
  PAUSE 400
  ob%(11)=0
  p%(42,1)=43
  pos$(42,0)=mess$(20)
  GOSUB curpos
RETURN
> PROCEDURE gargoyl
  GOSUB clrmes
  m=21
  mr=22
  GOSUB dismess ! 'm21 hostile gargoyle   etc
  IF RANDOM(100)>98
    @thief
    GOTO gar_end
  ENDIF
  m=22
  mr=24
  GOSUB dismess
  ob%(40)=cp ! 'm22 gargoyle  throws kni
  IF RANDOM(100)>98
    mr=25
    m=23
    GOSUB dismess
    @dead
    GOTO gar_end
  ENDIF
  m=24
  mr=25
  GOSUB dismess
  PAUSE 200
  @clrmes
  gar_end:
RETURN
> PROCEDURE fight
  GOSUB clrmes
  IF RANDOM(10)>1
    m=25
    mr=23
    GOSUB dismess
    ob%(40)=0
    np=0
  ELSE
    m=26
    mr=24
    GOSUB dismess
    ob%(40)=cp
  ENDIF
  ob%(4)=cp
  DEC zz
  PAUSE 200
RETURN
> PROCEDURE thief
  m=28
  mr=23
  GOSUB dismess
  gs=0
  text$=""
  IF ob%(2)=-1
    ob%(2)=63
    text$=text$+obj$(2)+" "
    INC gs
  ENDIF
  IF ob%(7)=-1
    ob%(7)=63
    text$=text$+obj$(7)+" "
    INC gs
  ENDIF
  IF ob%(14)=-1
    ob%(14)=63
    text$=text$+obj$(14)+" "
    INC gs
  ENDIF
  IF ob%(16)=-1
    ob%(16)=63
    text$=text$+obj$(16)+" "
    INC gs
  ENDIF
  IF text$<>""
    row=24
    GOSUB strout
    text$=""
  ENDIF
  IF ob%(19)=-1
    ob%(19)=63
    text$=text$+obj$(19)+" "
    INC gs
  ENDIF
  IF ob%(33)=-1
    ob%(33)=63
    text$=text$+obj$(33)+" "
    INC gs
  ENDIF
  IF ob%(34)=-1
    ob%(34)=63
    text$=text$+obj$(34)+" "
    INC gs
  ENDIF
  IF ob%(38)=-1
    ob%(38)=63
    text$=text$+obj$(38)+" "
    INC gs
  ENDIF
  IF ob%(44)=-1
    ob%(44)=63
    text$=text$+obj$(44)+" "
    INC gs
  ENDIF
  IF text$<>""
    row=25
    GOSUB strout
    text$=""
  ENDIF
  IF gs=0
    m=29
    mr=24
    GOSUB dismess
    PAUSE 200
  ENDIF
RETURN
' --------------------------------------------------
> PROCEDURE yesno
  y=2
  REPEAT
    column=70
    GOSUB nxtchar
    IF letter=89 OR letter=121
      y=1
    ENDIF
    IF letter=78 OR letter=110
      y=0
    ENDIF
  UNTIL y<>2
RETURN
' --------------------------------------------------
> PROCEDURE dismess  ! Display Message
  IF m<>0
    GOSUB clrline
    text$=mess$(m)
    row=mr
    textstyle=0
    @centertext
  ENDIF
  m=0
RETURN
> PROCEDURE disfile  !  Display file
  CLS
  FOR k=1 TO 22
    rn=(k+pn)
    r=k
    hi=0
    GOSUB disline
  NEXT k
RETURN
> PROCEDURE disline  !  Display line
  text$=row$(rn)
  row=r
  column=1
  textstyle=VAL(tstyle$(rn))
  GOSUB strout
RETURN
> PROCEDURE chrout   ! Display Character
  DPOKE CONTRL,8
  DPOKE CONTRL+2,1
  DPOKE CONTRL+6,1
  DPOKE PTSIN,(column-1)*8
  DPOKE PTSIN+2,(row*(8*rez))-2
  DPOKE INTIN,letter
  VDISYS
RETURN
> PROCEDURE strout   ! txt string to screen
  GOSUB setstyle
  IF column>=1 AND column<=80 AND row>=1 AND row=<25
    IF column+LEN(text$)>80
      text$=LEFT$(text$,81-column)
    ENDIF
    DPOKE CONTRL,8
    DPOKE CONTRL+2,1
    DPOKE CONTRL+6,LEN(text$)
    DPOKE PTSIN,(column-1)*8
    DPOKE PTSIN+2,(row*(8*rez))-2
    FOR i=0 TO LEN(text$)-1
      DPOKE INTIN+i*2,ASC(MID$(text$,i+1,1))
    NEXT i
    VDISYS
  ENDIF
RETURN
> PROCEDURE setstyle
  txt_size=13
  rot=0
  txt_color=1
  DEFTEXT txt_color,textstyle,rot,txt_size
RETURN
> PROCEDURE centertext
  GOSUB setstyle
  DPOKE CONTRL,8
  DPOKE CONTRL+2,1
  DPOKE CONTRL+6,LEN(text$)
  DPOKE PTSIN,(640-LEN(text$)*8)/2
  DPOKE PTSIN+2,(row*(8*rez))-2
  FOR i=0 TO LEN(text$)-1
    DPOKE INTIN+i*2,ASC(MID$(text$,i+1,1))
  NEXT i
  VDISYS
RETURN
> PROCEDURE setprgname ! Set title
  title$="Underground  Adventure "
  DEFFILL 1,3,1
  PBOX 0,1*rez,639,19*rez
  row=2
  text$=title$
  GOSUB centertext
RETURN
' --------------------------------------------------
> PROCEDURE desobj    ! Describe objects
  GOSUB clrobj
  text$=" You can  see :  "
  row=12
  column=2
  textstyle=8
  GOSUB strout
  textstyle=0
  FOR j=1 TO lo
    IF ob%(j)=cp
      text$=obj$(j)
      column=2
      INC row
      GOSUB strout
    ENDIF
  NEXT j
RETURN
> PROCEDURE desdir !  describe possible directions
  GOSUB clrdir
  text$=" You can  go   : "
  row=18
  column=2
  textstyle=8
  GOSUB strout
  textstyle=0
  text$=""
  FOR i=0 TO 3
    IF p%(cp,i)<>0
      text$=text$+dd$(i)+", "
    ENDIF
  NEXT i
  row=19
  column=12
  GOSUB strout
RETURN
> PROCEDURE curpos  !  Describe Current Position
  GOSUB clrpos
  text$=" You are : "
  row=4
  column=2
  textstyle=8
  GOSUB strout
  r=5
  FOR k=0 TO 4
    text$=pos$(cp,k)
    row=r+k
    column=2
    textstyle=0
    GOSUB strout
  NEXT k
RETURN
' --------------------------------------------------
> PROCEDURE clrpos   ! Clear position area
  DEFFILL 1,0,0
  PBOX 0,20*rez,639,84*rez
RETURN
> PROCEDURE clrobj    ! Clear object area
  DEFFILL 1,0,0
  PBOX 0,85*rez,639,133*rez
RETURN
> PROCEDURE clrmes    ! Clear Message area
  DEFFILL 1,0,0
  PBOX 0,167*rez,639,199*rez
RETURN
> PROCEDURE clrdir    ! Clear direction area
  DEFFILL 1,0,0
  PBOX 0,134*rez,639,166*rez
RETURN
> PROCEDURE clrline
  text$=SPACE$(78)
  column=2
  row=mr
  GOSUB strout
RETURN
' --------------------------------------------------
> PROCEDURE loadgame
  OPEN "I",#1,file_name$
  FOR k=1 TO lo
    INPUT #1,ob%(k)
  NEXT k
  INPUT #1,cp,tb,gf,pd,zz,sc,df,br,np
  FOR l=0 TO 5
    INPUT #1,pos$(10,l),pos$(60,l),pos$(50,l),pos$(27,l),pos$(15,l),pos$(21,l)
    INPUT #1,pos$(4,l),pos$(93,l),pos$(79,l),pos$(84,l),pos$(74,l),pos$(69,l)
    INPUT #1,pos$(42,l)
  NEXT l
  INPUT #1,obj$(30),obj$(3),obj$(31),obj$(5)
  INPUT #1,p%(45,1),p%(10,3),p%(3,0),p%(60,1),p%(50,3),p%(27,0),p%(15,1)
  INPUT #1,p%(15,2),p%(21,2),p%(14,3),p%(93,0),p%(79,2),p%(84,2),p%(84,3)
  INPUT #1,p%(74,3),p%(60,0),p%(60,3),p%(69,3),p%(42,1),p%(4,3)
  CLOSE #1
RETURN
> PROCEDURE savegame
  IF f=1
    KILL file_name$
  ENDIF
  OPEN "O",#1,file_name$
  FOR k=1 TO lo
    WRITE #1,ob%(k)
  NEXT k
  WRITE #1,cp,tb,gf,pd,zz,sc,df,br,np
  FOR l=0 TO 5
    WRITE #1,pos$(10,l),pos$(60,l),pos$(50,l),pos$(27,l),pos$(15,l),pos$(21,l)
    WRITE #1,pos$(4,l),pos$(93,l),pos$(79,l),pos$(84,l),pos$(74,l),pos$(69,l)
    WRITE #1,pos$(42,l)
  NEXT l
  WRITE #1,obj$(30),obj$(3),obj$(31),obj$(5)
  WRITE #1,p%(45,1),p%(10,3),p%(3,0),p%(60,1),p%(50,3),p%(27,0),p%(15,1)
  WRITE #1,p%(15,2),p%(21,2),p%(14,3),p%(93,0),p%(79,2),p%(84,2),p%(84,3)
  WRITE #1,p%(74,3),p%(60,0),p%(60,3),p%(69,3),p%(42,1),p%(4,3)
  CLOSE #1
RETURN
' -------------------------------------------------
> PROCEDURE done
  GOSUB play_again
  mr=24
  m=101
  @clrmes
  @dismess
  PAUSE 200 !  'bye
  EDIT
RETURN
> PROCEDURE dead
  m=18
  mr=24
  GOSUB dismess !' dead  !!
  GOSUB done
RETURN
> PROCEDURE play_again
  m=14
  mr=25
  GOSUB dismess
  GOSUB yesno
  IF y=1
    FOR k=1 TO 22
      row$(k)=""
    NEXT k
    RESTORE
    RUN
  ENDIF
RETURN
> PROCEDURE won
  mr=23
  m=41
  GOSUB dismess
  PAUSE 200 !  'out
  GOSUB done
RETURN
' ---------------------------------------------------
> PROCEDURE initdata  !  Initialise Var   DIM  arrays
  rez=2
  gf=1
  df=0
  cp=1
  nv=38
  nn=53
  p=100
  lo=53
  DIM pos$(p,5),p%(p,3),obj$(lo),ob%(lo),vb$(nv),no$(nn),dd$(4)
  sz=255
  DIM char(sz),mess$(185),row$(sz),tstyle$(sz)
  FOR i=0 TO sz
    char(i)=0
  NEXT i
  char(13)=1   !   Return
  char(8)=2  ! Backspace
  FOR i=32 TO 126 ! Pos chr
    char(i)=3
  NEXT i
  char(127)=4  !   Delete
  char(226)=5 !     Help
  char(200)=6  ! cur up N
  char(208)=7 ! cur dn S
  char(205)=8   ! cur rt E
  char(203)=9 ! cur lt w
RETURN
> PROCEDURE readdata  !   Read message files
  '
  fname$="uadv_mes.fil"
  GOSUB readfile
  FOR k=1 TO rt
    mess$(k)=row$(k)
  NEXT k
  '            **  read position desciption  from file     **
  fname$="uadv_pos.fil"
  GOSUB readfile
  FOR k=1 TO 60
    pos$(k,0)=row$(k)
  NEXT k
  FOR k=66 TO 100
    pos$(k,0)=row$(k)
  NEXT k
  FOR k=61 TO 65
    FOR l=0 TO 4
      pos$(k,l)=row$(61+l)
    NEXT l
  NEXT k
RETURN
PROCEDURE readfile
  OPEN "I",#1,fname$
  IF EOF(#1)=-1
    GOTO 2440
  ENDIF
  INPUT #1,t$,u$,rt
  FOR k=1 TO rt
    INPUT #1,row$(k)
  NEXT k
  CLOSE #1
  GOTO 2450
  2440:
  m=13
  mr=23
  GOSUB dismess
  PAUSE 400
  CLOSE #1
  2450:
RETURN
> PROCEDURE initdis !  Initial display
  CLS
  rt=22
  fname$="uadv_int.fil"
  GOSUB readfile
  pn=0
  GOSUB disfile
RETURN
> PROCEDURE assign  !  Assign data
  DATA 0,2,0,0,1,3,0,0,2,15,20,4,0,0,3,0,6,13,4,9,0,5,0,7,0,9,6,8
  DATA 0,10,7,0,7,12,5,10,8,11,9,0,10,0,12,0,9,0,13,11,5,0,0,12
  DATA 0,0,10,0,3,0,0,0,0,18,0,15,15,33,18,19,16,34,0,17,0,32,17,0
  DATA 0,0,21,3,0,0,0,20,23,0,0,21,25,22,24,0,26,0,0,23,0,23,26,0
  DATA 27,24,0,25,0,26,0,0,0,27,30,29,0,0,28,0,31,0,0,28,0,30,0,0
  DATA 19,42,33,41,17,0,34,32,18,0,35,33,0,0,0,34,0,0,38,0,0,38,39,0
  DATA 37,0,0,36,0,0,40,37,0,0,0,39,0,0,32,0,32,0,0,0,42,0,44,46
  DATA 0,45,0,43,44,0,0,0,0,43,0,0,0,52,49,48,0,51,47,50,0,53,54,47
  DATA 0,0,48,0,48,66,0,0,47,77,0,0,49,100,0,0,0,0,88,49,56,57,50,58
  DATA 0,55,0,0,55,0,0,0,0,0,55,59,60,0,58,0,0,59,0,0,61,61,61,62
  DATA 61,61,61,63,61,64,61,61,61,61,65,61,61,61,60,61,51,67,0,68
  DATA 66,0,0,69,0,69,66,0,68,0,67,0,0,71,69,0,70,72,0,74,70,0,0,73
  DATA 74,0,72,0,0,73,71,0,0,0,74,76,0,0,75,0,52,78,0,0,77,79,0,0
  DATA 78,0,0,0,0,83,0,79,0,82,79,0,81,0,0,0,80,84,0,0,83,0,0,0
  DATA 0,0,84,0,0,87,0,84,86,0,0,0,89,90,92,54,0,88,91,0,88,94,0,0
  DATA 0,92,93,89,91,0,97,88,0,97,0,91,90,0,0,0,0,93,96,0,99,0,98,95
  DATA 93,0,0,92,0,0,0,96,0,96,0,0,53,0,0,0
  FOR i=1 TO p
    FOR j=0 TO 3
      READ p%(i,j)
    NEXT j
  NEXT i
  '
  DATA 15,20,21,34,24,0,40,0,27,7,42,46,0,14,93,67,79,48,98,69
  FOR i=1 TO 20
    READ ob%(i)
  NEXT i
  DATA 50,84,74,60,76,87,3,53,63,31,73,0,0,100,0,3,1,0,0,39,0,0,0,0,0
  FOR i=29 TO lo
    READ ob%(i)
  NEXT i
  DATA A Vast Chasm,An Iron Staff,A Vast Tree,A Stout Axe
  DATA A Thick Coil of Rope,An Enchanted Bridge,Some Dynamite !
  DATA A Pile of Rubble,A Golden Bear,A Bun,A Big Black Panther
  DATA A Long Wooden Plank,A Tall Ladder,Some Nails
  DATA A Hazy Shimmring Curtain,A Polished Mirror,A Rusted up Truck
  DATA A Pool of Oil,An Empty Bottle,A Solid Wall of Hazy Mist
  FOR i=1 TO 20
    READ obj$(i)
  NEXT i
  DATA The Ghostly Denzian of the Caves,A Huge Bulbous Spider
  DATA An Enormous Fly !,A Rickety Old Door,A Lump of Solid Mortar
  DATA A Fly Spray !,A Solid Gate,A Narrow crack,A Shining Stone
  DATA A Trusty Sword,Some Whisky,A Living Gargoyle,An Evil Knife
  DATA A Key,A Wall,Some Matches,An Old Torch,A Blazing Torch
  DATA A Glowing Light,An Old Parchment,program,A Pile of Broken Glass
  DATA A bottle Of Oil,A Bottle of Whisky,Some Neatly Sawn Timber
  FOR i=29 TO lo
    READ obj$(i)
  NEXT i
  DATA CHA,STA,TRE,AXE,ROP,BRI,DYN,RUB,BEA,BUN,PAN,PLA,LAD,NAI,CUR,MIR
  DATA WHE,OIL,BOT,MIS,NOR,SOU,EAS,WES,N,S,E,W,GHO,SPI,FLY,DOO,MOR,SPR
  DATA GAT,CRA,STO,SWO,WHI,GAR,KNI,KEY,WAL,MAT,TOR,TOR,LIG,PAR,PRO,GLA
  DATA BOT,BOT,TIM
  FOR i=1 TO nn
    READ no$(i)
  NEXT i
  DATA GO,GET,LOO,INV,SCO,DRO,HEL,QUI,CRO,TAK,OPE,CLO,EAT,FEE,DRI,OFF
  DATA WAV,CUT,CHO,CLI,LIG,ATT,KIL,HIT,MAK,REF,OIL,STA,SPR,THR,RUB,REA
  DATA EXA,JUM,BRE,PUS,SAV,LOA
  FOR i=1 TO nv
    READ vb$(i)
  NEXT i
  DATA North,South,East,West
  FOR i=0 TO 3
    READ dd$(i)
  NEXT i
RETURN
