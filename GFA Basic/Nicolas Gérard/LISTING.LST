fre%=FRE(0)
RESERVE fre%-10000
t$=" VOICI UN SCROLL AUSSI NUL  QUE TOUS LES AUTRES..."
l%=LEN(t$)       ! longueur du texte
buffer%=MALLOC(l%*8*8*2)    ! on cree le buffer
superviseur%=GEMDOS(32,L:0) ! on passe en superviseur
compt%=0
FOR i%=0 TO l%
  PRINT AT(1,1);MID$(t$,i%,1) ! affichage des lettres unes … unes
  FOR y%=0 TO 7
    FOR x%=0 TO 7
      DPOKE buffer%+compt%,ABS((POINT(x%,y%)<>0)*compt%) ! stockage dans le buffer
      ADD compt%,2
    NEXT x%
  NEXT y%
NEXT i%
finbuff%=buffer%+compt%-2   ! calcul de la taille du buffer
CLS
c%=1
FOR i%=0 TO 63 STEP 8
  ALINE i%,0,i%+7,0,c%,-1,0
  INC c%
NEXT i%
GET 0,0,64,0,lig$
CLS
FOR x%=0 TO 319 STEP 80
  FOR y%=0 TO 199
    PUT x%,y%,lig$
  NEXT y%
NEXT x%
pos%=buffer%              ! on stocke l'adresse de depart dans pos%
POKE &HFFFA09,0           ! On coupe les priorit‚s MFP 68901
DO                        ! boucle principale
  IF pos%>finbuff%        ! test de fin de buffer
    pos%=buffer%          ! si oui alors on recommence
  ELSE                    ! sinon
    ADD pos%,4            ! on continue
  ENDIF
  posbuffer%=pos%         ! posbuffer%=adresse ou on va lire dans le buffer
  maxlig%=posbuffer%+16*15*4 ! Nombre de positions buffer … lire=nbre de boucles
  REPEAT                   ! routine de stabilisation
    stab|=BYTE{&HFFFF8209} ! lit la position du balayage
  UNTIL stab|
  VOID SHL(13,-stab|)      ! attend le bon moment
  REPEAT                   ! routine d'affichage
    lig%=posbuffer% AND -16 ! on garde les multiples de 16 pour la position
    LONG{&HFFFF8242}=LONG{lig%} ! installe couleurs 1 & 2
    LONG{&HFFFF8246}=LONG{lig%+4} ! couleurs 3 & 4
    LONG{&HFFFF824A}=LONG{lig%+8} ! couleurs 5 & 6
    LONG{&HFFFF824E}=LONG{lig%+12} ! couleurs 7 & 8
    ADD posbuffer%,4        ! on saute quelques lignes
    CARD{&HFFFF825E}=0      ! c'est trop rapide donc on attend un peu
    CARD{&HFFFF825E}=0      ! idem
    CARD{&HFFFF825E}=0      ! Remarque: si vous changez le nombre de
    CARD{&HFFFF825E}=0      ! couleurs par ligne, il vous faudra r‚duire
    CARD{&HFFFF825E}=0      ! ou augmenter ce nombre de lignes
    CARD{&HFFFF825E}=0      ! ...
    CARD{&HFFFF825E}=0      ! ...
    CARD{&HFFFF825E}=0      ! presque fini, courage
    CARD{&HFFFF825E}=0      ! ...
    CARD{&HFFFF825E}=0      ! fini...
  UNTIL posbuffer%>maxlig%  ! si maxlig% est atteind alors on change la position de base de lecture buffer
  VSYNC                     ! c'est la synchro VBL
  CARD{&HFFFF8240}=0        ! couleur de fond noire
LOOP UNTIL PEEK(&HFFFC02)=57  ! on sort si espace est press‚
POKE &HFFFA09,100           ! On rebranche notre ami MFP
SETCOLOR 15,1911            ! l'‚criture en blanc
SETCOLOR 0,0                ! le fond en noir
VOID MFREE(buffer%)         ! on libŠre la place prise par le buffer
RESERVE fre%                ! et on la r‚installe
VOID GEMDOS(32,L:superviseur%) ! surtout repasser en utilisateur
EDIT                        ! et bye bye
